(** MASC MCP Server - Eio Native Entry Point
    MCP Streamable HTTP Transport with Eio concurrency (OCaml 5.x)

    Uses h2-eio for HTTP/2 with unlimited SSE streams per connection.
    HTTP/2 multiplexing eliminates browser's 6-connection-per-domain limit.
*)

[@@@warning "-32-69"]  (* Suppress unused values/fields during migration *)

open Cmdliner

(** Module aliases *)
module Http = Masc_mcp.Http_server_eio
module Http_h2 = Masc_mcp.Http_server_h2
module Mcp_server = Masc_mcp.Mcp_server
module Mcp_eio = Masc_mcp.Mcp_server_eio
module Coord = Masc_mcp.Coord
module Coord_utils = Coord_utils
module Tool_keeper = Masc_mcp.Tool_keeper
module Keeper_types = Masc_mcp.Keeper_types
module Keeper_memory = Masc_mcp.Keeper_memory
module Keeper_execution = Masc_mcp.Keeper_execution
module Keeper_runtime = Masc_mcp.Keeper_runtime
module Tool_operator = Masc_mcp.Tool_operator
module Operator_control = Masc_mcp.Operator_control
module Dashboard_execution = Masc_mcp.Dashboard_execution
module Dashboard_mission = Masc_mcp.Dashboard_mission
(* module Dashboard_proof removed *)
module Dashboard_mission_briefing = Masc_mcp.Dashboard_mission_briefing
module Build_identity = Masc_mcp.Build_identity
module Config_doctor = Masc_mcp.Config_doctor
module Auth_doctor = Masc_mcp.Auth_doctor
module Auth_login = Masc_mcp.Auth_login
module Keeper_id = Masc_mcp.Keeper_id
module Keeper_msg_async = Masc_mcp.Keeper_msg_async
module Keeper_status_bridge = Masc_mcp.Keeper_status_bridge
module Keeper_tool_call_log = Masc_mcp.Keeper_tool_call_log
module Graphql_api = Masc_mcp.Graphql_api
module Types = Masc_domain
module Tempo = Masc_mcp.Tempo
module Auth = Masc_mcp.Auth
module Board = Masc_mcp.Board
module Board_curation = Masc_mcp.Board_curation
module Board_dispatch = Masc_mcp.Board_dispatch
module Task_dispatch = Masc_mcp.Task_dispatch
module Http_negotiation = Mcp_transport_protocol.Http_negotiation
module Progress = Masc_mcp.Progress
module Sse = Masc_mcp.Sse
module Safe_ops = Safe_ops
module Tool_board = Masc_mcp.Tool_board
module Server_mcp_transport_http = Masc_mcp.Server_mcp_transport_http


(* ============================================ *)
(* Extracted modules (lib/)                      *)
(* ============================================ *)
include Masc_mcp.Server_utils
include Masc_mcp.Server_auth
include Masc_mcp.Server_voice_config
include Masc_mcp.Server_dashboard_http
module Server_h2_gateway = Masc_mcp.Server_h2_gateway
module Server_runtime_bootstrap = Masc_mcp.Server_runtime_bootstrap
module Server_routes_http_runtime = Masc_mcp.Server_routes_http_runtime
module Server_openai_compat = Masc_mcp.Server_openai_compat
module Server_startup_takeover = Masc_mcp.Server_startup_takeover

let mcp_protocol_versions = Server_mcp_transport_http.mcp_protocol_versions

let mcp_protocol_version_default =
  Server_mcp_transport_http.mcp_protocol_version_default

let default_base_path = Server_mcp_transport_http.default_base_path

let implicit_base_path_resolution_source () = "implicit_base_path"

let is_valid_protocol_version =
  Server_mcp_transport_http.is_valid_protocol_version

let remember_protocol_version =
  Server_mcp_transport_http.remember_protocol_version

let remember_mcp_profile = Server_mcp_transport_http.remember_mcp_profile

let forget_mcp_session = Server_mcp_transport_http.forget_mcp_session

let validate_mcp_session_profile =
  Server_mcp_transport_http.validate_mcp_session_profile

let validate_mcp_session_delete_profile =
  Server_mcp_transport_http.validate_mcp_session_delete_profile

let protocol_version_from_body =
  Server_mcp_transport_http.protocol_version_from_body

let get_session_id_query = Server_mcp_transport_http.get_session_id_query

let get_header_any_case = Server_mcp_transport_http.get_header_any_case

let get_cookie_value = Server_mcp_transport_http.get_cookie_value

let get_session_id_any = Server_mcp_transport_http.get_session_id_any

let get_protocol_version = Server_mcp_transport_http.get_protocol_version

let get_protocol_version_for_session =
  Server_mcp_transport_http.get_protocol_version_for_session

module Server_routes_http = Masc_mcp.Server_routes_http

open Server_routes_http

(* Issue #8403: derive probe exemptions from Server_health_paths SSOT
   so a renamed probe stays exempt from rate limits without a separate
   manual edit here. *)
let is_rate_limit_exempt path =
  String.equal path "/health"
  || Masc_mcp.Server_health_paths.is_public path

(** [safe_reqd_respond reqd response body] guards all direct
    [Httpun.Reqd.respond_with_string] calls in the main request handler
    against the "invalid state, currently handling error" [Failure] that
    httpun raises when the reqd has already entered its error-handling path
    (e.g. client disconnect during a long OAS turn — 2026-05-05 cycle9
    FATAL race, also see [Http_server_eio.safe_respond_with_string]).
    [Eio.Cancel.Cancelled] is always re-raised. *)
let safe_reqd_respond reqd response body =
  try Httpun.Reqd.respond_with_string reqd response body
  with
  | Eio.Cancel.Cancelled _ as e -> raise e
  | Failure msg ->
      Log.Server.warn
        "[http] reqd respond skipped (invalid state; 2026-05-05 OAS cancel race): %s"
        msg
  | exn ->
      Log.Server.warn "[http] reqd respond unexpected exception: %s"
        (Printexc.to_string exn)

(** Returns true if the request was rate-limited and a 429 response was
    sent on [reqd]. Caller should short-circuit further handling in that
    case. Health-probe paths are always allowed through.

    Enforces two complementary rate limits:
    1. Per-client IP (via [client_addr]) — protects against volumetric abuse.
    2. Per-agent bearer token (via Authorization header) — enforces per-agent
       quotas regardless of source IP, complementing the IP-level check. *)
let try_rate_limit_block ~path ~client_addr ~request reqd =
  if is_rate_limit_exempt path then false
  else
    let rl_key = Masc_mcp.Rate_limit.key_of_sockaddr client_addr in
    if not (Masc_mcp.Rate_limit.check_global ~key:rl_key) then begin
      let body = Masc_mcp.Rate_limit.too_many_requests_body () in
      let rl_headers = Masc_mcp.Rate_limit.headers_global ~key:rl_key in
      let headers = Httpun.Headers.of_list (
        ("content-type", "application/json") ::
        ("content-length", string_of_int (String.length body)) ::
        rl_headers
      ) in
      safe_reqd_respond reqd
        (Httpun.Response.create ~headers `Too_many_requests) body;
      true
    end else
      match auth_token_from_request request with
      | None -> false
      | Some token ->
          match Masc_mcp.Rate_limit.agent_key_of_token_or_name ~token () with
          | None -> false
          | Some agent_key ->
              if Masc_mcp.Rate_limit.check_agent_global ~key:agent_key then false
              else begin
                let body = Masc_mcp.Rate_limit.too_many_agent_requests_body () in
                let rl_headers =
                  Masc_mcp.Rate_limit.headers_agent_global ~key:agent_key
                in
                let headers =
                  Httpun.Headers.of_list
                    (("content-type", "application/json")
                    :: ("content-length", string_of_int (String.length body))
                    :: rl_headers)
                in
                safe_reqd_respond reqd
                  (Httpun.Response.create ~headers `Too_many_requests)
                  body;
                true
              end

(** Path predicate: requests that go through the MCP transport surface
    (HTTP-based sessions, SSE, JSON-RPC messages) and therefore must pass
    origin and protocol-version checks. *)
let is_mcp_like_path path =
  String.equal path "/mcp"
  || String.equal path "/mcp/managed"
  || String.equal path "/mcp/operator"
  || String.equal path "/sse"

(** Returns true if the request failed origin or protocol-version
    validation and the corresponding error response was sent on [reqd].
    Caller should short-circuit further handling in that case. *)
let try_mcp_validation_block ~is_mcp_like ~request ~protocol_version ~origin reqd =
  if is_mcp_like && not (validate_origin request) then begin
    let body = json_rpc_error (-32600) "Invalid origin" in
    let headers = Httpun.Headers.of_list (
      ("content-length", string_of_int (String.length body))
      :: json_headers "-" protocol_version origin
    ) in
    let response = Httpun.Response.create ~headers `Forbidden in
    safe_reqd_respond reqd response body;
    true
  end
  else if is_mcp_like && request.Httpun.Request.meth <> `OPTIONS &&
          not (is_valid_protocol_version protocol_version) then begin
    let body = json_rpc_error (-32600) "Unsupported protocol version" in
    let headers = Httpun.Headers.of_list (
      ("content-length", string_of_int (String.length body))
      :: json_headers "-" protocol_version origin
    ) in
    let response = Httpun.Response.create ~headers `Bad_request in
    safe_reqd_respond reqd response body;
    true
  end
  else false

(** Method/path dispatcher for MCP-validated requests. Caller is
    responsible for rate limiting and origin/protocol-version checks
    before invoking this function. *)
let dispatch_route ~router ~request ~path reqd =
  match request.Httpun.Request.meth, path with
  | `OPTIONS, _ -> options_handler request reqd
  | `GET, "/ws" ->
    let body =
      Server_routes_http_runtime.websocket_discovery_json request
      |> Yojson.Safe.to_string
    in
    let headers = Httpun.Headers.of_list [
      ("content-type", "application/json");
      ("content-length", string_of_int (String.length body));
    ] in
    let response = Httpun.Response.create ~headers `OK in
    safe_reqd_respond reqd response body
  | `POST, "/webrtc/offer" when Masc_mcp.Server_webrtc_transport.is_enabled () ->
    Http.Request.read_body_async reqd (fun body ->
      match Masc_mcp.Server_webrtc_transport.handle_offer_request body with
      | Ok json -> Http.Response.json json reqd
      | Error msg ->
        Http.Response.json ~status:`Bad_request
          (Printf.sprintf {|{"error":"%s"}|} msg) reqd)
  | `POST, "/webrtc/answer" when Masc_mcp.Server_webrtc_transport.is_enabled () ->
    Http.Request.read_body_async reqd (fun body ->
      match Masc_mcp.Server_webrtc_transport.handle_answer_request body with
      | Ok json -> Http.Response.json json reqd
      | Error msg ->
        Http.Response.json ~status:`Bad_request
          (Printf.sprintf {|{"error":"%s"}|} msg) reqd)
  | `POST, "/v1/chat/completions" when Server_openai_compat.is_enabled () ->
    Http.Request.read_body_async reqd (fun body ->
      match !server_state with
      | None ->
        let origin = get_origin request in
        Http.Response.json ~status:`Internal_server_error
          ~extra_headers:(cors_headers origin)
          (Server_openai_compat.error_response
             ~status:"server_error" ~message:"Server not initialized" ())
          reqd
      | Some state ->
        let config = state.Mcp_server.room_config in
        (match state.Mcp_server.sw, state.Mcp_server.clock with
        | Some sw, Some clock ->
            let (status, resp_body) =
              Server_openai_compat.handle_chat_completions
                ~config ~sw ~clock body
            in
            let origin = get_origin request in
            Http.Response.json ~status
              ~extra_headers:(cors_headers origin)
              resp_body reqd
        | _ ->
            let origin = get_origin request in
            Http.Response.json ~status:`Internal_server_error
              ~extra_headers:(cors_headers origin)
              (Server_openai_compat.error_response
                 ~status:"server_error"
                 ~message:"Server runtime not fully initialized" ())
              reqd))
  | `DELETE, "/mcp" -> handle_delete_mcp request reqd
  | `DELETE, "/mcp/managed" ->
      handle_delete_mcp
        ~profile:Server_mcp_transport_http.Managed_agent request reqd
  | `DELETE, "/mcp/operator" ->
      handle_delete_mcp
        ~profile:Server_mcp_transport_http.Operator_remote request reqd
  | `GET, "/api/v1/board/flairs" ->
      let flairs = List.map Board.flair_to_yojson Board.available_flairs in
      let json = `Assoc [("flairs", `List flairs)] in
      Http.Response.json (Yojson.Safe.to_string json) reqd
  | `GET, "/api/v1/board/hearths" ->
      let hearths = Board_dispatch.list_hearths () in
      let json = `Assoc [
        ("hearths", `List (List.map (fun (name, count) ->
          `Assoc [("name", `String name); ("count", `Int count)]
        ) hearths));
      ] in
      Http.Response.json (Yojson.Safe.to_string json) reqd
  | `GET, "/api/v1/board/curation" ->
      let json =
        match Board_dispatch.latest_curation_snapshot () with
        | None -> `Assoc [ ("snapshot", `Null) ]
        | Some snap ->
            `Assoc [ ("snapshot", Board_curation.snapshot_to_yojson snap) ]
      in
      Http.Response.json (Yojson.Safe.to_string json) reqd
  | `GET, "/api/v1/board/sub-boards" ->
      let sub_boards = Board_dispatch.list_sub_boards () in
      let json =
        `Assoc
          [
            ( "sub_boards",
              `List (List.map Board.sub_board_to_yojson sub_boards) );
          ]
      in
      Http.Response.json (Yojson.Safe.to_string json) reqd
  | `GET, "/api/v1/board/karma/ledger" ->
      let agent = query_param request "agent" in
      let limit =
        int_query_param request "limit" ~default:500 |> clamp ~min_v:1 ~max_v:5000
      in
      let events = Board_dispatch.get_karma_ledger ?agent ~limit () in
      let totals =
        Board_dispatch.get_all_karma ()
        |> List.sort (fun (_, a) (_, b) -> compare b a)
      in
      let json =
        `Assoc
          [
            ("events", `List (List.map Board.karma_event_to_yojson events));
            ("count", `Int (List.length events));
            ("scoring_rule", `String "up=+1,down=0");
            ( "totals",
              `List
                (List.map
                   (fun (agent_name, k) ->
                     `Assoc
                       [ ("agent", `String agent_name); ("karma", `Int k) ])
                   totals) );
          ]
      in
      Http.Response.json (Yojson.Safe.to_string json) reqd
  | `POST, "/api/v1/board/reactions" ->
      Http.Request.read_body_async reqd (fun body ->
        try
          let args = Yojson.Safe.from_string body in
          let target_type_raw =
            Option.value ~default:""
              (Safe_ops.json_string_opt "target_type" args)
          in
          let target_id =
            Option.value ~default:"" (Safe_ops.json_string_opt "target_id" args)
          in
          let user_id =
            Option.value ~default:"" (Safe_ops.json_string_opt "user_id" args)
          in
          let emoji =
            Option.value ~default:"" (Safe_ops.json_string_opt "emoji" args)
          in
          match Board.reaction_target_type_of_string_opt target_type_raw with
          | None ->
              Http.Response.json ~status:`Bad_request
                {|{"error":"target_type must be post or comment"}|} reqd
          | Some target_type ->
              (match
                 Board_dispatch.toggle_reaction ~target_type ~target_id
                   ~user_id ~emoji
               with
               | Ok result ->
                   Http.Response.json
                     (Yojson.Safe.to_string
                        (Board.reaction_toggle_result_to_yojson result))
                     reqd
               | Error e ->
                   Http.Response.json ~status:`Bad_request
                     (Yojson.Safe.to_string
                        (`Assoc
                           [
                             ("error", `String (Tool_board.board_error_to_string e));
                           ]))
                     reqd)
        with
        | Yojson.Json_error msg ->
            Http.Response.json ~status:`Bad_request
              (Yojson.Safe.to_string
                 (`Assoc [ ("error", `String ("invalid JSON: " ^ msg)) ]))
              reqd)
  | `GET, "/api/v1/board/reactions" ->
      let target_type_raw =
        Option.value ~default:"" (query_param request "target_type")
      in
      let target_id =
        Option.value ~default:"" (query_param request "target_id")
      in
      let user_id = query_param request "user_id" in
      (match Board.reaction_target_type_of_string_opt target_type_raw with
       | None ->
           Http.Response.json ~status:`Bad_request
             {|{"error":"target_type must be post or comment"}|} reqd
       | Some target_type ->
           (match
              Board_dispatch.list_reactions ~target_type ~target_id ?user_id ()
            with
            | Ok summary ->
                let json =
                  `Assoc
                    [
                      ( "reactions",
                        `List (List.map Board.reaction_summary_to_yojson summary) );
                    ]
                in
                Http.Response.json (Yojson.Safe.to_string json) reqd
            | Error e ->
                Http.Response.json ~status:`Bad_request
                  (Yojson.Safe.to_string
                     (`Assoc
                        [
                          ("error", `String (Tool_board.board_error_to_string e));
                        ]))
                  reqd))
  | `GET, p when String.length p > 14 && String.sub p 0 14 = "/api/v1/board/" ->
      let post_id = String.sub p 14 (String.length p - 14) in
      let format = Option.value ~default:"nested" (query_param request "format") in
      let voter = board_voter_query request in
      let config =
        Option.map (fun state -> state.Mcp_server.room_config) !server_state
      in
      let (status, body) =
        board_post_detail_json ~include_moderation:false ~blind_votes:false
          ~config ~voter ~response_format:format ~post_id
      in
      Http.Response.json ~status body reqd
  | _ -> Http.Router.dispatch router request reqd

let log_late_response_failure ~context msg =
  Log.Http.warn "%s: response already unwritable; skipped late response (%s)"
    context msg

let try_internal_error_response reqd msg =
  try Http.Response.internal_error msg reqd with
  | Eio.Cancel.Cancelled _ as exn -> raise exn
  | exn -> (
      match Http.Late_response.classify_write_failure exn with
      | Some failure_msg ->
          log_late_response_failure ~context:"main_eio internal_error"
            failure_msg
      | None ->
          Log.Http.warn "main_eio internal_error response failed: %s"
            (Printexc.to_string exn))

(** Extended router to handle OPTIONS *)
let make_extended_handler routes =
  fun client_addr gluten_reqd ->
    let reqd = gluten_reqd.Gluten.Reqd.reqd in
    let request = Httpun.Reqd.request reqd in
    (* Rate limiting: enforce before any auth or routing. *)
    let path = Http.Request.path request in
    if try_rate_limit_block ~path ~client_addr ~request reqd then ()
    else
    try
      let is_mcp_like = is_mcp_like_path path in
      let session_id_for_version = get_session_id_any request in
      let protocol_version =
        get_protocol_version_for_session ?session_id:session_id_for_version request
      in
      let origin = get_origin request in
      if try_mcp_validation_block ~is_mcp_like ~request ~protocol_version ~origin reqd then ()
      else dispatch_route ~router:routes ~request ~path reqd
    with
    (* Re-raise cancellation so Eio structured concurrency propagates cleanly.
       Previously the catch-all swallowed Cancelled and tried to write a 500
       response; that masks shutdown signals and interferes with per-connection
       switch cleanup. *)
    | Eio.Cancel.Cancelled _ as exn -> raise exn
    | exn -> (
      let msg = Printexc.to_string exn in
      match Http.Late_response.classify_write_failure exn with
      | Some failure_msg ->
          log_late_response_failure ~context:"main_eio request handler"
            failure_msg
      | None -> try_internal_error_response reqd msg)

(** Main server loop *)
let run_server ~sw:_ ~env ~host ~port ~base_path =
  (* Use a dedicated sub-switch so that ALL fibers spawned by
     Server_runtime_bootstrap (background maintenance, keeper loops,
     dashboard refresh, etc.) are children of this switch.  When
     Eio.Fiber.first cancels the run_server fiber on SIGTERM, the
     sub-switch is cancelled too, which propagates Cancel to every
     child fiber — preventing the 10s force-exit timeout. *)
  Eio.Switch.run @@ fun server_sw ->
  try
    Server_runtime_bootstrap.run ~sw:server_sw ~env ~host ~port ~base_path ~make_routes
      ~make_request_handler:make_extended_handler
      ~make_h2_request_handler:Server_h2_gateway.make_request_handler
      ~make_h2_error_handler:Server_h2_gateway.make_error_handler
  with
  | Eio.Cancel.Cancelled _ as exn -> raise exn
  | exn ->
    Log.Server.error "[main] keeper bootstrap failed (continuing without keepers): %s" (Printexc.to_string exn)

(** CLI options *)
let port =
  let doc = "Port to listen on" in
  Arg.(value & opt int (Env_config_core.masc_http_port_int ()) & info ["p"; "port"] ~docv:"PORT" ~doc)

let host =
  let default = Env_config.masc_host () in
  let doc =
    "Host/IP to bind. Defaults to loopback (`127.0.0.1`). Use `0.0.0.0` or `::` only when you also enable room auth with `require_token=true`."
  in
  Arg.(value & opt string default & info ["host"] ~docv:"HOST" ~doc)

let base_path =
  let doc = "Base path for MASC data (.masc folder location)" in
  Arg.(value & opt string (default_base_path ()) & info ["base-path"] ~docv:"PATH" ~doc)

let doctor_json =
  let doc = "Emit machine-readable JSON instead of text output" in
  Arg.(value & flag & info ["json"] ~doc)

let parse_login_role value =
  match Masc_domain.agent_role_of_string (String.lowercase_ascii value) with
  | Ok role -> Ok role
  | Error msg -> Error (`Msg msg)

let login_role =
  let doc = "Role for the minted bearer token: admin or worker" in
  let role_printer fmt role =
    Format.pp_print_string fmt (Masc_domain.agent_role_to_string role)
  in
  let role_conv = Arg.conv (parse_login_role, role_printer) in
  Arg.(value & opt role_conv Masc_domain.Admin & info ["role"] ~docv:"ROLE" ~doc)

let login_agent =
  let doc = "Agent identity bound to the minted bearer token" in
  Arg.(
    value
    & opt string "local-admin"
    & info ["agent"] ~docv:"AGENT" ~doc)

let login_shell =
  let doc = "Emit shell export commands only" in
  Arg.(value & flag & info ["shell"] ~doc)

let login_client_env =
  let doc =
    "Env var name your MCP client reads to pick up the minted bearer \
     token. Required; the server holds no list of \"known\" MCP \
     clients. Example: MASC_MCP_TOKEN or any \
     operator-chosen name. The value is \
     rendered verbatim into the shell exports and JSON output."
  in
  Arg.(
    required
    & opt (some string) None
    & info ["client-env"] ~docv:"VAR" ~doc)

let login_no_expiry =
  let doc =
    "Mint a long-lived token without an [expires_at] field. \
     Appropriate for long-running local MCP daemons that cannot \
     easily refresh on expiry. Omit for the default expiring policy."
  in
  Arg.(value & flag & info ["no-expiry"] ~doc)

(** Graceful shutdown exception *)
(* Shutdown exception removed: graceful shutdown returns normally from
   await_shutdown_signal, letting Eio.Fiber.first cancel run_server. *)

let acquire_pid_lock port =
  match Server_startup_takeover.acquire_pid_lock port with
  | Server_startup_takeover.Acquired -> ()
  | Server_startup_takeover.Already_running { pid } ->
      Log.legacy_stderr ~level:Log.Error ~module_name:"Server"
        (Printf.sprintf
           "[FATAL] Another MASC server (PID %d) is already running on port %d. Kill it first: kill %d"
           pid port pid);
      exit 1

let acquire_base_path_lock base_path =
  match Server_startup_takeover.acquire_base_path_lock base_path with
  | Server_startup_takeover.Acquired -> ()
  | Server_startup_takeover.Already_running { pid } ->
      Log.legacy_stderr ~level:Log.Error ~module_name:"Server"
        (Printf.sprintf
           "[FATAL] Another MASC server (PID %d) already owns base path %s. Kill it first: kill %d"
           pid base_path pid);
      exit 1

(** Reject base_path that points to the server's own source repo.
    Detects by checking if the running executable lives under base_path/_build/.
    Runtime state (.masc/keepers, traces, logs) must not pollute the repo. *)
let guard_self_repo_base_path base_path =
  let base_path = Env_config.normalize_masc_base_path_input base_path in
  let abs_base =
    try Unix.realpath base_path with Unix.Unix_error _ -> base_path
  in
  let abs_exe =
    try Unix.realpath Sys.executable_name with Unix.Unix_error _ -> ""
  in
  let build_prefix = abs_base ^ "/_build/" in
  let is_self_repo =
    abs_exe <> ""
    && String.length abs_exe > String.length build_prefix
    && String.sub abs_exe 0 (String.length build_prefix) = build_prefix
  in
  if is_self_repo then begin
    Printf.eprintf
       "[FATAL] --base-path points to the server's own source repo: %s\n\
       (executable: %s)\n\
       Runtime state would pollute the repo. Use a workspace root instead:\n\
       \  --base-path $MASC_BASE_PATH    (recommended)\n\
       \  --base-path /path/to/workspace (explicit workspace root)\n\
       Or start via: sb mcp masc start\n"
      base_path abs_exe;
    exit 1
  end

let run_cmd host port base_path =
  Printexc.record_backtrace true;
  let raw_base_path = String.trim base_path in
  let normalized_base_path =
    Env_config.normalize_masc_base_path_input base_path
  in
  let resolution_source =
    match Sys.getenv_opt "MASC_BASE_PATH_RESOLUTION_SOURCE" with
    | Some source when String.trim source <> "" -> String.trim source
    | _ ->
        let inherited_env_matches =
          match Sys.getenv_opt "MASC_BASE_PATH" with
          | Some existing ->
              String.equal
                (Env_config.normalize_masc_base_path_input existing)
                normalized_base_path
          | None -> false
        in
        if inherited_env_matches then
          "explicit_env"
        else
          let default_path =
            Env_config.normalize_masc_base_path_input (default_base_path ())
          in
          if String.equal default_path normalized_base_path then
            implicit_base_path_resolution_source ()
          else
            "explicit_cli"
  in
  let stripped_base_path =
    Env_config.strip_path_trailing_slashes (String.trim base_path)
  in
  guard_self_repo_base_path normalized_base_path;
  if String.equal resolution_source "implicit_base_path" then begin
    Printf.eprintf
      "[FATAL] Server refused to start with an implicit base path.\n\
       Resolution source: %s\n\
       Resolved path: %s\n\n\
       Start the server with an explicit base path:\n\
       \  --base-path /path/to/workspace     (CLI flag)\n\
       \  MASC_BASE_PATH=/path/to/workspace  (environment variable)\n\n\
       Use a workspace root, not the repository checkout or $HOME directly.\n"
      resolution_source normalized_base_path;
    exit 1
  end;
  let masc_dir = Filename.concat normalized_base_path Common.masc_dirname in
  Fs_compat.mkdir_p masc_dir;
  acquire_pid_lock port;
  acquire_base_path_lock normalized_base_path;
  Log.init_from_env ();
  if stripped_base_path <> ""
     && String.equal (Filename.basename stripped_base_path) Common.masc_dirname
  then
    Log.Server.warn
      "Normalizing --base-path from %s to %s because runtime base paths must point at the workspace root, not the .masc directory."
      base_path normalized_base_path;
  Unix.putenv "MASC_BASE_PATH_INPUT" raw_base_path;
  Unix.putenv "MASC_BASE_PATH" normalized_base_path;
  Coord_utils_backend_setup.cache_resolved_base_path normalized_base_path;
  Unix.putenv "MASC_BASE_PATH_RESOLUTION_SOURCE" resolution_source;
  (* Persist logs inside .masc/logs/ — colocated with state, not a sibling.
     Previous code wrote to base_path/logs/ which diverged from .masc/ when
     base_path differed from the repo checkout directory. *)
  let log_dir = Filename.concat masc_dir "logs" in
  Fs_compat.mkdir_p log_dir;
  (* Migration: move .jsonl files from old base_path/logs/ if they exist *)
  let old_log_dir = Filename.concat normalized_base_path "logs" in
  (if Sys.file_exists old_log_dir && Sys.is_directory old_log_dir then
     let files = try Sys.readdir old_log_dir with Sys_error _ -> [||] in
     Array.iter (fun fname ->
       if Filename.check_suffix fname ".jsonl" then begin
         let src = Filename.concat old_log_dir fname in
         let dst = Filename.concat log_dir fname in
         if not (Sys.file_exists dst) then
           (try Sys.rename src dst;
                Log.info "log migration: moved %s -> .masc/logs/" fname
            with Sys_error _ -> ())
          end) files);
  Log.Ring.init_file_sink log_dir;
  Log.Ring.cleanup_old_files log_dir;
  Eio_main.run @@ fun env ->
  (* Initialize Mirage_crypto RNG - MUST be inside Eio_main.run for thread-local state *)
  Mirage_crypto_rng_unix.use_default ();

  (* Enable Eio-aware locking globally (single call replaces per-module enable_eio) *)
  Eio_guard.enable ();

  (* Set global clock for Time_compat (Eio-native timestamps).
     Dashboard_cache.now() reads from Time_compat directly. *)
  Time_compat.set_clock (Eio.Stdenv.clock env);

  (* Wire Runtime_events listener. After masc-mcp#18567 removed dead
     [Http_server_eio.start] (the only prior production caller), this
     would have been silently uninitialized. Idempotent-safe per
     [Masc_runtime_events] mli; consumed by Olly / custom callbacks
     to bracket agent turn spans ([emit_turn_start]/[emit_turn_end]). *)
  Masc_runtime_events.start_listener ();

  (* Signal handlers stay side-effect free. The Eio watcher fiber performs
     all shutdown work inside the event loop. *)
  let pending_shutdown_signal = Atomic.make None in
  let request_shutdown signal_name =
    if Option.is_none (Atomic.get pending_shutdown_signal) then
      Atomic.set pending_shutdown_signal (Some signal_name)
  in
  Sys.set_signal Sys.sigterm (Sys.Signal_handle (fun _ -> request_shutdown "SIGTERM"));
  Sys.set_signal Sys.sigint (Sys.Signal_handle (fun _ -> request_shutdown "SIGINT"));

  let max_bind_retries = 5 in
  let rec try_start attempt =
    (try
      Eio.Switch.run @@ fun sw ->
      let clock = Eio.Stdenv.clock env in
      let rec await_shutdown_signal () =
        match Atomic.exchange pending_shutdown_signal None with
        | None ->
            Eio.Time.sleep clock 0.05;
            await_shutdown_signal ()
        | Some signal_name ->
            let shutdown_cfg = Masc_mcp.Shutdown.config_from_env () in
            let force_timeout = shutdown_cfg.force_timeout_s in
            let t_shutdown_start = Unix.gettimeofday () in
            Log.Server.info
              "[MASC] Received %s, shutting down gracefully (timeout=%.0fs)..."
              signal_name force_timeout;
            Eio.Fiber.fork_daemon ~sw (fun () ->
                Eio.Time.sleep clock force_timeout;
                let elapsed = Unix.gettimeofday () -. t_shutdown_start in
                Log.Server.error
                  "[MASC] Graceful shutdown timed out after %.1fs (limit=%.0fs), forcing exit."
                  elapsed force_timeout;
                exit 1);
            (* Phase 1: Notify SSE clients *)
            let t_phase = Unix.gettimeofday () in
            let shutdown_data =
              Printf.sprintf
                {|{"jsonrpc":"2.0","method":"notifications/shutdown","params":{"reason":"%s","message":"Server is shutting down, please reconnect"}}|}
                signal_name
            in
            Sse.broadcast (Yojson.Safe.from_string shutdown_data);
            Log.Server.info
              "[Shutdown] Phase 1/4 NOTIFY: sent to %d SSE clients (%.2fs) [active conn: %d, ws: %d]"
              (Sse.client_count ())
              (Unix.gettimeofday () -. t_phase)
              (Masc_mcp.Server_mcp_transport_http_sse.active_session_count ())
              (Masc_mcp.Server_mcp_transport_ws.session_count ());

            Eio.Time.sleep clock shutdown_cfg.notify_delay_s;
            (* Phase 2: Run shutdown hooks with cleanup timeout *)
            let t_phase = Unix.gettimeofday () in
            Log.Server.info "[Shutdown] Phase 2/4 HOOKS: starting (timeout=%.1fs)"
              shutdown_cfg.cleanup_timeout_s;
            (try
              Eio.Time.with_timeout_exn clock shutdown_cfg.cleanup_timeout_s
                (fun () -> Masc_mcp.Shutdown_hooks.run_all ())
            with
            | Eio.Time.Timeout ->
                Log.Server.warn
                  "[Shutdown] Phase 2/4 HOOKS: timeout after %.1fs, proceeding (total=%.1fs)"
                  shutdown_cfg.cleanup_timeout_s
                  (Unix.gettimeofday () -. t_shutdown_start)
            | Eio.Cancel.Cancelled _ as e -> raise e
            | exn ->
                Log.Server.warn
                  "[Shutdown] Phase 2/4 HOOKS: failed after %.2fs: %s"
                  (Unix.gettimeofday () -. t_phase)
                  (Printexc.to_string exn));
            let now = Unix.gettimeofday () in
            Log.Server.info "[Shutdown] Phase 2/4 HOOKS: done (%.2fs, total=%.1fs) [active conn: %d, ws: %d]"
              (now -. t_phase)
              (now -. t_shutdown_start)
              (Masc_mcp.Server_mcp_transport_http_sse.active_session_count ())
              (Masc_mcp.Server_mcp_transport_ws.session_count ());
            (* Phase 3: Board flush with 2s timeout *)
            let t_phase = Unix.gettimeofday () in
            Log.Server.info "[Shutdown] Phase 3/4 BOARD: flush starting (timeout=2.0s)";
            (try
              Eio.Time.with_timeout_exn clock 2.0
                (fun () -> Board_dispatch.flush ())
            with
            | Eio.Time.Timeout ->
                Log.Server.warn
                  "[Shutdown] Phase 3/4 BOARD: timeout after 2.0s (total=%.1fs)"
                  (Unix.gettimeofday () -. t_shutdown_start)
            | Eio.Cancel.Cancelled _ as e -> raise e
            | exn ->
                Log.Server.warn
                  "[Shutdown] Phase 3/4 BOARD: skipped after %.2fs: %s"
                  (Unix.gettimeofday () -. t_phase)
                  (Printexc.to_string exn));
            let now = Unix.gettimeofday () in
            Log.Server.info "[Shutdown] Phase 3/4 BOARD: done (%.2fs, total=%.1fs) [active conn: %d, ws: %d]"
              (now -. t_phase)
              (now -. t_shutdown_start)
              (Masc_mcp.Server_mcp_transport_http_sse.active_session_count ())
              (Masc_mcp.Server_mcp_transport_ws.session_count ());

            (* Phase 4: Return normally — Eio.Fiber.first will cancel
               run_server cleanly via Eio.Cancel.Cancelled. *)
            Log.Server.info
              "[Shutdown] Phase 4/4 CANCEL: server cancel (total=%.1fs) [active conn: %d, ws: %d]"
              (Unix.gettimeofday () -. t_shutdown_start)
              (Masc_mcp.Server_mcp_transport_http_sse.active_session_count ())
              (Masc_mcp.Server_mcp_transport_ws.session_count ());
            ()
            in
            Eio.Fiber.first
            (fun () -> run_server ~sw ~env ~host ~port ~base_path)
            await_shutdown_signal;
            (* Server stopped; close SSE connections after server is down. *)
            (try close_all_sse_connections ()
            with
            | Eio.Cancel.Cancelled _ as e -> raise e
            | exn ->
                Log.Server.warn "shutdown: SSE close error: %s"
                  (Printexc.to_string exn));
            Log.Server.info "MASC MCP: Server stopped, waiting for background fibers... [active conn: %d, ws: %d]"
            (Masc_mcp.Server_mcp_transport_http_sse.active_session_count ())
            (Masc_mcp.Server_mcp_transport_ws.session_count ())

    with
    | Eio.Cancel.Cancelled _ ->
        Log.Server.info "MASC MCP: Server cancelled, waiting for background fibers..."
    | Unix.Unix_error (Unix.EADDRINUSE, _, _) when attempt < max_bind_retries ->
        let delay = Float.min 30.0 (2.0 ** Float.of_int attempt) in
        Log.Server.warn "Port %d in use, retrying in %.0fs (attempt %d/%d)"
          port delay (attempt + 1) max_bind_retries;
        Time_compat.sleep delay;
        try_start (attempt + 1)
    | Unix.Unix_error (Unix.EADDRINUSE, _, _) ->
        Log.Server.error "[FATAL] Port %d is still in use after %d retries. Try: lsof -i :%d | grep LISTEN"
          port max_bind_retries port;
        exit 1
    | Unix.Unix_error (Unix.EACCES, _, _) ->
        Log.Server.error "[FATAL] Permission denied binding to port %d" port;
        exit 1
    | Out_of_memory ->
        Printf.eprintf "[FATAL] Out_of_memory\n%!";
        exit 1
    | Stack_overflow ->
        Printf.eprintf "[FATAL] Stack_overflow\n%!";
        exit 1
    | exn ->
        let bt = Printexc.get_backtrace () in
        Log.Server.error "[FATAL] Unhandled exception: %s" (Printexc.to_string exn);
        if bt <> "" then Log.Server.error "[FATAL] Backtrace:\n%s" bt;
        exit 1)
  in
  try_start 0;
  Log.Server.info "MASC MCP: Shutdown complete."

let run_cmd_exit host port base_path =
  run_cmd host port base_path;
  Cmd.Exit.ok

let doctor_cmd_exit base_path as_json =
  let report =
    Eio_main.run @@ fun env ->
    Eio.Switch.run @@ fun sw ->
    Config_doctor.analyze_live
      ~sw
      ~net:(Eio.Stdenv.net env)
      ~clock:(Eio.Stdenv.clock env)
      ~fs:(Eio.Stdenv.fs env)
      ~proc_mgr:(Eio.Stdenv.process_mgr env)
      ~base_path_input:base_path
      ~default_base_path:(default_base_path ())
      ()
  in
  let output =
    if as_json then
      Config_doctor.to_yojson report |> Yojson.Safe.pretty_to_string
    else
      Config_doctor.render_text report
  in
  print_endline output;
  Config_doctor.exit_code report

let doctor_auth_cmd_exit base_path as_json =
  let report =
    Auth_doctor.analyze
      ~base_path_input:base_path
      ~default_base_path:(default_base_path ())
      ()
  in
  let output =
    if as_json then
      Auth_doctor.to_yojson report |> Yojson.Safe.pretty_to_string
    else
      Auth_doctor.render_text report
  in
  print_endline output;
  Auth_doctor.exit_code report

let keeper_doctor_json_member key = function
  | `Assoc fields -> Option.value ~default:`Null (List.assoc_opt key fields)
  | _ -> `Null

let keeper_doctor_json_string key json =
  match keeper_doctor_json_member key json with
  | `String value -> Some value
  | _ -> None

let keeper_doctor_json_int key json =
  match keeper_doctor_json_member key json with
  | `Int value -> Some value
  | `Intlit raw -> int_of_string_opt raw
  | _ -> None

let keeper_doctor_json_bool key json =
  match keeper_doctor_json_member key json with
  | `Bool value -> Some value
  | _ -> None

let keeper_doctor_json_string_list key json =
  match keeper_doctor_json_member key json with
  | `List values ->
    values
    |> List.filter_map (function
      | `String value when String.trim value <> "" -> Some value
      | _ -> None)
    |> List.sort_uniq String.compare
  | _ -> []

let keeper_doctor_string_has_prefix ~prefix value =
  let prefix_len = String.length prefix in
  String.length value >= prefix_len
  && String.equal (String.sub value 0 prefix_len) prefix

let keeper_doctor_string_contains ~needle value =
  let value = String.lowercase_ascii value in
  let needle = String.lowercase_ascii needle in
  let value_len = String.length value in
  let needle_len = String.length needle in
  let rec loop idx =
    idx + needle_len <= value_len
    && (String.equal (String.sub value idx needle_len) needle || loop (idx + 1))
  in
  needle_len = 0 || loop 0

let keeper_doctor_json_string_opt = function
  | Some value -> `String value
  | None -> `Null

let keeper_doctor_string_list_json values =
  `List (List.map (fun value -> `String value) values)

let keeper_doctor_json_bool_opt = function
  | Some value -> `Bool value
  | None -> `Null

let keeper_doctor_json_int_opt = function
  | Some value -> `Int value
  | None -> `Null

let keeper_doctor_tool_output_text json =
  match keeper_doctor_json_member "output" json with
  | `String value -> Some value
  | `Assoc _ as output -> (
    match keeper_doctor_json_member "_blob" output with
    | `Assoc _ as blob -> keeper_doctor_json_string "preview" blob
    | _ -> None)
  | _ -> None

let keeper_doctor_parse_tool_output json =
  match keeper_doctor_tool_output_text json with
  | None -> None
  | Some output -> (
    match Safe_ops.parse_json_safe ~context:"doctor_keeper.tool_output" output with
    | Ok parsed -> Some parsed
    | Error _ -> None)

let keeper_doctor_claim_status output =
  let result =
    Option.value ~default:"" (keeper_doctor_json_string "result" output)
    |> String.trim
  in
  match keeper_doctor_json_member "claimed_task" output with
  | `Assoc _ -> "claimed"
  | _ when keeper_doctor_string_has_prefix ~prefix:"No eligible tasks" result ->
    "no_eligible"
  | _ when keeper_doctor_string_has_prefix ~prefix:"No unclaimed tasks" result ->
    "no_unclaimed"
  | _ when keeper_doctor_string_has_prefix ~prefix:"Error:" result -> "error"
  | _ when result = "" -> "unknown"
  | _ -> "observed"

let keeper_doctor_claim_scope_json ~keeper_name =
  Keeper_tool_call_log.read_recent ~keeper_name ~n:100 ()
  |> List.find_opt (fun json ->
       String.equal
         (Option.value ~default:"" (keeper_doctor_json_string "tool" json))
         "keeper_task_claim")
  |> function
  | None ->
    `Assoc
      [ "present", `Bool false
      ; "status", `String "not_observed"
      ; "mode", `Null
      ; "excluded_count", `Null
      ; "active_goal_ids", `List []
      ; "effective_goal_ids", `List []
      ; "claimed_task_id", `Null
      ; "result", `Null
      ]
  | Some call ->
    let output =
      match keeper_doctor_parse_tool_output call with
      | Some (`Assoc _ as output) -> output
      | _ -> `Assoc []
    in
    let scope =
      match keeper_doctor_json_member "claim_scope" output with
      | `Assoc _ as scope -> scope
      | _ -> `Assoc []
    in
    let claimed_task =
      match keeper_doctor_json_member "claimed_task" output with
      | `Assoc _ as task -> Some task
      | _ -> None
    in
    `Assoc
      [ "present", `Bool true
      ; "status", `String (keeper_doctor_claim_status output)
      ; "mode", keeper_doctor_json_string_opt (keeper_doctor_json_string "mode" scope)
      ; "scoped", keeper_doctor_json_bool_opt (keeper_doctor_json_bool "scoped" scope)
      ; ( "excluded_count",
          keeper_doctor_json_int_opt
            (keeper_doctor_json_int "excluded_count" scope) )
      ; ( "active_goal_ids",
          keeper_doctor_string_list_json
            (keeper_doctor_json_string_list "active_goal_ids" scope) )
      ; ( "effective_goal_ids",
          keeper_doctor_string_list_json
            (keeper_doctor_json_string_list "effective_goal_ids" scope) )
      ; ( "claimed_task_id",
          match claimed_task with
          | Some task -> keeper_doctor_json_string_opt (keeper_doctor_json_string "task_id" task)
          | None -> `Null )
      ; "result", keeper_doctor_json_string_opt (keeper_doctor_json_string "result" output)
      ]

let keeper_doctor_config_drift_json ~config ~keeper_name =
  match Keeper_types.read_meta config keeper_name with
  | Ok (Some meta) ->
    let sources = Keeper_status_bridge.source_provenance_json config meta in
    let override_fields = keeper_doctor_json_string_list "override_fields" sources in
    let cascade_detail =
      match keeper_doctor_json_member "override_field_sources" sources with
      | `List values ->
        List.find_opt
          (fun value ->
            keeper_doctor_json_string "field" value = Some "model.cascade_name")
          values
      | _ -> None
    in
    let string_value key json =
      match keeper_doctor_json_member key json with
      | `String value -> Some value
      | _ -> None
    in
    let default_cascade_name, live_cascade_name =
      match cascade_detail with
      | Some detail -> string_value "default_value" detail, string_value "live_value" detail
      | None -> None, None
    in
    let cascade_override = Option.is_some cascade_detail in
    `Assoc
      [ "status", `String (if cascade_override then "drift" else "ok")
      ; "cascade_override", `Bool cascade_override
      ; "override_fields", keeper_doctor_string_list_json override_fields
      ; "default_cascade_name", keeper_doctor_json_string_opt default_cascade_name
      ; "live_cascade_name", keeper_doctor_json_string_opt live_cascade_name
      ; ( "active_config_root",
          keeper_doctor_json_string_opt
            (keeper_doctor_json_string "active_config_root" sources) )
      ]
  | Ok None ->
    `Assoc
      [ "status", `String "keeper_missing"
      ; "cascade_override", `Bool false
      ; "override_fields", `List []
      ; "default_cascade_name", `Null
      ; "live_cascade_name", `Null
      ; "active_config_root", `Null
      ]
  | Error message ->
    `Assoc
      [ "status", `String "error"
      ; "error", `String message
      ; "cascade_override", `Bool false
      ; "override_fields", `List []
      ; "default_cascade_name", `Null
      ; "live_cascade_name", `Null
      ; "active_config_root", `Null
      ]

let keeper_doctor_current_task_json config meta =
  match meta.Keeper_types.current_task_id with
  | None -> `Assoc [ "present", `Bool false; "task_id", `Null ]
  | Some task_id ->
    let task_id_s = Keeper_id.Task_id.to_string task_id in
    let task_result =
      try
        Ok
          (Coord.get_tasks_raw config
           |> List.find_opt (fun (task : Types.task) ->
                String.equal task.id task_id_s))
      with exn -> Error (Printexc.to_string exn)
    in
    (match task_result with
     | Error message ->
       `Assoc
         [ "present", `Bool true
         ; "task_id", `String task_id_s
         ; "status", `String "task_store_unavailable"
         ; "assignee", `Null
         ; "error", `String message
         ]
     | Ok task -> (
       match task with
       | None ->
         `Assoc
           [ "present", `Bool true
           ; "task_id", `String task_id_s
         ; "status", `String "missing_from_task_store"
         ; "assignee", `Null
         ]
     | Some task ->
       `Assoc
         [ "present", `Bool true
         ; "task_id", `String task.id
         ; "title", `String task.title
         ; "status", `String (Types.task_status_to_string task.task_status)
         ; "assignee", `String (Types.task_display_assignee task.task_status)
         ; "goal_id", keeper_doctor_json_string_opt task.goal_id
         ]))

let keeper_doctor_async_json ~keeper_name ~claim_scope =
  let latest =
    match Keeper_msg_async.list_for_keeper ~keeper_name with
    | entry :: _ -> Some entry
    | [] -> None
  in
  let latest_json =
    match latest with
    | Some entry -> Keeper_msg_async.entry_to_json entry
    | None -> `Null
  in
  let latest_status =
    match latest with
    | Some entry -> Keeper_msg_async.status_to_string entry.Keeper_msg_async.status
    | None -> "none"
  in
  let side_effect_visible =
    keeper_doctor_json_string "claimed_task_id" claim_scope <> None
  in
  `Assoc
    [ "latest_status", `String latest_status
    ; "latest", latest_json
    ; "side_effect_visible", `Bool side_effect_visible
    ; ( "lost_after_side_effect",
        `Bool (String.equal latest_status "lost" && side_effect_visible) )
    ]

let keeper_doctor_primary_reason ~claim_scope ~config_drift ~config_report ~async =
  let claim_status = keeper_doctor_json_string "status" claim_scope in
  let cascade_override =
    Option.value ~default:false
      (keeper_doctor_json_bool "cascade_override" config_drift)
  in
  let route_gap =
    List.exists
      (fun warning ->
        keeper_doctor_string_contains ~needle:"no_tool_capable_provider" warning
        || keeper_doctor_string_contains ~needle:"forced required-tool" warning)
      config_report.Config_doctor.warnings
  in
  let lost_after_side_effect =
    Option.value ~default:false
      (keeper_doctor_json_bool "lost_after_side_effect" async)
  in
  match claim_status with
  | Some "no_eligible" -> "claim_scope_no_eligible"
  | _ when cascade_override -> "keeper_cascade_override_drift"
  | _ when route_gap -> "route_tool_capability_gap"
  | _ when lost_after_side_effect -> "request_lost_after_side_effect"
  | _ when Config_doctor.has_blocking_warning config_report -> "config_doctor_not_ok"
  | _ -> "ok"

let keeper_doctor_next_actions reason =
  match reason with
  | "claim_scope_no_eligible" ->
    [ "Inspect active_goal_ids and link/create an eligible task before retrying." ]
  | "keeper_cascade_override_drift" ->
    [ "Inspect keeper TOML/live meta cascade_name and align it with the intended route." ]
  | "route_tool_capability_gap" ->
    [ "Run `masc-mcp doctor config --json` and route keeper_turn/tool_required to a tool-capable runtime lane." ]
  | "request_lost_after_side_effect" ->
    [ "Inspect current_task and recent tool-calls before retrying the same keeper message." ]
  | "config_doctor_not_ok" ->
    [ "Resolve config doctor warnings before restarting or retrying the keeper." ]
  | _ -> []

let doctor_keeper_report base_path keeper_name =
  let config_report =
    Eio_main.run @@ fun env ->
    Eio.Switch.run @@ fun sw ->
    Config_doctor.analyze_live
      ~sw
      ~net:(Eio.Stdenv.net env)
      ~clock:(Eio.Stdenv.clock env)
      ~fs:(Eio.Stdenv.fs env)
      ~proc_mgr:(Eio.Stdenv.process_mgr env)
      ~base_path_input:base_path
      ~default_base_path:(default_base_path ())
      ()
  in
  let config = Coord.default_config config_report.base_path in
  Keeper_tool_call_log.init ~base_path:config.base_path ();
  let meta_result = Keeper_types.read_meta config keeper_name in
  let claim_scope = keeper_doctor_claim_scope_json ~keeper_name in
  let config_drift = keeper_doctor_config_drift_json ~config ~keeper_name in
  let async = keeper_doctor_async_json ~keeper_name ~claim_scope in
  let current_task =
    match meta_result with
    | Ok (Some meta) -> keeper_doctor_current_task_json config meta
    | Ok None -> `Assoc [ "present", `Bool false; "error", `String "keeper_missing" ]
    | Error message -> `Assoc [ "present", `Bool false; "error", `String message ]
  in
  let reason =
    match meta_result with
    | Ok (Some _) ->
      keeper_doctor_primary_reason ~claim_scope ~config_drift ~config_report ~async
    | Ok None -> "keeper_missing"
    | Error _ -> "keeper_meta_error"
  in
  let next_actions = keeper_doctor_next_actions reason in
  let status =
    if String.equal reason "ok" then "ok"
    else if String.equal reason "claim_scope_no_eligible" then "warn"
    else "error"
  in
  let payload =
    `Assoc
      [ "status", `String status
      ; "keeper", `String keeper_name
      ; "server",
        `Assoc
          [ "base_path", `String config.base_path
          ; "config_status", `String (Config_doctor.status_to_string config_report.status)
          ; "active_config_root", `String config_report.active_config_root
          ]
      ; "scope", claim_scope
      ; "route",
        `Assoc
          [ "config_drift", config_drift
          ; "doctor_warnings",
            keeper_doctor_string_list_json config_report.warnings
          ]
      ; "tools",
        `Assoc
          [ "route_tool_capability_gap",
            `Bool
              (List.exists
                 (fun warning ->
                   keeper_doctor_string_contains
                     ~needle:"no_tool_capable_provider" warning)
                 config_report.warnings)
          ]
      ; "current_task", current_task
      ; "async_request", async
      ; "disposition", `Assoc [ "reason", `String reason ]
      ; "recommended_actions", keeper_doctor_string_list_json next_actions
      ]
  in
  payload, if String.equal status "ok" then 0 else 1

let render_doctor_keeper_text json =
  let section name =
    match keeper_doctor_json_member name json with
    | `Assoc _ as value -> Yojson.Safe.to_string value
    | `List _ as value -> Yojson.Safe.to_string value
    | `String value -> value
    | `Bool value -> string_of_bool value
    | `Int value -> string_of_int value
    | `Null -> "null"
    | other -> Yojson.Safe.to_string other
  in
  String.concat
    "\n"
    [ "MASC Keeper Doctor"
    ; "status: " ^ section "status"
    ; "keeper: " ^ section "keeper"
    ; "server: " ^ section "server"
    ; "scope: " ^ section "scope"
    ; "route: " ^ section "route"
    ; "tools: " ^ section "tools"
    ; "current_task: " ^ section "current_task"
    ; "async_request: " ^ section "async_request"
    ; "disposition: " ^ section "disposition"
    ; "recommended_actions: " ^ section "recommended_actions"
    ]

let doctor_keeper_cmd_exit base_path keeper_name as_json =
  let keeper_name = String.trim keeper_name in
  if keeper_name = "" then (
    Printf.eprintf "keeper name is required\n%!";
    1)
  else
    let payload, rc = doctor_keeper_report base_path keeper_name in
    let output =
      if as_json then Yojson.Safe.pretty_to_string payload
      else render_doctor_keeper_text payload
    in
    print_endline output;
    rc

let doctor_sidecar_exit name as_json =
  match Masc_mcp.Doctor_dispatch.sidecar_dir name with
  | None ->
    Printf.eprintf
      "unknown sidecar: %s (known: %s)\n"
      name
      Masc_mcp.Doctor_dispatch.known_summary;
    2
  | Some rel_dir ->
    let repo_root = Sys.getcwd () in
    let abs_dir =
      if Filename.is_relative rel_dir
      then Filename.concat repo_root rel_dir
      else rel_dir
    in
    if not (Sys.file_exists abs_dir)
    then begin
      Printf.eprintf
        "sidecar directory not found: %s\nhint: run from repository root\n"
        abs_dir;
      2
    end
    else begin
      let python =
        Option.value (Sys.getenv_opt "MASC_PYTHON") ~default:"python3"
      in
      let args =
        if as_json
        then [| python; "-m"; "src"; "doctor"; "--json" |]
        else [| python; "-m"; "src"; "doctor" |]
      in
      let prev = Sys.getcwd () in
      Sys.chdir abs_dir;
      let pid =
        try
          Some
            (Unix.create_process
               python
               args
               Unix.stdin
               Unix.stdout
               Unix.stderr)
        with Unix.Unix_error (err, _, _) ->
          Printf.eprintf
            "failed to exec %s: %s\nhint: set MASC_PYTHON to a valid interpreter\n"
            python
            (Unix.error_message err);
          None
      in
      Sys.chdir prev;
      match pid with
      | None -> 2
      | Some pid ->
        let _, status = Unix.waitpid [] pid in
        (match status with
         | Unix.WEXITED n -> n
         | Unix.WSIGNALED s -> 128 + s
         | Unix.WSTOPPED s -> 128 + s)
    end

let sidecar_name_arg =
  let doc =
    Printf.sprintf
      "Sidecar name (%s)"
      Masc_mcp.Doctor_dispatch.known_summary
  in
  Arg.(required & pos 0 (some string) None & info [] ~docv:"SIDECAR" ~doc)

let doctor_keeper_name =
  let doc = "Keeper name to diagnose" in
  Arg.(required & opt (some string) None & info ["name"] ~docv:"KEEPER" ~doc)

let doctor_config_cmd =
  let doc =
    "Diagnose config initialization, active config roots, and base-path shadowing"
  in
  let info = Cmd.info "config" ~doc in
  Cmd.v info Term.(const doctor_cmd_exit $ base_path $ doctor_json)

let doctor_auth_cmd =
  let doc =
    "Diagnose auth mode, bearer readiness, and role/permission mismatches"
  in
  let info = Cmd.info "auth" ~doc in
  Cmd.v info Term.(const doctor_auth_cmd_exit $ base_path $ doctor_json)

let doctor_keeper_cmd =
  let doc =
    "Diagnose one keeper's scope, cascade drift, tool lane, task, and async state"
  in
  let info = Cmd.info "keeper" ~doc in
  Cmd.v info Term.(const doctor_keeper_cmd_exit $ base_path $ doctor_keeper_name $ doctor_json)

let doctor_sidecar_cmd =
  let doc =
    "Run a sidecar's doctor and forward its output (spawns python -m src doctor)"
  in
  let info = Cmd.info "sidecar" ~doc in
  Cmd.v info Term.(const doctor_sidecar_exit $ sidecar_name_arg $ doctor_json)

let doctor_all_divider () =
  print_endline "========================================"

let doctor_all_section title =
  print_newline ();
  doctor_all_divider ();
  print_endline title;
  doctor_all_divider ()

let label_for_rc = function
  | 0 -> "정상"
  | 1 -> "경고"
  | 2 -> "오류"
  | _ -> "오류"

let doctor_all_json_exit base_path =
  let config_report =
    Eio_main.run @@ fun env ->
    Eio.Switch.run @@ fun sw ->
    Config_doctor.analyze_live
      ~sw
      ~net:(Eio.Stdenv.net env)
      ~clock:(Eio.Stdenv.clock env)
      ~fs:(Eio.Stdenv.fs env)
      ~proc_mgr:(Eio.Stdenv.process_mgr env)
      ~base_path_input:base_path
      ~default_base_path:(default_base_path ())
      ()
  in
  let config_payload = Config_doctor.to_yojson config_report in
  let config_rc = Config_doctor.exit_code config_report in
  let sidecar_entries =
    List.map
      (fun name ->
        match Masc_mcp.Doctor_dispatch.capture_sidecar_json name with
        | Ok (body, rc) ->
          let payload : Yojson.Safe.t =
            try Yojson.Safe.from_string body with
            | _ ->
              `Assoc
                [ "raw", `String body
                ; "parse_error", `String "invalid JSON from sidecar"
                ]
          in
          (name, rc, payload)
        | Error msg -> (name, 2, `Assoc [ "error", `String msg ]))
      Masc_mcp.Doctor_dispatch.known_sidecars
  in
  let all_entries =
    ("config", "config", config_rc, config_payload)
    :: List.map
         (fun (name, rc, payload) -> (name, "sidecar", rc, payload))
         sidecar_entries
  in
  let all_rcs = List.map (fun (_, _, rc, _) -> rc) all_entries in
  let total = List.length all_rcs in
  let count_eq v = List.length (List.filter (( = ) v) all_rcs) in
  let ok_count = count_eq 0 in
  let warn_count = count_eq 1 in
  let err_count = total - ok_count - warn_count in
  let doctors_json : Yojson.Safe.t =
    `List
      (List.map
         (fun (name, kind, rc, payload) ->
           `Assoc
             [ "name", `String name
             ; "kind", `String kind
             ; "exit_code", `Int rc
             ; "payload", payload
             ])
         all_entries)
  in
  let aggregate =
    Masc_mcp.Doctor_dispatch.aggregate_exit_code all_rcs
  in
  let result : Yojson.Safe.t =
    `Assoc
      [ "title", `String "MASC Doctor (전 계층)"
      ; "doctors", doctors_json
      ; ( "summary"
        , `Assoc
            [ "total", `Int total
            ; "ok", `Int ok_count
            ; "warn", `Int warn_count
            ; "error", `Int err_count
            ] )
      ; "exit_code", `Int aggregate
      ]
  in
  print_endline (Yojson.Safe.pretty_to_string result);
  aggregate

let doctor_all_exit base_path as_json =
  if as_json
  then doctor_all_json_exit base_path
  else begin
    doctor_all_section "Config Doctor";
    let config_rc = doctor_cmd_exit base_path false in
    let sidecar_rcs =
      List.map
        (fun name ->
          doctor_all_section
            (Printf.sprintf
               "%s Sidecar Doctor"
               (String.capitalize_ascii name));
          let rc = doctor_sidecar_exit name false in
          (name, rc))
        Masc_mcp.Doctor_dispatch.known_sidecars
    in
    let all_rcs = config_rc :: List.map snd sidecar_rcs in
    let total = List.length all_rcs in
    let count_eq v = List.length (List.filter (( = ) v) all_rcs) in
    let ok_count = count_eq 0 in
    let warn_count = count_eq 1 in
    let err_count = total - ok_count - warn_count in
    print_newline ();
    doctor_all_divider ();
    Printf.printf
      "합계: %d Doctor · 정상 %d · 경고 %d · 오류 %d\n"
      total
      ok_count
      warn_count
      err_count;
    let breakdown =
      ("config", config_rc) :: sidecar_rcs
      |> List.map (fun (name, rc) ->
             Printf.sprintf "%s=%s" name (label_for_rc rc))
      |> String.concat " · "
    in
    print_endline breakdown;
    doctor_all_divider ();
    Masc_mcp.Doctor_dispatch.aggregate_exit_code all_rcs
  end

let doctor_all_cmd =
  let doc =
    "Run config + every registered sidecar doctor and show an aggregate \
     summary"
  in
  let info = Cmd.info "all" ~doc in
  Cmd.v info Term.(const doctor_all_exit $ base_path $ doctor_json)

let doctor_cmd =
  let doc = "Doctor: diagnose MASC server and sidecars" in
  let info = Cmd.info "doctor" ~doc in
  Cmd.group
    ~default:Term.(const doctor_cmd_exit $ base_path $ doctor_json)
    info
    [ doctor_config_cmd
    ; doctor_auth_cmd
    ; doctor_keeper_cmd
    ; doctor_sidecar_cmd
    ; doctor_all_cmd
    ]

let login_cmd_exit base_path host port agent role client_env no_expiry
    as_json as_shell =
  let token_lifetime : Auth_login.token_lifetime =
    if no_expiry then Long_lived else With_expiry
  in
  match
    Auth_login.mint ~base_path ~host ~port ~agent_name:agent ~role
      ~token_env_var:client_env ~token_lifetime ()
  with
  | Error err ->
      Printf.eprintf "login failed: %s\n" (Masc_domain.masc_error_to_string err);
      1
  | Ok report ->
      let output =
        if as_shell then
          Auth_login.render_shell report
        else if as_json then
          Auth_login.to_yojson report |> Yojson.Safe.pretty_to_string
        else
          Auth_login.render_text report
      in
      print_endline output;
      0

let login_cmd =
  let doc =
    "Mint a local bearer token, persist its raw token file, and print \
     dashboard / MCP auth exports. Requires --client-env <VAR> to \
     name the env var your MCP client reads; the server itself is \
     client-agnostic."
  in
  let info = Cmd.info "login" ~doc in
  Cmd.v info
    Term.(
      const login_cmd_exit $ base_path $ host $ port $ login_agent
      $ login_role $ login_client_env $ login_no_expiry $ doctor_json
      $ login_shell)

let init_force =
  let doc = "Overwrite existing config files instead of skipping them" in
  Arg.(value & flag & info ["force"] ~doc)

type init_tally = { written : int; skipped : int; failed : int }

let seed_one ~target_root ~force tally rel =
  match Embedded_config.read rel with
  | None ->
    Printf.eprintf "init: missing embedded asset: %s\n" rel;
    { tally with failed = tally.failed + 1 }
  | Some content ->
    let dest = Filename.concat target_root rel in
    Fs_compat.mkdir_p (Filename.dirname dest);
    if Fs_compat.file_exists dest && not force then begin
      Printf.printf "skip   %s (exists, --force to overwrite)\n" dest;
      { tally with skipped = tally.skipped + 1 }
    end else
      try
        Fs_compat.save_file dest content;
        Printf.printf "wrote  %s (%d bytes)\n" dest (String.length content);
        { tally with written = tally.written + 1 }
      with Sys_error msg ->
        Printf.eprintf "init: %s: %s\n" dest msg;
        { tally with failed = tally.failed + 1 }

let init_cmd_exit base_path force =
  let base_path = Env_config.normalize_masc_base_path_input base_path in
  let target_root = Config_doctor.local_base_config_root ~base_path in
  Fs_compat.mkdir_p target_root;
  let result =
    List.fold_left
      (seed_one ~target_root ~force)
      { written = 0; skipped = 0; failed = 0 }
      Embedded_config.file_list
  in
  Printf.printf "init: %d written, %d skipped, %d failed (root=%s)\n"
    result.written result.skipped result.failed target_root;
  if result.failed > 0 then 1 else 0

let init_cmd =
  let doc = "Seed default .masc/config/ from binary-embedded assets" in
  let info = Cmd.info "init" ~doc in
  Cmd.v info Term.(const init_cmd_exit $ base_path $ init_force)

let cmd =
  let doc = "MASC MCP Server and operator diagnostics" in
  let info = Cmd.info "masc-mcp" ~version:Masc_mcp.Version.version ~doc in
  Cmd.group ~default:Term.(const run_cmd_exit $ host $ port $ base_path)
    info [ doctor_cmd; init_cmd; login_cmd ]

let () = exit (Cmd.eval' cmd)
