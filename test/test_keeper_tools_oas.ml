(** Tests for Keeper_tools_oas — OAS Tool.t wrapping of keeper tools. *)

module Mlog = Log
open Agent_sdk
open Alcotest
open Masc_mcp

let tool_ok ?(tool_name = "") message =
  Tool_result.make_ok ~tool_name ~start_time:0.0 ~data:(`String message) ()
;;

let ensure_wildcard_repo_mapping config keeper_name =
  let masc_root = Common.masc_dir_from_base_path ~base_path:config.Coord.base_path in
  let config_dir = Filename.concat masc_root "config" in
  Fs_compat.mkdir_p config_dir;
  let mapping_path = Filename.concat config_dir "keeper_repo_mappings.toml" in
  let content =
    Printf.sprintf
      "[mapping.%s]\nrepositories = [\"*\"]\n"
      keeper_name
  in
  Out_channel.with_open_text mapping_path (fun oc ->
    Out_channel.output_string oc content)

let make_test_meta
      ?(name = "test-keeper")
      ?(preset = Keeper_types.Full)
      ?(also_allow = [])
      ?(allowed_paths = [ "*" ])
      ?tool_access
      ()
  : Keeper_types.keeper_meta
  =
  let tool_access =
    match tool_access with
    | Some access -> access
    | None -> Keeper_types.Preset { preset; also_allow }
  in
  match
    Masc_test_deps.meta_of_json_fixture
      (`Assoc
          [ "name", `String name
          ; "agent_name", `String name
          ; "trace_id", `String "test-trace-001"
          ; "allowed_paths", `List (List.map (fun path -> `String path) allowed_paths)
          ; "tool_access", Keeper_types.tool_access_to_json tool_access
          ])
  with
  | Ok meta -> meta
  | Error e -> failwith (Printf.sprintf "make_test_meta failed: %s" e)
;;

let make_test_ctx () = Keeper_context_runtime.create ~system_prompt:"test" ~max_tokens:4000

let test_make_tools_returns_nonempty () =
  let meta = make_test_meta () in
  let ctx_snapshot = make_test_ctx () in
  let dir =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_keeper_tools_%d" (Random.int 100000))
  in
  (try Unix.mkdir dir 0o755 with
   | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
  Fun.protect
    ~finally:(fun () ->
      try
        Sys.readdir dir |> Array.iter (fun f -> Sys.remove (Filename.concat dir f));
        Unix.rmdir dir
      with
      | _ -> ())
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       let tools = Keeper_tools_oas_bundle.make_tools ~config ~meta ~ctx_snapshot () in
       check bool "tools nonempty" true (List.length tools > 0))
;;

let test_tools_have_valid_schemas () =
  let meta = make_test_meta () in
  let ctx_snapshot = make_test_ctx () in
  let dir =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_keeper_tools_schema_%d" (Random.int 100000))
  in
  (try Unix.mkdir dir 0o755 with
   | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
  Fun.protect
    ~finally:(fun () ->
      try
        Sys.readdir dir |> Array.iter (fun f -> Sys.remove (Filename.concat dir f));
        Unix.rmdir dir
      with
      | _ -> ())
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       let tools = Keeper_tools_oas_bundle.make_tools ~config ~meta ~ctx_snapshot () in
       List.iter
         (fun (tool : Agent_sdk.Tool.t) ->
            check
              bool
              (Printf.sprintf "tool %s has name" tool.schema.name)
              true
              (String.length tool.schema.name > 0);
            check
              bool
              (Printf.sprintf "tool %s has description" tool.schema.name)
              true
              (String.length tool.schema.description > 0))
         tools)
;;

let test_tool_count_matches_allowed () =
  let meta = make_test_meta () in
  let ctx_snapshot = make_test_ctx () in
  let dir =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_keeper_tools_count_%d" (Random.int 100000))
  in
  (try Unix.mkdir dir 0o755 with
   | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
  Fun.protect
    ~finally:(fun () ->
      try
        Sys.readdir dir |> Array.iter (fun f -> Sys.remove (Filename.concat dir f));
        Unix.rmdir dir
      with
      | _ -> ())
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       let tools = Keeper_tools_oas_bundle.make_tools ~config ~meta ~ctx_snapshot () in
       let allowed = Agent_tool_dispatch_runtime.keeper_allowed_tool_names meta in
       let tool_names = List.map (fun (t : Agent_sdk.Tool.t) -> t.schema.name) tools in
       (* RFC-0006 Phase A.2: aliased Tool.t entries (Execute/ReadFile) carry the
         public name on Tool.schema.name even though their handler dispatches
         the internal keeper_* name. Accept either: name in allowed OR the
         alias's internal target in allowed. *)
       check
         bool
         "all tools are in allowed list (or are an alias)"
         true
         (List.for_all
            (fun name ->
               List.mem name allowed
               ||
               match Agent_tool_descriptor.find_public name with
               | Some descriptor ->
                 Agent_tool_descriptor.internal_names descriptor
                 |> List.exists (fun internal_name -> List.mem internal_name allowed)
               | None -> false)
            tool_names))
;;

let find_tool name tools =
  List.find (fun (tool : Tool.t) -> String.equal tool.schema.name name) tools
;;

let find_read_tool tools = find_tool "ReadFile" tools

let read_repo_dir meta =
  Filename.concat "repos" meta.Keeper_types.name
;;

let read_path meta path = Filename.concat (read_repo_dir meta) path

let read_args meta path = `Assoc [ "file_path", `String (read_path meta path) ]

let ensure_read_repo_dir config meta =
  let root = Keeper_alerting_path.project_root_of_config config in
  Fs_compat.mkdir_p (Filename.concat root (read_repo_dir meta))
;;

let make_registered_tools ~config ~meta ~ctx_snapshot () =
  let keeper_name = meta.Keeper_types.name in
  ignore (Keeper_registry.register ~base_path:config.Coord.base_path keeper_name meta);
  ensure_wildcard_repo_mapping config keeper_name;
  Keeper_tools_oas_bundle.make_tools ~config ~meta ~ctx_snapshot ()
;;

let dummy_schedule : Agent_sdk.Hooks.tool_schedule =
  { planned_index = 0
  ; batch_index = 0
  ; batch_size = 1
  ; concurrency_class = "default"
  ; batch_kind = "sequential"
  }
;;

let string_contains ~sub text =
  let text_len = String.length text in
  let sub_len = String.length sub in
  let rec loop idx =
    if idx + sub_len > text_len
    then false
    else if String.sub text idx sub_len = sub
    then true
    else loop (idx + 1)
  in
  sub_len = 0 || loop 0
;;

let rec rm_rf path =
  match Unix.lstat path with
  | exception Unix.Unix_error (Unix.ENOENT, _, _) -> ()
  | stat ->
    (match stat.Unix.st_kind with
     | Unix.S_DIR ->
       Sys.readdir path |> Array.iter (fun name -> rm_rf (Filename.concat path name));
       Unix.rmdir path
     | _ -> Sys.remove path)
;;

let with_env name value f =
  let previous = Sys.getenv_opt name in
  Unix.putenv name value;
  Fun.protect
    ~finally:(fun () ->
      match previous with
      | Some prior -> Unix.putenv name prior
      | None -> Unix.putenv name "")
    f
;;

let test_public_alias_descriptions_are_frontdoor_safe () =
  let meta = make_test_meta () in
  let ctx_snapshot = make_test_ctx () in
  let dir = Filename.temp_file "test_keeper_tools_descriptions_" "" in
  Sys.remove dir;
  Unix.mkdir dir 0o755;
  Fun.protect
    ~finally:(fun () -> rm_rf dir)
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       let tools = Keeper_tools_oas_bundle.make_tools ~config ~meta ~ctx_snapshot () in
       let execute = find_tool "Execute" tools in
       let search_files = find_tool "SearchFiles" tools in
       check bool
         "Execute description names typed command front door"
         true
         (string_contains ~sub:"typed command" execute.schema.description);
       check bool
         "Execute description names deterministic gates"
         true
         (string_contains ~sub:"deterministic execution gates" execute.schema.description);
       check bool
         "Execute description hides internal tool_execute"
         false
         (string_contains ~sub:"tool_execute" execute.schema.description);
       check bool
         "Execute description drops Legendary wording"
         false
         (string_contains ~sub:"Legendary" execute.schema.description);
       check bool
         "SearchFiles description names ripgrep"
         true
         (string_contains ~sub:"ripgrep" search_files.schema.description);
       check bool
         "SearchFiles description hides internal tool_search_files"
         false
         (string_contains ~sub:"tool_search_files" search_files.schema.description))
;;

let test_tool_side_effect_failures_are_observed () =
  let meta =
    make_test_meta ~name:"test-keeper-side-effects" ~allowed_paths:[ "repos" ] ()
  in
  let ctx_snapshot = make_test_ctx () in
  let dir = Filename.temp_file "test_keeper_tools_side_effects_" "" in
  Sys.remove dir;
  Unix.mkdir dir 0o755;
  Fun.protect
    ~finally:(fun () -> rm_rf dir)
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       ensure_read_repo_dir config meta;
       let decision_path =
         Keeper_types_support.keeper_decision_log_path config meta.name
       in
       Fs_compat.mkdir_p (Filename.dirname decision_path);
       Unix.mkdir decision_path 0o755;
       let tools = make_registered_tools ~config ~meta ~ctx_snapshot () in
       let tool = find_read_tool tools in
       let keeper_labels = [ "keeper", meta.name ] in
       let sse_before =
         Prometheus.metric_value_or_zero
           Masc_mcp.Keeper_metrics.(to_string SseBroadcastFailures)
           ~labels:keeper_labels
           ()
       in
       let decision_before =
         Prometheus.metric_value_or_zero
           Masc_mcp.Keeper_metrics.(to_string DecisionAuditFlushFailures)
           ~labels:keeper_labels
           ()
       in
       let original_hook = Atomic.get Sse.buffer_commit_test_hook in
       Fun.protect
         ~finally:(fun () -> Atomic.set Sse.buffer_commit_test_hook original_hook)
         (fun () ->
            Atomic.set
              Sse.buffer_commit_test_hook
              (Some (fun () -> failwith "forced keeper tool SSE failure"));
            match
              Tool.execute
                tool
                (read_args meta "missing-side-effect-file.txt")
            with
            | Error _ -> ()
            | Ok _ -> fail "missing file should be surfaced as tool error");
       check
         (float 0.001)
         "SSE failure metric incremented"
         (sse_before +. 1.0)
         (Prometheus.metric_value_or_zero
            Masc_mcp.Keeper_metrics.(to_string SseBroadcastFailures)
            ~labels:keeper_labels
            ());
       check
         (float 0.001)
         "decision-log failure metric incremented"
         (decision_before +. 1.0)
         (Prometheus.metric_value_or_zero
            Masc_mcp.Keeper_metrics.(to_string DecisionAuditFlushFailures)
            ~labels:keeper_labels
            ()))
;;

let test_handler_does_not_write_tool_call_io_without_observer () =
  let meta = make_test_meta ~name:"test-keeper-direct-tool-io" () in
  let ctx_snapshot = make_test_ctx () in
  let dir = Filename.temp_file "test_keeper_tools_direct_io_" "" in
  Sys.remove dir;
  Unix.mkdir dir 0o755;
  Fun.protect
    ~finally:(fun () ->
      Keeper_tool_call_log.reset_for_testing ();
      rm_rf dir)
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       Keeper_tool_call_log.reset_for_testing ();
       Keeper_tool_call_log.init ~base_path:dir ();
       Keeper_tool_call_log.set_turn_context
         ~keeper_name:meta.name
         ~trace_id:"trace-direct-tool-io"
         ~turn:1
         ();
       let tools = Keeper_tools_oas_bundle.make_tools ~config ~meta ~ctx_snapshot () in
       let tool = find_tool "keeper_time_now" tools in
       (match Tool.execute tool (`Assoc []) with
        | Ok _ -> ()
        | Error { Agent_sdk.Types.message; _ } ->
          fail ("keeper_time_now should succeed: " ^ message));
       let entries = Keeper_tool_call_log.read_recent ~keeper_name:meta.name ~n:10 () in
       check int "handler does not write tool_call rows directly" 0 (List.length entries))
;;

let test_post_tool_hook_is_single_tool_call_log_writer () =
  let meta = make_test_meta ~name:"test-keeper-direct-tool-hook-writer" () in
  let meta_ref = ref meta in
  let ctx_snapshot = make_test_ctx () in
  let dir = Filename.temp_file "test_keeper_tools_direct_dedupe_" "" in
  Sys.remove dir;
  Unix.mkdir dir 0o755;
  Fun.protect
    ~finally:(fun () ->
      Keeper_tool_call_log.reset_for_testing ();
      rm_rf dir)
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       Keeper_tool_call_log.reset_for_testing ();
       Keeper_tool_call_log.init ~base_path:dir ();
       Keeper_tool_call_log.set_turn_context
         ~keeper_name:meta.name
         ~trace_id:"trace-hook-tool-io"
         ~turn:1
         ();
       let tools = Keeper_tools_oas_bundle.make_tools ~config ~meta ~ctx_snapshot () in
       let tool = find_tool "keeper_time_now" tools in
       let output_text =
         match Tool.execute tool (`Assoc []) with
         | Ok { Agent_sdk.Types.content; _ } -> content
         | Error { Agent_sdk.Types.message; _ } ->
           fail ("keeper_time_now should succeed: " ^ message)
       in
       let hooks = Keeper_hooks_oas.make_hooks ~config ~meta_ref ~generation:1 () in
       let post_tool_use =
         match hooks.Agent_sdk.Hooks.post_tool_use with
         | Some hook -> hook
         | None -> fail "post_tool_use hook missing"
       in
       ignore
         (post_tool_use
            (Agent_sdk.Hooks.PostToolUse
               { tool_use_id = "tu-direct-dedupe"
               ; tool_name = "keeper_time_now"
               ; input = `Assoc []
               ; output =
                   Ok
                     ({ Agent_sdk.Types.content = output_text }
                      : Agent_sdk.Types.tool_output)
               ; result_bytes = String.length output_text
               ; duration_ms = 2.0
               ; schedule = dummy_schedule
           }));
       let entries = Keeper_tool_call_log.read_recent ~keeper_name:meta.name ~n:10 () in
       check int "post_tool_use hook wrote one tool_call row" 1 (List.length entries);
       let row = List.hd entries in
       check
         string
         "tool"
         "keeper_time_now"
         Yojson.Safe.Util.(row |> member "tool" |> to_string);
       check
         string
         "trace id"
         "trace-hook-tool-io"
         Yojson.Safe.Util.(row |> member "trace_id" |> to_string))
;;

let test_oas_wrapper_records_keeper_internal_tool_call () =
  let meta = make_test_meta ~name:"test-keeper-tool-registry" () in
  let ctx_snapshot = make_test_ctx () in
  let dir = Filename.temp_file "test_keeper_tools_registry_" "" in
  Sys.remove dir;
  Unix.mkdir dir 0o755;
  Fun.protect
    ~finally:(fun () -> rm_rf dir)
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       Tool_registry.reset ();
       let config = Coord.default_config dir in
       let bundle = Keeper_tools_oas_bundle.make_tool_bundle ~config ~meta ~ctx_snapshot () in
       Fun.protect
         ~finally:(fun () ->
           bundle.cleanup ();
           Tool_registry.reset ())
         (fun () ->
            let tool = find_tool "keeper_stay_silent" bundle.tools in
            match Tool.execute tool (`Assoc []) with
            | Error { Agent_sdk.Types.message; _ } ->
              fail (Printf.sprintf "expected tool success, got error: %s" message)
            | Ok _ ->
              let stats = Tool_registry.get_stats () in
              let entry = List.assoc "keeper_stay_silent" stats in
              check int "call_count" 1 (Atomic.get entry.call_count);
              check int "success_count" 1 (Atomic.get entry.success_count);
              check int "keeper_internal_count" 1 (Atomic.get entry.keeper_internal_count)))
;;

let test_oas_tool_callbacks_respect_resource_gate () =
  let meta =
    make_test_meta ~name:"test-keeper-oas-tool-gate" ~allowed_paths:[ "*" ] ()
  in
  let ctx_snapshot = make_test_ctx () in
  let dir = Filename.temp_file "test_keeper_tools_gate_" "" in
  Sys.remove dir;
  Unix.mkdir dir 0o755;
  let run env =
    Fs_compat.set_fs (Eio.Stdenv.fs env);
    let clock = Eio.Stdenv.clock env in
    let config = Coord.default_config dir in
    ignore (Keeper_registry.register ~base_path:config.Coord.base_path meta.name meta);
    Tool_resource_gate.For_testing.set_limits ~shell:1 ();
    with_env "MASC_TOOL_GATE_WAIT_TIMEOUT_SEC" "0.05" (fun () ->
      let bundle =
        Keeper_tools_oas_bundle.make_tool_bundle ~config ~meta ~ctx_snapshot ~clock ()
      in
      Fun.protect
        ~finally:bundle.cleanup
        (fun () ->
           let execute = find_tool "Execute" bundle.tools in
           let blocker_started, unblock_blocker = Eio.Promise.create () in
           let release_blocker, resolve_release = Eio.Promise.create () in
           let run_blocker () =
             let result =
               Tool_resource_gate.with_permit
                 ~clock
                 ~tool_name:"tool_execute"
                 ~arguments:(`Assoc [ "cmd", `String "sleep 1" ])
                 ~is_read_only:false
                 ~start_time:(Eio.Time.now clock)
                 (fun () ->
                    Eio.Promise.resolve unblock_blocker ();
                    Eio.Promise.await release_blocker;
                    tool_ok ~tool_name:"tool_execute" "done")
             in
             check bool "blocking shell gate call completed" true (Tool_result.is_success result)
           in
           let run_rejected_callback () =
             Eio.Promise.await blocker_started;
             let result =
               Fun.protect
                 ~finally:(fun () -> Eio.Promise.resolve resolve_release ())
                 (fun () ->
                   Tool.execute
                      execute
                      (`Assoc
                         [ "executable", `String "echo"
                         ; "argv", `List [ `String "should-not-spawn" ]
                         ]))
             in
             match result with
             | Ok _ -> fail "expected saturated OAS callback to be rejected"
             | Error { Agent_sdk.Types.message; recoverable; error_class } ->
               check
                 bool
                 "resource gate error surfaced"
                 true
                 (string_contains ~sub:"tool_resource_gate_saturated" message);
               check bool "recoverable transient" true recoverable;
               check
                 bool
                 "typed transient"
                 true
                 (match error_class with
                  | Some Agent_sdk.Types.Transient -> true
                  | _ -> false)
           in
           Eio.Fiber.both run_blocker run_rejected_callback))
  in
  Fun.protect
    ~finally:(fun () ->
      Tool_resource_gate.For_testing.reset ();
      rm_rf dir)
    (fun () -> Eio_main.run run)
;;

let is_guardrail_message message =
  string_contains ~sub:"failed 3 times in a row with the same arguments" message
;;

let test_error_json_is_returned_as_tool_error () =
  let meta =
    make_test_meta ~name:"test-keeper-error-json" ~allowed_paths:[ "repos" ] ()
  in
  let ctx_snapshot = make_test_ctx () in
  let dir =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_keeper_tools_error_%d" (Random.int 100000))
  in
  (try Unix.mkdir dir 0o755 with
   | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
  Fun.protect
    ~finally:(fun () ->
      try
        Sys.readdir dir |> Array.iter (fun f -> Sys.remove (Filename.concat dir f));
        Unix.rmdir dir
      with
      | _ -> ())
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       ensure_read_repo_dir config meta;
       let tools = make_registered_tools ~config ~meta ~ctx_snapshot () in
       let tool = find_read_tool tools in
       match
         Tool.execute
           tool
           (read_args meta "missing-file-for-keeper-tools-oas.txt")
       with
       | Error { Agent_sdk.Types.message; _ } ->
         let json = Yojson.Safe.from_string message in
         (* After normalization, error results follow {"ok":false,"error":"...","detail":{...}} *)
         check bool "ok is false" false Yojson.Safe.Util.(member "ok" json |> to_bool);
         check
           bool
           "error field present"
           true
           (Option.is_some (Safe_ops.json_string_opt "error" json));
         let detail = Yojson.Safe.Util.member "detail" json in
         check
           bool
           "detail preserves path"
           true
           (Option.is_some (Safe_ops.json_string_opt "path" detail))
       | Ok _ -> fail "missing file should be surfaced as tool error")
;;

let test_oas_handler_rejects_missing_required_args () =
  let meta =
    make_test_meta ~name:"test-keeper-validation" ~allowed_paths:[ "repos" ] ()
  in
  let ctx_snapshot = make_test_ctx () in
  let dir =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_keeper_tools_validate_%d" (Random.int 100000))
  in
  (try Unix.mkdir dir 0o755 with
   | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
  Fun.protect
    ~finally:(fun () ->
      try
        Sys.readdir dir |> Array.iter (fun f -> Sys.remove (Filename.concat dir f));
        Unix.rmdir dir
      with
      | _ -> ())
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       ensure_read_repo_dir config meta;
       let tools = make_registered_tools ~config ~meta ~ctx_snapshot () in
       let tool = find_read_tool tools in
       match Tool.execute tool (`Assoc []) with
       | Error { Agent_sdk.Types.message; _ } ->
         let json = Yojson.Safe.from_string message in
         check bool "ok is false" false Yojson.Safe.Util.(member "ok" json |> to_bool);
         check
           bool
           "error mentions missing path"
           true
           (string_contains ~sub:"path" message);
         let detail = Yojson.Safe.Util.member "detail" json in
         check
           string
           "validation source"
           "oas_tool_middleware"
           Yojson.Safe.Util.(detail |> member "validation" |> to_string)
       | Ok _ -> fail "missing required path should be rejected by OAS validation")
;;

let latest_log_seq () =
  match Mlog.Ring.recent ~limit:1 () with
  | (entry : Mlog.Ring.entry) :: _ -> entry.seq
  | [] -> -1
;;

let test_error_result_logs_at_error_level () =
  let meta =
    make_test_meta ~name:"test-keeper-log-level" ~allowed_paths:[ "repos" ] ()
  in
  let ctx_snapshot = make_test_ctx () in
  let dir =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_keeper_tools_log_level_%d" (Random.int 100000))
  in
  (try Unix.mkdir dir 0o755 with
   | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
  Fun.protect
    ~finally:(fun () ->
      try
        Sys.readdir dir |> Array.iter (fun f -> Sys.remove (Filename.concat dir f));
        Unix.rmdir dir
      with
      | _ -> ())
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       ensure_read_repo_dir config meta;
       let tools = make_registered_tools ~config ~meta ~ctx_snapshot () in
       let tool = find_read_tool tools in
       Keeper_tools_oas.reset_tool_retry_dedupe_for_testing ();
       let baseline = latest_log_seq () in
       (match
         Tool.execute
           tool
            (read_args meta "log-level-only-missing-file-for-keeper-tools-oas.txt")
        with
        | Error _ -> ()
        | Ok _ -> fail "missing file should be surfaced as tool error");
       let entry =
         Mlog.Ring.recent ~limit:50 ~module_filter:"Keeper" ~since_seq:baseline ()
         |> List.find_opt (fun (entry : Mlog.Ring.entry) ->
           string_contains ~sub:"returned error result" entry.message)
       in
       match entry with
       | None -> fail "expected keeper error log for failing tool result"
       | Some (entry : Mlog.Ring.entry) ->
         check string "failing tool result logs at ERROR" "ERROR"
           (Mlog.level_to_string entry.level))
;;

let test_missing_file_error_redacts_directory_suggestions () =
  let meta =
    make_test_meta ~name:"test-keeper-suggestions" ~allowed_paths:[ "repos" ] ()
  in
  let ctx_snapshot = make_test_ctx () in
  let dir =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_keeper_tools_suggest_%d" (Random.int 100000))
  in
  (try Unix.mkdir dir 0o755 with
   | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
  Fun.protect
    ~finally:(fun () ->
      try
        Sys.readdir dir |> Array.iter (fun f -> Sys.remove (Filename.concat dir f));
        Unix.rmdir dir
      with
      | _ -> ())
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       let read_dir =
         Filename.concat (Keeper_alerting_path.project_root_of_config config)
           (read_repo_dir meta)
       in
       Fs_compat.mkdir_p read_dir;
       let existing = Filename.concat read_dir "known.txt" in
       Out_channel.with_open_text existing (fun oc ->
         Out_channel.output_string oc "known");
       let tools = make_registered_tools ~config ~meta ~ctx_snapshot () in
       let tool = find_read_tool tools in
       match Tool.execute tool (read_args meta "missing.txt") with
       | Error { Agent_sdk.Types.message; _ } ->
         let json = Yojson.Safe.from_string message in
         let detail = Yojson.Safe.Util.member "detail" json in
         check
           bool
           "detail preserves path"
           true
           (Option.is_some (Safe_ops.json_string_opt "path" detail));
         check
           bool
           "directory entries are not leaked"
           true
           (match Yojson.Safe.Util.member "suggested_entries" detail with
            | `Null | `List [] -> true
            | _ -> false)
       | Ok _ -> fail "missing file should be surfaced as tool error")
;;

let test_repeated_error_results_are_blocked () =
  let meta =
    make_test_meta ~name:"test-keeper-guardrail" ~allowed_paths:[ "repos" ] ()
  in
  let ctx_snapshot = make_test_ctx () in
  let dir =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_keeper_tools_guard_%d" (Random.int 100000))
  in
  (try Unix.mkdir dir 0o755 with
   | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
  Fun.protect
    ~finally:(fun () ->
      try
        Sys.readdir dir |> Array.iter (fun f -> Sys.remove (Filename.concat dir f));
        Unix.rmdir dir
      with
      | _ -> ())
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       ensure_read_repo_dir config meta;
       let tools = make_registered_tools ~config ~meta ~ctx_snapshot () in
       let tool = find_read_tool tools in
       let args = read_args meta "missing-file-for-keeper-tools-oas.txt" in
       for _ = 1 to Keeper_tools_oas.max_consecutive_failures do
         match Tool.execute tool args with
         | Error _ -> ()
         | Ok _ -> fail "missing file should be counted as a failure"
       done;
       match Tool.execute tool args with
       | Error { Agent_sdk.Types.message; _ } ->
         check
           bool
           "guardrail blocks repeated failures"
           true
           (is_guardrail_message message)
       | Ok _ -> fail "guardrail should block the repeated failure")
;;

let test_failure_count_resets_after_success () =
  let meta =
    make_test_meta ~name:"test-keeper-reset" ~allowed_paths:[ "repos" ] ()
  in
  let ctx_snapshot = make_test_ctx () in
  let dir =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_keeper_tools_reset_%d_%d" (Unix.getpid ()) (Random.int 100000))
  in
  Unix.mkdir dir 0o755;
  Fun.protect
    ~finally:(fun () ->
      (try rm_rf dir with
       | _ -> ()))
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       let read_dir =
         Filename.concat (Keeper_alerting_path.project_root_of_config config)
           (read_repo_dir meta)
       in
       Fs_compat.mkdir_p read_dir;
       let tools = make_registered_tools ~config ~meta ~ctx_snapshot () in
       let tool = find_read_tool tools in
       let path = "reset-after-success.txt" in
       let args = read_args meta path in
       for _ = 1 to Keeper_tools_oas.max_consecutive_failures - 1 do
         match Tool.execute tool args with
         | Error _ -> ()
         | Ok _ ->
           fail "missing file should fail before reset"
       done;
       let abs_path = Filename.concat read_dir path in
       Out_channel.with_open_text abs_path (fun oc -> Out_channel.output_string oc "ok");
       (match Tool.execute tool args with
        | Ok _ -> ()
        | Error _ -> fail "existing file should reset failure count");
       Sys.remove abs_path;
       for _ = 1 to Keeper_tools_oas.max_consecutive_failures do
         match Tool.execute tool args with
         | Error { Agent_sdk.Types.message; _ } when is_guardrail_message message ->
           fail "failure count should have reset after success"
         | Error _ -> ()
         | Ok _ -> fail "missing file should fail after removing reset file"
       done;
       match Tool.execute tool args with
       | Error { Agent_sdk.Types.message; _ } ->
         check
           bool
           "guardrail triggers only after fresh streak"
           true
           (is_guardrail_message message)
       | Ok _ -> fail "guardrail should eventually re-trigger after reset")
;;

let test_failure_tracking_is_independent_per_args () =
  let meta =
    make_test_meta ~name:"test-keeper-independent" ~allowed_paths:[ "repos" ] ()
  in
  let ctx_snapshot = make_test_ctx () in
  let dir =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_keeper_tools_independent_%d" (Random.int 100000))
  in
  (try Unix.mkdir dir 0o755 with
   | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
  Fun.protect
    ~finally:(fun () ->
      try
        Sys.readdir dir |> Array.iter (fun f -> Sys.remove (Filename.concat dir f));
        Unix.rmdir dir
      with
      | _ -> ())
    (fun () ->
       Eio_main.run
       @@ fun env ->
       Fs_compat.set_fs (Eio.Stdenv.fs env);
       let config = Coord.default_config dir in
       ensure_read_repo_dir config meta;
       let tools = make_registered_tools ~config ~meta ~ctx_snapshot () in
       let tool = find_read_tool tools in
       let args_a = read_args meta "missing-a.txt" in
       let args_b = read_args meta "missing-b.txt" in
       for _ = 1 to Keeper_tools_oas.max_consecutive_failures do
         match Tool.execute tool args_a with
         | Error _ -> ()
         | Ok _ -> fail "first path should fail before guardrail"
       done;
       (match Tool.execute tool args_b with
        | Error { Agent_sdk.Types.message; _ } ->
          check
            bool
            "different args are not blocked by prior failures"
            false
            (is_guardrail_message message)
        | Ok _ -> fail "second path should still fail normally");
       match Tool.execute tool args_a with
       | Error { Agent_sdk.Types.message; _ } ->
         check bool "original args are blocked" true (is_guardrail_message message)
       | Ok _ -> fail "guardrail should block original failing args")
;;

let make_research_meta ?tool_access () : Keeper_types.keeper_meta =
  let tool_access =
    match tool_access with
    | Some access -> access
    | None -> Keeper_types.Preset { preset = Keeper_types.Research; also_allow = [] }
  in
  match
    Masc_test_deps.meta_of_json_fixture
      (`Assoc
          [ "name", `String "test-researcher"
          ; "agent_name", `String "test-researcher"
          ; "trace_id", `String "test-trace-research"
          ; "soul_profile", `String "research"
          ; "tool_access", Keeper_types.tool_access_to_json tool_access
          ])
  with
  | Ok meta -> meta
  | Error e -> failwith (Printf.sprintf "make_research_meta failed: %s" e)
;;

let make_learned_meta () : Keeper_types.keeper_meta =
  match
    Masc_test_deps.meta_of_json_fixture
      (`Assoc
          [ "name", `String "test-learned"
          ; "agent_name", `String "test-learned"
          ; "trace_id", `String "test-trace-learned"
          ; ( "tool_access"
            , Keeper_types.tool_access_to_json
                (Keeper_types.Preset { preset = Keeper_types.Full; also_allow = [] }) )
          ])
  with
  | Ok meta -> meta
  | Error e -> failwith (Printf.sprintf "make_learned_meta failed: %s" e)
;;

let test_all_keepers_have_library_tools () =
  let meta = make_learned_meta () in
  let allowed = Agent_tool_dispatch_runtime.keeper_allowed_tool_names meta in
  check bool "has keeper_library_search" true (List.mem "keeper_library_search" allowed);
  check bool "has keeper_library_read" true (List.mem "keeper_library_read" allowed)
;;

let test_library_search_returns_results () =
  let fake_home =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "test_lib_home_%d" (Random.int 100000))
  in
  let base_path = Filename.concat fake_home "me" in
  let lib_path = List.fold_left Filename.concat base_path [ "docs"; "library" ] in
  let rec mkdir_p path =
    if not (Sys.file_exists path)
    then (
      mkdir_p (Filename.dirname path);
      try Unix.mkdir path 0o755 with
      | Unix.Unix_error (Unix.EEXIST, _, _) -> ())
  in
  mkdir_p lib_path;
  let doc_path = Filename.concat lib_path "test-mlfq-scheduler-20260321.md" in
  let oc = open_out doc_path in
  output_string
    oc
    "---\n\
     title: MLFQ Scheduler for LLM Agents\n\
     source: research\n\
     confidence: 0.85\n\
     author: test\n\
     created: 2026-03-21\n\
     tags: [llm-scheduling, mlfq]\n\
     ---\n\n\
     Multi-Level Feedback Queue scheduler for LLM request priority.\n";
  close_out oc;
  Fun.protect
    ~finally:(fun () ->
      try Sys.remove doc_path with
      | _ -> ())
    (fun () ->
       with_env "MASC_BASE_PATH" base_path (fun () ->
         let ctx = Tool_library.{ agent_name = "test-keeper" } in
         (* Search *)
         let search_result =
           Tool_library.handle_search
             ~tool_name:"test_tool"
             ~start_time:0.0
             ctx
             (`Assoc [ "query", `String "mlfq" ])

         in
         check bool "search succeeds" true (Tool_result.is_success search_result);
         check
           bool
           "search finds mlfq doc"
           true
           (let low = String.lowercase_ascii (Tool_result.message search_result) in
            String.length low > 0
            && not (Tool_library.string_contains ~sub:"no documents" low));
         (* Read *)
         let read_result =
           Tool_library.handle_read
             ~tool_name:"test_tool"
             ~start_time:0.0
             ctx
             (`Assoc [ "topic", `String "test-mlfq" ])

         in
         check bool "read succeeds" true (Tool_result.is_success read_result);
         check
           bool
           "read contains MLFQ content"
           true
           (Tool_library.string_contains
              ~sub:"Multi-Level Feedback Queue"
              (Tool_result.message read_result))))
;;

let test_library_search_empty_query () =
  let ctx = Tool_library.{ agent_name = "test-keeper" } in
  let result =
    Tool_library.handle_search
      ~tool_name:"test_tool"
      ~start_time:0.0
      ctx
      (`Assoc [ "query", `String "" ])

  in
  check bool "empty query fails" false (Tool_result.is_success result)
;;

let test_library_read_missing_topic () =
  let ctx = Tool_library.{ agent_name = "test-keeper" } in
  let result =
    Tool_library.handle_read
      ~tool_name:"test_tool"
      ~start_time:0.0
      ctx
      (`Assoc [ "topic", `String "nonexistent-topic-xyz-999" ])

  in
  check bool "missing topic fails" false (Tool_result.is_success result)
;;

(* ── normalize_tool_result tests ──────────────────────────── *)

let parse json_str = Yojson.Safe.from_string json_str
let json_bool key json = Yojson.Safe.Util.(member key json |> to_bool)
let json_int key json = Yojson.Safe.Util.(member key json |> to_int)
let json_string key json = Yojson.Safe.Util.(member key json |> to_string)

let test_normalize_success_json () =
  let raw = {|{"ok":true,"path":"/tmp/a.ml","bytes":42}|} in
  let normalized = Keeper_tools_oas.normalize_tool_result ~success:true raw in
  let json = parse normalized in
  check bool "ok is true" true (json_bool "ok" json);
  let result = Yojson.Safe.Util.member "result" json in
  check
    string
    "path preserved"
    "/tmp/a.ml"
    Yojson.Safe.Util.(member "path" result |> to_string)
;;

let test_normalize_success_plain_text () =
  let raw = "📋 No tasks yet." in
  let normalized = Keeper_tools_oas.normalize_tool_result ~success:true raw in
  let json = parse normalized in
  check bool "ok is true" true (json_bool "ok" json);
  check string "result is text" "📋 No tasks yet." (json_string "result" json)
;;

let test_normalize_failure_error_field () =
  let raw = {|{"error":"file not found","path":"/tmp/missing"}|} in
  let normalized = Keeper_tools_oas.normalize_tool_result ~success:false raw in
  let json = parse normalized in
  check bool "ok is false" false (json_bool "ok" json);
  check string "error extracted" "file not found" (json_string "error" json);
  let detail = Yojson.Safe.Util.member "detail" json in
  check
    string
    "detail preserves path"
    "/tmp/missing"
    Yojson.Safe.Util.(member "path" detail |> to_string)
;;

let test_normalize_failure_status_error () =
  let raw = {|{"status":"error","agent_id":"v1","message":"voice unavailable"}|} in
  let normalized = Keeper_tools_oas.normalize_tool_result ~success:false raw in
  let json = parse normalized in
  check bool "ok is false" false (json_bool "ok" json);
  check string "error from message" "voice unavailable" (json_string "error" json)
;;

let test_normalize_failure_ok_false () =
  let raw = {|{"ok":false,"error":"command_blocked","reason":"not in allowlist"}|} in
  let normalized = Keeper_tools_oas.normalize_tool_result ~success:false raw in
  let json = parse normalized in
  check bool "ok is false" false (json_bool "ok" json);
  check string "error extracted" "command_blocked" (json_string "error" json)
;;

let test_normalize_failure_preserves_failure_class () =
  let raw =
    {|{"ok":false,"error":"[TaskError] Invalid task state","failure_class":"workflow_rejection","error_class":"deterministic","recoverable":false}|}
  in
  let normalized = Keeper_tools_oas.normalize_tool_result ~success:false raw in
  let json = parse normalized in
  check bool "ok is false" false (json_bool "ok" json);
  check
    string
    "failure class preserved"
    "workflow_rejection"
    (json_string "failure_class" json);
  check
    string
    "error class preserved"
    "deterministic"
    (json_string "error_class" json);
  check bool "recoverable preserved" false (json_bool "recoverable" json)
;;

let workflow_rejection_shape_block_raw =
  {|{"ok":false,"error":"tool_execute_command_shape_blocked","detail":{"ok":false,"error":"tool_execute_command_shape_blocked","hint":"Do not inspect task state by guessing .masc/backlog.json. Use keeper_tasks_list.","diagnosis":{"rule_id":"tool_execute_repo_wide_scan_blocked","tool_suggestion":"keeper_tasks_list"}},"failure_class":"workflow_rejection"}|}
;;

let workflow_rejection_scope_block_raw =
  {|{"ok":false,"error":"missing evidence","failure_class":"workflow_rejection","error_class":"deterministic","recoverable":false,"diagnosis":{"rule_id":"submit_verification_missing_evidence","scope_policy":"block_scope"}}|}
;;

let test_workflow_rejection_recovery_fields_expose_next_tool () =
  let recovery_fields =
    Keeper_tools_oas_workflow.workflow_rejection_recovery_fields
      ~tool_name:"tool_execute"
      ~count:1
      workflow_rejection_shape_block_raw
  in
  let normalized =
    Keeper_tools_oas.normalize_tool_result
      ~workflow_rejection_recovery_fields:recovery_fields
      ~success:false
      workflow_rejection_shape_block_raw
  in
  let json = parse normalized in
  check bool "ok is false" false (json_bool "ok" json);
  check bool "self-correction required" true (json_bool "self_correction_required" json);
  check string "do not retry tool" "tool_execute" (json_string "do_not_retry_tool" json);
  check string "required next tool" "keeper_tasks_list" (json_string "required_next_tool" json);
  let recovery = Yojson.Safe.Util.member "workflow_rejection_recovery" json in
  check int "workflow rejection count" 1 (json_int "count" recovery);
  check
    string
    "rule id"
    "tool_execute_repo_wide_scan_blocked"
    (json_string "rule_id" recovery);
  check string "tool suggestion" "keeper_tasks_list" (json_string "tool_suggestion" recovery);
  check string "scope policy" "observe" (json_string "scope_policy" recovery)
;;

let test_workflow_rejection_recovery_fields_mark_loop () =
  let recovery_fields =
    Keeper_tools_oas_workflow.workflow_rejection_recovery_fields
      ~tool_name:"tool_execute"
      ~count:2
      workflow_rejection_shape_block_raw
  in
  let normalized =
    Keeper_tools_oas.normalize_tool_result
      ~workflow_rejection_recovery_fields:recovery_fields
      ~success:false
      workflow_rejection_shape_block_raw
  in
  let json = parse normalized in
  check bool "workflow rejection loop" true (json_bool "workflow_rejection_loop" json);
  let recovery = Yojson.Safe.Util.member "workflow_rejection_recovery" json in
  check int "workflow rejection count" 2 (json_int "count" recovery)
;;

let test_workflow_rejection_scope_policy_defaults_to_observe () =
  match
    Keeper_tools_oas_workflow.workflow_rejection_info_of_raw
      workflow_rejection_shape_block_raw
  with
  | None -> fail "expected workflow rejection info"
  | Some info ->
    check
      bool
      "unmarked workflow rejection does not scope-block"
      false
      (Keeper_tools_oas_workflow.workflow_rejection_should_scope_block info);
    check
      string
      "scope policy string"
      "observe"
      (Keeper_tools_oas_workflow.workflow_rejection_scope_policy_to_string
         info.scope_policy)
;;

let test_workflow_rejection_scope_policy_block_scope () =
  match
    Keeper_tools_oas_workflow.workflow_rejection_info_of_raw
      workflow_rejection_scope_block_raw
  with
  | None -> fail "expected workflow rejection info"
  | Some info ->
    check
      bool
      "explicit block_scope blocks scope"
      true
      (Keeper_tools_oas_workflow.workflow_rejection_should_scope_block info)
;;

let test_workflow_rejection_retry_policy_requires_explicit_markers () =
  let should_skip raw =
    match
      Keeper_tools_oas_workflow.workflow_rejection_payload_of_json (parse raw)
    with
    | Some payload ->
      Keeper_tools_oas_workflow.workflow_rejection_should_skip_retry payload
    | None -> fail "expected workflow rejection payload"
  in
  check
    bool
    "failure_class only observes"
    false
    (should_skip
       {|{"ok":false,"error":"some_rule","failure_class":"workflow_rejection"}|});
  check
    bool
    "deterministic but recoverable observes"
    false
    (should_skip
       {|{"ok":false,"error":"some_rule","failure_class":"workflow_rejection","error_class":"deterministic","recoverable":true}|});
  check
    bool
    "transient nonrecoverable observes"
    false
    (should_skip
       {|{"ok":false,"error":"some_rule","failure_class":"workflow_rejection","error_class":"transient","recoverable":false}|});
  check
    bool
    "deterministic nonrecoverable skips"
    true
    (should_skip workflow_rejection_scope_block_raw)
;;

let test_tool_result_error_json_preserves_structured_workflow_rejection () =
  let message =
    Tool_task_payloads.workflow_rejection_payload_json
      ~rule_id:"submit_verification_missing_evidence"
      ~scope_policy:"block_scope"
      "missing evidence"
  in
  let tr =
    Tool_result.error
      ~failure_class:(Some Tool_result.Workflow_rejection)
      ~tool_name:"masc_transition"
      ~start_time:0.0
      message
  in
  let json = parse (Agent_tool_shared_runtime.tool_result_error_json tr) in
  check string "error preserved" "missing evidence" (json_string "error" json);
  check
    string
    "failure class preserved"
    "workflow_rejection"
    (json_string "failure_class" json);
  let diagnosis = Yojson.Safe.Util.member "diagnosis" json in
  check
    string
    "scope policy preserved"
    "block_scope"
    (json_string "scope_policy" diagnosis)
;;

(* RFC-0195 P0: workflow_rejection_payload_json must surface a typed
   [alternatives] field when callers provide one, and must omit the
   field when the list is empty (the default). *)
let test_workflow_rejection_alternatives_field_emitted () =
  let message =
    Tool_task_payloads.workflow_rejection_payload_json
      ~rule_id:"task_done_result_missing"
      ~alternatives:[ "keeper_task_done"; "keeper_task_submit_for_verification" ]
      "result is required"
  in
  let json = parse message in
  let alternatives_field = Yojson.Safe.Util.member "alternatives" json in
  match alternatives_field with
  | `List items ->
    check
      (list string)
      "alternatives surfaces both typed tool names"
      [ "keeper_task_done"; "keeper_task_submit_for_verification" ]
      (List.map
         (function
           | `String name -> name
           | _ -> Alcotest.fail "alternatives entries must be strings")
         items)
  | _ ->
    Alcotest.fail "alternatives field missing or wrong shape"
;;

let test_workflow_rejection_alternatives_field_omitted_when_empty () =
  let message =
    Tool_task_payloads.workflow_rejection_payload_json
      ~rule_id:"some_rule"
      "some error"
  in
  let json = parse message in
  let alternatives_field = Yojson.Safe.Util.member "alternatives" json in
  check
    bool
    "empty alternatives must not emit the field"
    true
    (alternatives_field = `Null)
;;

let test_failure_boundary_ignores_error_text_without_failure_class () =
  let decision =
    Keeper_tools_oas_failure_boundary.classify_raw_failure
      {|{"ok":false,"error":"[TaskError] Invalid task state"}|}
  in
  check
    string
    "missing failure_class defaults to runtime_failure"
    "runtime_failure"
    (Tool_result.tool_failure_class_to_string decision.failure_class);
  check bool "not workflow rejection" false decision.is_workflow_rejection;
  check
    bool
    "no deterministic retry skip from text"
    true
    (Option.is_none decision.deterministic_classification)
;;

let test_failure_boundary_requires_non_retryable_deterministic_marker () =
  let decision =
    Keeper_tools_oas_failure_boundary.classify_raw_failure
      {|{"ok":false,"error":"timeout","failure_class":"transient_error","deterministic_retry":{"reason":"command_shape_blocked","retry_same_args":false}}|}
  in
  check
    string
    "transient failure class preserved"
    "transient_error"
    (Tool_result.tool_failure_class_to_string decision.failure_class);
  check
    bool
    "transient payload cannot force deterministic skip"
    true
    (Option.is_none decision.deterministic_classification)
;;

let test_failure_boundary_accepts_typed_workflow_rejection_skip () =
  let decision =
    Keeper_tools_oas_failure_boundary.classify_raw_failure
      workflow_rejection_scope_block_raw
  in
  check
    string
    "workflow failure class"
    "workflow_rejection"
    (Tool_result.tool_failure_class_to_string decision.failure_class);
  check bool "workflow rejection" true decision.is_workflow_rejection;
  match decision.deterministic_classification with
  | None -> fail "expected deterministic workflow rejection"
  | Some classification ->
    check
      string
      "deterministic source"
      "workflow_rejection_marker"
      (Keeper_tool_deterministic_error.classification_source_to_string
         classification.source)
;;

let test_deterministic_recovery_plan_fields_promote_next_tool () =
  let raw =
    {|{"ok":false,"error":"command_blocked","recovery_plan":{"kind":"structured_tool_rewrite","next_tool":"SearchFiles","next_args":{"pattern":"term","path":"lib"},"instruction":"Use SearchFiles with a scoped path.","reason":"shell_shape_requires_visible_search_tool","confidence":"high","do_not_retry_same_args":true}}|}
  in
  let fields = Keeper_tools_oas_deterministic_error.deterministic_recovery_plan_fields raw in
  let normalized =
    Keeper_tools_oas.normalize_tool_result
      ~workflow_rejection_recovery_fields:fields
      ~success:false
      raw
  in
  let json = parse normalized in
  check string "required next tool" "SearchFiles" (json_string "required_next_tool" json);
  let plan = Yojson.Safe.Util.member "recovery_plan" json in
  check string "plan next tool" "SearchFiles" (json_string "next_tool" plan);
  check
    string
    "plan pattern"
    "term"
    Yojson.Safe.Util.(member "next_args" plan |> member "pattern" |> to_string)
;;

(* #18501: stale failure counts must expire after TTL. *)
let test_failure_count_ttl_expires_stale_entries () =
  let counts = Keeper_tools_oas.create_failure_counts () in
  let key = "tool_execute:123456789" in
  Keeper_tools_oas.inject_stale_failure_count_for_test counts key 3;
  Alcotest.(check int) "stale count returns 0" 0
    (Keeper_tools_oas.failure_count_get counts key)
;;

let test_failure_count_ttl_fresh_entries_preserved () =
  let counts = Keeper_tools_oas.create_failure_counts () in
  let key = "tool_execute:987654321" in
  let n = Keeper_tools_oas.failure_count_record_failure counts key in
  Alcotest.(check int) "recorded 1" 1 n;
  Alcotest.(check int) "fresh count returns 1" 1
    (Keeper_tools_oas.failure_count_get counts key)
;;

let test_workflow_rejection_same_args_short_circuits_after_first_failure () =
  with_env "MASC_TOOL_EXTERNALIZE" "0" (fun () ->
  with_env "MASC_VERIFICATION_FSM_ENABLED" "true" (fun () ->
    let meta =
      make_test_meta
        ~name:"test-keeper-workflow-no-repeat"
        ~allowed_paths:[ "*" ]
        ()
    in
    let ctx_snapshot = make_test_ctx () in
    let dir =
      Filename.concat
        (Filename.get_temp_dir_name ())
        (Printf.sprintf "test_keeper_tools_workflow_%d" (Random.int 100000))
    in
    let previous_dispatch = !(Agent_tool_shared_runtime.tag_dispatch_fn) in
    (try Unix.mkdir dir 0o755 with
     | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
    Fun.protect
      ~finally:(fun () ->
        Agent_tool_shared_runtime.tag_dispatch_fn := previous_dispatch;
        rm_rf dir)
      (fun () ->
         Eio_main.run
         @@ fun env ->
         Fs_compat.set_fs (Eio.Stdenv.fs env);
         Agent_tool_shared_runtime.tag_dispatch_fn := Keeper_tag_dispatch.dispatch;
         let config = Coord.default_config dir in
         ignore (Coord.init config ~agent_name:(Some meta.agent_name));
         ignore
           (Coord.add_task
              config
              ~title:"Needs verification evidence"
              ~priority:1
              ~description:"");
         ignore (Coord.claim_task config ~agent_name:meta.agent_name ~task_id:"task-001");
         let tools = make_registered_tools ~config ~meta ~ctx_snapshot () in
         let transition = find_tool "masc_transition" tools in
         let deterministic_metric_labels =
           [ ( "tool", "masc_transition" )
           ; ( "reason"
             , Keeper_tool_deterministic_error.to_telemetry_key
                 Keeper_tool_deterministic_error.Workflow_rejection_blocked )
           ]
         in
         let deterministic_metric_before =
           Prometheus.metric_value_or_zero
             Keeper_metrics.(to_string ToolsOasDeterministicFailures)
             ~labels:deterministic_metric_labels
             ()
         in
         let args =
           `Assoc
             [ "agent_name", `String meta.agent_name
             ; "task_id", `String "task-001"
             ; "action", `String "submit_for_verification"
             ; "notes", `String "No PR evidence."
             ]
         in
         (match Tool.execute transition args with
          | Error { Agent_sdk.Types.message; _ } ->
            let json = parse message in
            check bool "first failure is not guardrail" false (is_guardrail_message message);
            check
              string
              "failure class"
              "workflow_rejection"
              (json_string "failure_class" json);
            check
              bool
              "self correction required"
              true
              (json_bool "self_correction_required" json);
            check
              (float 0.001)
              "deterministic failure metric increments"
              (deterministic_metric_before +. 1.0)
              (Prometheus.metric_value_or_zero
                 Keeper_metrics.(to_string ToolsOasDeterministicFailures)
                 ~labels:deterministic_metric_labels
                 ())
          | Ok _ -> fail "missing verification evidence should be a workflow rejection");
         match Tool.execute transition args with
         | Error { Agent_sdk.Types.message; _ } ->
           check
             bool
             "same deterministic workflow rejection is blocked"
             true
             (is_guardrail_message message)
         | Ok _ -> fail "same workflow rejection should be blocked before execution")))
;;

let test_workflow_rejection_scope_blocks_transition_variants () =
  with_env "MASC_TOOL_EXTERNALIZE" "0" (fun () ->
  with_env "MASC_VERIFICATION_FSM_ENABLED" "true" (fun () ->
    let meta =
      make_test_meta
        ~name:"test-keeper-workflow-scope"
        ~allowed_paths:[ "*" ]
        ()
    in
    let ctx_snapshot = make_test_ctx () in
    let dir =
      Filename.concat
        (Filename.get_temp_dir_name ())
        (Printf.sprintf "test_keeper_tools_workflow_scope_%d" (Random.int 100000))
    in
    let previous_dispatch = !(Agent_tool_shared_runtime.tag_dispatch_fn) in
    (try Unix.mkdir dir 0o755 with
     | Unix.Unix_error (Unix.EEXIST, _, _) -> ());
    Fun.protect
      ~finally:(fun () ->
        Agent_tool_shared_runtime.tag_dispatch_fn := previous_dispatch;
        rm_rf dir)
      (fun () ->
         Eio_main.run
         @@ fun env ->
         Fs_compat.set_fs (Eio.Stdenv.fs env);
         Agent_tool_shared_runtime.tag_dispatch_fn := Keeper_tag_dispatch.dispatch;
         let config = Coord.default_config dir in
         ignore (Coord.init config ~agent_name:(Some meta.agent_name));
         ignore
           (Coord.add_task
              config
              ~title:"Needs verification evidence"
              ~priority:1
              ~description:"");
         ignore (Coord.claim_task config ~agent_name:meta.agent_name ~task_id:"task-001");
         let tools = make_registered_tools ~config ~meta ~ctx_snapshot () in
         let transition = find_tool "masc_transition" tools in
         let first_args =
           `Assoc
             [ "agent_name", `String meta.agent_name
             ; "task_id", `String "task-001"
             ; "action", `String "submit_for_verification"
             ; "notes", `String "Implementation complete."
             ]
         in
         (match Tool.execute transition first_args with
          | Error { Agent_sdk.Types.message; _ } ->
            let json = parse message in
            check
              string
              "first rejection class"
              "workflow_rejection"
              (json_string "failure_class" json);
            check
              bool
              "first rejection is not scope blocker"
              false
              (String.equal "workflow_rejection_open_loop_blocked" (json_string "error" json));
            check
              bool
              "first rejection asks self correction"
              true
              (json_bool "self_correction_required" json)
          | Ok _ -> fail "missing verification evidence should reject");
         let variant_args =
           `Assoc
             [ "agent_name", `String meta.agent_name
             ; "task_id", `String "task-001"
             ; "action", `String "submit_for_verification"
             ; "notes", `String "Still complete, no PR evidence."
             ]
         in
         (match Tool.execute transition variant_args with
          | Error { Agent_sdk.Types.message; _ } ->
            let json = parse message in
            check
              string
              "variant blocked by workflow scope"
              "workflow_rejection_open_loop_blocked"
              (json_string "error" json);
            check bool "scope loop marked" true (json_bool "workflow_rejection_loop" json);
            check
              string
              "scope retry skipped reason"
              "deterministic_workflow_scope_blocked"
              (json_string "retry_skipped_reason" json)
          | Ok _ -> fail "same task/action missing-evidence variant should be blocked");
         let corrected_args =
           `Assoc
             [ "agent_name", `String meta.agent_name
             ; "task_id", `String "task-001"
             ; "action", `String "submit_for_verification"
             ; ( "notes"
               , `String
                   "completion_notes: Implementation complete. \
                    pr_url_or_artifact_ref: PR evidence attached." )
             ; "pr_url", `String "https://github.com/jeong-sik/masc-mcp/pull/12345"
             ]
         in
         match Tool.execute transition corrected_args with
         | Ok _ -> ()
         | Error { Agent_sdk.Types.message; _ } ->
           fail ("corrected evidence-bearing call should not be scope-blocked: " ^ message))))
;;

(* #18500: scope blocks must expire after TTL so agents can retry. *)
let test_workflow_rejection_scope_block_ttl_expires () =
  let counts = Keeper_tools_oas.create_failure_counts () in
  let key = "masc_transition:action=submit_for_verification:task=test-001:missing_evidence" in
  Keeper_tools_oas.inject_stale_workflow_block_for_test counts key;
  match Keeper_tools_oas.workflow_rejection_scope_block_get counts key with
  | None -> ()
  | Some _ -> Alcotest.fail "stale scope block should have been expired"
;;

let test_normalize_failure_plain_text () =
  let raw = "tool tool_execute failed (3/5): Unix_error(ENOENT)" in
  let normalized = Keeper_tools_oas.normalize_tool_result ~success:false raw in
  let json = parse normalized in
  check bool "ok is false" false (json_bool "ok" json);
  check string "error is raw text" raw (json_string "error" json)
;;

let test_transient_mutex_contention_envelope () =
  let normalized =
    Keeper_tools_oas.transient_mutex_contention_tool_error
      ~tool_name:"tool_search_files"
      ~error_text:"Sys_error(\"Mutex.lock: Resource deadlock avoided\")"
      ~backtrace:"Raised at Mutex.lock"
      ()
  in
  let json = parse normalized in
  check bool "ok is false" false (json_bool "ok" json);
  check bool "recoverable" true Yojson.Safe.Util.(member "recoverable" json |> to_bool);
  check
    string
    "error_class"
    "transient_mutex_contention"
    Yojson.Safe.Util.(member "error_class" json |> to_string);
  check
    string
    "failure_class"
    "transient_error"
    Yojson.Safe.Util.(member "failure_class" json |> to_string);
  check
    bool
    "retry recommended"
    true
    Yojson.Safe.Util.(member "retry_recommended" json |> to_bool);
  let detail = Yojson.Safe.Util.member "detail" json in
  check
    string
    "tool_name"
    "tool_search_files"
    Yojson.Safe.Util.(member "tool_name" detail |> to_string);
  check
    bool
    "backtrace available"
    true
    Yojson.Safe.Util.(member "backtrace_available" detail |> to_bool)
;;

let test_result_markers_capture_docker_approve () =
  let output =
    Keeper_tools_oas.normalize_tool_result
      ~success:true
      {|{"ok":true,"via":"docker","event":"APPROVE"}|}
  in
  let markers = Keeper_tools_oas_markers.tool_exec_result_markers ~input:(`Assoc []) ~output in
  check bool "via marker" true (List.mem "via=docker" markers);
  check bool "approve marker" true (List.mem "event=APPROVE" markers)
;;

let test_result_markers_keep_git_push_class_only () =
  let output =
    Keeper_tools_oas.normalize_tool_result ~success:true {|{"ok":true,"via":"docker"}|}
  in
  let markers =
    Keeper_tools_oas_markers.tool_exec_result_markers
      ~input:(`Assoc [ "cmd", `String "git push origin feature/secret-proof" ])
      ~output
  in
  check bool "git push class marker" true (List.mem "git push" markers);
  check bool "via marker" true (List.mem "via=docker" markers);
  check
    bool
    "raw command not persisted as marker"
    false
    (List.mem "git push origin feature/secret-proof" markers)
;;

let test_result_markers_ignore_lifecycle_mentions () =
  let output =
    Keeper_tools_oas.normalize_tool_result ~success:true {|{"ok":true,"via":"docker"}|}
  in
  let markers =
    Keeper_tools_oas_markers.tool_exec_result_markers
      ~input:
        (`Assoc
            [ "cmd", `String "echo git push"; "command", `String "printf 'draft request'" ])
      ~output
  in
  check bool "mentioned git push not marked" false (List.mem "git push" markers);
  check bool "output via marker still captured" true (List.mem "via=docker" markers)
;;

let test_result_markers_ignore_input_via () =
  let output = Keeper_tools_oas.normalize_tool_result ~success:true {|{"ok":true}|} in
  let markers =
    Keeper_tools_oas_markers.tool_exec_result_markers
      ~input:(`Assoc [ "via", `String "docker" ])
      ~output
  in
  check bool "input via marker ignored" false (List.mem "via=docker" markers)
;;

let test_result_markers_ignore_input_route_fields () =
  let output = Keeper_tools_oas.normalize_tool_result ~success:true {|{"ok":true}|} in
  let markers =
    Keeper_tools_oas_markers.tool_exec_result_markers
      ~input:
        (`Assoc
            [ "action", `String "push"
            ; "event", `String "APPROVE"
            ; "operation", `String "publish"
            ])
      ~output
  in
  check bool "input action marker ignored" false (List.mem "git push" markers);
  check bool "input event marker ignored" false (List.mem "event=APPROVE" markers)
;;

let test_result_markers_reject_untrusted_via () =
  let output =
    Keeper_tools_oas.normalize_tool_result
      ~success:true
      {|{"ok":true,"via":"<script>alert(1)</script>"}|}
  in
  let markers = Keeper_tools_oas_markers.tool_exec_result_markers ~input:(`Assoc []) ~output in
  check
    bool
    "untrusted via marker rejected"
    false
    (List.exists (fun marker -> String.starts_with ~prefix:"via=" marker) markers)
;;

(* ── Tool_output_validation tests (memory cap) ──────────────── *)

let test_cap_short_unchanged () =
  let short = "hello world" in
  let result = Tool_output_validation.cap short in
  check string "short output unchanged" short result
;;

let test_cap_exact_limit_unchanged () =
  let exact = String.make Tool_output_validation.max_output_chars 'x' in
  let result = Tool_output_validation.cap exact in
  check string "exact limit unchanged" exact result
;;

let test_cap_over_limit () =
  let long = String.make (Tool_output_validation.max_output_chars + 1000) 'a' in
  let result = Tool_output_validation.cap long in
  check
    bool
    "result shorter than original"
    true
    (String.length result < String.length long);
  check bool "contains capped marker" true (string_contains ~sub:"[capped:" result)
;;

let test_cap_preserves_prefix () =
  let prefix = "HEADER:" in
  let long = prefix ^ String.make (Tool_output_validation.max_output_chars + 1000) 'z' in
  let result = Tool_output_validation.cap long in
  check
    bool
    "prefix preserved"
    true
    (String.length result >= String.length prefix
     && String.sub result 0 (String.length prefix) = prefix)
;;

let () =
  let base_path = Masc_test_deps.find_project_root () in
  Agent_tool_dispatch_runtime.inject_masc_schemas Config.raw_all_tool_schemas;
  (match Agent_tool_dispatch_runtime.init_policy_config ~base_path with
   | Ok () -> ()
   | Error err -> Printf.eprintf "[WARN] init_policy_config failed: %s\n" err);
  run
    "Keeper_tools_oas"
    [ ( "make_tools"
      , [ test_case "returns nonempty" `Quick test_make_tools_returns_nonempty
        ; test_case "valid schemas" `Quick test_tools_have_valid_schemas
        ; test_case
            "public alias descriptions are front-door safe"
            `Quick
            test_public_alias_descriptions_are_frontdoor_safe
        ; test_case "count matches allowed" `Quick test_tool_count_matches_allowed
        ; test_case
            "error json becomes tool error"
            `Quick
            test_error_json_is_returned_as_tool_error
        ; test_case
            "missing required args rejected before keeper exec"
            `Quick
            test_oas_handler_rejects_missing_required_args
        ; test_case
            "error result logs at error level"
            `Quick
            test_error_result_logs_at_error_level
        ; test_case
            "missing file error redacts suggestions"
            `Quick
            test_missing_file_error_redacts_directory_suggestions
        ; test_case
            "repeated errors are blocked"
            `Quick
            test_repeated_error_results_are_blocked
        ; test_case
            "failure count resets after success"
            `Quick
            test_failure_count_resets_after_success
        ; test_case
            "failure tracking is independent per args"
            `Quick
            test_failure_tracking_is_independent_per_args
        ; test_case
            "tool side-effect failures are observed"
            `Quick
            test_tool_side_effect_failures_are_observed
        ; test_case
            "handler does not write tool-call I/O without observer"
            `Quick
            test_handler_does_not_write_tool_call_io_without_observer
        ; test_case
            "post_tool_use hook is the single tool-call I/O writer"
            `Quick
            test_post_tool_hook_is_single_tool_call_log_writer
        ; test_case
            "wrapper records keeper-internal calls"
            `Quick
            test_oas_wrapper_records_keeper_internal_tool_call
        ; test_case
            "OAS callbacks respect resource gate"
            `Quick
            test_oas_tool_callbacks_respect_resource_gate
        ] )
    ; ( "normalize_tool_result"
      , [ test_case "success JSON wraps under result" `Quick test_normalize_success_json
        ; test_case
            "success plain text wraps as string"
            `Quick
            test_normalize_success_plain_text
        ; test_case
            "failure extracts error field"
            `Quick
            test_normalize_failure_error_field
        ; test_case
            "failure extracts message from status:error"
            `Quick
            test_normalize_failure_status_error
        ; test_case
            "failure handles ok:false hybrid"
            `Quick
            test_normalize_failure_ok_false
        ; test_case
            "failure preserves failure_class"
            `Quick
            test_normalize_failure_preserves_failure_class
        ; test_case
            "workflow rejection exposes next tool"
            `Quick
            test_workflow_rejection_recovery_fields_expose_next_tool
        ; test_case
            "workflow rejection marks repeated loop"
            `Quick
            test_workflow_rejection_recovery_fields_mark_loop
        ; test_case
            "workflow rejection scope policy defaults to observe"
            `Quick
            test_workflow_rejection_scope_policy_defaults_to_observe
        ; test_case
            "workflow rejection scope policy block_scope"
            `Quick
            test_workflow_rejection_scope_policy_block_scope
        ; test_case
            "workflow rejection retry policy requires explicit markers"
            `Quick
            test_workflow_rejection_retry_policy_requires_explicit_markers
        ; test_case
            "structured Tool_result workflow rejection stays structured"
            `Quick
            test_tool_result_error_json_preserves_structured_workflow_rejection
        ; test_case
            "failure boundary ignores error text without failure_class"
            `Quick
            test_failure_boundary_ignores_error_text_without_failure_class
        ; test_case
            "failure boundary rejects transient deterministic contradiction"
            `Quick
            test_failure_boundary_requires_non_retryable_deterministic_marker
        ; test_case
            "failure boundary accepts typed workflow retry skip"
            `Quick
            test_failure_boundary_accepts_typed_workflow_rejection_skip
        ; test_case
            "deterministic recovery plan promotes next tool"
            `Quick
            test_deterministic_recovery_plan_fields_promote_next_tool
        ; test_case
            "failure count TTL expires stale entries (#18501)"
            `Quick
            test_failure_count_ttl_expires_stale_entries
        ; test_case
            "failure count TTL preserves fresh entries (#18501)"
            `Quick
            test_failure_count_ttl_fresh_entries_preserved
        ; test_case
            "workflow rejection same args stops after first failure"
            `Quick
            test_workflow_rejection_same_args_short_circuits_after_first_failure
        ; test_case
            "workflow rejection task/action variants stop after first failure"
            `Quick
            test_workflow_rejection_scope_blocks_transition_variants
        ; test_case
            "workflow rejection scope block TTL expires (#18500)"
            `Quick
            test_workflow_rejection_scope_block_ttl_expires
        ; test_case
            "failure plain text wraps as error"
            `Quick
            test_normalize_failure_plain_text
        ; test_case
            "EDEADLK envelope is recoverable"
            `Quick
            test_transient_mutex_contention_envelope
        ] )
    ; ( "result_markers"
      , [ test_case
            "captures docker approve markers"
            `Quick
            test_result_markers_capture_docker_approve
        ; test_case
            "keeps git push class only"
            `Quick
            test_result_markers_keep_git_push_class_only
        ; test_case
            "ignores lifecycle mentions"
            `Quick
            test_result_markers_ignore_lifecycle_mentions
        ; test_case
            "ignores caller-provided via marker"
            `Quick
            test_result_markers_ignore_input_via
        ; test_case
            "ignores caller-provided route fields"
            `Quick
            test_result_markers_ignore_input_route_fields
        ; test_case
            "rejects untrusted via marker"
            `Quick
            test_result_markers_reject_untrusted_via
        ] )
    ; ( "library_tools"
      , [ test_case
            "all keepers have library tools"
            `Quick
            test_all_keepers_have_library_tools
        ; test_case "search returns results" `Quick test_library_search_returns_results
        ; test_case "empty query fails" `Quick test_library_search_empty_query
        ; test_case "missing topic fails" `Quick test_library_read_missing_topic
        ] )
    ; ( "output_cap"
      , [ test_case "short output unchanged" `Quick test_cap_short_unchanged
        ; test_case "exact limit unchanged" `Quick test_cap_exact_limit_unchanged
        ; test_case "over limit capped with marker" `Quick test_cap_over_limit
        ; test_case "prefix preserved after cap" `Quick test_cap_preserves_prefix
        ] )
    ]
;;
