open Alcotest
open Masc_mcp

type surface_contract = {
  id : string;
  label : string;
  exposure_status : string;
  hidden_from_nav : bool;
  meets_main_gate : bool;
  route_hash : string option;
}

let find_surface surfaces target_id =
  List.find_opt (fun surface -> String.equal surface.id target_id) surfaces

let surface_contract_of_json json =
  let open Yojson.Safe.Util in
  {
    id = json |> member "id" |> to_string;
    label = json |> member "label" |> to_string;
    exposure_status = json |> member "exposure_status" |> to_string;
    hidden_from_nav = json |> member "hidden_from_nav" |> to_bool;
    meets_main_gate = json |> member "meets_main_gate" |> to_bool;
    route_hash = json |> member "route_hash" |> to_string_option;
  }

let load_surface_contracts_from_json json =
  let open Yojson.Safe.Util in
  json |> member "surfaces" |> to_list |> List.map surface_contract_of_json

let find_verification_ref surface label =
  Yojson.Safe.Util.(surface |> member "verification_refs" |> to_list)
  |> List.find_opt
       (fun json ->
         Yojson.Safe.Util.(json |> member "label" |> to_string = label))

let file_exists path =
  try Sys.file_exists path with Sys_error _ -> false

let repo_root () =
  let rec loop dir =
    let dashboard_dir = Filename.concat dir "dashboard" in
    let src_dir = Filename.concat dashboard_dir "src" in
    let config_dir = Filename.concat src_dir "config" in
    let nav_file = Filename.concat config_dir "navigation.ts" in
    if file_exists nav_file
    then dir
    else
      let parent = Filename.dirname dir in
      if String.equal parent dir
      then fail "repo root not found"
      else loop parent
  in
  loop (Sys.getcwd ())

let load_nav_contract_from_script () =
  let root = repo_root () in
  let tmp = Filename.temp_file "dashboard-surface-contract" ".json" in
  let out_fd =
    Unix.openfile tmp [ Unix.O_WRONLY; Unix.O_TRUNC ] 0o600
  in
  let code =
    Fun.protect
      ~finally:(fun () -> Unix.close out_fd)
      (fun () ->
        let original_cwd = Sys.getcwd () in
        Fun.protect
          ~finally:(fun () -> Sys.chdir original_cwd)
          (fun () ->
            Sys.chdir root;
            let argv =
              [|
                "bash";
                "scripts/check-dashboard-surface-parity.sh";
                "--print-nav-json";
              |]
            in
            let pid =
              Unix.create_process_env "bash" argv (Unix.environment ())
                Unix.stdin out_fd Unix.stderr
            in
            match snd (Unix.waitpid [] pid) with
            | Unix.WEXITED code -> code
            | Unix.WSIGNALED _ | Unix.WSTOPPED _ -> 255))
  in
  match code with
  | 0 ->
    let json = Yojson.Safe.from_file tmp in
    Sys.remove tmp;
    load_surface_contracts_from_json json
  | code ->
    (try Sys.remove tmp with Sys_error _ -> ());
    fail (Printf.sprintf "surface parity helper failed with exit code %d" code)

let load_readiness_contract () =
  Dashboard_surface_readiness.json () |> load_surface_contracts_from_json

let check_surface expected actual =
  check string (expected.id ^ " label") expected.label actual.label;
  check string
    (expected.id ^ " exposure_status")
    expected.exposure_status
    actual.exposure_status;
  check bool
    (expected.id ^ " hidden_from_nav")
    expected.hidden_from_nav
    actual.hidden_from_nav;
  check bool
    (expected.id ^ " meets_main_gate")
    expected.meets_main_gate
    actual.meets_main_gate;
  check (option string)
    (expected.id ^ " route_hash")
    expected.route_hash
    actual.route_hash

let test_surface_contract_matches_navigation_ssot () =
  let expected = load_nav_contract_from_script () in
  let actual = load_readiness_contract () in
  let expected_ids = List.map (fun surface -> surface.id) expected in
  let actual_ids = List.map (fun surface -> surface.id) actual in
  check (list string) "canonical surface ids" expected_ids actual_ids;
  List.iter2 check_surface expected actual

let test_surface_id_filter_returns_single_current_surface () =
  let json = Dashboard_surface_readiness.json ~surface_id:"workspace.verification" () in
  let surfaces = load_surface_contracts_from_json json in
  check int "single surface returned" 1 (List.length surfaces);
  match surfaces with
  | [ surface ] ->
      check string "surface id" "workspace.verification" surface.id;
      check (option string)
        "route hash"
        (Some "#workspace?section=verification")
        surface.route_hash
  | _ -> fail "unexpected surface count"

let test_live_spotcheck_serializes_route_values_as_routes () =
  let json = Dashboard_surface_readiness.json ~surface_id:"overview" () in
  let surfaces = Yojson.Safe.Util.(json |> member "surfaces" |> to_list) in
  match List.find_opt
          (fun surface ->
            Yojson.Safe.Util.(surface |> member "id" |> to_string = "overview"))
          surfaces
  with
  | None -> fail "overview missing"
  | Some surface ->
      (match find_verification_ref surface "live_spotcheck" with
       | None -> fail "overview live_spotcheck missing"
       | Some ref_json ->
           check string "live_spotcheck kind" "route"
             Yojson.Safe.Util.(ref_json |> member "kind" |> to_string))

let test_live_spotcheck_keeps_script_values_as_scripts () =
  let json = Dashboard_surface_readiness.json ~surface_id:"command.operations" () in
  let surfaces = Yojson.Safe.Util.(json |> member "surfaces" |> to_list) in
  match List.find_opt
          (fun surface ->
            Yojson.Safe.Util.(surface |> member "id" |> to_string = "command.operations"))
          surfaces
  with
  | None -> fail "command.operations missing"
  | Some surface ->
      (match find_verification_ref surface "live_spotcheck" with
       | None -> fail "command.operations live_spotcheck missing"
       | Some ref_json ->
           check string "live_spotcheck kind" "script"
             Yojson.Safe.Util.(ref_json |> member "kind" |> to_string))

let test_cognition_readiness_uses_cognition_read_model () =
  let json = Dashboard_surface_readiness.json ~surface_id:"monitoring.cognition" () in
  let surfaces = Yojson.Safe.Util.(json |> member "surfaces" |> to_list) in
  match List.find_opt
          (fun surface ->
            Yojson.Safe.Util.(surface |> member "id" |> to_string = "monitoring.cognition"))
          surfaces
  with
  | None -> fail "monitoring.cognition missing"
  | Some surface ->
      (match find_verification_ref surface "live_spotcheck" with
       | None -> fail "monitoring.cognition live_spotcheck missing"
       | Some ref_json ->
           let open Yojson.Safe.Util in
           check string "live_spotcheck kind" "route"
             (ref_json |> member "kind" |> to_string);
           check string "live_spotcheck value"
             "/api/v1/dashboard/memory-subsystems"
             (ref_json |> member "value" |> to_string);
           check string "surface verification ref labels"
             "live_spotcheck+logs+metrics"
             (surface |> member "verification_ref_bar" |> to_string))

let test_runtime_readiness_uses_cascade_health_read_model () =
  let json = Dashboard_surface_readiness.json ~surface_id:"monitoring.runtime" () in
  let surfaces = Yojson.Safe.Util.(json |> member "surfaces" |> to_list) in
  match List.find_opt
          (fun surface ->
            Yojson.Safe.Util.(surface |> member "id" |> to_string = "monitoring.runtime"))
          surfaces
  with
  | None -> fail "monitoring.runtime missing"
  | Some surface ->
      (match find_verification_ref surface "live_spotcheck" with
       | None -> fail "monitoring.runtime live_spotcheck missing"
       | Some ref_json ->
           let open Yojson.Safe.Util in
           check string "live_spotcheck kind" "route"
             (ref_json |> member "kind" |> to_string);
           check string "live_spotcheck value" "/api/v1/cascade/health"
             (ref_json |> member "value" |> to_string);
           check string "surface verification ref labels"
             "live_spotcheck+logs+metrics+tool"
             (surface |> member "verification_ref_bar" |> to_string))

let test_code_ide_readiness_uses_ide_presence_read_model () =
  let json = Dashboard_surface_readiness.json ~surface_id:"code.ide-shell" () in
  let surfaces = Yojson.Safe.Util.(json |> member "surfaces" |> to_list) in
  match List.find_opt
          (fun surface ->
            Yojson.Safe.Util.(surface |> member "id" |> to_string = "code.ide-shell"))
          surfaces
  with
  | None -> fail "code.ide-shell missing"
  | Some surface ->
      (match find_verification_ref surface "live_spotcheck" with
       | None -> fail "code.ide-shell live_spotcheck missing"
       | Some ref_json ->
           let open Yojson.Safe.Util in
           check string "live_spotcheck kind" "route"
             (ref_json |> member "kind" |> to_string);
           check string "live_spotcheck value"
             "/api/v1/ide/presence"
             (ref_json |> member "value" |> to_string);
           check string "surface verification ref labels"
             "live_spotcheck+logs+metrics"
             (surface |> member "verification_ref_bar" |> to_string))

let test_verification_ref_bar_reflects_declared_refs () =
  let overview_json = Dashboard_surface_readiness.json ~surface_id:"overview" () in
  let open Yojson.Safe.Util in
  check string "single-surface ref coverage"
    "live:1/1 logs:1/1 metrics:1/1"
    (overview_json |> member "verification_ref_bar" |> to_string);
  (match overview_json |> member "surfaces" |> to_list with
   | [ surface ] ->
       check string "surface verification ref labels"
         "fixture+live_spotcheck+logs+metrics+tool"
         (surface |> member "verification_ref_bar" |> to_string)
   | _ -> fail "overview surface missing");
  let all_json = Dashboard_surface_readiness.json () in
  let all_refs = all_json |> member "verification_ref_bar" |> to_string in
  check bool "aggregate refs no longer fixture constant" false
    (String.equal all_refs "fixture+live_spotcheck");
  check bool "aggregate refs report live coverage" true
    (String.starts_with ~prefix:"live:" all_refs)

let test_legacy_surfaces_removed_from_readiness_inventory () =
  let surfaces = load_readiness_contract () in
  let legacy_ids =
    [
      "monitoring.sessions";
      "monitoring.safe_autonomy";
      "monitoring.safe-autonomy";
      "monitoring.activity";
      "monitoring.live";
      "monitoring.git-graph";
      "monitoring.cascade-inspector";
      "monitoring.cost";
      "monitoring.attribution";
      "command.intervene";
      "command.namespace";
      "command.governance";
      "connectors.connector-discord";
      "connectors.connector-imessage";
      "connectors.connector-slack";
      "connectors.connector-telegram";
      "workspace.evidence";
      "workspace.goals";
      "workspace.worktrees";
      "workspace.collab-mvp";
      "monitoring.memory-subsystems";
      "lab.features";
      "lab.config";
    ]
  in
  List.iter
    (fun legacy_id ->
      check bool (legacy_id ^ " removed") true
        (Option.is_none (find_surface surfaces legacy_id)))
    legacy_ids

let test_hidden_diagnostic_surfaces_are_not_main_gate () =
  let surfaces =
    Dashboard_surface_readiness.json () |> load_surface_contracts_from_json
  in
  let check_hidden surface_id =
    match find_surface surfaces surface_id with
    | None -> fail (surface_id ^ " missing")
    | Some surface ->
        check string (surface_id ^ " exposure_status") "diagnostic"
          surface.exposure_status;
        check bool (surface_id ^ " hidden_from_nav") true surface.hidden_from_nav;
        check bool (surface_id ^ " meets_main_gate") false
          surface.meets_main_gate
  in
  List.iter check_hidden
    [
      "cockpit";
      "monitoring.journey";
    ]

let () =
  run "Dashboard_surface_readiness"
    [
      ( "surface_readiness",
        [
          test_case "surface contract matches navigation ssot" `Quick
            test_surface_contract_matches_navigation_ssot;
          test_case "surface_id filter returns current verification surface" `Quick
            test_surface_id_filter_returns_single_current_surface;
          test_case "route live spotchecks stay routes" `Quick
            test_live_spotcheck_serializes_route_values_as_routes;
          test_case "script live spotchecks stay scripts" `Quick
            test_live_spotcheck_keeps_script_values_as_scripts;
          test_case "cognition readiness uses cognition read model" `Quick
            test_cognition_readiness_uses_cognition_read_model;
          test_case "runtime readiness uses cascade health read model" `Quick
            test_runtime_readiness_uses_cascade_health_read_model;
          test_case "code ide readiness uses ide presence read model" `Quick
            test_code_ide_readiness_uses_ide_presence_read_model;
          test_case "verification ref bar reflects declared refs" `Quick
            test_verification_ref_bar_reflects_declared_refs;
          test_case "legacy surfaces removed from readiness inventory" `Quick
            test_legacy_surfaces_removed_from_readiness_inventory;
          test_case "hidden diagnostics are not main gate" `Quick
            test_hidden_diagnostic_surfaces_are_not_main_gate;
        ] );
    ]
