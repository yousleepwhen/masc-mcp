open Alcotest

module KTP = Masc_mcp.Keeper_types_profile
module KT = Masc_mcp.Keeper_types
module KPolicy = Masc_mcp.Keeper_tool_policy
module TaskPayloads = Masc_mcp.Tool_task_payloads

(** Validate that every .toml file in config/keepers/ parses successfully
    with the OCaml TOML parser.  This catches syntax that is valid standard
    TOML but unsupported by our minimal parser (e.g. multi-line arrays before
    the fix).  Runs as part of [dune test], so CI will fail before deploy. *)

let test_all_keeper_tomls_parse () =
  let relative_config_dir = "config/keepers" in
  let config_dir =
    match Sys.getenv_opt "DUNE_SOURCEROOT" with
    | Some repo_root -> Filename.concat repo_root relative_config_dir
    | None -> relative_config_dir
  in
  if not (Sys.file_exists config_dir && Sys.is_directory config_dir) then
    fail
      (Printf.sprintf
         "Could not locate %s (resolved to %s)"
         relative_config_dir config_dir)
  else
    let files =
      Sys.readdir config_dir
      |> Array.to_list
      |> List.filter (fun f -> Filename.check_suffix f ".toml")
      |> List.sort String.compare
    in
    check bool "at least one toml file" true (List.length files > 0);
    List.iter (fun f ->
      let path = Filename.concat config_dir f in
      match KTP.load_keeper_toml path with
      | Ok _ -> ()
      | Error e ->
        fail (Printf.sprintf "%s: %s" f e)
    ) files

let test_named_keeper_docker_defaults () =
  let config_dir =
    match Sys.getenv_opt "DUNE_SOURCEROOT" with
    | Some repo_root -> Filename.concat repo_root "config/keepers"
    | None -> "config/keepers"
  in
  let expect_keeper ~name ~persona =
    let path = Filename.concat config_dir (name ^ ".toml") in
    match KTP.load_keeper_toml path with
    | Error e -> fail (Printf.sprintf "%s: %s" name e)
    | Ok (_loaded_name, defaults) ->
        check (option string) (name ^ " persona_name") (Some persona)
          defaults.persona_name;
        check (option string) (name ^ " sandbox_profile") (Some "docker")
          (Option.map KTP.sandbox_profile_to_string defaults.sandbox_profile);
        (* Docker keepers request [Network_inherit] so tool_execute can dispatch
           git/gh. *)
        check (option string) (name ^ " network_mode") (Some "inherit")
          (Option.map KTP.network_mode_to_string defaults.network_mode);
        check (option string) (name ^ " repo_cli_identity")
          (Some "anyang-keepers") defaults.repo_cli_identity
  in
  expect_keeper ~name:"issue_king" ~persona:"issue_king";
  expect_keeper ~name:"masc-improver" ~persona:"analyst";
  expect_keeper ~name:"sangsu" ~persona:"sangsu"

let test_committed_keepers_are_pr_work_capable () =
  let project_root = Masc_test_deps.find_project_root () in
  Masc_test_deps.init_keeper_tool_registry ();
  (match KPolicy.init_policy_config ~base_path:project_root with
   | Ok () -> ()
   | Error e -> fail (Printf.sprintf "init_policy_config: %s" e));
  let config_dir =
    match Sys.getenv_opt "DUNE_SOURCEROOT" with
    | Some repo_root -> Filename.concat repo_root "config/keepers"
    | None -> Filename.concat project_root "config/keepers"
  in
  let files =
    Sys.readdir config_dir
    |> Array.to_list
    |> List.filter (fun f -> Filename.check_suffix f ".toml")
    |> List.filter (fun f -> f <> "base.toml")
    |> List.sort String.compare
  in
  check bool "at least one keeper manifest" true (files <> []);
  List.iter
    (fun file ->
       let name = Filename.remove_extension file in
       let path = Filename.concat config_dir file in
       match KTP.load_keeper_toml path with
       | Error e -> fail (Printf.sprintf "%s: %s" file e)
       | Ok (_loaded_name, defaults) ->
           check (option string) (name ^ " sandbox_profile") (Some "docker")
             (Option.map KTP.sandbox_profile_to_string defaults.sandbox_profile);
           check (option string) (name ^ " network_mode") (Some "inherit")
             (Option.map KTP.network_mode_to_string defaults.network_mode);
           let expected_repo_cli_identity =
             match name with
             | "verifier" -> "reviewer-keepers-nonoperator-0506b"
             | _ -> "anyang-keepers"
           in
           check (option string) (name ^ " repo_cli_identity")
             (Some expected_repo_cli_identity) defaults.repo_cli_identity;
           check (option string) (name ^ " git_identity_mode")
             (Some "repo_cli_identity") defaults.git_identity_mode;
           let preset =
             match defaults.tool_preset with
             | None -> fail (Printf.sprintf "%s: tool_access.preset is required" file)
             | Some raw ->
                 (match KT.tool_preset_of_string raw with
                  | Some preset -> preset
                  | None -> fail (Printf.sprintf "%s: unknown preset %S" file raw))
           in
           let meta =
             match
               Masc_test_deps.meta_of_json_fixture
                 (`Assoc [
                    ("name", `String name);
                    ("agent_name", `String name);
                    ("trace_id", `String (name ^ "-capability-test"));
                    ( "tool_access",
                      `Assoc [
                        ("kind", `String "preset");
                        ("preset", `String (KT.tool_preset_to_string preset));
                        ("also_allow", `List []);
                      ] );
                    ("tool_denylist", `List []);
                  ])
             with
             | Ok meta -> meta
             | Error e -> fail (Printf.sprintf "%s: meta fixture: %s" file e)
           in
           let lookup = KPolicy.tool_access_lookup_of_meta meta in
           List.iter
             (fun tool_name ->
                check bool (name ^ " can execute " ^ tool_name) true
                  (KPolicy.can_execute ~lookup tool_name))
             [
               "tool_search_files";
               "tool_execute";
             ])
    files

let test_verifier_config_hides_worker_lifecycle_tools () =
  let project_root = Masc_test_deps.find_project_root () in
  Masc_test_deps.init_keeper_tool_registry ();
  (match KPolicy.init_policy_config ~base_path:project_root with
   | Ok () -> ()
   | Error e -> fail (Printf.sprintf "init_policy_config: %s" e));
  let path = Filename.concat project_root "config/keepers/verifier.toml" in
  match KTP.load_keeper_toml path with
  | Error e -> fail (Printf.sprintf "verifier.toml: %s" e)
  | Ok (_loaded_name, defaults) ->
      let contains ~needle haystack =
        let len = String.length haystack in
        let nlen = String.length needle in
        let found = ref false in
        if nlen <= len then
          for i = 0 to len - nlen do
            if String.sub haystack i nlen = needle then found := true
          done;
        !found
      in
      let instructions = Option.value ~default:"" defaults.instructions in
      check
        bool
        "verifier treats PR refs as artifact evidence"
        true
        (contains ~needle:"PR artifact 검증" instructions);
      check
        bool
        "verifier must not reject solely on empty task worktree"
        true
        (contains ~needle:"task-local worktree가 없거나 비어 있다는 이유만으로 reject하지 마라" instructions);
      check
        bool
        "verifier blocks instead of rejecting inaccessible GitHub artifacts"
        true
        (contains ~needle:"GitHub/main artifact에 접근할 수 없으면 reject 대신 blocker" instructions);
      check
        bool
        "verifier instructions avoid hidden Bash implementation name"
        false
        (contains ~needle:"tool_execute" instructions);
      check
        bool
        "verifier instructions avoid hidden shell implementation name"
        false
        (contains ~needle:"tool_search_files" instructions);
      let preset =
        match defaults.tool_preset with
        | Some raw -> (
            match KT.tool_preset_of_string raw with
            | Some preset -> preset
            | None -> fail (Printf.sprintf "unknown verifier preset %S" raw))
        | None -> fail "verifier tool_access.preset is required"
      in
      let also_allow = Option.value ~default:[] defaults.tool_also_allow in
      let denylist = Option.value ~default:[] defaults.tool_denylist in
      let meta =
        match
          Masc_test_deps.meta_of_json_fixture
            (`Assoc
               [
                 ("name", `String "verifier");
                 ("agent_name", `String "keeper-verifier-agent");
                 ("trace_id", `String "verifier-tool-surface-test");
                 ( "tool_access",
                   `Assoc
                     [
                       ("kind", `String "preset");
                       ("preset", `String (KT.tool_preset_to_string preset));
                       ( "also_allow",
                         `List (List.map (fun value -> `String value) also_allow) );
                     ] );
                 ( "tool_denylist",
                   `List (List.map (fun value -> `String value) denylist) );
               ])
        with
        | Ok meta -> meta
        | Error e -> fail (Printf.sprintf "verifier meta fixture: %s" e)
      in
      let lookup = KPolicy.tool_access_lookup_of_meta meta in
      let visible_tools = KPolicy.keeper_allowed_tool_names meta in
      List.iter
        (fun tool_name ->
          check bool ("verifier keeps " ^ tool_name) true
            (KPolicy.can_execute ~lookup tool_name);
          check bool ("verifier exposes " ^ tool_name) true
            (List.mem tool_name visible_tools))
        [
          "keeper_tasks_list";
          "masc_tasks";
          "masc_task_history";
          "masc_transition";
        ];
      List.iter
        (fun tool_name ->
          check bool ("verifier hides " ^ tool_name) false
            (KPolicy.can_execute ~lookup tool_name);
          check bool ("verifier does not expose " ^ tool_name) false
            (List.mem tool_name visible_tools))
        [
          "keeper_task_claim";
          "keeper_task_create";
          "keeper_task_done";
          "keeper_task_force_done";
          "keeper_task_force_release";
          "keeper_task_submit_for_verification";
          "masc_add_task";
          "masc_batch_add_tasks";
          "masc_claim_next";
          "masc_deliver";
        ];
      List.iter
        (fun action ->
          check bool ("verifier blocks transition action " ^ action) true
            (TaskPayloads.transition_action_denied_by_denylist
               ~tool_denylist:denylist
               ~action))
        [
          "claim";
          "start";
          "done";
          "cancel";
          "release";
          "submit_for_verification";
          "submit_pr_evidence";
        ];
      List.iter
        (fun action ->
          check bool ("verifier allows transition action " ^ action) false
            (TaskPayloads.transition_action_denied_by_denylist
               ~tool_denylist:denylist
               ~action))
        [ "approve"; "reject" ]

(** Write a temporary TOML file, run load_keeper_toml, clean up. *)
let with_temp_toml content f =
  let path = Filename.temp_file "keeper_test_" ".toml" in
  let oc = open_out path in
  output_string oc content;
  close_out oc;
  Fun.protect
    ~finally:(fun () -> try Sys.remove path with _ -> ())
    (fun () -> f path)

let write_file path contents =
  let rec mkdir_p path =
    if path = "" || path = "." || path = "/" then
      ()
    else if Sys.file_exists path then
      ()
    else begin
      mkdir_p (Filename.dirname path);
      Unix.mkdir path 0o755
    end
  in
  mkdir_p (Filename.dirname path);
  let oc = open_out path in
  Fun.protect
    ~finally:(fun () -> close_out_noerr oc)
    (fun () -> output_string oc contents)

let rec rm_rf path =
  if Sys.file_exists path then
    if Sys.is_directory path then begin
      Sys.readdir path
      |> Array.iter (fun name -> rm_rf (Filename.concat path name));
      Unix.rmdir path
    end else
      Sys.remove path

let with_env key value f =
  let prior = Sys.getenv_opt key in
  Unix.putenv key value;
  Fun.protect
    ~finally:(fun () ->
      match prior with
      | Some value -> Unix.putenv key value
      | None -> Unix.putenv key "")
    f

let minimal_cascade_profile_metadata_toml = {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[models.qwen3-small]
api-name = "qwen3:1.7b"
max-context = 32768
tools-support = true

[ollama.qwen3]
is-default = true
max-concurrent = 1

[ollama.qwen3-small]
max-concurrent = 1

[tier.primary]
members = ["ollama.qwen3"]
strategy = "failover"

[tier.backup]
members = ["ollama.qwen3-small"]
strategy = "failover"

[tier.scoring]
keeper-assignable = false
members = ["ollama.qwen3"]
strategy = "failover"

[tier-group.primary]
tiers = ["primary", "backup"]
strategy = "priority_tier"
fallback = true

[tier-group.scoring]
tiers = ["scoring"]
strategy = "priority_tier"
fallback = false
keeper-assignable = false

[routes.keeper_turn]
target = "tier-group.primary"

[routes.llm_rerank]
target = "tier-group.scoring"
|}

let with_temp_config_dir cascade_toml f =
  let dir = Filename.temp_file "keeper_cascade_config_" "" in
  Sys.remove dir;
  Unix.mkdir dir 0o755;
  let config_root = Filename.concat dir "config" in
  let cascade_path = Filename.concat config_root "cascade.toml" in
  write_file cascade_path cascade_toml;
  let reset () =
    Config_dir_resolver.reset ();
    Masc_mcp.Cascade_catalog_runtime.reset_cache_for_tests ()
  in
  Fun.protect
    ~finally:(fun () -> rm_rf dir)
    (fun () ->
      with_env "MASC_CONFIG_DIR" config_root @@ fun () ->
      reset ();
      Fun.protect ~finally:reset (fun () -> f ~config_root ~cascade_path))

let repo_config_dir () =
  match Sys.getenv_opt "DUNE_SOURCEROOT" with
  | Some repo_root -> Filename.concat repo_root "config"
  | None -> "config"

let with_repo_config_dir f =
  let prior = Sys.getenv_opt "MASC_CONFIG_DIR" in
  Unix.putenv "MASC_CONFIG_DIR" (repo_config_dir ());
  Config_dir_resolver.reset ();
  Fun.protect
    ~finally:(fun () ->
      (match prior with
       | Some value -> Unix.putenv "MASC_CONFIG_DIR" value
       | None -> Unix.putenv "MASC_CONFIG_DIR" "");
      Config_dir_resolver.reset ())
    f

let contains ~needle haystack =
  let len = String.length haystack in
  let nlen = String.length needle in
  let found = ref false in
  if nlen <= len then
    for i = 0 to len - nlen do
      if String.sub haystack i nlen = needle then found := true
    done;
  !found

let test_base_config_avoids_hidden_shell_tool_names () =
  let project_root = Masc_test_deps.find_project_root () in
  let path = Filename.concat project_root "config/keepers/base.toml" in
  match KTP.load_keeper_toml path with
  | Error e -> fail (Printf.sprintf "base.toml: %s" e)
  | Ok (_loaded_name, defaults) ->
      let instructions = Option.value ~default:"" defaults.instructions in
      check
        bool
        "base instructions avoid hidden Bash implementation name"
        false
        (contains ~needle:"tool_execute" instructions);
      check
        bool
        "base instructions avoid hidden shell implementation name"
        false
        (contains ~needle:"tool_search_files" instructions)

let test_cascade_name_rejects_unknown () =
  let result =
    with_temp_toml
      "[keeper]\nname = \"testkeeper\"\ncascade_name = \"definitely_missing_profile\"\n"
      KTP.load_keeper_toml
  in
  match result with
  | Ok _ -> fail "definitely_missing_profile cascade_name should be rejected"
  | Error e ->
      check bool "error mentions cascade_name" true
        (contains ~needle:"invalid cascade_name" e)

let test_cascade_name_accepts_known () =
  with_repo_config_dir @@ fun () ->
  let check_ok label cascade_name =
    let result =
      with_temp_toml
        (Printf.sprintf "[keeper]\nname = \"testkeeper\"\ncascade_name = \"%s\"\n"
           cascade_name)
        KTP.load_keeper_toml
    in
    match result with
    | Ok _ -> ()
    | Error e ->
        fail (Printf.sprintf "%s: '%s' should be accepted but got: %s" label
                cascade_name e)
  in
  check_ok "primary variant" "primary";
  check_ok "local_only phase-routing" "local_only";
  check_ok "local_recovery phase-routing" "local_recovery";
  check_ok "tool_use_strict reserved tool lane" "tool_use_strict"

let test_cascade_name_accepts_tool_lane_without_catalog () =
  let missing_dir =
    Filename.concat (Filename.get_temp_dir_name ())
      "missing-masc-config-for-tool-use-strict"
  in
  let prior = Sys.getenv_opt "MASC_CONFIG_DIR" in
  Unix.putenv "MASC_CONFIG_DIR" missing_dir;
  Config_dir_resolver.reset ();
  Fun.protect
    ~finally:(fun () ->
      (match prior with
       | Some value -> Unix.putenv "MASC_CONFIG_DIR" value
       | None -> Unix.putenv "MASC_CONFIG_DIR" "");
      Config_dir_resolver.reset ())
    (fun () ->
      let result =
        with_temp_toml
          "[keeper]\nname = \"testkeeper\"\ncascade_name = \"tool_use_strict\"\n"
          KTP.load_keeper_toml
      in
      match result with
      | Ok _ -> ()
      | Error e ->
          fail
            (Printf.sprintf
               "tool_use_strict is a reserved tool lane and should not require \
                a readable live catalog: %s"
               e))

let test_cascade_name_accepts_catalog_entry () =
  with_repo_config_dir @@ fun () ->
  (* Tests that the live declarative catalog is consulted during
     validation. *)
  let catalog =
    try Masc_mcp.Keeper_cascade_profile.keeper_catalog_names ()
    with _ -> []
  in
  let test_name =
    (* Pick any keeper-assignable catalog entry that isn't a phase-routing
       reserved alias — the validator now treats catalog membership as the
       only acceptance criterion. *)
    match
      List.find_opt
        (fun n -> not (List.mem n [ "local_only"; "local_recovery" ]))
        catalog
    with
    | Some name -> name
    | None -> "tool_use_strict" (* fallback, may not be in catalog *)
  in
  let result =
    with_temp_toml
      (Printf.sprintf "[keeper]\nname = \"testkeeper\"\ncascade_name = \"%s\"\n"
         test_name)
      KTP.load_keeper_toml
  in
  match result with
  | Ok _ -> ()
  | Error e ->
      (* If catalog is unavailable, skip rather than fail *)
      if catalog = [] then ()
      else fail (Printf.sprintf "%s should be accepted: %s" test_name e)

let test_resolve_model_strings_reads_declarative_profile () =
  with_temp_config_dir minimal_cascade_profile_metadata_toml
  @@ fun ~config_root:_ ~cascade_path ->
  let models =
    Masc_mcp.Cascade_config.resolve_model_strings
      ~config_path:cascade_path ~name:"primary" ~defaults:["fallback"] ()
  in
  check (list string) "primary group models"
    ["ollama:qwen3:8b"; "ollama:qwen3:1.7b"]
    models

let test_resolve_model_strings_uses_provider_protocol_for_custom_id () =
  let cascade_toml =
    {|
[providers.custom]
protocol = "provider_d-http"
endpoint = "https://example.invalid/v1"

[models.stable]
api-name = "gpt-custom"
max-context = 32768
tools-support = true

[custom.stable]
max-concurrent = 1

[tier.primary]
members = ["custom.stable"]
strategy = "failover"

[routes.keeper_turn]
target = "tier.primary"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  let models =
    Masc_mcp.Cascade_config.resolve_model_strings
      ~config_path:cascade_path ~name:"primary" ~defaults:["fallback"] ()
  in
  check (list string) "custom provider resolved from protocol"
    ["custom:gpt-custom@https://example.invalid/v1"]
    models

let test_cascade_profile_metadata_from_toml () =
  with_temp_config_dir minimal_cascade_profile_metadata_toml
  @@ fun ~config_root:_ ~cascade_path:_ ->
  check (list string) "keeper assignable catalog"
    ["backup"; "primary"; "tier-group.primary"; "tier.backup"; "tier.primary"]
    (Masc_mcp.Keeper_cascade_profile.keeper_catalog_names ());
  check bool "rerank route is system-only" true
    (Masc_mcp.Keeper_cascade_profile.is_system_only_cascade "llm_rerank");
  check bool "scoring catalog entry is system-only" true
    (Masc_mcp.Keeper_cascade_profile.is_system_only_cascade "scoring");
  check (option string) "primary fallback hint" (Some "tier.backup")
    (Masc_mcp.Keeper_cascade_profile.fallback_cascade_for "primary")

let test_cascade_name_accepts_unrouted_assignable_catalog_entry () =
  with_temp_config_dir minimal_cascade_profile_metadata_toml
  @@ fun ~config_root:_ ~cascade_path:_ ->
  let result =
    with_temp_toml
      "[keeper]\nname = \"testkeeper\"\ncascade_name = \"backup\"\n"
      KTP.load_keeper_toml
  in
  match result with
  | Ok _ -> ()
  | Error e ->
      fail
        (Printf.sprintf
           "unrouted keeper-assignable catalog entry should be accepted: %s"
           e)

let test_keeper_assignability_uses_preferred_qualified_profile () =
  let cascade_toml =
    {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[ollama.qwen3]
max-concurrent = 1

[tier.primary]
members = ["ollama.qwen3"]
strategy = "failover"

[tier-group.primary]
tiers = ["primary"]
strategy = "priority_tier"
keeper-assignable = false

[routes.keeper_turn]
target = "tier-group.primary"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  check bool "preferred tier-group controls public assignability" false
    (List.mem "primary" (Masc_mcp.Keeper_cascade_profile.keeper_catalog_names ()));
  check bool "preferred tier-group is system-only" true
    (Masc_mcp.Keeper_cascade_profile.is_system_only_cascade "primary");
  check bool "qualified tier remains assignable" false
    (Masc_mcp.Keeper_cascade_profile.is_system_only_cascade "tier.primary");
  check bool "qualified tier-group remains system-only" true
    (Masc_mcp.Keeper_cascade_profile.is_system_only_cascade "tier-group.primary");
  let resolved_string raw =
    match
      Masc_mcp.Keeper_cascade_profile.resolve_live_result
        ~config_path:cascade_path raw
    with
    | Ok name -> Cascade_name.to_string name
    | Error (`Unresolved raw) ->
        fail
          (Printf.sprintf
             "qualified cascade %S did not resolve against test catalog"
             raw)
  in
  check string "qualified tier resolves without fallback" "tier.primary"
    (resolved_string "tier.primary");
  check string "qualified tier-group resolves without fallback" "tier-group.primary"
    (resolved_string "tier-group.primary");
  (match
     with_temp_toml
       "[keeper]\nname = \"testkeeper\"\ncascade_name = \"tier.primary\"\n"
       KTP.load_keeper_toml
   with
   | Ok _ -> ()
   | Error e ->
       fail
         (Printf.sprintf
            "explicit qualified tier.primary should be accepted: %s"
            e));
  let result =
    with_temp_toml
      "[keeper]\nname = \"testkeeper\"\ncascade_name = \"primary\"\n"
      KTP.load_keeper_toml
  in
  match result with
  | Ok _ -> fail "preferred system-only tier-group should reject public primary"
  | Error e ->
      check bool "error mentions system-only" true
        (contains ~needle:"system-only" e)

let test_fallback_cascade_preserves_qualified_source_profile () =
  let cascade_toml =
    {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[ollama.qwen3]
is-default = true
max-concurrent = 1

[tier.primary]
members = ["ollama.qwen3"]
strategy = "failover"

[tier.mid]
members = ["ollama.qwen3"]
strategy = "failover"

[tier.slow]
members = ["ollama.qwen3"]
strategy = "failover"

[tier-group.primary]
tiers = ["mid", "slow"]
strategy = "priority_tier"
fallback = true

[tier-group.alt]
tiers = ["primary", "mid"]
strategy = "priority_tier"
fallback = true

[routes.keeper_turn]
target = "tier-group.primary"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  check (option string) "public primary resolves as tier-group.primary"
    (Some "tier.slow")
    (Masc_mcp.Keeper_cascade_profile.fallback_cascade_for
       ~config_path:cascade_path "primary");
  check (option string) "qualified tier.primary keeps tier edge"
    (Some "tier.mid")
    (Masc_mcp.Keeper_cascade_profile.fallback_cascade_for
       ~config_path:cascade_path "tier.primary")

let test_fallback_cascade_returns_canonical_tier_target () =
  let cascade_toml =
    {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[ollama.qwen3]
is-default = true
max-concurrent = 1

[tier.primary]
members = ["ollama.qwen3"]
strategy = "failover"

[tier.local_llama]
members = ["ollama.qwen3"]
strategy = "failover"

[tier.glm]
members = ["ollama.qwen3"]
strategy = "failover"

[tier-group.coding]
tiers = ["primary", "local_llama", "glm"]
strategy = "priority_tier"
fallback = true

[routes.keeper_turn]
target = "tier-group.coding"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  check
    (option string)
    "tier-group fallback keeps canonical tier target"
    (Some "tier.local_llama")
    (Masc_mcp.Keeper_cascade_profile.fallback_cascade_for
       ~config_path:cascade_path "tier-group.coding")

let test_normalize_declared_name_canonicalizes_public_catalog_members () =
  let cascade_toml =
    {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[ollama.qwen3]
is-default = true
max-concurrent = 1

[tier.strict_tool_candidates]
members = ["ollama.qwen3"]
strategy = "failover"

[tier.local_llama]
members = ["ollama.qwen3"]
strategy = "failover"

[tier-group.strict_tool_candidates]
tiers = ["strict_tool_candidates"]
strategy = "priority_tier"

[routes.keeper_turn]
target = "tier-group.strict_tool_candidates"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  check string "public tier-group alias canonicalizes to tier-group"
    "tier-group.strict_tool_candidates"
    (Masc_mcp.Keeper_cascade_profile.normalize_declared_name
       ~config_path:cascade_path "strict_tool_candidates");
  check string "public tier alias canonicalizes to tier"
    "tier.local_llama"
    (Masc_mcp.Keeper_cascade_profile.normalize_declared_name
       ~config_path:cascade_path "local_llama")

let test_keeper_runtime_declared_name_ignores_non_keeper_route_target () =
  let cascade_toml =
    {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[ollama.qwen3]
max-concurrent = 1

[tier.provider_k-coding-primary]
members = ["ollama.qwen3"]
strategy = "failover"

[tier.ollama_cloud_primary]
members = ["ollama.qwen3"]
strategy = "failover"
keeper-assignable = true

[tier-group.provider_k-coding-with-spark]
tiers = ["provider_k-coding-primary"]
strategy = "failover"

[routes.keeper_turn]
target = "tier-group.provider_k-coding-with-spark"

[routes.provider_benchmark]
target = "tier.ollama_cloud_primary"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  check string "assignable concrete route target is preserved"
    "tier.ollama_cloud_primary"
    (Masc_mcp.Keeper_cascade_profile.normalize_keeper_runtime_declared_name
       ~config_path:cascade_path "tier.ollama_cloud_primary")

let test_catalog_validator_surfaces_adapter_errors () =
  let cascade_toml =
    {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[tier.broken]
members = ["ollama.qwen3"]
strategy = "failover"

[routes.keeper_turn]
target = "tier.broken"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  let issues =
    Masc_mcp.Cascade_catalog_validator.diagnose_catalog
      ~config_path:cascade_path
  in
  let has_adapter_error =
    List.exists
      (fun (issue : Masc_mcp.Cascade_catalog_validator.issue) ->
         match issue.severity with
         | Masc_mcp.Cascade_catalog_validator.Catalog_warn -> false
         | Masc_mcp.Cascade_catalog_validator.Catalog_error ->
             contains
               ~needle:"Declarative cascade adapter error"
               issue.message
             && contains ~needle:"Binding_resolution_failed" issue.message)
      issues
  in
  check bool "adapter error is surfaced as catalog error" true has_adapter_error

let test_catalog_validator_surfaces_declarative_parse_errors () =
  let cascade_toml =
    {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[ollama.qwen3]
max-concurrent = 1

[tier.primary]
members = ["ollama.qwen3"]
strategy = "not_a_strategy"

[routes.keeper_turn]
target = "tier.primary"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  let issues =
    Masc_mcp.Cascade_catalog_validator.diagnose_catalog
      ~config_path:cascade_path
  in
  let has_parse_error =
    List.exists
      (fun (issue : Masc_mcp.Cascade_catalog_validator.issue) ->
         match issue.severity with
         | Masc_mcp.Cascade_catalog_validator.Catalog_warn -> false
         | Masc_mcp.Cascade_catalog_validator.Catalog_error ->
             contains
               ~needle:"Declarative cascade parse error"
               issue.message
             && contains ~needle:"not_a_strategy" issue.message)
      issues
  in
  check bool "parse error is surfaced as catalog error" true has_parse_error

let rejection_error_messages rejection =
  let json = Masc_mcp.Cascade_catalog_runtime.rejection_to_yojson rejection in
  Yojson.Safe.Util.member "errors" json
  |> Yojson.Safe.Util.to_list
  |> List.filter_map (function
       | `String value -> Some value
       | _ -> None)

let test_runtime_validation_rejects_declarative_parse_errors () =
  let cascade_toml =
    {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[ollama.qwen3]
max-concurrent = 1

[tier.primary]
members = ["ollama.qwen3"]
strategy = "not_a_strategy"

[routes.keeper_turn]
target = "tier.primary"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  match
    Masc_mcp.Cascade_catalog_runtime.validate_path ~config_path:cascade_path ()
  with
  | Ok _ -> fail "runtime validation should reject declarative parse errors"
  | Error rejection ->
      let errors = rejection_error_messages rejection in
      check bool "runtime rejection contains parse error" true
        (List.exists
           (fun message ->
              contains ~needle:"declarative cascade parse error" message
              && contains ~needle:"not_a_strategy" message)
           errors)

let test_runtime_validation_rejects_declarative_adapter_errors () =
  let cascade_toml =
    {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[tier.broken]
members = ["ollama.qwen3"]
strategy = "failover"

[routes.keeper_turn]
target = "tier.broken"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  match
    Masc_mcp.Cascade_catalog_runtime.validate_path ~config_path:cascade_path ()
  with
  | Ok _ -> fail "runtime validation should reject declarative adapter errors"
  | Error rejection ->
      let errors = rejection_error_messages rejection in
      check bool "runtime rejection contains adapter error" true
        (List.exists
           (fun message ->
              contains ~needle:"declarative cascade adapter error" message
              && contains ~needle:"Binding_resolution_failed" message)
           errors)

let test_runtime_validation_rejects_deprecated_profile_names () =
  let cascade_toml =
    {|
[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.qwen3]
api-name = "qwen3:8b"
max-context = 32768
tools-support = true

[ollama.qwen3]
max-concurrent = 1

[tier.local_only]
members = ["ollama.qwen3"]
strategy = "failover"

[tier-group.local_only]
tiers = ["local_only"]

[routes.keeper_turn]
target = "tier-group.local_only"
|}
  in
  with_temp_config_dir cascade_toml @@ fun ~config_root:_ ~cascade_path ->
  match
    Masc_mcp.Cascade_catalog_runtime.validate_path ~config_path:cascade_path ()
  with
  | Ok _ -> fail "runtime validation should reject deprecated cascade profile names"
  | Error rejection ->
      let errors = rejection_error_messages rejection in
      check bool "runtime rejection contains deprecated profile name" true
        (List.exists
           (fun message ->
              contains ~needle:"deprecated cascade profile name" message
              && contains ~needle:"local_only" message)
           errors)

let test_cascade_name_rejects_system_only_catalog_entry () =
  with_temp_config_dir minimal_cascade_profile_metadata_toml
  @@ fun ~config_root:_ ~cascade_path:_ ->
  let result =
    with_temp_toml
      "[keeper]\nname = \"testkeeper\"\ncascade_name = \"scoring\"\n"
      KTP.load_keeper_toml
  in
  match result with
  | Ok _ -> fail "system-only cascade_name should be rejected"
  | Error e ->
      check bool "error mentions system-only" true
        (contains ~needle:"system-only" e)

let test_tool_access_accepts_dispatch () =
  let result =
    with_temp_toml
      "[keeper]\nname = \"taskmaster\"\n\n[keeper.tool_access]\nkind = \"preset\"\npreset = \"dispatch\"\n"
      KTP.load_keeper_toml
  in
  match result with
  | Error e -> fail (Printf.sprintf "dispatch should be accepted: %s" e)
  | Ok (_loaded_name, defaults) ->
      check (option string) "dispatch preset parsed" (Some "dispatch")
        defaults.tool_preset

(** Reject [network_mode = "bogus"] at TOML load time so invalid strings
    do not silently fall back to persona defaults. *)
let test_network_mode_rejects_unknown () =
  let result =
    with_temp_toml
      "[keeper]\nname = \"nettest\"\nnetwork_mode = \"bogus\"\n"
      KTP.load_keeper_toml
  in
  match result with
  | Ok _ -> fail "network_mode=bogus should be rejected"
  | Error e ->
      let lowered = String.lowercase_ascii e in
      check bool "error mentions invalid network_mode" true
        (contains ~needle:"invalid network_mode" lowered);
      check bool "error lists canonical values" true
        (contains ~needle:"allowed: none, inherit" lowered)

(** Reject [network_mode = "host"] instead of treating it as an alias.
    The closed network_mode enum is [none | inherit]; Docker's "--network host"
    is an execution detail derived downstream from [Network_inherit]. *)
let test_network_mode_rejects_host_alias () =
  let result =
    with_temp_toml
      "[keeper]\nname = \"hosttest\"\nsandbox_profile = \"docker\"\n\
       network_mode = \"host\"\n"
      KTP.load_keeper_toml
  in
  match result with
  | Ok _ -> fail "network_mode=host should be rejected"
  | Error e ->
      let lowered = String.lowercase_ascii e in
      check bool "error mentions invalid network_mode" true
        (contains ~needle:"invalid network_mode" lowered);
      check bool "error mentions raw host value" true
        (contains ~needle:"'host'" lowered);
      check bool "error lists canonical values" true
        (contains ~needle:"allowed: none, inherit" lowered);
      check bool "error does not mention deprecated alias" false
        (contains ~needle:"deprecated alias" lowered)

(** Regression: classify_toml_failure_reason must bucket raw error strings
    into a small cardinality set so the Prometheus label set stays bounded. *)
let test_classify_toml_failure_reason_buckets () =
  let f = KTP.classify_toml_failure_reason in
  check string "invalid network_mode" "invalid_network_mode"
    (f "invalid network_mode 'bogus' (allowed: none, inherit)");
  check string "invalid sandbox_profile" "invalid_sandbox_profile"
    (f "invalid sandbox_profile 'lol' (allowed: local, docker)");
  check string "unknown field" "unknown_field"
    (f "unknown field 'legacy_scope'");
  check string "parse error" "parse_error"
    (f "parse error at line 3");
  check string "expected key parse error" "parse_error"
    (f "line 62: expected key = value");
  check string "uncategorized" "other" (f "completely novel problem")

let test_keeper_toml_config_errors_are_typed () =
  let dir = Filename.temp_file "keeper_config_errors_" "" in
  Sys.remove dir;
  Unix.mkdir dir 0o755;
  let invalid_path = Filename.concat dir "broken.toml" in
  let valid_path = Filename.concat dir "valid.toml" in
  Fun.protect
    ~finally:(fun () ->
      (try Sys.remove invalid_path with _ -> ());
      (try Sys.remove valid_path with _ -> ());
      try Unix.rmdir dir with _ -> ())
    (fun () ->
      write_file invalid_path "[keeper]\nname = \"broken\"\n\"dangling\"\n";
      write_file valid_path "[keeper]\nname = \"valid\"\n";
      match KTP.keeper_toml_config_errors_in_dir dir with
      | [ err ] ->
          check string "keeper name" "broken" err.keeper_name;
          check string "path" invalid_path err.path;
          check string "reason" "parse_error" err.reason;
          let json = KTP.keeper_toml_config_error_to_json err in
          check string "terminal reason" "config_parse_failed"
            (Yojson.Safe.Util.member "terminal_reason" json
             |> Yojson.Safe.Util.to_string)
      | errors ->
          fail
            (Printf.sprintf "expected one typed config error, got %d"
               (List.length errors)))

let () =
  run "Keeper TOML Config Validation"
    [
      ( "config/keepers",
        [
          test_case "all toml files parse" `Quick
            (fun () -> with_repo_config_dir test_all_keeper_tomls_parse);
          test_case "named keepers default to docker" `Quick
            (fun () -> with_repo_config_dir test_named_keeper_docker_defaults);
          test_case "committed keepers can do PR work" `Quick
            (fun () ->
              with_repo_config_dir test_committed_keepers_are_pr_work_capable);
          test_case "base instructions avoid hidden shell names" `Quick
            (fun () ->
              with_repo_config_dir
                test_base_config_avoids_hidden_shell_tool_names);
          test_case "verifier hides worker lifecycle tools" `Quick
            (fun () ->
              with_repo_config_dir
                test_verifier_config_hides_worker_lifecycle_tools);
        ] );
      ( "cascade_name validation",
        [
          test_case "rejects unknown cascade_name" `Quick
            test_cascade_name_rejects_unknown;
          test_case "accepts known cascade names" `Quick
            test_cascade_name_accepts_known;
          test_case "accepts reserved tool lane without live catalog" `Quick
            test_cascade_name_accepts_tool_lane_without_catalog;
          test_case "accepts live catalog entry" `Quick
            test_cascade_name_accepts_catalog_entry;
          test_case "resolves declarative profile model strings" `Quick
            test_resolve_model_strings_reads_declarative_profile;
          test_case "resolves custom provider ids through protocol" `Quick
            test_resolve_model_strings_uses_provider_protocol_for_custom_id;
          test_case "derives profile metadata from cascade.toml" `Quick
            test_cascade_profile_metadata_from_toml;
          test_case "accepts unrouted assignable catalog entry" `Quick
            test_cascade_name_accepts_unrouted_assignable_catalog_entry;
          test_case "assignability follows preferred qualified profile" `Quick
            test_keeper_assignability_uses_preferred_qualified_profile;
          test_case "fallback preserves qualified source profile" `Quick
            test_fallback_cascade_preserves_qualified_source_profile;
          test_case "fallback returns canonical tier target" `Quick
            test_fallback_cascade_returns_canonical_tier_target;
          test_case "declared public names canonicalize to catalog members" `Quick
            test_normalize_declared_name_canonicalizes_public_catalog_members;
          test_case "keeper runtime ignores non-keeper route target" `Quick
            test_keeper_runtime_declared_name_ignores_non_keeper_route_target;
          test_case "surfaces declarative adapter errors" `Quick
            test_catalog_validator_surfaces_adapter_errors;
          test_case "surfaces declarative parse errors" `Quick
            test_catalog_validator_surfaces_declarative_parse_errors;
          test_case "runtime rejects declarative parse errors" `Quick
            test_runtime_validation_rejects_declarative_parse_errors;
          test_case "runtime rejects declarative adapter errors" `Quick
            test_runtime_validation_rejects_declarative_adapter_errors;
          test_case "runtime rejects deprecated profile names" `Quick
            test_runtime_validation_rejects_deprecated_profile_names;
          test_case "rejects system-only catalog entry" `Quick
            test_cascade_name_rejects_system_only_catalog_entry;
          test_case "accepts dispatch tool_access preset" `Quick
            test_tool_access_accepts_dispatch;
        ] );
      ( "network_mode validation",
        [
          test_case "rejects unknown network_mode" `Quick
            test_network_mode_rejects_unknown;
          test_case "rejects host network_mode alias" `Quick
            test_network_mode_rejects_host_alias;
          test_case "classifies failures into bounded label set" `Quick
            test_classify_toml_failure_reason_buckets;
          test_case "surfaces typed config parse errors" `Quick
            test_keeper_toml_config_errors_are_typed;
        ] );
    ]
