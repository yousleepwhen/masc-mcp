(** Unit tests for Cascade_strategy and Cascade_client_capacity.

    Strategy ordering is pure — these tests use a synthetic record
    type plus an adapter rather than building real Provider_config.t
    values, isolating strategy behaviour from provider construction.

    Cascade_client_capacity carries process-global state via a
    Hashtbl; each test that mutates the registry calls [unregister_all]
    in setup. *)

open Alcotest
module S = Masc_mcp.Cascade_strategy
module H = Masc_mcp.Cascade_health_tracker
module C = Masc_mcp.Cascade_client_capacity
module CH = Masc_mcp.Cascade_client_capacity_history
module ST = Masc_mcp.Cascade_strategy_trace
module Kcp = Masc_mcp.Keeper_cascade_profile
module T = Masc_mcp.Cascade_throttle
module Cascade_state = Masc_mcp.Cascade_state
module DC = Masc_mcp.Dashboard_cascade
module Json = Yojson.Safe.Util

(* ── Test fixture ────────────────────────────────────────────── *)

type cand = {
  name : string;          (* health key *)
  url : string;           (* capacity key *)
}

let mk_cand ?(url = "http://test/" ^ "x") name =
  { name; url = url ^ name }

let adapter : cand S.adapter = {
  health_key = (fun c -> c.name);
  capacity_key = (fun c -> c.url);
}

let names cands = List.map (fun c -> c.name) cands

let mk_capacity_info ~total ~active = {
  T.total;
  process_active = active;
  process_available = max 0 (total - active);
  process_queue_length = 0;
  source = Llm_provider.Provider_throttle.Fallback;
}

let json_string key json =
  match Json.member key json with
  | `String value -> value
  | value ->
    failf "expected JSON string field %s, got %s"
      key (Yojson.Safe.to_string value)
;;

let json_int key json =
  match Json.member key json with
  | `Int value -> value
  | value ->
    failf "expected JSON int field %s, got %s" key (Yojson.Safe.to_string value)
;;

let json_float key json =
  match Json.member key json with
  | `Float value -> value
  | `Int value -> float_of_int value
  | value ->
    failf "expected JSON float field %s, got %s"
      key (Yojson.Safe.to_string value)
;;

let json_object key json =
  match Json.member key json with
  | `Assoc _ as value -> value
  | value ->
    failf "expected JSON object field %s, got %s"
      key (Yojson.Safe.to_string value)
;;

let check_nonempty_string_field key json =
  check bool (key ^ " present") true (String.length (json_string key json) > 0)
;;

(* Capacity stub: caller supplies a closure mapping URL → capacity_info. *)
let stub_capacity table url =
  try Some (List.assoc url table) with Not_found -> None

let mk_ctx ?(health = H.create ())
           ?(capacity = fun _ -> None)
           ?(now = 0.0)
           ?(rand = fun _ -> 0)
           ?(keeper_name = "")
           ?(cascade_name = "tier.test")
           () : S.signal_ctx =
  { health; capacity; now; rand_int = rand;
    keeper_name; cascade_name = Cascade_name.of_string_exn cascade_name }

let mk_t ?(cycle = S.default_cycle_policy)
         ?(tiers = [])
         kind : S.t =
  { kind; cycle; tiers }

(* ── S1 Failover ─────────────────────────────────────────────── *)

let test_failover_preserves_order () =
  let cands = [mk_cand "a"; mk_cand "b"; mk_cand "c"] in
  let ctx = mk_ctx () in
  let ordered = S.order_candidates S.failover ~adapter ~ctx ~cycle:0 cands in
  check (list string) "input order preserved"
    ["a"; "b"; "c"] (names ordered)

let test_failover_filters_cooldown () =
  let h = H.create () in
  H.record_failure h ~provider_key:"a" ();
  H.record_failure h ~provider_key:"a" ();
  H.record_failure h ~provider_key:"a" ();
  let cands = [mk_cand "a"; mk_cand "b"; mk_cand "c"] in
  let ctx = mk_ctx ~health:h () in
  let ordered = S.order_candidates S.failover ~adapter ~ctx ~cycle:0 cands in
  check (list string) "cooldown candidate removed, remaining order preserved"
    ["b"; "c"] (names ordered)

let test_failover_dedupes_full_shared_capacity_key () =
  let cands =
    [
      { name = "a"; url = "https://shared.example/v1" };
      { name = "b"; url = "https://shared.example/v1" };
      { name = "c"; url = "https://other.example/v1" };
    ]
  in
  let table =
    [
      "https://shared.example/v1", mk_capacity_info ~total:1 ~active:1;
      "https://other.example/v1", mk_capacity_info ~total:1 ~active:0;
    ]
  in
  let ctx = mk_ctx ~capacity:(stub_capacity table) () in
  let ordered = S.order_candidates S.failover ~adapter ~ctx ~cycle:0 cands in
  check (list string) "shared full capacity key represented once"
    ["a"; "c"] (names ordered)

let test_failover_keeps_available_shared_capacity_key () =
  let cands =
    [
      { name = "a"; url = "https://shared.example/v1" };
      { name = "b"; url = "https://shared.example/v1" };
    ]
  in
  let table =
    [ "https://shared.example/v1", mk_capacity_info ~total:2 ~active:0 ]
  in
  let ctx = mk_ctx ~capacity:(stub_capacity table) () in
  let ordered = S.order_candidates S.failover ~adapter ~ctx ~cycle:0 cands in
  check (list string) "available shared capacity key keeps model fallback"
    ["a"; "b"] (names ordered)

(* ── Cycle policy + backoff ───────────────────────────────────── *)

let test_default_cycle_policy_backward_compat () =
  let p = S.default_cycle_policy in
  check int "max_cycles=1 (no retry)" 1 p.max_cycles;
  check int "backoff_base_ms=500" 500 p.backoff_base_ms;
  check int "backoff_cap_ms=10000" 10_000 p.backoff_cap_ms

let test_backoff_zero_at_cycle_zero () =
  check int "cycle 0 → 0ms (no sleep before first attempt)"
    0 (S.backoff_ms S.default_cycle_policy ~cycle:0)

let test_backoff_exponential_capped () =
  let p = { S.max_cycles = 5; backoff_base_ms = 100; backoff_cap_ms = 500 } in
  check int "cycle 1 → base"          100 (S.backoff_ms p ~cycle:1);
  check int "cycle 2 → base*2"        200 (S.backoff_ms p ~cycle:2);
  check int "cycle 3 → base*4"        400 (S.backoff_ms p ~cycle:3);
  check int "cycle 4 → cap (would be 800)"
                                       500 (S.backoff_ms p ~cycle:4);
  check int "cycle 30 → cap (would overflow)"
                                       500 (S.backoff_ms p ~cycle:30)

(* ── parse_kind ───────────────────────────────────────────────── *)

let test_parse_kind_known () =
  let check_ok s expected =
    match S.parse_kind s with
    | Ok k ->
      check string ("parse " ^ s) (S.kind_to_string expected) (S.kind_to_string k)
    | Error msg -> fail (Printf.sprintf "expected Ok, got Error %s" msg)
  in
  check_ok "failover" S.Failover;
  check_ok "priority_tier" S.Priority_tier

let string_contains haystack needle =
  let nlen = String.length needle in
  let hlen = String.length haystack in
  let rec loop i =
    if i + nlen > hlen then false
    else if String.sub haystack i nlen = needle then true
    else loop (i + 1)
  in
  nlen = 0 || loop 0

let test_parse_kind_unknown () =
  match S.parse_kind "round_robin_xx" with
  | Ok _ -> fail "expected Error for unknown kind"
  | Error msg ->
    check bool "error mentions the rejected name"
      true
      (String.length msg > 0 && string_contains msg "round_robin_xx")

let test_parse_config_kind_supported () =
  check
    (list string)
    "config kind strings"
    [ "failover"; "priority_tier" ]
    S.config_kind_strings;
  let check_ok s expected =
    match S.parse_config_kind s with
    | Ok k ->
      check string ("parse config " ^ s) (S.kind_to_string expected) (S.kind_to_string k)
    | Error msg -> fail (Printf.sprintf "expected Ok, got Error %s" msg)
  in
  check_ok "failover" S.Failover;
  check_ok "priority_tier" S.Priority_tier

let test_parse_config_kind_retired_rejected () =
  let rejected =
    [ "capacity_aware"
    ; "weighted_random"
    ; "circuit_breaker_cycling"
    ; "sticky"
    ; "round_robin"
    ; "does_not_exist"
    ]
  in
  List.iter
    (fun raw ->
       match S.parse_config_kind raw with
       | Ok kind ->
         fail
           (Printf.sprintf
              "expected Error for %s, got %s"
              raw
              (S.kind_to_string kind))
       | Error msg ->
         check
           bool
           ("config error mentions supported kinds for " ^ raw)
           true
           (string_contains msg "failover" && string_contains msg "priority_tier"))
    rejected

(* ── Cascade_client_capacity ─────────────────────────────────── *)

let test_client_capacity_register_query () =
  C.unregister_all ();
  C.register ~url:"http://localhost:11434" ~max_concurrent:1;
  match C.capacity "http://localhost:11434" with
  | None -> fail "expected Some after register"
  | Some info ->
    check int "total = 1" 1 info.total;
    check int "active = 0 initially" 0 info.process_active;
    check int "available = 1 initially" 1 info.process_available

let test_client_capacity_acquire_release () =
  C.unregister_all ();
  C.register ~url:"http://x:11434" ~max_concurrent:1;
  match C.try_acquire "http://x:11434" with
  | Unregistered | Full _ -> fail "first acquire should succeed"
  | Acquired release ->
    (match C.capacity "http://x:11434" with
     | Some info -> check int "active = 1 after acquire" 1 info.process_active
     | None -> fail "capacity disappeared");
    (* Second acquire must fail. *)
    (match C.try_acquire "http://x:11434" with
     | Acquired _ -> fail "second acquire on 1-slot must fail"
     | Unregistered -> fail "endpoint unregistered unexpectedly"
     | Full _ -> ());
    release ();
    (match C.capacity "http://x:11434" with
     | Some info ->
       check int "active = 0 after release" 0 info.process_active
     | None -> fail "capacity disappeared after release")

let test_client_capacity_release_idempotent () =
  C.unregister_all ();
  C.register ~url:"http://y:11434" ~max_concurrent:1;
  match C.try_acquire "http://y:11434" with
  | Unregistered | Full _ -> fail "acquire failed"
  | Acquired release ->
    release ();
    release ();  (* second release must be a no-op, not underflow *)
    match C.capacity "http://y:11434" with
    | Some info ->
      check int "active = 0 not -1" 0 info.process_active
    | None -> fail "capacity disappeared"

let test_declared_client_capacity_registers_generic_endpoint () =
  C.unregister_all ();
  let cfg =
    Llm_provider.Provider_config.make
      ~kind:Llm_provider.Provider_config.Provider_d_compat
      ~model_id:"runpod-provider_h"
      ~base_url:"https://runpod.example/v1"
      ~internal_model_rotation_count:2
      ()
  in
  let candidate = Masc_mcp.Cascade_runtime_candidate.of_provider_config cfg in
  check (option int) "declared capacity"
    (Some 2)
    (Masc_mcp.Cascade_runtime_candidate.declared_client_capacity candidate);
  Masc_mcp.Cascade_runtime_candidate.register_declared_client_capacity candidate;
  match C.capacity "https://runpod.example/v1" with
  | None -> fail "declared endpoint capacity was not registered"
  | Some info ->
    check int "generic endpoint total" 2 info.total;
    check int "generic endpoint available" 2 info.process_available

let test_client_capacity_unregistered_url () =
  C.unregister_all ();
  check (option int) "capacity = None for unregistered URL"
    None (Option.map (fun (i : T.capacity_info) -> i.total)
            (C.capacity "http://nope:9999"));
  check bool "try_acquire = Unregistered for unregistered URL"
    true (C.try_acquire "http://nope:9999" = Unregistered)

let test_client_capacity_clamp_max () =
  C.unregister_all ();
  C.register ~url:"http://z:11434" ~max_concurrent:0;  (* clamped up to 1 *)
  match C.capacity "http://z:11434" with
  | None -> fail "register did not register"
  | Some info ->
    check int "max_concurrent <=0 clamped to 1" 1 info.total

(* The previous HTTP auto-registration tests exercised the substring-scan path
   inside [Cascade_client_capacity]. That path is gone now: callers consult the
   registered probe surface and call [Cascade_client_capacity.register]
   explicitly, so the removed auto-register functions have no test surface. *)

(* ── Phase C3: CLI sentinel auto-registration ──────────────── *)

let test_cli_auto_register_filters_sentinels () =
  C.unregister_all ();
  C.auto_register_cli_for_candidates ~capacity_keys:[
    "cli:cli_tool_d";
    "cli:cli_tool_b";
    "http://127.0.0.1:8085";  (* HTTP, not CLI *)
    "";                       (* unknown / empty *)
  ];
  let urls = C.registered_urls () in
  check bool "cli:cli_tool_d registered"
    true (List.mem "cli:cli_tool_d" urls);
  check bool "cli:cli_tool_b registered"
    true (List.mem "cli:cli_tool_b" urls);
  check bool "http URL NOT registered as CLI"
    false (List.mem "http://127.0.0.1:8085" urls);
  check bool "empty key NOT registered"
    false (List.mem "" urls)

let test_cli_register_with_override () =
  C.unregister_all ();
  C.auto_register_cli_with_override
    ~capacity_keys:["cli:cli_tool_a"]
    ~max_concurrent:3;
  match C.capacity "cli:cli_tool_a" with
  | None -> fail "expected CLI registration"
  | Some info ->
    check int "CLI override max=3" 3 info.total

let test_cli_acquire_blocks_at_cap () =
  C.unregister_all ();
  C.auto_register_cli_with_override
    ~capacity_keys:["cli:cli_tool_d"]
    ~max_concurrent:1;
  match C.try_acquire "cli:cli_tool_d" with
  | Unregistered | Full _ -> fail "first acquire should succeed"
  | Acquired release ->
    check bool "second acquire returns Full at cap"
      true
      (match C.try_acquire "cli:cli_tool_d" with
       | Full _ -> true | _ -> false);
    release ();
    check bool "after release: capacity available again"
      true
      (match C.try_acquire "cli:cli_tool_d" with
       | Acquired _ -> true | _ -> false)

let test_cli_idempotent_registration () =
  C.unregister_all ();
  C.auto_register_cli_with_override
    ~capacity_keys:["cli:cli_tool_b"] ~max_concurrent:5;
  C.auto_register_cli_for_candidates
    ~capacity_keys:["cli:cli_tool_b"];  (* should be no-op *)
  match C.capacity "cli:cli_tool_b" with
  | None -> fail "expected registration"
  | Some info ->
    check int "first override preserved (idempotent)" 5 info.total

let test_snapshot_returns_all_entries () =
  C.unregister_all ();
  C.register ~url:"http://127.0.0.1:11434" ~max_concurrent:1;
  C.register ~url:"cli:cli_tool_d" ~max_concurrent:2;
  let entries = C.snapshot () in
  check int "snapshot contains both entries" 2 (List.length entries);
  let lookup k = List.assoc_opt k entries in
  (match lookup "http://127.0.0.1:11434" with
   | Some info -> check int "http probe total" 1 info.total
   | None -> fail "http probe entry missing");
  (match lookup "cli:cli_tool_d" with
   | Some info ->
     check int "cli total" 2 info.total;
     check int "cli initial active" 0 info.process_active;
     check int "cli initial available" 2 info.process_available
   | None -> fail "cli entry missing")

let test_snapshot_reflects_active_acquires () =
  C.unregister_all ();
  C.register ~url:"cli:cli_tool_a" ~max_concurrent:2;
  match C.try_acquire "cli:cli_tool_a" with
  | Unregistered | Full _ -> fail "first acquire failed"
  | Acquired _release ->
    let entries = C.snapshot () in
    match List.assoc_opt "cli:cli_tool_a" entries with
    | None -> fail "snapshot missing entry"
    | Some info ->
      check int "active counted" 1 info.process_active;
      check int "available decremented" 1 info.process_available

let test_client_capacity_json_exposes_provenance () =
  C.unregister_all ();
  C.register ~url:"cli:cli_tool_a" ~max_concurrent:2;
  let json = DC.client_capacity_json () in
  check string "dashboard surface"
    "/api/v1/cascade/client_capacity"
    (json_string "dashboard_surface" json);
  check string "source" "cascade_client_capacity_registry"
    (json_string "source" json);
  check_nonempty_string_field "generated_at_iso" json;
  let retention = json_object "retention" json in
  check string "retention scope" "cascade_client_capacity"
    (json_string "scope" retention);
  check string "store kind" "process_registry" (json_string "store_kind" retention)

(* ── Phase B: Priority_tier (S5) ───────────────────────────── *)

let test_priority_tier_picks_first_tier () =
  let cands = [mk_cand "a"; mk_cand "b"; mk_cand "c"] in
  let strat = mk_t S.Priority_tier
      ~tiers:[["a"]; ["b"; "c"]]
  in
  let ctx = mk_ctx () in
  let ordered = S.order_candidates strat ~adapter ~ctx ~cycle:0 cands in
  check (list string) "cycle 0 → tier 0 (a only)" ["a"] (names ordered)

let test_priority_tier_advances_with_cycle () =
  let cands = [mk_cand "a"; mk_cand "b"; mk_cand "c"] in
  let strat = mk_t S.Priority_tier
      ~tiers:[["a"]; ["b"; "c"]]
  in
  let ctx = mk_ctx () in
  let cycle1 = S.order_candidates strat ~adapter ~ctx ~cycle:1 cands in
  check (list string) "cycle 1 → tier 1 (b, c)" ["b"; "c"] (names cycle1)

let test_priority_tier_clamps_overflow () =
  let cands = [mk_cand "a"; mk_cand "b"] in
  let strat = mk_t S.Priority_tier ~tiers:[["a"]; ["b"]] in
  let ctx = mk_ctx () in
  let cycle99 = S.order_candidates strat ~adapter ~ctx ~cycle:99 cands in
  check (list string) "cycle ≥ tiers count → last tier" ["b"] (names cycle99)

let test_priority_tier_capacity_filter () =
  let cands = [mk_cand "a"; mk_cand "b"] in
  let table = [
    (List.nth cands 0).url, mk_capacity_info ~total:1 ~active:1;
  ] in
  let strat = mk_t S.Priority_tier ~tiers:[["a"; "b"]] in
  let ctx = mk_ctx ~capacity:(stub_capacity table) () in
  let ordered = S.order_candidates strat ~adapter ~ctx ~cycle:0 cands in
  check (list string) "tier 0 with 'a' busy → only 'b'" ["b"] (names ordered)

let test_priority_tier_starvation_guard () =
  (* All tier candidates report capacity=0.  Without the guard the
     cascade would exit empty and surface as "all candidates filtered
     after N cycle(s)"; with it, the tier list itself is returned so
     at least one real call is attempted. *)
  let cands = [mk_cand "a"; mk_cand "b"] in
  let table = [
    (List.nth cands 0).url, mk_capacity_info ~total:1 ~active:1;
    (List.nth cands 1).url, mk_capacity_info ~total:1 ~active:1;
  ] in
  let strat = mk_t S.Priority_tier ~tiers:[["a"; "b"]] in
  let ctx = mk_ctx ~capacity:(stub_capacity table) () in
  let ordered = S.order_candidates strat ~adapter ~ctx ~cycle:0 cands in
  check (list string) "all-busy tier → fall through with tier list"
    ["a"; "b"] (names ordered)

let test_priority_tier_starvation_guard_dedupes_full_shared_capacity_key () =
  let cands =
    [
      { name = "a"; url = "https://shared.example/v1" };
      { name = "b"; url = "https://shared.example/v1" };
    ]
  in
  let table =
    [ "https://shared.example/v1", mk_capacity_info ~total:1 ~active:1 ]
  in
  let strat = mk_t S.Priority_tier ~tiers:[["a"; "b"]] in
  let ctx = mk_ctx ~capacity:(stub_capacity table) () in
  let ordered = S.order_candidates strat ~adapter ~ctx ~cycle:0 cands in
  check (list string) "all-busy shared key → one representative"
    ["a"] (names ordered)

(* ── Cascade_state auto-rotation primitive ───────────────────── *)

let test_cascade_state_round_robin_negative_bound () =
  Cascade_state.clear_all ();
  let v = Cascade_state.rotate_round_robin ~cascade:"x" ~bound:0 in
  check int "bound<=0 → returns 0" 0 v;
  let v2 = Cascade_state.rotate_round_robin ~cascade:"x" ~bound:(-3) in
  check int "negative bound → returns 0" 0 v2

(* ── Client capacity history (Phase D follow-up) ─────────── *)

let test_history_record_snapshot_roundtrip () =
  CH.clear ();
  CH.record { ts = 1000.0; key = "cli:cli_tool_d";
              kind = Acquired; active_after = 1 };
  CH.record { ts = 1001.0; key = "cli:cli_tool_d";
              kind = Released; active_after = 0 };
  CH.record { ts = 1002.0; key = "http://127.0.0.1:11434";
              kind = Rejected_full; active_after = 1 };
  let events = CH.snapshot () in
  check int "3 events recorded" 3 (List.length events);
  (* Newest-first ordering: ts=1002 must come first. *)
  (match events with
   | e0 :: e1 :: e2 :: [] ->
     check (float 0.0) "newest ts=1002" 1002.0 e0.ts;
     check (float 0.0) "middle ts=1001" 1001.0 e1.ts;
     check (float 0.0) "oldest ts=1000" 1000.0 e2.ts;
     check bool "newest kind = Rejected_full"
       true (e0.kind = CH.Rejected_full);
     check bool "middle kind = Released"
       true (e1.kind = CH.Released);
     check bool "oldest kind = Acquired"
       true (e2.kind = CH.Acquired)
   | _ -> fail "expected exactly 3 events")

let test_history_ring_buffer_drops_oldest () =
  CH.clear ();
  let cap = CH.capacity () in
  (* Record cap+5 events; oldest 5 must be dropped. *)
  for i = 0 to cap + 4 do
    CH.record { ts = float_of_int i;
                key = "cli:x";
                kind = Acquired;
                active_after = i }
  done;
  check int "count clamped to capacity" cap (CH.size ());
  let events = CH.snapshot ~limit:(cap + 10) () in
  check int "snapshot count = capacity" cap (List.length events);
  (* Newest must be ts=cap+4.  Oldest retained must be ts=5
     (i.e. the first 5 inserts at ts=0..4 were overwritten). *)
  (match events with
   | [] -> fail "expected at least one event"
   | newest :: _ ->
     check (float 0.0) "newest ts = cap+4"
       (float_of_int (cap + 4)) newest.ts);
  let oldest = List.nth events (cap - 1) in
  check (float 0.0) "oldest retained ts = 5 (earlier 5 dropped)"
    5.0 oldest.ts

let test_history_snapshot_kind_filter () =
  CH.clear ();
  CH.record { ts = 1.0; key = "cli:cli_tool_d";
              kind = Acquired; active_after = 1 };
  CH.record { ts = 2.0; key = "http://127.0.0.1:11434";
              kind = Acquired; active_after = 1 };
  CH.record { ts = 3.0; key = "http://other.example/api";
              kind = Rejected_full; active_after = 0 };
  CH.record { ts = 4.0; key = "cli:cli_tool_b";
              kind = Released; active_after = 0 };
  (* cli filter → 2 events, both cli:* keys *)
  let cli_events = CH.snapshot ~kind:"cli" () in
  check int "cli filter → 2 events" 2 (List.length cli_events);
  List.iter
    (fun e ->
       check string "cli filter matches classify_key"
         "cli" (CH.classify_key e.CH.key))
    cli_events;
  (* http_probe filter -> 1 event for the registered probe URL. *)
  let http_probe_events = CH.snapshot ~kind:"http_probe" () in
  check int "http_probe filter -> 1 event" 1 (List.length http_probe_events);
  (* other filter → 1 event for http://other *)
  let other_events = CH.snapshot ~kind:"other" () in
  check int "other filter → 1 event" 1 (List.length other_events);
  (* Unknown kind → empty list *)
  let unknown = CH.snapshot ~kind:"no_such_kind" () in
  check int "unknown kind → empty" 0 (List.length unknown)

let test_history_try_acquire_records_events () =
  CH.clear ();
  C.unregister_all ();
  C.register ~url:"cli:cli_tool_d" ~max_concurrent:1;
  (* First acquire → Acquired recorded *)
  (match C.try_acquire "cli:cli_tool_d" with
   | Unregistered | Full _ -> fail "first acquire should succeed"
   | Acquired release ->
     (* Second acquire → Rejected_full recorded *)
     check bool "second acquire hits cap"
       true
       (match C.try_acquire "cli:cli_tool_d" with
        | Full _ -> true | _ -> false);
     release ();
     let events = CH.snapshot () in
     (* Expected newest-first: Released, Rejected_full, Acquired. *)
     check int "3 events recorded" 3 (List.length events);
     (match events with
      | r :: f :: a :: [] ->
        check bool "newest = Released" true (r.kind = CH.Released);
        check int "released active_after = 0" 0 r.active_after;
        check bool "middle = Rejected_full"
          true (f.kind = CH.Rejected_full);
        check int "rejected active_after = 1" 1 f.active_after;
        check bool "oldest = Acquired" true (a.kind = CH.Acquired);
        check int "acquired active_after = 1" 1 a.active_after;
        check string "all keys = cli:cli_tool_d"
          "cli:cli_tool_d" a.key
      | _ -> fail "expected 3 events"))

let test_try_acquire_unregistered_returns_unregistered () =
  C.unregister_all ();
  match C.try_acquire "http://never-registered.example/api" with
  | Unregistered -> ()
  | Acquired _ -> fail "unregistered URL should not acquire"
  | Full _ -> fail "unregistered URL should not report Full"

let test_try_acquire_full_returns_retry_after () =
  C.unregister_all ();
  C.register ~url:"cli:test_retry" ~max_concurrent:1;
  let acquired =
    match C.try_acquire "cli:test_retry" with
    | Acquired r -> r
    | Unregistered | Full _ -> fail "first acquire should succeed"
  in
  (* Capacity is now saturated. *)
  (match C.try_acquire "cli:test_retry" with
   | Full { retry_after_s } ->
     check (option (float 0.01)) "retry_after_s is Some 5.0"
       (Some 5.0) retry_after_s
   | Acquired _ -> fail "second acquire should be Full"
   | Unregistered -> fail "registered URL should not be Unregistered");
  acquired ()

let test_try_acquire_full_then_release_then_acquire () =
  C.unregister_all ();
  C.register ~url:"cli:test_cycle" ~max_concurrent:1;
  let release1 =
    match C.try_acquire "cli:test_cycle" with
    | Acquired r -> r
    | Unregistered | Full _ -> fail "first acquire should succeed"
  in
  (* Exhaust capacity. *)
  (match C.try_acquire "cli:test_cycle" with
   | Full _ -> ()
   | Acquired _ -> fail "should be Full at cap"
   | Unregistered -> fail "registered URL should not be Unregistered");
  (* Release and re-acquire. *)
  release1 ();
  (match C.try_acquire "cli:test_cycle" with
   | Acquired r -> r ()
   | Unregistered | Full _ -> fail "re-acquire after release should succeed");
  (* Verify counter is back to zero via snapshot. *)
  match C.snapshot () with
  | (url, info) :: _ when url = "cli:test_cycle" ->
    check int "active after full cycle" 0 info.process_active
  | _ -> ()

(* ── Prometheus counter coverage (LT-6) ──────────────────

   The counter increment runs outside the ring-buffer mutex and uses the
   same (kind, key_type) labels as the JSON projection.  We exercise the
   full surface in-memory by scraping Masc_mcp.Prometheus.to_prometheus_text
   after a record() call.  The counter value check is >= rather than = so
   the test is robust to other cases in the suite touching the same metric. *)

let counter_value_from_text text kind key_type =
  (* Scan lines of the form:
       masc_cascade_capacity_events_total{kind="acquired",key_type="cli"} 3.0
     and return the numeric value for the matching label pair.  Returns
     [None] when the line is missing. *)
  let target_kind = Printf.sprintf {|kind="%s"|} kind in
  let target_key  = Printf.sprintf {|key_type="%s"|} key_type in
  let lines = String.split_on_char '\n' text in
  let matching =
    List.filter (fun line ->
      String.length line > 0
      && String.length line >= String.length "masc_cascade_capacity_events_total"
      && String.sub line 0 (String.length "masc_cascade_capacity_events_total")
         = "masc_cascade_capacity_events_total"
      && (let has s =
            let nlen = String.length s in
            let llen = String.length line in
            let rec f i =
              if i + nlen > llen then false
              else if String.sub line i nlen = s then true
              else f (i + 1)
            in f 0
          in has target_kind && has target_key))
      lines
  in
  match matching with
  | [] -> None
  | line :: _ ->
    (* Last whitespace-separated token is the value. *)
    let parts = String.split_on_char ' ' line in
    (match List.rev parts with
     | v :: _ -> float_of_string_opt (String.trim v)
     | [] -> None)

let test_history_prometheus_counter_increments () =
  CH.clear ();
  let before =
    counter_value_from_text
      (Masc_mcp.Prometheus.to_prometheus_text ()) "acquired" "cli"
    |> Option.value ~default:0.0
  in
  CH.record { ts = 1.0; key = "cli:cli_tool_d";
              kind = Acquired; active_after = 1 };
  CH.record { ts = 2.0; key = "cli:cli_tool_b";
              kind = Acquired; active_after = 1 };
  CH.record { ts = 3.0; key = "http://127.0.0.1:11434";
              kind = Rejected_full; active_after = 1 };
  let text = Masc_mcp.Prometheus.to_prometheus_text () in
  let cli_acquired =
    counter_value_from_text text "acquired" "cli"
    |> Option.value ~default:0.0
  in
  let http_probe_rejected =
    counter_value_from_text text "rejected_full" "http_probe"
    |> Option.value ~default:0.0
  in
  check bool "cli/acquired counter advanced by >= 2"
    true (cli_acquired >= before +. 2.0);
  check bool "http_probe/rejected_full counter advanced by >= 1"
    true (http_probe_rejected >= 1.0)

let test_history_json_exposes_provenance () =
  CH.clear ();
  CH.record { ts = 10.0; key = "cli:cli_tool_a";
              kind = Acquired; active_after = 1 };
  let json = DC.client_capacity_history_json ~limit:1 ~kind:"cli" ~since_ts:1.0 () in
  check string "dashboard surface"
    "/api/v1/cascade/client_capacity/history"
    (json_string "dashboard_surface" json);
  check string "source" "cascade_client_capacity_history_ring"
    (json_string "source" json);
  check_nonempty_string_field "generated_at_iso" json;
  let retention = json_object "retention" json in
  check string "retention scope" "cascade_client_capacity_history"
    (json_string "scope" retention);
  check string "store kind" "process_ring_buffer"
    (json_string "store_kind" retention);
  check int "ring capacity" (CH.capacity ()) (json_int "ring_capacity" retention);
  let query = json_object "query" json in
  check int "query limit" 1 (json_int "limit" query);
  check string "query kind" "cli" (json_string "kind" query);
  check (float 0.0) "query since_ts" 1.0 (json_float "since_ts" query)

(* ── Strategy decision trace (LT-5) ─────────────────── *)

let mk_trace_event ?(ts = 0.0) ?(cascade_name = "tier.primary")
    ?(strategy = "failover") ?(cycle = 0) ?(candidates_in = 3)
    ?(candidates_out = 3) ?(backoff_ms = 0) ?(kind = ST.Ordered)
    ?trace_id ?(confidence_score = None) () =
  { ST.ts; cascade_name = Cascade_name.of_string_exn cascade_name; strategy; cycle;
    candidates_in; candidates_out;
    backoff_ms; kind; trace_id; confidence_score }

let test_trace_record_snapshot_roundtrip () =
  ST.clear ();
  ST.record (mk_trace_event ~ts:1000.0 ~cycle:0 ~kind:ST.Ordered ());
  ST.record (mk_trace_event ~ts:1001.0 ~cycle:1
               ~candidates_out:0 ~backoff_ms:500 ~kind:ST.Filtered_empty ());
  ST.record (mk_trace_event ~ts:1002.0 ~cycle:2
               ~candidates_out:0 ~kind:ST.Exhausted ());
  let events = ST.snapshot () in
  check int "3 events recorded" 3 (List.length events);
  (match events with
   | e0 :: e1 :: e2 :: [] ->
     check (float 0.0) "newest ts" 1002.0 e0.ts;
     check (float 0.0) "middle ts" 1001.0 e1.ts;
     check (float 0.0) "oldest ts" 1000.0 e2.ts;
     check bool "newest kind Exhausted" true (e0.kind = ST.Exhausted);
     check bool "middle kind Filtered_empty" true (e1.kind = ST.Filtered_empty);
     check bool "oldest kind Ordered" true (e2.kind = ST.Ordered)
   | _ -> fail "expected 3 events")

let test_trace_cascade_filter () =
  ST.clear ();
  ST.record (mk_trace_event ~cascade_name:"tier.primary" ~ts:1.0 ());
  ST.record (mk_trace_event ~cascade_name:"tier.nick0cave" ~ts:2.0 ());
  ST.record (mk_trace_event ~cascade_name:"tier.primary" ~ts:3.0 ());
  let unified = ST.snapshot ~cascade:"tier.primary" () in
  check int "primary → 2 events" 2 (List.length unified);
  List.iter
    (fun e ->
      check string "cascade filter" "tier.primary"
        (Cascade_name.to_string e.ST.cascade_name))
    unified;
  let missing = ST.snapshot ~cascade:"tier.does_not_exist" () in
  check int "missing cascade → empty" 0 (List.length missing)

let test_trace_ring_drops_oldest () =
  ST.clear ();
  let cap = ST.capacity () in
  for i = 0 to cap + 4 do
    ST.record (mk_trace_event ~ts:(float_of_int i) ~cycle:i ())
  done;
  check int "count clamped to capacity" cap (ST.size ());
  let events = ST.snapshot ~limit:(cap + 10) () in
  check int "snapshot count = capacity" cap (List.length events);
  (match events with
   | newest :: _ ->
     check (float 0.0) "newest ts = cap+4" (float_of_int (cap + 4)) newest.ts
   | [] -> fail "expected events");
  let oldest = List.nth events (cap - 1) in
  check (float 0.0) "oldest retained ts = 5" 5.0 oldest.ts

let test_trace_limit_clamp () =
  ST.clear ();
  for i = 0 to 9 do
    ST.record (mk_trace_event ~ts:(float_of_int i) ())
  done;
  let five = ST.snapshot ~limit:5 () in
  check int "limit 5" 5 (List.length five);
  let zero = ST.snapshot ~limit:0 () in
  check int "limit 0 → empty" 0 (List.length zero);
  let huge = ST.snapshot ~limit:9999 () in
  check int "limit>count clamps to count" 10 (List.length huge)

let test_trace_kind_labels () =
  check string "ordered" "ordered" (ST.kind_to_string ST.Ordered);
  check string "filtered_empty" "filtered_empty"
    (ST.kind_to_string ST.Filtered_empty);
  check string "exhausted" "exhausted" (ST.kind_to_string ST.Exhausted)

(* ── Prometheus counter coverage (LT-7) ────────────────── *)

let find_strategy_counter_value text ~cascade ~strategy ~kind =
  let target_cascade = Printf.sprintf {|cascade="%s"|} cascade in
  let target_strategy = Printf.sprintf {|strategy="%s"|} strategy in
  let target_kind = Printf.sprintf {|kind="%s"|} kind in
  let prefix = "masc_cascade_strategy_decisions_total" in
  let plen = String.length prefix in
  let has haystack needle =
    let nlen = String.length needle in
    let hlen = String.length haystack in
    let rec loop i =
      if i + nlen > hlen then false
      else if String.sub haystack i nlen = needle then true
      else loop (i + 1)
    in loop 0
  in
  let lines = String.split_on_char '\n' text in
  let matching =
    List.filter (fun line ->
      String.length line >= plen
      && String.sub line 0 plen = prefix
      && has line target_cascade
      && has line target_strategy
      && has line target_kind)
      lines
  in
  match matching with
  | [] -> None
  | line :: _ ->
    (match List.rev (String.split_on_char ' ' line) with
     | v :: _ -> float_of_string_opt (String.trim v)
     | [] -> None)

let test_trace_prometheus_counter_increments () =
  ST.clear ();
  let before =
    find_strategy_counter_value
      (Masc_mcp.Prometheus.to_prometheus_text ())
      ~cascade:"tier.primary" ~strategy:"failover" ~kind:"ordered"
    |> Option.value ~default:0.0
  in
  ST.record (mk_trace_event ~cascade_name:"tier.primary"
               ~strategy:"failover" ~kind:ST.Ordered ());
  ST.record (mk_trace_event ~cascade_name:"tier.primary"
               ~strategy:"failover" ~kind:ST.Ordered ());
  ST.record (mk_trace_event ~cascade_name:"tier.nick0cave"
               ~strategy:"priority_tier"
               ~kind:ST.Filtered_empty ~backoff_ms:500 ());
  let text = Masc_mcp.Prometheus.to_prometheus_text () in
  let ordered =
    find_strategy_counter_value text
      ~cascade:"tier.primary" ~strategy:"failover" ~kind:"ordered"
    |> Option.value ~default:0.0
  in
  let filtered =
    find_strategy_counter_value text
      ~cascade:"tier.nick0cave" ~strategy:"priority_tier"
      ~kind:"filtered_empty"
    |> Option.value ~default:0.0
  in
  check bool "primary/failover/ordered advanced by >= 2"
    true (ordered >= before +. 2.0);
  check bool "nick0cave/priority_tier/filtered_empty >= 1"
    true (filtered >= 1.0)

let test_strategy_trace_json_exposes_provenance () =
  ST.clear ();
  ST.record
    (mk_trace_event ~cascade_name:"tier.primary" ~strategy:"failover"
       ~kind:ST.Ordered ());
  let json = DC.strategy_trace_json ~limit:1 ~cascade:"tier.primary" () in
  check string "dashboard surface" "/api/v1/cascade/strategy_trace"
    (json_string "dashboard_surface" json);
  check string "source" "cascade_strategy_trace_ring" (json_string "source" json);
  check_nonempty_string_field "generated_at_iso" json;
  let retention = json_object "retention" json in
  check string "retention scope" "cascade_strategy_trace"
    (json_string "scope" retention);
  check string "store kind" "process_ring_buffer"
    (json_string "store_kind" retention);
  check int "ring capacity" (ST.capacity ()) (json_int "ring_capacity" retention);
  let query = json_object "query" json in
  check int "query limit" 1 (json_int "limit" query);
  check string "query cascade" "tier.primary" (json_string "cascade" query)

let test_audit_runs_json_exposes_provenance () =
  let base_path =
    Filename.concat (Filename.get_temp_dir_name ()) "masc-cascade-provenance-test"
  in
  let json =
    DC.audit_runs_json ~base_path ~limit:2 ~cascade:"keeper_unified" ()
  in
  check string "dashboard surface" "/api/v1/cascade/audit_runs"
    (json_string "dashboard_surface" json);
  check string "source" "cascade_audit_jsonl" (json_string "source" json);
  check_nonempty_string_field "generated_at_iso" json;
  let retention = json_object "retention" json in
  check string "retention scope" "cascade_audit_runs"
    (json_string "scope" retention);
  check string "store kind" "dated_jsonl" (json_string "store_kind" retention);
  check string "durable store"
    (Filename.concat (Filename.concat base_path ".masc") "cascade_audit")
    (json_string "durable_store" retention);
  let query = json_object "query" json in
  check int "query limit" 2 (json_int "limit" query);
  check string "query cascade" "keeper_unified" (json_string "cascade" query)

let () =
  run "cascade_strategy" [
    "failover", [
      test_case "preserves order" `Quick test_failover_preserves_order;
      test_case "filters cooldown" `Quick test_failover_filters_cooldown;
      test_case "dedupes full shared capacity key" `Quick
        test_failover_dedupes_full_shared_capacity_key;
      test_case "keeps available shared capacity key" `Quick
        test_failover_keeps_available_shared_capacity_key;
    ];
    "cycle_policy", [
      test_case "default policy backward-compat" `Quick
        test_default_cycle_policy_backward_compat;
      test_case "backoff zero at cycle 0" `Quick
        test_backoff_zero_at_cycle_zero;
      test_case "backoff exponential, capped" `Quick
        test_backoff_exponential_capped;
    ];
    "parse_kind", [
      test_case "known kinds parse" `Quick test_parse_kind_known;
      test_case "unknown kind returns Error with name" `Quick
        test_parse_kind_unknown;
      test_case "config kinds parse" `Quick test_parse_config_kind_supported;
      test_case "retired config kinds rejected" `Quick
        test_parse_config_kind_retired_rejected;
    ];
    "client_capacity", [
      test_case "register + query" `Quick test_client_capacity_register_query;
      test_case "acquire + release lifecycle" `Quick
        test_client_capacity_acquire_release;
      test_case "release is idempotent" `Quick
        test_client_capacity_release_idempotent;
      test_case "declared generic endpoint capacity registers" `Quick
        test_declared_client_capacity_registers_generic_endpoint;
      test_case "unregistered URL returns None" `Quick
        test_client_capacity_unregistered_url;
      test_case "max_concurrent <= 0 clamped to 1" `Quick
        test_client_capacity_clamp_max;
      test_case "cli sentinel auto-register filters non-CLI" `Quick
        test_cli_auto_register_filters_sentinels;
      test_case "cli auto_register override sets max" `Quick
        test_cli_register_with_override;
      test_case "cli acquire blocks at cap, releases freely" `Quick
        test_cli_acquire_blocks_at_cap;
      test_case "cli registration is idempotent" `Quick
        test_cli_idempotent_registration;
      test_case "snapshot returns all registered entries" `Quick
        test_snapshot_returns_all_entries;
      test_case "snapshot reflects active acquires" `Quick
        test_snapshot_reflects_active_acquires;
      test_case "dashboard JSON exposes provenance" `Quick
        test_client_capacity_json_exposes_provenance;
    ];
    "priority_tier", [
      test_case "cycle 0 picks first tier" `Quick
        test_priority_tier_picks_first_tier;
      test_case "cycle advances with tier index" `Quick
        test_priority_tier_advances_with_cycle;
      test_case "cycle overflow clamps to last tier" `Quick
        test_priority_tier_clamps_overflow;
      test_case "tier respects capacity filter" `Quick
        test_priority_tier_capacity_filter;
      test_case "all-busy tier falls through (starvation guard)" `Quick
        test_priority_tier_starvation_guard;
      test_case "all-busy shared key falls through once" `Quick
        test_priority_tier_starvation_guard_dedupes_full_shared_capacity_key;
    ];
    "cascade_state", [
      test_case "round_robin bound<=0 returns 0" `Quick
        test_cascade_state_round_robin_negative_bound;
    ];
    "client_capacity_history", [
      test_case "record + snapshot roundtrip newest-first" `Quick
        test_history_record_snapshot_roundtrip;
      test_case "ring buffer drops oldest when full" `Quick
        test_history_ring_buffer_drops_oldest;
      test_case "snapshot kind filter" `Quick
        test_history_snapshot_kind_filter;
      test_case "try_acquire records events on registered URL" `Quick
        test_history_try_acquire_records_events;
      test_case "try_acquire unregistered returns Unregistered" `Quick
        test_try_acquire_unregistered_returns_unregistered;
      test_case "try_acquire Full carries retry_after_sec" `Quick
        test_try_acquire_full_returns_retry_after;
      test_case "try_acquire release cycle frees slot" `Quick
        test_try_acquire_full_then_release_then_acquire;
      test_case "record bumps Prometheus counter with label" `Quick
        test_history_prometheus_counter_increments;
      test_case "dashboard JSON exposes provenance" `Quick
        test_history_json_exposes_provenance;
    ];
    "strategy_trace", [
      test_case "record + snapshot newest-first" `Quick
        test_trace_record_snapshot_roundtrip;
      test_case "cascade filter scopes events" `Quick
        test_trace_cascade_filter;
      test_case "ring buffer drops oldest" `Quick
        test_trace_ring_drops_oldest;
      test_case "limit clamp" `Quick
        test_trace_limit_clamp;
      test_case "kind_to_string serialisation" `Quick
        test_trace_kind_labels;
      test_case "record bumps Prometheus counter with labels" `Quick
        test_trace_prometheus_counter_increments;
      test_case "dashboard JSON exposes provenance" `Quick
        test_strategy_trace_json_exposes_provenance;
      test_case "audit runs JSON exposes provenance" `Quick
        test_audit_runs_json_exposes_provenance;
    ];
  ]
