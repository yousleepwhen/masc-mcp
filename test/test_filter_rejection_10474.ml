(** #10474: pin the [classify_rejection] vocabulary that feeds
    [masc_cascade_filter_rejection_total]. Each branch represents
    a distinct operator action, so collapsing the labels would
    erase the actionable diagnosis ("swap to stdio MCP" vs "add
    a header-capable provider" vs "fix cascade authoring"). *)

open Alcotest
module P = Masc_mcp.Provider_tool_support
module L = Llm_provider

(* Build a minimal Provider_config; [Provider_config.make] applies
   per-kind defaults so the test stays aligned with capability
   resolution (which keys off [kind] + [model_id]). *)
let make_provider ~kind ~model_id =
  L.Provider_config.make ~kind ~model_id ~base_url:"" ()

let agent_code = make_provider ~kind:Cli_tool_a ~model_id:"model-d-5.4"
let provider_c = make_provider ~kind:Cli_tool_c ~model_id:"model-c-coding"

let policy_with_headers : L.Llm_transport.runtime_mcp_policy =
  { L.Llm_transport.empty_runtime_mcp_policy with
    servers = [
      L.Llm_transport.Http_server {
        name = "test_http";
        url = "https://example/mcp";
        headers = [ ("authorization", "Bearer x") ];
      }
    ];
  }

let policy_with_masc_identity_headers : L.Llm_transport.runtime_mcp_policy =
  { L.Llm_transport.empty_runtime_mcp_policy with
    servers = [
      L.Llm_transport.Http_server {
        name = "masc";
        url = "https://example/mcp";
        headers = [
          ("x-masc-agent-name", "keeper-sangsu-agent");
          ("x-masc-keeper-name", "sangsu");
        ];
      }
    ];
  }

let policy_without_headers : L.Llm_transport.runtime_mcp_policy =
  { L.Llm_transport.empty_runtime_mcp_policy with
    servers = [
      L.Llm_transport.Stdio_server {
        name = "test_stdio";
        command = "mcp";
        args = [];
        env = [];
      }
    ];
  }

let label = function
  | Some r -> P.rejection_reason_label r
  | None -> "<accepted>"

(* Cli_tool_a has tool_policy=no_tool_http_headers; so it cannot satisfy
   a runtime_mcp_policy that needs headers. *)
let test_codex_blocked_by_headers () =
  let r =
    P.classify_rejection ~runtime_mcp_policy:policy_with_headers
      ~require_tool_choice_support:true ~require_tool_support:true agent_code
  in
  check string "cli_tool_a rejected with header-required policy"
    "runtime_mcp_http_headers_required" (label r)

(* Cli_tool_c advertises request-scoped runtime MCP HTTP-header support,
   so a header-bearing policy is accepted through the runtime MCP lane. *)
let test_kimi_accepts_headers () =
  let r =
    P.classify_rejection ~runtime_mcp_policy:policy_with_headers
      ~require_tool_choice_support:true ~require_tool_support:true provider_c
  in
  check string "cli_tool_c accepted with header-required policy"
    "<accepted>" (label r)

(* Cli_tool_a cannot carry arbitrary request-scoped headers, but the
   provider-normalized MASC identity headers are safe to keep. They are
   enough for the MASC server to disambiguate the caller when ambient auth
   supplies the bearer token. *)
let test_codex_accepts_masc_identity_headers () =
  let r =
    P.classify_rejection ~runtime_mcp_policy:policy_with_masc_identity_headers
      ~require_tool_choice_support:true ~require_tool_support:true agent_code
  in
  check string "cli_tool_a accepted with MASC identity headers"
    "<accepted>" (label r)

(* Without HTTP headers in the policy, cli_tool_c passes via runtime
   mcp because it has runtime_mcp_tools=true. *)
let test_kimi_passes_stdio_policy () =
  let r =
    P.classify_rejection ~runtime_mcp_policy:policy_without_headers
      ~require_tool_choice_support:true ~require_tool_support:true provider_c
  in
  check string "cli_tool_c accepted with stdio-only policy"
    "<accepted>" (label r)

let test_filter_disabled () =
  let r =
    P.classify_rejection
      ~require_tool_choice_support:false ~require_tool_support:false agent_code
  in
  check string "filter disabled returns None"
    "<accepted>" (label r)

let () =
  run "filter_rejection_10474" [
    ("classify_rejection", [
        test_case "cli_tool_a blocked by HTTP-headers policy" `Quick
          test_codex_blocked_by_headers;
        test_case "cli_tool_c accepts HTTP-headers policy" `Quick
          test_kimi_accepts_headers;
        test_case "cli_tool_a accepts MASC identity headers" `Quick
          test_codex_accepts_masc_identity_headers;
        test_case "cli_tool_c passes stdio-only policy" `Quick
          test_kimi_passes_stdio_policy;
        test_case "filter disabled bypasses classification" `Quick
          test_filter_disabled;
      ]);
  ]
