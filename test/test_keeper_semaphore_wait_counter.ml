(* #9771: pin canonical metric name + label vocabulary for the
   semaphore wait timeout counter.  The keepalive path emits this
   counter at three sites:
     - [autonomous_queue_head]: fairness FIFO head wait exceeded
     - [autonomous]: autonomous-track semaphore acquire timeout
     - [turn]: shared turn semaphore acquire timeout

   Test exercises the counter directly so the metric vocabulary
   is pinned independently of the surrounding Eio + semaphore
   plumbing. *)

module KHL = Masc_mcp.Keeper_heartbeat_loop
module KTS = Masc_mcp.Keeper_turn_slot
module KT = Masc_mcp.Keeper_types

let counter_for ~keeper ~channel =
  Masc_mcp.Prometheus.metric_value_or_zero
    Masc_mcp.Keeper_metrics.(to_string SemaphoreWaitTimeout)
    ~labels:[
      ("keeper", keeper);
      ("channel", channel);
    ]
    ()

let queue_depth_for ~channel =
  Masc_mcp.Prometheus.metric_value_or_zero
    Masc_mcp.Keeper_metrics.(to_string TurnQueueDepth)
    ~labels:[ ("channel", channel) ]
    ()

let semaphore_wait_bucket_for ~keeper_name ~cascade_profile ~channel ~le =
  Masc_mcp.Prometheus.metric_value_or_zero
    Masc_mcp.Keeper_metrics.(to_string SemaphoreWaitSecondsBucket)
    ~labels:[
      ("keeper_name", keeper_name);
      ("cascade_profile", cascade_profile);
      ("channel", channel);
      ("le", le);
    ]
    ()

let make_meta name =
  match
    Masc_test_deps.meta_of_json_fixture
      (`Assoc [
         ("name", `String name);
         ("agent_name", `String name);
         ("trace_id", `String ("trace-" ^ name));
       ])
  with
  | Ok meta -> meta
  | Error err -> Alcotest.fail ("make_meta failed: " ^ err)

let contains_substring haystack needle =
  let haystack_len = String.length haystack in
  let needle_len = String.length needle in
  let rec loop idx =
    if needle_len = 0 then true
    else if idx + needle_len > haystack_len then false
    else if String.sub haystack idx needle_len = needle then true
    else loop (idx + 1)
  in
  loop 0

let make_timeout ?queue_ahead ?(holders = []) phase :
    KTS.semaphore_wait_timeout =
  { KTS.timeout_wait_sec = 180.0;
    timeout_phase = phase;
    timeout_autonomous_available = 6;
    timeout_reactive_available = 4;
    timeout_turn_available = 12;
    timeout_queue_depth = 9;
    timeout_queue_ahead = queue_ahead;
    timeout_holders = holders;
  }

let test_metric_name_stable () =
  Alcotest.(check string)
    "semaphore wait timeout canonical metric name"
    "masc_keeper_semaphore_wait_timeout_total"
    Masc_mcp.Keeper_metrics.(to_string SemaphoreWaitTimeout);
  Alcotest.(check string)
    "turn queue depth canonical metric name"
    "masc_keeper_turn_queue_depth"
    Masc_mcp.Keeper_metrics.(to_string TurnQueueDepth);
  Alcotest.(check string)
    "semaphore wait seconds canonical metric name"
    "masc_keeper_semaphore_wait_seconds"
    Masc_mcp.Keeper_metrics.(to_string SemaphoreWaitSeconds);
  Alcotest.(check string)
    "semaphore wait seconds bucket canonical metric name"
    "masc_keeper_semaphore_wait_seconds_bucket"
    Masc_mcp.Keeper_metrics.(to_string SemaphoreWaitSecondsBucket)

let test_autonomous_queue_depth_gauge_tracks_fifo () =
  Eio_main.run @@ fun _env ->
  let module KK = Masc_mcp.Keeper_keepalive in
  KK.reset_autonomous_turn_queue_for_test ();
  Alcotest.(check (float 0.0001))
    "reset records zero depth"
    0.0
    (queue_depth_for ~channel:"autonomous_queue");
  let first = KK.enqueue_autonomous_waiter_for_test "alpha-depth" in
  Alcotest.(check (float 0.0001))
    "first enqueue records depth"
    1.0
    (queue_depth_for ~channel:"autonomous_queue");
  let second = KK.enqueue_autonomous_waiter_for_test "beta-depth" in
  Alcotest.(check (float 0.0001))
    "second enqueue records depth"
    2.0
    (queue_depth_for ~channel:"autonomous_queue");
  KK.drop_autonomous_waiter_for_test first;
  Alcotest.(check (float 0.0001))
    "drop records reduced depth"
    1.0
    (queue_depth_for ~channel:"autonomous_queue");
  KK.drop_autonomous_waiter_for_test second;
  Alcotest.(check (float 0.0001))
    "final drop records zero depth"
    0.0
    (queue_depth_for ~channel:"autonomous_queue")

let test_successful_acquire_emits_wait_seconds_buckets () =
  Eio_main.run @@ fun _env ->
  let module KK = Masc_mcp.Keeper_keepalive in
  let keeper_name = "wait-histogram-keeper-0506" in
  let cascade_profile = "wait-histogram-cascade-0506" in
  let channel = "scheduled_autonomous" in
  KK.reset_autonomous_completion_for_test ();
  KK.reset_autonomous_turn_queue_for_test ();
  let before_inf =
    semaphore_wait_bucket_for ~keeper_name ~cascade_profile ~channel ~le:"+Inf"
  in
  let before_60 =
    semaphore_wait_bucket_for ~keeper_name ~cascade_profile ~channel ~le:"60"
  in
  (match
     KK.with_keeper_turn_slot_for_test
       ~cascade_profile
       ~keeper_name
       ~channel:Masc_mcp.Keeper_world_observation.Scheduled_autonomous
       (fun ~semaphore_wait_ms:_ -> ())
   with
   | Ok () -> ()
   | Error (`Semaphore_wait_timeout _) ->
       Alcotest.fail "unexpected semaphore wait timeout");
  Alcotest.(check (float 0.0001))
    "+Inf bucket increments"
    (before_inf +. 1.0)
    (semaphore_wait_bucket_for ~keeper_name ~cascade_profile ~channel ~le:"+Inf");
  Alcotest.(check (float 0.0001))
    "60s bucket increments"
    (before_60 +. 1.0)
    (semaphore_wait_bucket_for ~keeper_name ~cascade_profile ~channel ~le:"60")

let test_increments_per_channel () =
  let keeper = "sangsu-test-9771" in
  let channels =
    [ "autonomous_queue_head"; "autonomous"; "turn" ]
  in
  List.iter
    (fun channel ->
      let before = counter_for ~keeper ~channel in
      Masc_mcp.Prometheus.inc_counter
        Masc_mcp.Keeper_metrics.(to_string SemaphoreWaitTimeout)
        ~labels:[ ("keeper", keeper); ("channel", channel) ]
        ();
      Alcotest.(check (float 0.0001))
        (Printf.sprintf "%s +1" channel)
        (before +. 1.0)
        (counter_for ~keeper ~channel))
    channels

let test_keeper_isolation () =
  (* Different keepers must land in different series. *)
  let channel = "autonomous" in
  let keeper_a = "alpha-9771" in
  let keeper_b = "beta-9771" in
  let before_a = counter_for ~keeper:keeper_a ~channel in
  Masc_mcp.Prometheus.inc_counter
    Masc_mcp.Keeper_metrics.(to_string SemaphoreWaitTimeout)
    ~labels:[ ("keeper", keeper_b); ("channel", channel) ]
    ();
  Alcotest.(check (float 0.0001))
    "alpha unaffected by beta"
    before_a
    (counter_for ~keeper:keeper_a ~channel)

let test_channel_isolation () =
  (* Different channels for the same keeper must not bleed. *)
  let keeper = "channel-iso-9771" in
  let before_turn = counter_for ~keeper ~channel:"turn" in
  Masc_mcp.Prometheus.inc_counter
    Masc_mcp.Keeper_metrics.(to_string SemaphoreWaitTimeout)
    ~labels:[ ("keeper", keeper); ("channel", "autonomous_queue_head") ]
    ();
  Alcotest.(check (float 0.0001))
    "turn channel unchanged when queue_head fires"
    before_turn
    (counter_for ~keeper ~channel:"turn")

let test_wait_observation_reason_labels () =
  Alcotest.(check (list string))
    "pending reactive reasons"
    [ "semaphore_wait_pending"; "peers_holding_slot"; "channel_reactive" ]
    (Masc_mcp.Keeper_heartbeat_loop.semaphore_wait_observation_reasons
       ~kind:Masc_mcp.Keeper_heartbeat_loop.Semaphore_wait_pending
       ~channel:Masc_mcp.Keeper_world_observation.Reactive
       ());
  Alcotest.(check (list string))
    "timeout scheduled autonomous reasons"
    [
      "semaphore_wait_timeout";
      "peers_holding_slot";
      "channel_scheduled_autonomous";
      "class_slot_wait_timeout";
    ]
    (Masc_mcp.Keeper_heartbeat_loop.semaphore_wait_observation_reasons
       ~kind:Masc_mcp.Keeper_heartbeat_loop.Semaphore_wait_timeout
       ~channel:Masc_mcp.Keeper_world_observation.Scheduled_autonomous
       ());
  Alcotest.(check (list string))
    "timeout reasons can carry precise phase"
    [
      "semaphore_wait_timeout";
      "phase_autonomous_queue_head";
      "channel_scheduled_autonomous";
      "class_admission_queue_wait_timeout";
    ]
    (Masc_mcp.Keeper_heartbeat_loop.semaphore_wait_observation_reasons
       ~phase_label:"autonomous_queue_head"
       ~kind:Masc_mcp.Keeper_heartbeat_loop.Semaphore_wait_timeout
       ~channel:Masc_mcp.Keeper_world_observation.Scheduled_autonomous
       ())

let test_cascade_backpressure_decision () =
  let blocked_resilience : Masc_mcp.Keeper_cascade_resilience.cascade_resilience =
    {
      ok = false;
      cascade_name = "tier-group.provider_k-coding-with-spark";
      model_labels = [ "ollama.ollama-local-default.recovery" ];
      pure_local = true;
      fallback_cascade = None;
      blocker = Some "pure_local_single_provider_no_fallback";
      error = None;
      hint = Some "local-only guard is active";
    }
  in
  let unhealthy =
    KHL.cascade_backpressure_decision
      ~cascade_resilience:None
      ~should_run_turn:true
      ~cascade_name:"primary"
      ~cascade_status:(Masc_mcp.Keeper_health_probe.Unhealthy "failure_ratio")
  in
  (match unhealthy with
   | KHL.Cascade_backpressured { cascade_name; reason } ->
     Alcotest.(check string) "cascade name" "primary" cascade_name;
     Alcotest.(check string) "reason" "failure_ratio" reason
   | KHL.Cascade_admitted -> Alcotest.fail "unhealthy cascade was admitted");
  (match
     KHL.cascade_backpressure_decision
       ~cascade_resilience:None
       ~should_run_turn:true
       ~cascade_name:"primary"
       ~cascade_status:Masc_mcp.Keeper_health_probe.Healthy
   with
   | KHL.Cascade_admitted -> ()
   | KHL.Cascade_backpressured _ -> Alcotest.fail "healthy cascade was blocked");
  (match
     KHL.cascade_backpressure_decision
       ~cascade_resilience:None
       ~should_run_turn:true
       ~cascade_name:"primary"
       ~cascade_status:Masc_mcp.Keeper_health_probe.Unknown
   with
   | KHL.Cascade_admitted -> ()
   | KHL.Cascade_backpressured _ -> Alcotest.fail "unknown cascade was blocked");
  (match
     KHL.cascade_backpressure_decision
       ~cascade_resilience:(Some blocked_resilience)
       ~should_run_turn:true
       ~cascade_name:"tier-group.provider_k-coding-with-spark"
       ~cascade_status:Masc_mcp.Keeper_health_probe.Healthy
   with
   | KHL.Cascade_backpressured { cascade_name; reason } ->
     Alcotest.(check string)
       "resilience cascade name"
       "tier-group.provider_k-coding-with-spark"
       cascade_name;
     Alcotest.(check string)
       "resilience reason"
       "cascade_resilience_pure_local_single_provider_no_fallback"
       reason
   | KHL.Cascade_admitted -> Alcotest.fail "bad cascade resilience was admitted");
  match
    KHL.cascade_backpressure_decision
      ~cascade_resilience:(Some blocked_resilience)
      ~should_run_turn:false
      ~cascade_name:"primary"
      ~cascade_status:(Masc_mcp.Keeper_health_probe.Unhealthy "failure_ratio")
  with
  | KHL.Cascade_admitted -> ()
  | KHL.Cascade_backpressured _ ->
    Alcotest.fail "already-skipped turn was reclassified"

let test_cascade_backpressure_reason_labels () =
  Alcotest.(check (list string))
    "cascade backpressure reasons"
    [ "cascade_backpressure"; "cascade_unhealthy"; "reason_failure_ratio_" ]
    (KHL.cascade_backpressure_observation_reasons ~reason:"Failure Ratio!");
  Alcotest.(check (list string))
    "provider dns cascade backpressure reasons"
    [
      "cascade_backpressure";
      "cascade_unhealthy";
      "class_provider_dns_failure";
      "reason_failure_ratio_provider_dns_failure";
    ]
    (KHL.cascade_backpressure_observation_reasons
       ~reason:"failure_ratio:provider_dns_failure");
  Alcotest.(check (list string))
    "cascade resilience backpressure reasons"
    [
      "cascade_backpressure";
      "cascade_resilience";
      "reason_cascade_resilience_pure_local_single_provider_no_fallback";
    ]
    (KHL.cascade_backpressure_observation_reasons
       ~reason:"cascade_resilience_pure_local_single_provider_no_fallback")

let test_cascade_backpressure_updates_registry () =
  let base_path =
    Filename.concat
      (Filename.get_temp_dir_name ())
      (Printf.sprintf "masc-cascade-backpressure-observation-%d" (Unix.getpid ()))
  in
  let keeper = "cascade-backpressure-153xx" in
  Masc_mcp.Keeper_registry.unregister ~base_path keeper;
  let meta = make_meta keeper in
  ignore (Masc_mcp.Keeper_registry.register ~base_path keeper meta);
  Fun.protect
    ~finally:(fun () -> Masc_mcp.Keeper_registry.unregister ~base_path keeper)
    (fun () ->
       let before =
         match Masc_mcp.Keeper_registry.get ~base_path keeper with
         | Some entry -> entry.meta.runtime.usage.last_turn_ts
         | None -> Alcotest.fail "registered keeper missing before observation"
       in
       KHL.record_cascade_backpressure_observation
         ~base_path
         ~keeper_name:keeper
         ~reason:"failure_ratio";
       match Masc_mcp.Keeper_registry.get ~base_path keeper with
       | Some
           { Masc_mcp.Keeper_registry.last_skip_observation = Some (_, reasons)
           ; meta = updated_meta
           ; _
           } ->
         Alcotest.(check (list string))
           "backpressure stamped for watchdog routing"
           [ "cascade_backpressure"; "cascade_unhealthy"; "reason_failure_ratio" ]
           reasons;
         Alcotest.(check bool)
           "last_turn_ts touched"
           true
           (updated_meta.runtime.usage.last_turn_ts >= before)
       | Some _ -> Alcotest.fail "last_skip_observation was not stamped"
       | None -> Alcotest.fail "registered keeper missing")

let test_queue_head_timeout_diagnostic_names_fifo_blocker () =
  let timeout = make_timeout ~queue_ahead:3 KTS.Autonomous_queue_head in
  let blocker_class = KHL.semaphore_wait_timeout_blocker_class timeout in
  Alcotest.(check string)
    "queue head maps to admission queue blocker"
    "admission_queue_wait_timeout"
    (KT.blocker_class_to_string blocker_class);
  let persisted, log_diagnostic =
    KHL.semaphore_wait_timeout_diagnostics ~cascade_name:"queue-cascade" timeout
  in
  Alcotest.(check bool)
    "persisted detail names fifo blocker"
    true
    (contains_substring persisted "queue_blocker=autonomous_fifo");
  Alcotest.(check bool)
    "persisted detail records queue ahead"
    true
    (contains_substring persisted "queue_ahead=3");
  Alcotest.(check bool)
    "log diagnostic names queue head"
    true
    (contains_substring log_diagnostic
       "queue_head=[blocker=autonomous_fifo ahead=3 depth=9]");
  Alcotest.(check bool)
    "queue head diagnostic does not claim missing holders"
    false
    (contains_substring log_diagnostic "holders=[none]")

let test_autonomous_slot_timeout_keeps_holder_diagnostic () =
  let timeout =
    make_timeout
      ~holders:[ ("slot-holder-a", 181.4); ("slot-holder-b", 12.0) ]
      KTS.Autonomous_slot
  in
  let blocker_class = KHL.semaphore_wait_timeout_blocker_class timeout in
  Alcotest.(check string)
    "slot timeout maps to autonomous slot blocker"
    "autonomous_slot_wait_timeout"
    (KT.blocker_class_to_string blocker_class);
  let _persisted, log_diagnostic =
    KHL.semaphore_wait_timeout_diagnostics ~cascade_name:"slot-cascade" timeout
  in
  Alcotest.(check bool)
    "slot diagnostic still names holders"
    true
    (contains_substring log_diagnostic "holders=[slot-holder-a/181s")

let test_wait_observation_updates_registry_skip_stamp () =
  let base_path =
    Filename.concat (Filename.get_temp_dir_name ())
      (Printf.sprintf "masc-semaphore-wait-observation-%d" (Unix.getpid ()))
  in
  let keeper = "wait-observation-9771" in
  Masc_mcp.Keeper_registry.unregister ~base_path keeper;
  let meta = make_meta keeper in
  ignore (Masc_mcp.Keeper_registry.register ~base_path keeper meta);
  Fun.protect
    ~finally:(fun () ->
      Masc_mcp.Keeper_registry.unregister ~base_path keeper)
    (fun () ->
      Masc_mcp.Keeper_heartbeat_loop.record_semaphore_wait_observation
        ~base_path
        ~keeper_name:keeper
        ~channel:Masc_mcp.Keeper_world_observation.Reactive
        ~kind:Masc_mcp.Keeper_heartbeat_loop.Semaphore_wait_pending
        ();
      match Masc_mcp.Keeper_registry.get ~base_path keeper with
      | Some { Masc_mcp.Keeper_registry.last_skip_observation = Some (_, reasons); _ } ->
        Alcotest.(check (list string))
          "pending wait stamped for watchdog suppression"
          [ "semaphore_wait_pending"; "peers_holding_slot"; "channel_reactive" ]
          reasons
      | Some _ -> Alcotest.fail "last_skip_observation was not stamped"
      | None -> Alcotest.fail "registered keeper missing")

let test_provider_timeout_observation_reason_labels () =
  Alcotest.(check (list string))
    "provider timeout watchdog reasons"
    [
      "provider_runtime_error";
      "provider_timeout";
      "keeper_turn_retry_backoff";
    ]
    Masc_mcp.Keeper_heartbeat_loop.provider_timeout_observation_reasons

let test_provider_timeout_observation_updates_registry () =
  let base_path =
    Filename.concat (Filename.get_temp_dir_name ())
      (Printf.sprintf "masc-oas-timeout-observation-%d" (Unix.getpid ()))
  in
  let keeper = "oas-timeout-observation-12431" in
  Masc_mcp.Keeper_registry.unregister ~base_path keeper;
  let meta = make_meta keeper in
  ignore (Masc_mcp.Keeper_registry.register ~base_path keeper meta);
  Fun.protect
    ~finally:(fun () ->
      Masc_mcp.Keeper_registry.unregister ~base_path keeper)
    (fun () ->
      let before =
        match Masc_mcp.Keeper_registry.get ~base_path keeper with
        | Some entry -> entry.meta.runtime.usage.last_turn_ts
        | None -> Alcotest.fail "registered keeper missing before observation"
      in
      Masc_mcp.Keeper_heartbeat_loop.record_provider_timeout_observation
        ~base_path
        ~keeper_name:keeper;
      match Masc_mcp.Keeper_registry.get ~base_path keeper with
      | Some { Masc_mcp.Keeper_registry.last_skip_observation = Some (_, reasons);
               meta = updated_meta; _ } ->
        Alcotest.(check (list string))
          "provider timeout stamped for watchdog routing"
          Masc_mcp.Keeper_heartbeat_loop.provider_timeout_observation_reasons
          reasons;
        Alcotest.(check bool)
          "last_turn_ts touched"
          true
          (updated_meta.runtime.usage.last_turn_ts >= before)
      | Some _ -> Alcotest.fail "last_skip_observation was not stamped"
      | None -> Alcotest.fail "registered keeper missing")

let () =
  Alcotest.run "keeper_semaphore_wait_counter_9771" [
    "metric_name", [
      Alcotest.test_case "canonical name stable" `Quick
        test_metric_name_stable;
    ];
    "queue_depth", [
      Alcotest.test_case "autonomous FIFO depth gauge tracks queue" `Quick
        test_autonomous_queue_depth_gauge_tracks_fifo;
    ];
    "histogram", [
      Alcotest.test_case "successful acquire emits wait buckets" `Quick
        test_successful_acquire_emits_wait_seconds_buckets;
    ];
    "counter", [
      Alcotest.test_case "all 3 channels increment" `Quick
        test_increments_per_channel;
    ];
    "isolation", [
      Alcotest.test_case "keepers isolated" `Quick test_keeper_isolation;
      Alcotest.test_case "channels isolated" `Quick test_channel_isolation;
    ];
    "watchdog_observation", [
      Alcotest.test_case "reason labels are stable" `Quick
        test_wait_observation_reason_labels;
      Alcotest.test_case "cascade backpressure blocks unhealthy" `Quick
        test_cascade_backpressure_decision;
      Alcotest.test_case "cascade backpressure labels are stable" `Quick
        test_cascade_backpressure_reason_labels;
      Alcotest.test_case "cascade backpressure registry stamp is updated" `Quick
        test_cascade_backpressure_updates_registry;
      Alcotest.test_case
        "queue-head timeout names fifo blocker, not empty holders" `Quick
        test_queue_head_timeout_diagnostic_names_fifo_blocker;
      Alcotest.test_case
        "slot timeout keeps holder diagnostic" `Quick
        test_autonomous_slot_timeout_keeps_holder_diagnostic;
      Alcotest.test_case "registry skip stamp is updated" `Quick
        test_wait_observation_updates_registry_skip_stamp;
      Alcotest.test_case "oas timeout labels are stable" `Quick
        test_provider_timeout_observation_reason_labels;
      Alcotest.test_case "oas timeout registry stamp is updated" `Quick
        test_provider_timeout_observation_updates_registry;
    ];
  ]
