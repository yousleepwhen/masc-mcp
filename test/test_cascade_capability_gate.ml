(** test_cascade_capability_gate — Provider ceiling validation/clamping.

    Verifies TLA+ KeeperCoreTriad.CapabilityGate (S3 invariant):
    requested_max_tokens never exceeds provider ceiling before dispatch. *)

open Alcotest

module CI = Masc_mcp.Cascade_inference
module CE = Masc_mcp.Cascade_error_classify
module CR = Masc_mcp.Cascade_runtime
module P = Masc_mcp.Prometheus

let cascade_name = Cascade_name.of_string_exn "route.keeper_turn"
let metric_max_tokens_clamped = "masc_cascade_max_tokens_clamped_total"

let validate ?(provider_ceiling = Some 40960) max_tokens =
  CI.validate_max_tokens_within_ceiling
    ~cascade_name
    ~provider_ceiling
    max_tokens

let check_violation expected_reason expected_requested expected_ceiling = function
  | Error
      (CE.Max_tokens_ceiling_violation
         { cascade_name; requested_max_tokens; provider_ceiling; reason }) ->
    check
      string
      "cascade_name"
      "route.keeper_turn"
      (CE.cascade_name_to_string cascade_name);
    check int "requested_max_tokens" expected_requested requested_max_tokens;
    check int "provider_ceiling" expected_ceiling provider_ceiling;
    check string "reason" expected_reason reason
  | Error _ -> fail "expected max_tokens ceiling violation"
  | Ok value -> failf "expected validation error, got Ok %d" value

let check_ok label expected = function
  | Ok actual -> check int label expected actual
  | Error _ -> failf "expected Ok %d" expected

let test_clamp_above_ceiling () =
  validate 65536
  |> check_ok "above ceiling clamped to provider ceiling" 40960

let test_allow_below_ceiling () =
  let result = validate ~provider_ceiling:(Some 131072) 32768 in
  check_ok "32768 accepted (below ceiling)" 32768 result

let test_allow_equal_ceiling () =
  let result = validate ~provider_ceiling:(Some 32768) 32768 in
  check_ok "equal to ceiling accepted" 32768 result

let test_allow_no_ceiling () =
  let result = validate ~provider_ceiling:None 65536 in
  check_ok "None ceiling accepted" 65536 result

let test_reject_zero_ceiling () =
  validate ~provider_ceiling:(Some 0) 1024
  |> check_violation "provider_ceiling_not_positive" 1024 0

let test_reject_nonpositive_max_tokens () =
  validate 0 |> check_violation "max_tokens_not_positive" 0 40960

let test_sdk_error_round_trip_preserves_structured_violation () =
  match validate 0 with
  | Ok _ -> fail "expected validation error"
  | Error internal_error ->
    let err = CE.sdk_error_of_masc_internal_error internal_error in
    (match CE.classify_masc_internal_error err with
     | Some
         (CE.Max_tokens_ceiling_violation
            { requested_max_tokens; provider_ceiling; reason; _ }) ->
       check int "requested round trip" 0 requested_max_tokens;
       check int "ceiling round trip" 40960 provider_ceiling;
       check string "reason round trip" "max_tokens_not_positive" reason
     | _ -> fail "expected structured violation round trip")

let write_file path body =
  let oc = open_out_bin path in
  Fun.protect
    ~finally:(fun () -> close_out_noerr oc)
    (fun () -> output_string oc body)

let with_temp_cascade_toml body f =
  let dir = Filename.temp_file "cascade-capability-gate-" "" in
  Sys.remove dir;
  Unix.mkdir dir 0o755;
  let config_path = Filename.concat dir "cascade.toml" in
  write_file config_path body;
  let saved_config_dir = Sys.getenv_opt "MASC_CONFIG_DIR" in
  let restore_env () =
    match saved_config_dir with
    | Some value -> Unix.putenv "MASC_CONFIG_DIR" value
    | None -> Unix.putenv "MASC_CONFIG_DIR" ""
  in
  Unix.putenv "MASC_CONFIG_DIR" dir;
  Config_dir_resolver.reset ();
  Masc_mcp.Cascade_catalog_runtime.reset_cache_for_tests ();
  Fun.protect
    ~finally:(fun () ->
      restore_env ();
      Config_dir_resolver.reset ();
      Masc_mcp.Cascade_catalog_runtime.reset_cache_for_tests ();
      Sys.remove config_path;
      Unix.rmdir dir)
    f

let test_cascade_output_cap_not_context_window () =
  with_temp_cascade_toml
    {|
[providers.remote]
protocol = "provider_d-http"
endpoint = "https://example.test/v1"

[models.long]
api-name = "remote-long"
max-context = 200000
tools-support = true

[models.long.capabilities]
max-output-tokens = 8192

[remote.long]
max-concurrent = 1

[tier.primary]
members = ["remote.long"]

[routes.keeper_turn]
target = "tier.primary"
|}
    (fun () ->
      let ceiling = CR.max_output_tokens_ceiling_of_cascade_name cascade_name in
      check (option int) "output ceiling" (Some 8192) ceiling;
      CI.validate_max_tokens_within_ceiling ~cascade_name
        ~provider_ceiling:ceiling 65536
      |> check_ok "above output ceiling clamped" 8192)

let test_public_cap_helper_clamps_caller_override_to_cascade_ceiling () =
  with_temp_cascade_toml
    {|
[providers.remote]
protocol = "provider_d-http"
endpoint = "https://example.test/v1"

[models.narrow]
api-name = "remote-narrow"
max-context = 200000
tools-support = true

[models.narrow.capabilities]
max-output-tokens = 8192

[remote.narrow]
max-concurrent = 1

[tier.primary]
members = ["remote.narrow"]

[routes.keeper_turn]
target = "tier.primary"
|}
    (fun () ->
      let ceiling = CR.max_output_tokens_ceiling_of_cascade_name cascade_name in
      check (option int) "output ceiling" (Some 8192) ceiling;
      let capped =
        CI.cap_max_tokens_to_cascade_ceiling
          ~cascade_name
          ~source:"caller_override"
          16384
      in
      check int "caller override capped to ceiling" 8192 capped;
      CI.validate_max_tokens_within_ceiling ~cascade_name
        ~provider_ceiling:ceiling capped
      |> check_ok "capped caller override accepted" 8192)

let test_resolve_max_tokens_caps_automatic_value_to_cascade_ceiling () =
  with_temp_cascade_toml
    {|
[providers.cli_tool_d]
protocol = "provider_a-cli"
command = "agent_llm_a"
is-non-interactive = true

[providers.cli_tool_c]
protocol = "provider_c-cli"
command = "provider_c"
is-non-interactive = true

[models.agent_llm_a-auto]
api-name = "auto"
max-context = 200000
tools-support = true

[models.agent_llm_a-auto.capabilities]
max-output-tokens = 64000

[models.provider_c-cli-coding]
api-name = "model-c-coding"
max-context = 128000
tools-support = true

[models.provider_c-cli-coding.capabilities]
max-output-tokens = 16384

[cli_tool_d.agent_llm_a-auto]
max-concurrent = 1

[cli_tool_c.provider_c-cli-coding]
max-concurrent = 1

[cli_tool_d.agent_llm_a-auto.tool_candidate]
max-output = 64000
temperature = 0.2

[cli_tool_c.provider_c-cli-coding.tool_candidate]
max-output = 64000
temperature = 0.2

[tier.strict_tool_candidates]
members = ["cli_tool_d.agent_llm_a-auto.tool_candidate", "cli_tool_c.provider_c-cli-coding.tool_candidate"]
strategy = "failover"

[tier-group.strict_tool_candidates]
tiers = ["strict_tool_candidates"]
strategy = "failover"

[routes.keeper_turn]
target = "tier-group.strict_tool_candidates"
|}
    (fun () ->
      let ceiling = CR.max_output_tokens_ceiling_of_cascade_name cascade_name in
      check (option int) "mixed cascade output ceiling" (Some 16384) ceiling;
      let before =
        P.metric_value_or_zero metric_max_tokens_clamped ()
      in
      let resolved =
        CI.resolve_max_tokens ~cascade_name ~fallback:(fun () -> 65536)
      in
      check int "automatic max_tokens capped to ceiling" 16384 resolved;
      let resolved_again =
        CI.resolve_max_tokens ~cascade_name ~fallback:(fun () -> 65536)
      in
      check int "repeat automatic max_tokens capped to ceiling" 16384 resolved_again;
      check
        (float 0.0001)
        "clamp metric counts every automatic clamp"
        (before +. 2.0)
        (P.metric_value_or_zero metric_max_tokens_clamped ());
      CI.validate_max_tokens_within_ceiling ~cascade_name
        ~provider_ceiling:ceiling resolved
      |> check_ok "capped value accepted" 16384)

let test_auto_max_tokens_clamp_warning_dedupes_by_tuple () =
  CI.For_testing.reset_auto_max_tokens_clamp_warnings ();
  check
    bool
    "first tuple logs"
    true
    (CI.For_testing.should_log_auto_max_tokens_clamp
       ~cascade_name
       ~source:"fallback"
       ~max_tokens:65536
       ~ceiling:16384);
  check
    bool
    "same tuple suppressed"
    false
    (CI.For_testing.should_log_auto_max_tokens_clamp
       ~cascade_name
       ~source:"fallback"
       ~max_tokens:65536
       ~ceiling:16384);
  check
    bool
    "different ceiling logs"
    true
    (CI.For_testing.should_log_auto_max_tokens_clamp
       ~cascade_name
       ~source:"fallback"
       ~max_tokens:65536
       ~ceiling:8192)

let test_resolve_provider_derived_max_tokens_matches_failover_ceiling () =
  with_temp_cascade_toml
    {|
[providers.cli_tool_d]
protocol = "provider_a-cli"
command = "agent_llm_a"
is-non-interactive = true

[providers.cli_tool_c]
protocol = "provider_c-cli"
command = "provider_c"
is-non-interactive = true

[providers.ollama]
protocol = "ollama-http"
endpoint = "http://localhost:11434"

[models.agent_llm_a-auto]
api-name = "auto"
max-context = 200000
tools-support = true

[models.agent_llm_a-auto.capabilities]
max-output-tokens = 64000

[models.provider_c-cli-coding]
api-name = "model-c-coding"
max-context = 128000
tools-support = true

[models.provider_c-cli-coding.capabilities]
max-output-tokens = 16384

[models.local-recovery]
api-name = "local"
max-context = 32768
tools-support = true

[models.local-recovery.capabilities]
max-output-tokens = 8192

[cli_tool_d.agent_llm_a-auto]
max-concurrent = 1

[cli_tool_c.provider_c-cli-coding]
max-concurrent = 1

[ollama.local-recovery]
max-concurrent = 1

[cli_tool_d.agent_llm_a-auto.tool_candidate]
max-output = 64000
temperature = 0.2

[cli_tool_c.provider_c-cli-coding.tool_candidate]
max-output = 16384
temperature = 0.2

[ollama.local-recovery.recovery]
max-output = 8192
temperature = 0.2

[tier.strict_tool_candidates]
members = ["cli_tool_d.agent_llm_a-auto.tool_candidate", "cli_tool_c.provider_c-cli-coding.tool_candidate"]
strategy = "failover"

[tier.recovery]
members = ["ollama.local-recovery.recovery"]
strategy = "failover"

[tier-group.strict_tool_candidates]
tiers = ["strict_tool_candidates", "recovery"]
strategy = "failover"

[routes.keeper_turn]
target = "tier-group.strict_tool_candidates"
|}
    (fun () ->
      let ceiling = CR.max_output_tokens_ceiling_of_cascade_name cascade_name in
      check (option int) "mixed cascade output ceiling" (Some 8192) ceiling;
      let resolved =
        CI.resolve_max_tokens ~cascade_name ~fallback:(fun () -> 65536)
      in
      check int "provider-derived max_tokens follows narrowest failover ceiling"
        8192
        resolved;
      CI.validate_max_tokens_within_ceiling ~cascade_name
        ~provider_ceiling:ceiling resolved
      |> check_ok "narrowest failover value accepted" 8192)

let test_resolve_tier_group_max_tokens_uses_model_capability_ceiling () =
  let cascade_name =
    Cascade_name.of_string_exn
      "tier-group.strict_tool_candidates"
  in
  with_temp_cascade_toml
    {|
[providers.runpod_mtp]
protocol = "provider_d-http"
endpoint = "https://example.test/v1"

[providers.provider_k-coding]
protocol = "provider_d-http"
endpoint = "https://provider_k.example.test/v1"

[models.qwen36-mtp]
api-name = "provider_h"
max-context = 160000
tools-support = true

[models.qwen36-mtp.capabilities]
max-output-tokens = 8192
supports-tool-choice = true

[models.provider_k-turbo]
api-name = "provider_k-5-turbo"
max-context = 128000
tools-support = true

[models.provider_k-turbo.capabilities]
max-output-tokens = 16384
supports-tool-choice = true

[runpod_mtp.qwen36-mtp]
is-default = true

[runpod_mtp.qwen36-mtp.keeper]
temperature = 0.3

[provider_k-coding.provider_k-turbo]
is-default = true

[provider_k-coding.provider_k-turbo.keeper]
max-output = 16384
temperature = 0.3

[tier.strict_tool_candidates]
members = ["runpod_mtp.qwen36-mtp.keeper", "provider_k-coding.provider_k-turbo.keeper"]
strategy = "failover"

[tier-group.strict_tool_candidates]
tiers = ["strict_tool_candidates"]
strategy = "failover"

[routes.keeper_turn]
target = "tier-group.strict_tool_candidates"
|}
    (fun () ->
      let ceiling = CR.max_output_tokens_ceiling_of_cascade_name cascade_name in
      check (option int) "tier-group output ceiling" (Some 8192) ceiling;
      let resolved =
        CI.resolve_max_tokens ~cascade_name ~fallback:(fun () -> 65536)
      in
      check int "tier-group max_tokens follows model capability ceiling" 8192
        resolved;
      CI.validate_max_tokens_within_ceiling ~cascade_name
        ~provider_ceiling:ceiling resolved
      |> check_ok "model capability capped value accepted" 8192)

let () =
  run "cascade_capability_gate" [
    "max_tokens_ceiling_validation", [
      test_case "above ceiling -> clamped" `Quick test_clamp_above_ceiling;
      test_case "below ceiling -> accepted" `Quick test_allow_below_ceiling;
      test_case "equal ceiling -> accepted" `Quick test_allow_equal_ceiling;
      test_case "no ceiling -> accepted" `Quick test_allow_no_ceiling;
      test_case "zero ceiling -> rejected" `Quick test_reject_zero_ceiling;
      test_case "nonpositive max_tokens -> rejected" `Quick test_reject_nonpositive_max_tokens;
      test_case
        "structured error round trip"
        `Quick
        test_sdk_error_round_trip_preserves_structured_violation;
      test_case
        "cascade output cap, not context window, gates max_tokens"
        `Quick
        test_cascade_output_cap_not_context_window;
      test_case
        "caller override clamp uses cascade output ceiling"
        `Quick
        test_public_cap_helper_clamps_caller_override_to_cascade_ceiling;
      test_case
        "automatic max_tokens respects mixed failover ceiling"
        `Quick
        test_resolve_max_tokens_caps_automatic_value_to_cascade_ceiling;
      test_case
        "automatic max_tokens clamp warning dedupes by tuple"
        `Quick
        test_auto_max_tokens_clamp_warning_dedupes_by_tuple;
      test_case
        "provider-derived max_tokens matches failover ceiling"
        `Quick
        test_resolve_provider_derived_max_tokens_matches_failover_ceiling;
      test_case
        "tier-group max_tokens uses model capability ceiling"
        `Quick
        test_resolve_tier_group_max_tokens_uses_model_capability_ceiling;
    ];
  ]
