open Alcotest
open Masc_mcp

let () = Server_startup_state.mark_state_ready ~backend_mode:"test"

let tuple_of_tool_result result =
  Tool_result.is_success result, Tool_result.message result
;;

let temp_dir () =
  let dir = Filename.temp_file "test_keeper_meta_listing_" "" in
  Unix.unlink dir;
  Unix.mkdir dir 0o755;
  dir

let ensure_fs env =
  if not (Fs_compat.has_fs ()) then
    Fs_compat.set_fs (Eio.Stdenv.fs env)

let with_env name value_opt f =
  let original = Sys.getenv_opt name in
  let restore () =
    match original with
    | Some value -> Unix.putenv name value
    | None -> Unix.putenv name ""
  in
  Fun.protect
    ~finally:restore
    (fun () ->
      (match value_opt with
       | Some value -> Unix.putenv name value
       | None -> Unix.putenv name "");
      f ())

let with_clean_base_path_env f =
  with_env "MASC_BASE_PATH" None @@ fun () ->
  with_env "MASC_BASE_PATH_INPUT" None @@ fun () ->
  with_env "MASC_CONFIG_DIR" None @@ fun () ->
  with_env "MASC_PERSONAS_DIR" None @@ fun () ->
  with_env "MASC_TEST_SYNCED_BASE_PATH" None @@ fun () ->
  with_env "MASC_BASE_PATH_RESOLUTION_SOURCE" None f

let cleanup_dir dir =
  let rec rm path =
    if Sys.file_exists path then
      if Sys.is_directory path then (
        Array.iter (fun name -> rm (Filename.concat path name)) (Sys.readdir path);
        Unix.rmdir path)
      else
        Unix.unlink path
  in
  try rm dir with _ -> ()

let write_json path json =
  Out_channel.with_open_bin path (fun oc ->
      output_string oc (Yojson.Safe.pretty_to_string json))

let write_file path content =
  Out_channel.with_open_bin path (fun oc -> output_string oc content)

let write_minimal_cascade_toml config_root =
  write_file
    (Filename.concat config_root "cascade.toml")
    {|[providers.custom]
protocol = "provider_d-http"
endpoint = "http://127.0.0.1:9/v1"

[models.mock]
api-name = "mock"
max-context = 128000
tools-support = true

[custom.mock]

[tier.primary]
members = ["custom.mock"]

[tier-group.primary]
tiers = ["primary"]

[routes.keeper_turn]
target = "tier-group.primary"
|}

let write_keeper_toml_exn ?autoboot_enabled config ~name =
  let keepers_dir =
    Filename.concat (Coord.masc_root_dir config) "config/keepers"
  in
  let autoboot_line =
    match autoboot_enabled with
    | Some value -> Printf.sprintf "autoboot_enabled = %b\n" value
    | None -> ""
  in
  Fs_compat.mkdir_p keepers_dir;
  Fs_compat.save_file
    (Filename.concat keepers_dir (name ^ ".toml"))
    (Printf.sprintf
       "[keeper]\n\
        goal = \"test keeper\"\n\
        sandbox_profile = \"local\"\n\
        %s\
        proactive_enabled = false\n"
       autoboot_line)

let write_keeper_persona_toml_exn ?autoboot_enabled config ~name ~persona_name =
  let keepers_dir =
    Filename.concat (Coord.masc_root_dir config) "config/keepers"
  in
  let autoboot_line =
    match autoboot_enabled with
    | Some value -> Printf.sprintf "autoboot_enabled = %b\n" value
    | None -> ""
  in
  Fs_compat.mkdir_p keepers_dir;
  Fs_compat.save_file
    (Filename.concat keepers_dir (name ^ ".toml"))
    (Printf.sprintf
       "[keeper]\n\
        persona_name = %S\n\
        goal = \"test persona keeper\"\n\
        sandbox_profile = \"local\"\n\
        %s\
        proactive_enabled = false\n"
       persona_name autoboot_line)

let write_persona_profile_exn config ~name =
  let persona_dir =
    Filename.concat
      (Filename.concat (Coord.masc_root_dir config) "config/personas")
      name
  in
  Fs_compat.mkdir_p persona_dir;
  write_json
    (Filename.concat persona_dir "profile.json")
    (`Assoc
       [
         ("name", `String name);
         ("role", `String "test persona");
         ( "keeper",
           `Assoc
	             [
	               ("goal", `String "test persona keeper");
	             ] );
       ])

let write_corrupt_keeper_meta_exn config ~name =
  write_file (Keeper_types.keeper_meta_path config name) "{not-json"

let write_keeper_meta_exn ?(autoboot_enabled = true)
    ?(social_model = "bdi_speech_v1")
    ?(last_social_transition_reason = "")
    ?(paused = false)
    ?active_goal_ids config ~name ~trace_id =
  let active_goal_ids =
    match active_goal_ids with
    | Some goal_ids -> goal_ids
    | None -> [ "goal-" ^ name ]
  in
  let json =
    `Assoc
      [
        ("name", `String name);
        ("agent_name", `String ("keeper-" ^ name ^ "-agent"));
        ("trace_id", `String trace_id);
        ("goal", `String "test keeper");
        ("social_model", `String social_model);
        ("last_social_transition_reason", `String last_social_transition_reason);
        ("autoboot_enabled", `Bool autoboot_enabled);
        ("paused", `Bool paused);
        ( "active_goal_ids",
          `List (List.map (fun goal_id -> `String goal_id) active_goal_ids) );
      ]
  in
  let meta =
    match Masc_test_deps.meta_of_json_fixture json with
    | Ok meta -> meta
    | Error e -> fail ("meta_of_json failed: " ^ e)
  in
  match Keeper_types.write_meta ~force:true config meta with
  | Ok () -> ()
  | Error e -> fail ("write_meta failed: " ^ e)

let register_keeper_offline_exn config ~name =
  match Keeper_types.read_meta config name with
  | Ok (Some meta) ->
      ignore
        (Keeper_registry.register_offline ~base_path:config.base_path name meta)
  | Ok None -> fail ("expected keeper meta for " ^ name)
  | Error e -> fail ("read_meta failed: " ^ e)

let mark_task_done_by_title config ~title ~agent_name =
  let backlog = Coord.read_backlog config in
  let seen = ref false in
  let tasks =
    List.map
      (fun (task : Masc_domain.task) ->
        if String.equal task.title title then (
          seen := true;
          {
            task with
            task_status =
              Masc_domain.Done
                {
                  assignee = agent_name;
                  completed_at = Masc_domain.now_iso ();
                  notes = Some "done";
                };
          })
        else task)
      backlog.tasks
  in
  if not !seen then fail ("expected task to mark done: " ^ title);
  Coord.write_backlog config { backlog with tasks; version = backlog.version + 1 }

let parse_json_exn body =
  try Yojson.Safe.from_string body
  with Yojson.Json_error err -> failwith ("invalid json: " ^ err)

let keeper_json_by_name json name =
  Yojson.Safe.Util.(json |> member "keepers" |> to_list)
  |> List.find_opt (fun keeper ->
         Yojson.Safe.Util.(keeper |> member "name" |> to_string = name))

let audit_item_by_name json name =
  Yojson.Safe.Util.(json |> member "items" |> to_list)
  |> List.find_opt (fun item ->
         Yojson.Safe.Util.(item |> member "name" |> to_string = name))

let string_list_of_json json =
  Yojson.Safe.Util.to_list json
  |> List.filter_map (function `String value -> Some value | _ -> None)

let keeper_ctx env sw config agent_name : _ Tool_keeper.context =
  {
    config;
    agent_name;
    sw;
    clock = Eio.Stdenv.clock env;
    proc_mgr = Some (Eio.Stdenv.process_mgr env);
    net = None;
  }

let test_read_meta_resolved_rejects_meta_aliases () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      (* See test setup: initialized state is not needed for this direct meta lookup. *)
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_meta_exn config ~name:"alpha-beta" ~trace_id:"trace-alpha";
      List.iter
        (fun alias ->
          match Keeper_types.read_meta_resolved config alias with
          | Ok None -> ()
          | Error e -> fail ("read_meta_resolved failed: " ^ e)
          | Ok (Some (resolved_name, _)) ->
            fail
              (Printf.sprintf
                 "meta alias %s unexpectedly resolved to %s"
                 alias
                 resolved_name))
        [ "alpha_beta"; "keeper-alpha-beta-agent" ])

let test_keeper_listing_ignores_sidecar_json_files () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_toml_exn config ~name:"sangsu";
      write_keeper_toml_exn config ~name:"dot.name";
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      write_keeper_meta_exn config ~name:"sangsu" ~trace_id:"trace-sangsu";
      write_keeper_meta_exn config ~name:"dot.name" ~trace_id:"trace-dot-name";
      let dataset_path =
        Filename.concat (Keeper_fs.keeper_dir config) "sangsu.dataset.json"
      in
      write_json dataset_path (`Assoc [ ("kind", `String "dataset") ]);
      let names = Keeper_types.keeper_names config in
      check (list string) "keeper_names filters sidecars"
        [ "dot.name"; "sangsu" ] names;
      let keepalive_names = Keeper_types.keepalive_keeper_names config in
      check (list string) "keepalive_keeper_names filters sidecars"
        [ "dot.name"; "sangsu" ] keepalive_names;
      let ctx = keeper_ctx env sw config "operator" in
      let ok, body =
        tuple_of_tool_result
          (Keeper_status.handle_keeper_list ctx (`Assoc [ ("limit", `Int 10) ]))
      in
      check bool "keeper status list ok" true ok;
      let json = parse_json_exn body in
      let listed =
        Yojson.Safe.Util.(json |> member "keepers" |> to_list |> filter_string)
      in
      check (list string) "status handler filters sidecars"
        [ "dot.name"; "sangsu" ] listed;
      check int "status handler count filters sidecars" 2
        Yojson.Safe.Util.(json |> member "count" |> to_int);
      let ok, body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_list"
            ~args:(`Assoc [ ("limit", `Int 10) ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_list dispatch"
      in
      check bool "tool keeper list ok without registry entries" true ok;
      let json = parse_json_exn body in
      let listed =
        Yojson.Safe.Util.(json |> member "keepers" |> to_list |> filter_string)
      in
      check (list string) "tool keeper list includes persisted keepers"
        [ "dot.name"; "sangsu" ] listed;
      check int "tool keeper list count includes persisted keepers" 2
        Yojson.Safe.Util.(json |> member "count" |> to_int);
      check int "tool keeper list rows include persisted keepers" 2
        Yojson.Safe.Util.(json |> member "items" |> to_list |> List.length);
      (* detailed=true is the primary surface for the operator dashboard
         after a server restart, so pin its behaviour explicitly:
         masc_keeper_list with detailed=true must include the persisted
         keepers even when the in-memory registry is empty. *)
      let ok_detailed, body_detailed =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_list"
            ~args:(`Assoc [ ("limit", `Int 10); ("detailed", `Bool true) ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_list dispatch (detailed)"
      in
      check bool "tool keeper list (detailed) ok without registry entries" true
        ok_detailed;
      let json_detailed = parse_json_exn body_detailed in
      let listed_detailed =
        Yojson.Safe.Util.(
          json_detailed |> member "keepers" |> to_list
          |> List.filter_map (fun row ->
                 match row |> member "name" with
                 | `String name -> Some name
                 | _ -> None))
      in
      check (list string)
        "tool keeper list (detailed) includes persisted keepers"
        [ "dot.name"; "sangsu" ] listed_detailed;
      check int
        "tool keeper list (detailed) count includes persisted keepers" 2
        Yojson.Safe.Util.(json_detailed |> member "count" |> to_int);
      check int
        "tool keeper list (detailed) rows include persisted keepers" 2
        Yojson.Safe.Util.(
          json_detailed |> member "keepers" |> to_list |> List.length))

let test_bootable_keeper_names_skip_autoboot_disabled_meta () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun _sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_toml_exn config ~name:"sangsu";
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      write_keeper_meta_exn
        ~autoboot_enabled:false config ~name:"sangsu" ~trace_id:"trace-sangsu";
      let names = Keeper_runtime.bootable_keeper_names config in
      check bool "autoboot disabled sangsu excluded from bootable list" false
        (List.mem "sangsu" names))

let test_bootable_keeper_names_use_declarative_autoboot_true_over_stale_meta () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun _sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_toml_exn ~autoboot_enabled:true config ~name:"verifier";
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      write_keeper_meta_exn
        ~autoboot_enabled:false config ~name:"verifier" ~trace_id:"trace-verifier";
      let bootable_names = Keeper_runtime.bootable_keeper_names config in
      check bool "declarative autoboot true restores bootable keeper" true
        (List.mem "verifier" bootable_names);
      let keepalive_names = Keeper_types.keepalive_keeper_names config in
      check bool "declarative autoboot true restores keepalive keeper" true
        (List.mem "verifier" keepalive_names);
      let exclusions =
        Keeper_runtime.autoboot_excluded_keeper_reasons config
        |> List.map (fun Keeper_runtime.{ keeper_name; reason } ->
          keeper_name, reason)
      in
      check (list (pair string string))
        "declarative autoboot true clears stale disabled exclusion"
        []
        exclusions)

let test_autoboot_exclusion_reasons_explain_skipped_keepers () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun _sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_toml_exn config ~name:"active";
      write_keeper_toml_exn config ~name:"disabled";
      write_keeper_toml_exn config ~name:"paused";
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      write_keeper_meta_exn
        config
        ~name:"active"
        ~trace_id:"trace-active";
      write_keeper_meta_exn
        ~autoboot_enabled:false
        config
        ~name:"disabled"
        ~trace_id:"trace-disabled";
      write_keeper_meta_exn
        ~paused:true
        config
        ~name:"paused"
        ~trace_id:"trace-paused";
      let exclusions =
        Keeper_runtime.autoboot_excluded_keeper_reasons config
        |> List.map (fun Keeper_runtime.{ keeper_name; reason } ->
          keeper_name, reason)
      in
      check (list (pair string string))
        "autoboot exclusion reasons"
        [ "disabled", "autoboot_disabled"; "paused", "paused" ]
        exclusions)

let test_declarative_autoboot_disabled_skips_boot_without_meta () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun _sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_toml_exn ~autoboot_enabled:false config ~name:"sangsu";
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      let bootable_names = Keeper_runtime.bootable_keeper_names config in
      check bool "bootable list excludes declarative autoboot-disabled keeper" false
        (List.mem "sangsu" bootable_names);
      let keepalive_names = Keeper_types.keepalive_keeper_names config in
      check bool "keepalive list excludes declarative autoboot-disabled keeper" false
        (List.mem "sangsu" keepalive_names))

let test_autoboot_policy_resync_from_declarative_toml () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun _sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Cascade_catalog_runtime.reset_cache_for_tests ();
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_toml_exn ~autoboot_enabled:false config ~name:"sangsu";
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      let cascade_path = Filename.concat config_root "cascade.toml" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      Cascade_catalog_runtime.install_snapshot_for_tests
        ~source_path:cascade_path
        ~profile_names:[ (Keeper_config.default_cascade_name ()) ];
      write_keeper_meta_exn
        ~autoboot_enabled:true config ~name:"sangsu" ~trace_id:"trace-sangsu";
      match Keeper_runtime.ensure_keeper_meta config "sangsu" with
      | Error e -> fail ("ensure_keeper_meta failed: " ^ e)
      | Ok updated ->
          check bool "autoboot_enabled resynced from TOML" false
            updated.Keeper_types.autoboot_enabled)

let test_keeper_up_uses_toml_autoboot_default () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  let keeper_name = "toml-autoboot-default" in
  Fun.protect
    ~finally:(fun () ->
      Keeper_keepalive.stop_keepalive keeper_name;
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_toml_exn ~autoboot_enabled:false config ~name:keeper_name;
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      let ctx = keeper_ctx env sw config "operator" in
      let ok, body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_up"
            ~args:(`Assoc [ ("name", `String keeper_name) ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_up dispatch"
      in
      check bool "keeper_up ok" true ok;
      match Keeper_types.read_meta config keeper_name with
      | Ok (Some meta) ->
          check bool "autoboot_enabled defaulted from TOML" false
            meta.autoboot_enabled
      | Ok None -> fail "keeper meta missing after keeper_up"
      | Error e -> fail ("read_meta failed: " ^ e))

let test_keeper_up_update_resyncs_declarative_profile_defaults () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  let keeper_name = "toml-update-defaults" in
  Fun.protect
    ~finally:(fun () ->
      Keeper_keepalive.stop_keepalive keeper_name;
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      let keepers_dir = Filename.concat config_root "keepers" in
      Fs_compat.mkdir_p keepers_dir;
      Fs_compat.save_file
        (Filename.concat keepers_dir (keeper_name ^ ".toml"))
        {|[keeper]
goal = "fresh goal"
short_goal = "fresh short"
mid_goal = "fresh mid"
long_goal = "fresh long"
instructions = "fresh instructions"
sandbox_profile = "local"
autoboot_enabled = false
proactive_enabled = true
proactive_idle_sec = 120
proactive_cooldown_sec = 240
per_provider_timeout = 120.0
tool_denylist = ["keeper_task_claim", "masc_claim_next", "masc_transition"]

[keeper.tool_access]
kind = "preset"
preset = "delivery"
also_allow = ["masc_tasks", "masc_transition"]
|};
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      let stale_meta =
        match
          Masc_test_deps.meta_of_json_fixture
            (`Assoc
              [
                ("name", `String keeper_name);
                ("agent_name", `String ("keeper-" ^ keeper_name ^ "-agent"));
                ("trace_id", `String "trace-toml-update-defaults");
                ("goal", `String "stale goal");
                ("short_goal", `String "stale short");
                ("mid_goal", `String "stale mid");
                ("long_goal", `String "stale long");
                ("instructions", `String "stale instructions");
                ("autoboot_enabled", `Bool true);
                ( "tool_access",
                  `Assoc
                    [
                      ("kind", `String "preset");
                      ("preset", `String "research");
                      ("also_allow", `List []);
                    ] );
              ])
        with
        | Ok meta -> meta
        | Error e -> fail ("meta_of_json failed: " ^ e)
      in
      (match Keeper_types.write_meta ~force:true config stale_meta with
       | Ok () -> ()
       | Error e -> fail ("write_meta failed: " ^ e));
      let ctx = keeper_ctx env sw config "operator" in
      let ok, _body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_up"
            ~args:(`Assoc [ ("name", `String keeper_name) ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_up dispatch"
      in
      check bool "keeper_up update ok" true ok;
      match Keeper_types.read_meta config keeper_name with
      | Ok (Some meta) ->
          check string "goal resynced" "fresh goal" meta.goal;
          check string "short_goal resynced" "fresh short" meta.short_goal;
          check string "mid_goal resynced" "fresh mid" meta.mid_goal;
          check string "long_goal resynced" "fresh long" meta.long_goal;
          check string "instructions resynced" "fresh instructions"
            meta.instructions;
          check bool "autoboot_enabled resynced" false
            meta.autoboot_enabled;
          check bool "proactive enabled resynced" true meta.proactive.enabled;
          check int "proactive idle resynced" 120 meta.proactive.idle_sec;
          check int "proactive cooldown resynced" 240
            meta.proactive.cooldown_sec;
          check
            (option string)
            "tool preset resynced"
            (Some "delivery")
            (Keeper_types.tool_access_preset meta.tool_access
             |> Option.map Keeper_types.tool_preset_to_string);
          check
            (list string)
            "tool allowlist resynced"
            [ "masc_tasks"; "masc_transition" ]
            (Keeper_types.tool_access_also_allowlist meta.tool_access);
          check
            (option (float 0.0001))
            "per provider timeout resynced"
            (Some 120.0)
            meta.per_provider_timeout_s;
          check
            (list string)
            "tool denylist resynced"
            [ "keeper_task_claim"; "masc_claim_next"; "masc_transition" ]
            meta.tool_denylist
      | Ok None -> fail "keeper meta missing after keeper_up update"
      | Error e -> fail ("read_meta failed: " ^ e))

let test_keeper_list_normalizes_unknown_social_model () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_toml_exn config ~name:"sangsu";
      write_keeper_meta_exn config ~name:"sangsu" ~trace_id:"trace-sangsu"
        ~social_model:"experimental_v99";
      register_keeper_offline_exn config ~name:"sangsu";
      let ctx = keeper_ctx env sw config "operator" in
      let ok, body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_list"
            ~args:(`Assoc [ ("limit", `Int 10); ("detailed", `Bool true) ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_list dispatch"
      in
      check bool "tool keeper list ok" true ok;
      let json = parse_json_exn body in
      match keeper_json_by_name json "sangsu" with
      | Some keeper ->
          check string "social_model normalized" "bdi_speech_v1"
            Yojson.Safe.Util.(keeper |> member "social_model" |> to_string);
          check string "configured_social_model preserved" "experimental_v99"
            Yojson.Safe.Util.(keeper |> member "configured_social_model" |> to_string);
          check bool "social_model_recognized false" false
            Yojson.Safe.Util.(keeper |> member "social_model_recognized" |> to_bool);
          check string "social_model_fallback explicit" "bdi_speech_v1"
            Yojson.Safe.Util.(keeper |> member "social_model_fallback" |> to_string)
      | None -> fail "expected sangsu row in keeper list")

let test_keeper_list_exposes_last_social_transition_reason () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_toml_exn config ~name:"sangsu";
      write_keeper_meta_exn config ~name:"sangsu" ~trace_id:"trace-sangsu"
        ~last_social_transition_reason:"tool_only:visible_reply";
      register_keeper_offline_exn config ~name:"sangsu";
      let ctx = keeper_ctx env sw config "operator" in
      let ok, body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_list"
            ~args:(`Assoc [ ("limit", `Int 10); ("detailed", `Bool true) ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_list dispatch"
      in
      check bool "tool keeper list ok" true ok;
      let json = parse_json_exn body in
      match keeper_json_by_name json "sangsu" with
      | Some keeper ->
          check string "transition reason surfaced" "tool_only:visible_reply"
            Yojson.Safe.Util.(
              keeper |> member "last_social_transition_reason" |> to_string)
      | None -> fail "expected sangsu row in keeper list")

let test_keeper_persona_audit_reports_durable_live_persona_keeper () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_persona_profile_exn config ~name:"analyst";
      write_keeper_persona_toml_exn config ~name:"analyst"
        ~persona_name:"analyst";
      write_keeper_meta_exn config ~name:"analyst" ~trace_id:"trace-analyst";
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      (match Keeper_types.read_meta config "analyst" with
       | Ok (Some meta) ->
           ignore
             (Keeper_registry.register ~base_path:config.base_path "analyst"
                meta)
       | Ok None -> fail "expected analyst meta"
       | Error e -> fail ("read_meta failed: " ^ e));
      let ctx = keeper_ctx env sw config "operator" in
      let ok, body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_persona_audit"
            ~args:(`Assoc [ ("name", `String "analyst") ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_persona_audit dispatch"
      in
      check bool "tool audit ok" true ok;
      let json = parse_json_exn body in
      check int "summary total" 1
        Yojson.Safe.Util.(json |> member "summary" |> member "total" |> to_int);
      check int "summary ok" 1
        Yojson.Safe.Util.(json |> member "summary" |> member "ok" |> to_int);
      match audit_item_by_name json "analyst" with
      | None -> fail "expected analyst audit item"
      | Some item ->
          check bool "item ok" true
            Yojson.Safe.Util.(item |> member "ok" |> to_bool);
          check string "default source" "toml"
            Yojson.Safe.Util.(item |> member "default_source_kind" |> to_string);
          check string "persona name" "analyst"
            Yojson.Safe.Util.(item |> member "persona_name" |> to_string);
          check bool "keeper toml exists" true
            Yojson.Safe.Util.(
              item |> member "keeper_toml" |> member "exists" |> to_bool);
          check bool "persona profile exists" true
            Yojson.Safe.Util.(
              item |> member "persona_profile" |> member "exists" |> to_bool);
          check bool "runtime meta exists" true
            Yojson.Safe.Util.(
              item |> member "runtime_meta" |> member "exists" |> to_bool);
          check bool "registry present" true
            Yojson.Safe.Util.(item |> member "registry_present" |> to_bool);
          check bool "keepalive running" true
            Yojson.Safe.Util.(item |> member "keepalive_running" |> to_bool);
          check int "no issues" 0
            Yojson.Safe.Util.(item |> member "issues" |> to_list |> List.length))

let test_keeper_persona_audit_reports_dormant_autoboot_disabled_keeper () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_persona_profile_exn config ~name:"dormant";
      write_keeper_persona_toml_exn config ~name:"dormant"
        ~persona_name:"dormant" ~autoboot_enabled:false;
      write_keeper_meta_exn config ~name:"dormant" ~trace_id:"trace-dormant"
        ~autoboot_enabled:false;
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      let ctx = keeper_ctx env sw config "operator" in
      let ok, body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_persona_audit"
            ~args:(`Assoc [ ("name", `String "dormant") ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_persona_audit dispatch"
      in
      check bool "tool audit ok" true ok;
      let json = parse_json_exn body in
      check int "summary ok" 1
        Yojson.Safe.Util.(json |> member "summary" |> member "ok" |> to_int);
      check int "summary registry missing" 0
        Yojson.Safe.Util.(
          json |> member "summary" |> member "registry_missing" |> to_int);
      check int "summary dormant" 1
        Yojson.Safe.Util.(
          json |> member "summary" |> member "dormant_autoboot_disabled"
          |> to_int);
      check int "summary autoboot disabled" 1
        Yojson.Safe.Util.(
          json |> member "summary" |> member "autoboot_disabled" |> to_int);
      match audit_item_by_name json "dormant" with
      | None -> fail "expected dormant audit item"
      | Some item ->
          check bool "item ok" true
            Yojson.Safe.Util.(item |> member "ok" |> to_bool);
          check bool "dormant flag" true
            Yojson.Safe.Util.(item |> member "dormant" |> to_bool);
          check string "dormant reason" "autoboot_disabled"
            Yojson.Safe.Util.(item |> member "dormant_reason" |> to_string);
          check int "no issues" 0
            Yojson.Safe.Util.(item |> member "issues" |> to_list |> List.length))

let test_keeper_persona_audit_flags_stale_active_goal_ids () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_persona_profile_exn config ~name:"analyst";
      write_keeper_persona_toml_exn config ~name:"analyst"
        ~persona_name:"analyst";
      let stale_goal, _ =
        match Goal_store.upsert_goal config ~title:"Finished scoped goal" () with
        | Ok payload -> payload
        | Error msg -> fail msg
      in
      let other_goal, _ =
        match Goal_store.upsert_goal config ~title:"Open global goal" () with
        | Ok payload -> payload
        | Error msg -> fail msg
      in
      write_keeper_meta_exn config ~name:"analyst" ~trace_id:"trace-analyst"
        ~active_goal_ids:[ stale_goal.id ];
      ignore
        (Coord_task.add_task ~goal_id:stale_goal.id config
           ~title:"Done scoped task" ~priority:3 ~description:"desc");
      ignore
        (Coord_task.add_task ~goal_id:other_goal.id config
           ~title:"Open global task" ~priority:1 ~description:"desc");
      mark_task_done_by_title config ~title:"Done scoped task"
        ~agent_name:"keeper-analyst-agent";
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      (match Keeper_types.read_meta config "analyst" with
       | Ok (Some meta) ->
           ignore
             (Keeper_registry.register ~base_path:config.base_path "analyst"
                meta)
       | Ok None -> fail "expected analyst meta"
       | Error e -> fail ("read_meta failed: " ^ e));
      let ctx = keeper_ctx env sw config "operator" in
      let ok, body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_persona_audit"
            ~args:(`Assoc [ ("name", `String "analyst") ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_persona_audit dispatch"
      in
      check bool "tool audit ok" true ok;
      let json = parse_json_exn body in
      check int "summary stale active goal ids" 1
        Yojson.Safe.Util.(
          json |> member "summary" |> member "stale_active_goal_ids" |> to_int);
      match audit_item_by_name json "analyst" with
      | None -> fail "expected analyst audit item"
      | Some item ->
          let issues =
            Yojson.Safe.Util.(item |> member "issues") |> string_list_of_json
          in
          let scope = Yojson.Safe.Util.(item |> member "active_goal_scope") in
          check bool "flags stale active goals" true
            (List.mem "stale_active_goal_ids" issues);
          check bool "item not ok" false
            Yojson.Safe.Util.(item |> member "ok" |> to_bool);
          check int "scoped tasks counted" 1
            Yojson.Safe.Util.(scope |> member "scoped_task_count" |> to_int);
          check int "scoped open tasks counted" 0
            Yojson.Safe.Util.(scope |> member "scoped_open_task_count" |> to_int);
          check int "scoped terminal tasks counted" 1
            Yojson.Safe.Util.(
              scope |> member "scoped_terminal_task_count" |> to_int);
          check int "global open tasks counted" 1
            Yojson.Safe.Util.(scope |> member "global_open_task_count" |> to_int);
          check bool "scope marked stale" true
            Yojson.Safe.Util.(scope |> member "stale" |> to_bool))

let test_keeper_persona_audit_flags_missing_persona_runtime () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_persona_toml_exn config ~name:"ghost"
        ~persona_name:"missing-persona";
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      let ctx = keeper_ctx env sw config "operator" in
      let ok, body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_persona_audit"
            ~args:(`Assoc [ ("name", `String "ghost") ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_persona_audit dispatch"
      in
      check bool "tool audit ok" true ok;
      let json = parse_json_exn body in
      check int "missing persona count" 1
        Yojson.Safe.Util.(
          json |> member "summary" |> member "missing_persona_profile"
          |> to_int);
      check int "missing runtime count" 1
        Yojson.Safe.Util.(
          json |> member "summary" |> member "missing_runtime_meta" |> to_int);
      match audit_item_by_name json "ghost" with
      | None -> fail "expected ghost audit item"
      | Some item ->
          let issues =
            Yojson.Safe.Util.(item |> member "issues") |> string_list_of_json
          in
          check bool "flags missing persona" true
            (List.mem "missing_persona_profile" issues);
          check bool "flags missing runtime" true
            (List.mem "missing_runtime_meta" issues);
          check bool "keeper toml exists" true
            Yojson.Safe.Util.(
              item |> member "keeper_toml" |> member "exists" |> to_bool);
          check bool "persona profile missing" false
            Yojson.Safe.Util.(
              item |> member "persona_profile" |> member "exists" |> to_bool);
          check string "persona profile candidate path surfaced"
            (Filename.concat
               (Filename.concat
                  (Filename.concat config_root "personas")
                  "missing-persona")
               "profile.json")
            Yojson.Safe.Util.(
              item |> member "persona_profile" |> member "path" |> to_string))

let test_keeper_persona_audit_flags_runtime_meta_parse_error () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_persona_profile_exn config ~name:"broken";
      write_keeper_persona_toml_exn config ~name:"broken" ~persona_name:"broken";
      write_corrupt_keeper_meta_exn config ~name:"broken";
      let config_root = Filename.concat (Coord.masc_root_dir config) "config" in
      write_minimal_cascade_toml config_root;
      Unix.putenv "MASC_CONFIG_DIR" config_root;
      Config_dir_resolver.reset ();
      let ctx = keeper_ctx env sw config "operator" in
      let ok, body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_persona_audit"
            ~args:(`Assoc [ ("name", `String "broken") ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_persona_audit dispatch"
      in
      check bool "tool audit ok" true ok;
      let json = parse_json_exn body in
      check int "runtime meta parse error count" 1
        Yojson.Safe.Util.(
          json |> member "summary" |> member "runtime_meta_error" |> to_int);
      check int "runtime meta file is not missing" 0
        Yojson.Safe.Util.(
          json |> member "summary" |> member "missing_runtime_meta" |> to_int);
      match audit_item_by_name json "broken" with
      | None -> fail "expected broken audit item"
      | Some item ->
          let issues =
            Yojson.Safe.Util.(item |> member "issues") |> string_list_of_json
          in
          check bool "flags runtime meta error" true
            (List.mem "runtime_meta_error" issues);
          check bool "item not ok" false
            Yojson.Safe.Util.(item |> member "ok" |> to_bool);
          check bool "runtime meta file exists" true
            Yojson.Safe.Util.(
              item |> member "runtime_meta" |> member "exists" |> to_bool);
          check bool "runtime meta error surfaced" true
            (Yojson.Safe.Util.(
               item |> member "runtime_meta" |> member "error" |> to_string)
             <> ""))

let test_keeper_list_preserves_known_social_model () =
  Eio_main.run @@ fun env ->
  ensure_fs env;
  with_clean_base_path_env @@ fun () ->
  Eio.Switch.run @@ fun sw ->
  let base_dir = temp_dir () in
  Fun.protect
    ~finally:(fun () ->
      Config_dir_resolver.reset ();
      Keeper_registry.clear ();
      Keeper_runtime.reset_test_state base_dir;
      cleanup_dir base_dir)
    (fun () ->
      let config = Coord.default_config base_dir in
      ignore (Coord.init config ~agent_name:(Some "operator"));
      write_keeper_toml_exn config ~name:"sangsu";
      write_keeper_meta_exn config ~name:"sangsu" ~trace_id:"trace-sangsu"
        ~social_model:"magentic_ledger_v1";
      register_keeper_offline_exn config ~name:"sangsu";
      let ctx = keeper_ctx env sw config "operator" in
      let ok, body =
        match
          Tool_keeper.dispatch ctx ~name:"masc_keeper_list"
            ~args:(`Assoc [ ("limit", `Int 10); ("detailed", `Bool true) ])
        with
        | Some result -> tuple_of_tool_result result
        | None -> fail "expected masc_keeper_list dispatch"
      in
      check bool "tool keeper list ok" true ok;
      let json = parse_json_exn body in
      match keeper_json_by_name json "sangsu" with
      | Some keeper ->
          check string "known model preserved" "magentic_ledger_v1"
            Yojson.Safe.Util.(keeper |> member "social_model" |> to_string)
      | None -> fail "expected sangsu row in keeper list")

let test_keeper_list_cache_retries_after_inflight_invalidation () =
  Tool_keeper.For_testing.reset_keeper_list_cache ();
  Fun.protect
    ~finally:Tool_keeper.For_testing.reset_keeper_list_cache
    (fun () ->
      let calls = ref 0 in
      let body =
        Tool_keeper.For_testing.cached_keeper_list_text ~key:"race"
          ~ttl_s:60.0 (fun () ->
            incr calls;
            if !calls = 1 then (
              Tool_keeper.For_testing.invalidate_keeper_list_cache ();
              "stale")
            else
              "fresh")
      in
      check string "in-flight invalidation forces recompute" "fresh" body;
      check int "compute retried after invalidation" 2 !calls;
      let cached =
        Tool_keeper.For_testing.cached_keeper_list_text ~key:"race"
          ~ttl_s:60.0 (fun () -> "unexpected")
      in
      check string "fresh recompute was cached" "fresh" cached)

let () =
  run "keeper_meta_listing"
    [
      ( "listing",
        [
          test_case "read_meta_resolved rejects meta aliases" `Quick
            test_read_meta_resolved_rejects_meta_aliases;
          test_case "keeper_names and keeper_list ignore sidecar json" `Quick
            test_keeper_listing_ignores_sidecar_json_files;
          test_case "bootable list skips autoboot-disabled meta" `Quick
            test_bootable_keeper_names_skip_autoboot_disabled_meta;
          test_case
            "bootable list uses declarative autoboot true over stale meta"
            `Quick
            test_bootable_keeper_names_use_declarative_autoboot_true_over_stale_meta;
          test_case "autoboot exclusion reasons explain skipped keepers" `Quick
            test_autoboot_exclusion_reasons_explain_skipped_keepers;
          test_case "declarative autoboot-disabled keeper skips boot without meta"
            `Quick test_declarative_autoboot_disabled_skips_boot_without_meta;
          test_case "autoboot policy resyncs from declarative TOML" `Quick
            test_autoboot_policy_resync_from_declarative_toml;
          test_case "keeper_up uses TOML autoboot default" `Quick
            test_keeper_up_uses_toml_autoboot_default;
          test_case "keeper_up update resyncs declarative profile defaults"
            `Quick test_keeper_up_update_resyncs_declarative_profile_defaults;
          test_case "tool keeper list normalizes unknown social model" `Quick
            test_keeper_list_normalizes_unknown_social_model;
          test_case "tool keeper list preserves known social model" `Quick
            test_keeper_list_preserves_known_social_model;
          test_case "keeper list cache retries after in-flight invalidation"
            `Quick test_keeper_list_cache_retries_after_inflight_invalidation;
          test_case "tool keeper list exposes last social transition reason"
            `Quick test_keeper_list_exposes_last_social_transition_reason;
          test_case "keeper persona audit reports durable live keeper" `Quick
            test_keeper_persona_audit_reports_durable_live_persona_keeper;
          test_case "keeper persona audit reports dormant autoboot-disabled keeper"
            `Quick
            test_keeper_persona_audit_reports_dormant_autoboot_disabled_keeper;
          test_case "keeper persona audit flags stale active goal ids" `Quick
            test_keeper_persona_audit_flags_stale_active_goal_ids;
          test_case "keeper persona audit flags missing persona runtime" `Quick
            test_keeper_persona_audit_flags_missing_persona_runtime;
          test_case "keeper persona audit flags runtime meta parse error" `Quick
            test_keeper_persona_audit_flags_runtime_meta_parse_error;
        ] );
    ]
