(** Memory_oas_bridge — MASC-side adapter that projects product memory into
    OAS Memory.t 5-tier primitives.

    Tier mapping:
    - {b Long_term} — JSONL files under [.masc/memory/<agent>/<session>.jsonl]
    - {b Episodic}  — [load_episodes_text] reads recent [Institution_eio] JSONL
                       episodes; [flush_episodes] writes new OAS episodes back
    - {b Procedural} — [load_procedures_text] reads [Procedural_memory] entries;
                        [flush_procedures] writes back
    - {b Working/Scratchpad} — managed by OAS in-memory; no backend needed

    Memory injection follows the hook-first pattern (RFC-MASC-004):
    pure-read [load_*_text] functions provide text for system context
    injection via hooks, and [flush_incremental] persists new data
    after each turn.  The imperative seeding functions
    ([seed_episodes], [seed_procedures_as_oas], [create_memory_full])
    were removed in Phase 3.

    Filesystem-first policy: Long_term always uses JSONL, regardless of
    whether a PG pool is available.  PG long_term was removed in 2.140.0.

    @since 2.122.0 (long_term only)
    @since 2.124.0 (5-tier: episodic + procedural seeding/flushing)
    @since 2.140.0 (filesystem-first: JSONL long_term_backend always)
    @since 2.266.0 (RFC-MASC-004 Phase 3: imperative seeding removed) *)

module SMap = Memory_oas_bridge_cache.SMap

(** Default importance for memories stored via OAS Memory.store.
    Configurable via MASC_MEMORY_OAS_DEFAULT_IMPORTANCE. *)
let default_importance () = Env_config.Memory_oas.default_importance

(** Extract importance from JSON value if present, else use default. *)
let importance_of_json (json : Yojson.Safe.t) : int =
  match json with
  | `Assoc fields ->
    (match List.assoc_opt "importance" fields with
     | Some (`Int n) -> max 1 (min 10 n)
     | _ -> default_importance ())
  | _ -> default_importance ()

(** Extract content string from JSON value. *)
let content_of_json (json : Yojson.Safe.t) : string =
  match json with
  | `String s -> s
  | `Assoc fields ->
    (match List.assoc_opt "content" fields with
     | Some (`String s) -> s
     | _ -> Yojson.Safe.to_string json)
  | _ -> Yojson.Safe.to_string json

(** Generate a timestamp-based session ID as fallback. *)
let generate_session_id () =
  Printf.sprintf "%d" (int_of_float (Unix.gettimeofday ()))

(** Resolve the JSONL fallback root for OAS memory.

    Preference order:
    1. Explicit [base_dir]
    2. Coord-scoped [.masc] under [config.base_path]
    3. Process-scoped [.masc] under [MASC_BASE_PATH] (or cwd fallback) *)
let resolve_base_dir ?(base_dir : string option) ?(config : Coord_utils.config option) () =
  match base_dir, config with
  | Some dir, _ -> dir
  | None, Some cfg -> Common.masc_dir_from_base_path ~base_path:cfg.base_path
  | None, None -> Common.masc_dir_from_base_path ~base_path:(Env_config.base_path ())

let cached_recent_episodes = Memory_oas_bridge_cache.cached_recent_episodes
let persisted_episode_ids = Memory_oas_bridge_cache.persisted_episode_ids
let note_episode_flush = Memory_oas_bridge_cache.note_episode_flush
let load_procedures_cached = Memory_oas_bridge_cache.load_procedures_cached
let store_procedures_cache = Memory_oas_bridge_cache.store_procedures_cache
let top_procedures_cached = Memory_oas_bridge_cache.top_procedures_cached

(* Observability for the JSONL-backed [long_term_backend].  The
   sub-library [Memory_jsonl] is a dependency leaf (RFC-0056 Phase
   1F) and cannot increment Prometheus counters directly; this
   wrapper records each operation outcome with a typed label.
   Pair with [.tmp/memory-compacting-analysis.html] memory_jsonl
   silent-path entries; the in-module log-only PR (#15668) handles
   parse-line drops + truncation flags. *)
let () =
  Prometheus.register_counter
    ~name:Keeper_metrics.(to_string MemoryJsonlOps)
    ~help:
      "Total [Agent_sdk.Memory.long_term_backend] operations served \
       by the JSONL backend, classified by label [outcome] \
       (governed by Memory_oas_bridge_op_outcome).  Label [agent] \
       names the keeper/agent owning the session.  Rising failure \
       or miss rates surface JSONL-side issues that the \
       dependency-leaf Memory_jsonl cannot self-report."
    ()
;;

let record_op_outcome
    ~(agent_name : string)
    ~(outcome : Memory_oas_bridge_op_outcome.t) =
  Prometheus.inc_counter
    Keeper_metrics.(to_string MemoryJsonlOps)
    ~labels:
      [ ("outcome", Memory_oas_bridge_op_outcome.to_label outcome)
      ; ("agent", agent_name)
      ]
    ()

(** Create an OAS [long_term_backend].

    Always uses session-based JSONL files under
    [.masc/memory/<agent_name>/<session_id>.jsonl].
    Filesystem-first: PG pool availability is not checked.

    Wraps each of the 5 closures so the JSONL operations are
    observable in [/metrics] without violating the leaf-library
    boundary of [Memory_jsonl].  Query uses [Memory_jsonl]'s
    pre-collapse observer because the OAS backend contract returns
    an empty list for both legitimate empty results and failures. *)
let make_backend ?base_dir ~(agent_name : string) ~(session_id : string) ()
  : Agent_sdk.Memory.long_term_backend =
  let base_dir = resolve_base_dir ?base_dir () in
  let inner =
    let on_query_result = function
      | Ok _ ->
        record_op_outcome
          ~agent_name
          ~outcome:Memory_oas_bridge_op_outcome.Query_ok
      | Error _ ->
        record_op_outcome
          ~agent_name
          ~outcome:Memory_oas_bridge_op_outcome.Query_failed
    in
    Memory_jsonl.make_backend_with_query_observer
      ~on_query_result
      ~base_dir
      ~agent_name
      ~session_id
  in
  let persist ~key value =
    let r = inner.persist ~key value in
    let outcome =
      match r with
      | Ok () -> Memory_oas_bridge_op_outcome.Persist_ok
      | Error _ -> Memory_oas_bridge_op_outcome.Persist_failed
    in
    record_op_outcome ~agent_name ~outcome;
    r
  in
  let retrieve ~key =
    let r = inner.retrieve ~key in
    let outcome =
      match r with
      | Some _ -> Memory_oas_bridge_op_outcome.Retrieve_hit
      | None -> Memory_oas_bridge_op_outcome.Retrieve_miss
    in
    record_op_outcome ~agent_name ~outcome;
    r
  in
  let remove ~key =
    let r = inner.remove ~key in
    let outcome =
      match r with
      | Ok () -> Memory_oas_bridge_op_outcome.Remove_ok
      | Error _ -> Memory_oas_bridge_op_outcome.Remove_failed
    in
    record_op_outcome ~agent_name ~outcome;
    r
  in
  let batch_persist entries =
    let r = inner.batch_persist entries in
    let outcome =
      match r with
      | Ok () -> Memory_oas_bridge_op_outcome.Batch_persist_ok
      | Error _ -> Memory_oas_bridge_op_outcome.Batch_persist_failed
    in
    record_op_outcome ~agent_name ~outcome;
    r
  in
  let query ~prefix ~limit =
    inner.query ~prefix ~limit
  in
  { persist; retrieve; remove; batch_persist; query }

type created_memory =
  { created_memory : Agent_sdk.Memory.t
  ; created_memory_long_term_backend : Agent_sdk.Memory.long_term_backend
  }

(** Create an OAS [Memory.t] instance.

    Uses JSONL long_term_backend (filesystem-first).
    @param session_id Session identifier; defaults to timestamp-based ID. *)
let create_memory_with_backend ~(agent_name : string) ?(base_dir : string option)
    ?(session_id : string option)
    () : created_memory =
  let sid = match session_id with
    | Some s -> s
    | None -> generate_session_id ()
  in
  let backend = make_backend ?base_dir ~agent_name ~session_id:sid () in
  { created_memory = Agent_sdk.Memory.create ~long_term:backend ()
  ; created_memory_long_term_backend = backend
  }

let create_memory ~(agent_name : string) ?(base_dir : string option)
    ?(session_id : string option)
    () : Agent_sdk.Memory.t =
  (create_memory_with_backend ~agent_name ?base_dir ?session_id ()).created_memory

(** Load and return the institution welcome text, or [None] when empty.
    Used by [load_institution_text]. *)
let read_institution_welcome (config : Coord_utils.config) : string option =
  let welcome = Institution_eio.load_and_format_for_welcome ~fs:() config in
  if welcome = "" then None else Some welcome

(* ================================================================ *)
(* Episodic tier: Institution_eio JSONL <-> OAS episodes            *)
(* ================================================================ *)

let default_episode_salience (episode : Institution_eio.episode) =
  let base =
    match episode.outcome with
    | `Success -> 0.75
    | `Failure -> 0.95
    | `Partial -> 0.6
  in
  let learning_bonus =
    min 0.15 (float_of_int (List.length episode.learnings) *. 0.03)
  in
  Float.min 1.0 (base +. learning_bonus)

let oas_outcome_of_institution (episode : Institution_eio.episode) =
  match episode.outcome with
  | `Success -> Agent_sdk.Memory.Success episode.summary
  | `Failure -> Agent_sdk.Memory.Failure episode.summary
  | `Partial -> Agent_sdk.Memory.Neutral

let metadata_string key metadata =
  match List.assoc_opt key metadata with
  | Some (`String value) when String.trim value <> "" -> Some value
  | _ -> None

let metadata_string_list key metadata =
  match List.assoc_opt key metadata with
  | Some (`List values) ->
      values
      |> List.filter_map (function
           | `String value when String.trim value <> "" -> Some value
           | _ -> None)
  | _ -> []

let metadata_context key metadata =
  match List.assoc_opt key metadata with
  | Some (`Assoc fields) ->
      fields
      |> List.filter_map (function
           | k, `String value -> Some (k, value)
           | _ -> None)
  | _ -> []

let metadata_float key metadata =
  match List.assoc_opt key metadata with
  | Some (`Float value) -> Some value
  | Some (`Int value) -> Some (float_of_int value)
  | Some (`Intlit value) -> float_of_string_opt value
  | _ -> None

let institution_outcome_to_string = function
  | `Success -> "success"
  | `Failure -> "failure"
  | `Partial -> "partial"

let institution_outcome_of_string = function
  | "success" -> Some `Success
  | "failure" -> Some `Failure
  | "partial" -> Some `Partial
  | _ -> None

let oas_episode_of_institution (episode : Institution_eio.episode) :
    Agent_sdk.Memory.episode =
  {
    id = episode.id;
    timestamp = episode.timestamp;
    participants = episode.participants;
    action = episode.summary;
    outcome = oas_outcome_of_institution episode;
    salience = default_episode_salience episode;
    metadata =
      [
        ("event_type", `String episode.event_type);
        ("institution_summary", `String episode.summary);
        ( "institution_outcome",
          `String (institution_outcome_to_string episode.outcome) );
        ( "learnings",
          `List (List.map (fun learning -> `String learning) episode.learnings)
        );
        ( "context",
          `Assoc
            (List.map (fun (key, value) -> (key, `String value)) episode.context)
        );
        ("source", `String "institution_jsonl");
      ];
  }

let institution_episode_of_oas ~(agent_name : string)
    (episode : Agent_sdk.Memory.episode) : Institution_eio.episode =
  let summary =
    metadata_string "institution_summary" episode.metadata
    |> Option.value ~default:episode.action
  in
  let event_type =
    metadata_string "event_type" episode.metadata
    |> Option.value ~default:"oas_memory"
  in
  let learnings = metadata_string_list "learnings" episode.metadata in
  let context = metadata_context "context" episode.metadata in
  let outcome =
    match
      Option.bind
        (metadata_string "institution_outcome" episode.metadata)
        institution_outcome_of_string
    with
    | Some preserved -> preserved
    | None -> (
        match episode.outcome with
        | Agent_sdk.Memory.Success _ -> `Success
        | Agent_sdk.Memory.Failure _ -> `Failure
        | Agent_sdk.Memory.Neutral -> `Partial)
  in
  let participants =
    if episode.participants <> [] then episode.participants
    else [ agent_name ]
  in
  {
    Institution_eio.id = episode.id;
    timestamp =
      metadata_float "timestamp" episode.metadata
      |> Option.value ~default:episode.timestamp;
    participants;
    event_type;
    summary;
    outcome;
    learnings;
    context;
  }

(** Create an OAS episode from a keeper [STATE] snapshot and store it
    in [Memory.t].  The episode is later flushed to institution JSONL by
    the AfterTurn hook's [flush_episodes].

    Metadata keys match what [institution_episode_of_oas] expects, so
    the round-trip Institution_eio -> OAS -> Institution_eio is lossless. *)
let store_episode_from_snapshot
    ~(memory : Agent_sdk.Memory.t)
    ~(keeper_name : string)
    ~(turn : int)
    ?(oas_turn_count : int option)
    ~(trace_id : string)
    (snapshot : Keeper_memory_policy.keeper_state_snapshot) : unit =
  let parts =
    List.filter_map Fun.id
      [
        Option.map (fun g -> "Goal: " ^ g) snapshot.goal;
        Option.map (fun p -> "Progress: " ^ p) snapshot.progress;
        Option.map (fun d -> "Done: " ^ d) snapshot.done_summary;
      ]
  in
  let summary =
    match parts with
    | [] -> "keeper turn " ^ string_of_int turn
    | _ -> String.concat "; " parts
  in
  let learnings =
    (snapshot.decisions @ snapshot.constraints)
    |> List.filter (fun s -> String.trim s <> "")
  in
  let outcome_str, outcome =
    if snapshot.done_summary <> None then
      ("success", Agent_sdk.Memory.Success summary)
    else ("partial", Agent_sdk.Memory.Neutral)
  in
  let ts = Time_compat.now () in
  let episode_id =
    Printf.sprintf "keeper-%s-t%d-%d" keeper_name turn
      (int_of_float (ts *. 1000.0) mod 1_000_000)
  in
  let episode : Agent_sdk.Memory.episode =
    let context =
      [
        ("trace_id", `String trace_id);
        ("turn", `String (string_of_int turn));
      ]
      @ (match oas_turn_count with
         | None -> []
         | Some count -> [ ("oas_turn_count", `String (string_of_int count)) ])
    in
    {
      id = episode_id;
      timestamp = ts;
      participants = [ keeper_name ];
      action = summary;
      outcome;
      salience = 0.6;
      metadata =
        [
          ("event_type", `String "keeper_turn");
          ("institution_summary", `String summary);
          ("institution_outcome", `String outcome_str);
          ( "learnings",
            `List (List.map (fun l -> `String l) learnings) );
          ("context", `Assoc context);
        ];
    }
  in
  Agent_sdk.Memory.store_episode memory episode

(** #10341 (#10350): emit Agent_stress Timeout event for timeout-shaped
    error_kind from institution failure path. *)
type error_kind = Error_kind of string

let error_kind_of_string value = Error_kind value
let error_kind_to_string (Error_kind value) = value

let canonical_error_kind_label = function
  | value -> value

let timeout_error_kinds =
  List.map error_kind_of_string
    [
      "provider_timeout";
      "provider_timeout_loop";
      "turn_timeout";
      "admission_queue_timeout";
    ]

let stress_kind_for_error_kind error_kind =
  let trimmed =
    error_kind_to_string error_kind
    |> String.trim
    |> canonical_error_kind_label
  in
  if
    List.exists
      (fun kind -> String.equal trimmed (error_kind_to_string kind))
      timeout_error_kinds
  then
    Some Agent_stress.Timeout
  else None

let emit_stress_for_failure ~keeper_name ~error_kind =
  match stress_kind_for_error_kind error_kind with
  | None -> ()
  | Some stress_kind ->
      Agent_stress.record
        {
          agent_name = keeper_name;
          room_id = "";
          kind = stress_kind;
          timestamp = Unix.gettimeofday ();
        }

(** #10325 (#10339): per-failure-kind counter + structured learnings replacing
    boilerplate. Was 97% identical static string before. *)
let institution_episode_failure_kind_metric =
  "masc_institution_episode_failure_kind_total"

let () =
  Prometheus.register_counter
    ~name:institution_episode_failure_kind_metric
    ~help:
      "Total institution_episodes failure entries grouped by \
       error_kind. Pre-#10325 the [learnings] field was a static \
       boilerplate string in 97% of failure rows; this counter \
       surfaces the actual failure-mode distribution so operators \
       can see which kind dominates without grepping the JSONL.  \
       Labels: error_kind."
    ()

let normalize_error_kind kind =
  let trimmed =
    error_kind_to_string kind
    |> String.trim
    |> canonical_error_kind_label
  in
  if trimmed = "" then "unspecified" else trimmed

let failure_learnings ~error_kind ~error_preview =
  let kind_part =
    Printf.sprintf "failure_kind: %s" (normalize_error_kind error_kind)
  in
  let preview_part =
    let trimmed = String.trim error_preview in
    if trimmed = "" then None
    else Some (Printf.sprintf "error_preview: %s" trimmed)
  in
  match preview_part with
  | Some p -> [ kind_part; p ]
  | None -> [ kind_part ]

let store_failed_turn_episode
    ~(memory : Agent_sdk.Memory.t)
    ~(keeper_name : string)
    ~(turn : int)
    ?(oas_turn_count : int option)
    ~(trace_id : string)
    ~(error_kind : error_kind)
    ~(error_message : string)
    () : unit =
  let error_preview =
    String_util.utf8_safe ~max_bytes:403 ~suffix:"..." error_message
    |> String_util.to_string
  in
  let error_context =
    String_util.utf8_safe ~max_bytes:4099 ~suffix:"..." error_message
    |> String_util.to_string
  in
  let error_kind_label = normalize_error_kind error_kind in
  let summary =
    Printf.sprintf "keeper turn %d failed (%s): %s" turn error_kind_label
      error_preview
  in
  Prometheus.inc_counter institution_episode_failure_kind_metric
    ~labels:[ ("error_kind", normalize_error_kind error_kind) ]
    ();
  let ts = Time_compat.now () in
  let episode_id =
    Printf.sprintf "keeper-%s-t%d-failure-%d" keeper_name turn
      (int_of_float (ts *. 1000.0) mod 1_000_000)
  in
  emit_stress_for_failure ~keeper_name ~error_kind;
  let learnings =
    failure_learnings ~error_kind ~error_preview
  in
  let episode : Agent_sdk.Memory.episode =
    let context =
      [
        ("trace_id", `String trace_id);
        ("turn", `String (string_of_int turn));
      ]
      @ (match oas_turn_count with
         | None -> []
         | Some count -> [ ("oas_turn_count", `String (string_of_int count)) ])
      @ [
          ("error_kind", `String error_kind_label);
          ("error_message", `String error_context);
        ]
    in
    {
      id = episode_id;
      timestamp = ts;
      participants = [ keeper_name ];
      action = summary;
      outcome = Agent_sdk.Memory.Failure error_context;
      salience = 0.8;
      metadata =
        [
          ("event_type", `String "keeper_turn");
          ("institution_summary", `String summary);
          ("institution_outcome", `String "failure");
          (* #10325: emit failure-specific learning tags from the
             structured error metadata.  Generic boilerplate
             ("persist failed keeper turns ...") was removed because
             it filled 97% of failure entries with no per-failure
             information, defeating the institution-memory contract.
             When neither error_kind nor error_message has signal,
             emit the explicit [NO_LEARNING] sentinel so downstream
             readers can distinguish absent data from a placeholder. *)
          ( "learnings",
            `List (List.map (fun s -> `String s) learnings) );
          ("context", `Assoc context);
        ];
    }
  in
  Agent_sdk.Memory.store_episode memory episode

(** Flush new OAS episodes.

    Appends newly created OAS episodes to the institution JSONL store,
    preserving IDs and institution metadata when present. *)
let flush_episodes ~(memory : Agent_sdk.Memory.t) ~(agent_name : string) : int =
  let persisted_ids = persisted_episode_ids () in
  let path = Institution_eio.episodes_jsonl_path () in
  Fs_compat.mkdir_p (Filename.dirname path);
  let flushed, _ =
    Agent_sdk.Memory.recall_episodes memory ~limit:max_int ()
    |> List.fold_left
         (fun (flushed, p_ids) (episode : Agent_sdk.Memory.episode) ->
           if SMap.mem episode.id p_ids then (flushed, p_ids)
           else (
             let persisted = institution_episode_of_oas ~agent_name episode in
             Fs_compat.append_jsonl path
               (Institution_eio.episode_to_json persisted);
             note_episode_flush persisted;
             (flushed + 1, SMap.add episode.id () p_ids)))
         (0, persisted_ids)
  in
  (* Cap file growth. Called after append so we only rewrite when needed. *)
  if flushed > 0 then begin
    try
      let dropped = Institution_eio.cap_episodes_jsonl () in
      if dropped > 0 then
        Log.Institution.info "capped institution_episodes.jsonl: dropped %d old entries" dropped
    with
    | Eio.Cancel.Cancelled _ as e -> raise e
    | exn ->
      Log.Institution.warn "episode cap failed: %s" (Printexc.to_string exn)
  end;
  flushed

(* ================================================================ *)
(* Procedural tier: Procedural_memory <-> OAS procedures            *)
(* ================================================================ *)

(** Convert a [Procedural_memory.procedure] to an OAS [procedure].

    MASC's [pattern] field contains "When X, do Y" as a single string.
    OAS separates [pattern] (trigger) from [action] (what to do).
    We use the full string for both fields since they are combined
    in MASC's representation. *)
let oas_procedure_of_masc (p : Procedural_memory.procedure) :
    Agent_sdk.Memory.procedure =
  {
    id = p.id;
    pattern = p.pattern;
    action = p.pattern;  (* MASC combines trigger+action in pattern *)
    success_count = p.success_count;
    failure_count = p.failure_count;
    confidence = p.confidence;
    last_used = p.last_applied;
    metadata = [
      ("agent_name", `String p.agent_name);
      ("created_at", `Float p.created_at);
      ("evidence_count", `Int (List.length p.evidence));
    ];
  }

let render_lesson_prompt_context ~(memory : Agent_sdk.Memory.t)
    ~(pattern : string) ~(limit : int) =
  Agent_sdk.Lesson_memory.retrieve_lessons memory ~pattern ~limit ()
  |> Agent_sdk.Lesson_memory.render_prompt_context

let record_failure_lesson ~(memory : Agent_sdk.Memory.t)
    ~(pattern : string) ~(summary : string)
    ?action ?stdout ?stderr ?diff_summary ?trace_summary ?metric_name
    ?metric_error ~(participants : string list)
    ~(metadata : (string * Yojson.Safe.t) list) () =
  ignore
    (Agent_sdk.Lesson_memory.record_failure memory
       {
         pattern;
         summary;
         action;
         stdout;
         stderr;
         diff_summary;
         trace_summary;
         metric_name;
         metric_error;
         participants;
         metadata;
       })

let replace_first_procedure_by_id id updated procs =
  let rec go = function
    | [] -> []
    | (p : Procedural_memory.procedure) :: rest when String.equal p.id id ->
      updated :: rest
    | p :: rest -> p :: go rest
  in
  go procs

(** Flush OAS procedures back to [Procedural_memory].

    Extracts procedures from the Procedural tier that have been updated
    (new success/failure counts) and persists them.
    Returns the number of procedures flushed. *)
let flush_procedures ~(memory : Agent_sdk.Memory.t) ~(agent_name : string) : int =
  let oas_procs =
    Agent_sdk.Memory.matching_procedures memory
      ~pattern:"" ()
  in
  let procedures = ref (load_procedures_cached ~agent_name) in
  let needs_rewrite = ref false in
  let flushed = ref 0 in
  List.iter (fun (op : Agent_sdk.Memory.procedure) ->
    let updated =
      match List.find_opt (fun (p : Procedural_memory.procedure) ->
        p.id = op.id
      ) !procedures with
      | Some old_p ->
        (* Only flush if counts changed *)
        if old_p.success_count <> op.success_count
           || old_p.failure_count <> op.failure_count then begin
          let updated_p = { old_p with
            success_count = op.success_count;
            failure_count = op.failure_count;
            confidence = op.confidence;
            last_applied = op.last_used;
          } in
          procedures := replace_first_procedure_by_id old_p.id updated_p !procedures;
          needs_rewrite := true;
          true
        end else false
      | None ->
        (* New procedure from OAS -- create in MASC *)
        let new_p : Procedural_memory.procedure = {
          id = op.id;
          agent_name;
          pattern = op.pattern;
          evidence = [];
          success_count = op.success_count;
          failure_count = op.failure_count;
          confidence = op.confidence;
          created_at = Unix.gettimeofday ();
          last_applied = op.last_used;
        } in
        procedures := !procedures @ [new_p];
        needs_rewrite := true;
        true
    in
    if updated then incr flushed
  ) oas_procs;
  if !needs_rewrite then
    Procedural_memory.rewrite_procedures ~agent_name !procedures;
  store_procedures_cache ~agent_name !procedures;
  !flushed

(* ================================================================ *)
(* Pure-read functions for hook-first memory injection               *)
(* (RFC-MASC-004: no side effects, no OAS Memory.t push)            *)
(* ================================================================ *)

(** Load recent episodes as a text block suitable for system context injection.

    Returns [None] when no episodes are available.  The returned string
    is a compact summary — one line per episode — designed to fit inside
    [extra_system_context] without blowing up token count.

    Pure read: does not touch OAS [Memory.t].

    @since v2.265.0 (RFC-MASC-004 Phase 1) *)
let load_episodes_text ~(limit : int) : string option =
  let episodes = cached_recent_episodes ~limit in
  match episodes with
  | [] -> None
  | eps ->
    let lines = List.map (fun (ep : Institution_eio.episode) ->
      Printf.sprintf "- [%s] %s (%s)"
        ep.event_type ep.summary
        (institution_outcome_to_string ep.outcome)
    ) eps in
    Some (Printf.sprintf "[episodic memory: %d episodes]\n%s"
      (List.length eps) (String.concat "\n" lines))

(** Load crystallized procedures as a text block for system context injection.

    Returns [None] when no procedures pass the crystallization threshold.
    Pure read: does not touch OAS [Memory.t].

    @since v2.265.0 (RFC-MASC-004 Phase 1) *)
let load_procedures_text ~(agent_name : string) ~(limit : int) : string option =
  let procs = top_procedures_cached ~agent_name ~limit in
  match procs with
  | [] -> None
  | ps ->
    let lines = List.map (fun (p : Procedural_memory.procedure) ->
      Printf.sprintf "- [%.0f%% confidence] %s" (p.confidence *. 100.0) p.pattern
    ) ps in
    Some (Printf.sprintf "[procedural memory: %d procedures]\n%s"
      (List.length ps) (String.concat "\n" lines))

let compact_world_text raw =
  raw
  |> String.map (function '\n' | '\r' | '\t' -> ' ' | c -> c)
  |> String_util.utf8_safe ~max_bytes:603 ~suffix:"..."
  |> String_util.to_string

let take_unique_entries limit entries =
  if limit <= 0 then
    []
  else
    let seen = Hashtbl.create (limit + 1) in
    let rec loop remaining acc = function
      | [] -> List.rev acc
      | _ when remaining <= 0 -> List.rev acc
      | (key, value) :: rest ->
          if Hashtbl.mem seen key then
            loop remaining acc rest
          else begin
            Hashtbl.replace seen key ();
            loop (remaining - 1) ((key, value) :: acc) rest
          end
    in
    loop limit [] entries

let memory_context_query ~(memory : Agent_sdk.Memory.t) ~prefix ~limit =
  let ctx = Agent_sdk.Memory.context memory in
  Agent_sdk.Context.keys_in_scope ctx (Agent_sdk.Context.Custom "lt")
  |> List.filter (fun key -> String.starts_with ~prefix key)
  |> List.filter_map (fun key ->
         match Agent_sdk.Context.get_scoped ctx (Agent_sdk.Context.Custom "lt") key with
         | Some value -> Some (key, value)
         | None -> None)
  |> take_unique_entries limit

let load_world_text
    ~backend
    ~(memory : Agent_sdk.Memory.t)
    ~(limit : int) : string option =
  let entries =
    let backend_entries =
      match backend with
      | Some backend -> backend.Agent_sdk.Memory.query ~prefix:"world" ~limit
      | None -> []
    in
    take_unique_entries limit
      (backend_entries @ memory_context_query ~memory ~prefix:"world" ~limit)
  in
  match entries with
  | [] -> None
  | rows ->
    let lines =
      List.map
        (fun (key, json) ->
          Printf.sprintf "- %s: %s" key
            (json |> content_of_json |> compact_world_text))
        rows
    in
    Some
      (Printf.sprintf "[world memory: %d entries]\n%s"
         (List.length rows)
         (String.concat "\n" lines))

(** Load institutional memory as a text block for system context injection.

    Returns [None] when no institution config is available.
    Pure read: does not touch OAS [Memory.t].

    @since v2.265.0 (RFC-MASC-004 Phase 1) *)
let load_institution_text ~(config : Coord_utils.config) : string option =
  Option.map
    (fun w -> Printf.sprintf "[institutional memory]\n%s" w)
    (read_institution_welcome config)

(** Incrementally flush episodes and procedures.

    Designed to be called from an [AfterTurn] hook on every turn boundary.
    JSONL append-only semantics make repeated calls idempotent —
    already-persisted entries are skipped via ID check.

    @since v2.265.0 (RFC-MASC-004 Phase 1) *)
let flush_incremental ~(memory : Agent_sdk.Memory.t) ~(agent_name : string)
    : int * int =
  (* Reuse existing flush logic which is already incremental
     (skips persisted episode IDs, only writes changed procedures). *)
  let ep = flush_episodes ~memory ~agent_name in
  let pr = flush_procedures ~memory ~agent_name in
  (ep, pr)
