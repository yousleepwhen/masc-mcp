open Yojson.Safe.Util

type runtime_snapshot = {
  judge_online : bool;
  refreshing : bool;
  generated_at : string option;
  expires_at : string option;
  model_used : string option;
  keeper_name : string;
  last_error : string option;
}

type state = {
  mutex : Eio.Mutex.t;
  mutable started : bool;
  mutable refreshing : bool;
  mutable judge_online : bool;
  mutable generated_at_unix : float option;
  mutable expires_at_unix : float option;
  mutable generated_at : string option;
  mutable expires_at : string option;
  mutable model_used : string option;
  mutable last_error : string option;
  mutable judgments : (string, Yojson.Safe.t) Hashtbl.t;
}

let governance_dir base_path =
  Filename.concat (Filename.concat base_path ".masc") "governance"

(** Legacy single-file path (for fallback reads). *)
let judgments_path base_path =
  Filename.concat (governance_dir base_path) "judgments.jsonl"

(** Date-split store: [.masc/governance/judgments/YYYY-MM/DD.jsonl].
    Cached per base_dir so all callers share the same Eio.Mutex. *)
let judgments_store_cache : (string, Dated_jsonl.t) Hashtbl.t = Hashtbl.create 4

let get_judgments_store base_path : Dated_jsonl.t =
  let dir = Filename.concat (governance_dir base_path) "judgments" in
  match Hashtbl.find_opt judgments_store_cache dir with
  | Some store -> store
  | None ->
    let store = Dated_jsonl.create ~base_dir:dir () in
    Hashtbl.replace judgments_store_cache dir store;
    store

let states : (string, state) Hashtbl.t = Hashtbl.create 4

let with_lock (st : state) f =
  Eio.Mutex.use_rw ~protect:true st.mutex f

let ensure_dir path =
  Fs_compat.mkdir_p path

let iso_of_unix = Dashboard_utils.iso_of_unix
let parse_iso_opt = Dashboard_utils.parse_iso_opt

let now_iso () = Types.now_iso ()
let option_to_yojson f = function Some value -> f value | None -> `Null

let interval_sec () =
  match Sys.getenv_opt "MASC_DASHBOARD_GOVERNANCE_JUDGE_INTERVAL_SEC" with
  | Some raw -> (
      try max 15 (int_of_string (String.trim raw)) with Failure _ -> 60)
  | None -> 60

let cache_ttl_sec () = float_of_int (interval_sec () * 2)

let enabled () =
  match Sys.getenv_opt "MASC_DASHBOARD_GOVERNANCE_JUDGE_ENABLED" with
  | Some raw -> (
      match String.lowercase_ascii (String.trim raw) with
      | "0" | "false" | "no" | "off" -> false
      | _ -> true)
  | None -> true

let keeper_name = "operator-judge"

let get_state base_path =
  match Hashtbl.find_opt states base_path with
  | Some st -> st
  | None ->
      let st =
        {
          mutex = Eio.Mutex.create ();
          started = false;
          refreshing = false;
          judge_online = false;
          generated_at_unix = None;
          expires_at_unix = None;
          generated_at = None;
          expires_at = None;
          model_used = None;
          last_error = None;
          judgments = Hashtbl.create 32;
        }
      in
      Hashtbl.add states base_path st;
      st

let key_of kind id = kind ^ ":" ^ id

let judgment_key json =
  let kind = json |> member "target_kind" |> to_string in
  let id = json |> member "target_id" |> to_string in
  key_of kind id

let judgment_generated_at json =
  json |> member "generated_at" |> to_string_option |> parse_iso_opt
  |> Option.value ~default:0.0

let load_judgments_into_table jsons =
  let table = Hashtbl.create 32 in
  List.iter (fun json ->
    try
      let status = json |> member "status" |> to_string_option in
      if status = Some "active" then
        let key = judgment_key json in
        match Hashtbl.find_opt table key with
        | Some current
          when judgment_generated_at current >= judgment_generated_at json -> ()
        | _ -> Hashtbl.replace table key json
    with
    | Yojson.Safe.Util.Type_error _ -> ()
    | exn -> Log.Governance.warn "load_latest_from_disk parse: %s" (Printexc.to_string exn)
  ) jsons;
  table

(** Load latest judgments.
    Tries date-split store first; falls back to legacy single file. *)
let load_latest_from_disk base_path =
  let store = get_judgments_store base_path in
  let jsons = Dated_jsonl.read_recent store 10_000 in
  if jsons <> [] then
    load_judgments_into_table jsons
  else
    (* Legacy fallback *)
    let path = judgments_path base_path in
    if not (Sys.file_exists path) then Hashtbl.create 32
    else
      let content = Fs_compat.load_file path in
      let legacy_jsons =
        String.split_on_char '\n' content
        |> List.filter (fun line -> String.trim line <> "")
        |> List.filter_map (fun line ->
            try Some (Yojson.Safe.from_string line)
            with Yojson.Json_error _ -> None)
      in
      load_judgments_into_table legacy_jsons

let latest_judgments base_path =
  let st = get_state base_path in
  with_lock st (fun () ->
      if Hashtbl.length st.judgments = 0 then st.judgments <- load_latest_from_disk base_path;
      Hashtbl.to_seq_values st.judgments |> List.of_seq)

let runtime_status base_path =
  let st = get_state base_path in
  with_lock st (fun () ->
      {
        judge_online =
          st.judge_online
          &&
          (match st.expires_at_unix with
          | Some expires_at -> Unix.gettimeofday () < expires_at
          | None -> false);
        refreshing = st.refreshing;
        generated_at = st.generated_at;
        expires_at = st.expires_at;
        model_used = st.model_used;
        keeper_name;
        last_error = st.last_error;
      })

let parse_string_list json key =
  match json |> member key with
  | `List items ->
      items
      |> List.filter_map (function
             | `String value ->
                 let trimmed = String.trim value in
                 if trimmed = "" then None else Some trimmed
             | _ -> None)
  | _ -> []

let normalize_text raw =
  raw |> String.trim |> String.split_on_char '\n' |> List.map String.trim
  |> List.filter (fun item -> item <> "") |> String.concat " " |> String.trim

let allowed_tool tool =
  List.mem tool
    [
      "masc_governance_status";
      "masc_execution_orders";
      "masc_execute_dry_run";
      "masc_execute";
      "masc_operator_confirm";
    ]

let parse_recommended_action json =
  let action_json = json |> member "recommended_action" in
  match action_json with
  | `Assoc _ ->
      let resolved_tool = action_json |> member "resolved_tool" |> to_string_option in
      let resolved_tool =
        match resolved_tool with
        | Some tool when allowed_tool tool -> Some tool
        | _ -> None
      in
      Some
        (`Assoc
          [
            ("action_kind", action_json |> member "action_kind");
            ("resolved_tool", option_to_yojson (fun value -> `String value) resolved_tool);
            ("target_type", action_json |> member "target_type");
            ("target_id", action_json |> member "target_id");
            ( "reason",
              `String
                (normalize_text
                   (action_json |> member "reason" |> to_string_option
                  |> Option.value ~default:"")) );
            ("payload_preview", action_json |> member "payload_preview");
          ])
  | _ -> None

let parse_item_judgment ~generated_at ~expires_at ~model_used json =
  let target_kind =
    json |> member "kind" |> to_string_option |> Option.value ~default:""
    |> String.lowercase_ascii
  in
  let target_id = json |> member "id" |> to_string_option |> Option.value ~default:"" in
  if target_kind = "" || target_id = "" then None
  else
    let summary =
      normalize_text (json |> member "summary" |> to_string_option |> Option.value ~default:"")
    in
    if summary = "" then None
    else
      let confidence =
        match json |> member "confidence" with
        | `Float value -> max 0.0 (min 1.0 value)
        | `Int value -> max 0.0 (min 1.0 (float_of_int value))
        | _ -> 0.0
      in
      let evidence_refs = parse_string_list json "evidence_refs" in
      let recommended_action = parse_recommended_action json in
      let guardrail_state =
        match json |> member "guardrail_state" with
        | `Assoc _ as state_json ->
            Some
              (`Assoc
                [
                  ("requires_human_gate", state_json |> member "requires_human_gate");
                  ("pending_confirm_token", state_json |> member "pending_confirm_token");
                  ("ready_to_execute", state_json |> member "ready_to_execute");
                ])
        | _ -> None
      in
      Some
        (`Assoc
          [
            ("judgment_id", `String (Uuidm.to_string (Uuidm.v4_gen (Random.State.make_self_init ()) ())));
            ("target_kind", `String target_kind);
            ("target_id", `String target_id);
            ("status", `String "active");
            ("summary", `String summary);
            ("confidence", `Float confidence);
            ("generated_at", `String generated_at);
            ("expires_at", `String expires_at);
            ("model_used", `String model_used);
            ("keeper_name", `String keeper_name);
            ("evidence_refs", `List (List.map (fun item -> `String item) evidence_refs));
            ("recommended_action", option_to_yojson (fun value -> value) recommended_action);
            ("guardrail_state", option_to_yojson (fun value -> value) guardrail_state);
          ])

let prompt_for_facts facts_json =
  match
    Prompt_registry.render_prompt_template "dashboard.governance_judge"
      [ ("facts_json", Yojson.Safe.to_string facts_json) ]
  with
  | Ok value -> value
  | Error _ -> Prompt_registry.get_prompt "dashboard.governance_judge"

let compute_judgments
    ~(masc_tools : Types.tool_schema list)
    ~(dispatch : name:string -> args:Yojson.Safe.t -> bool * string)
    ~factual_json =
  let _timeout_sec = Env_config.Inference.dashboard_governance_judge_timeout_seconds in
  let prompt = prompt_for_facts factual_json in
  match
    Oas_worker.run_named_with_masc_tools ~cascade_name:"governance_judge"
      ~goal:prompt ~masc_tools ~dispatch ~max_turns:3
      ()
  with
  | Error message -> Error message
  | Ok result -> (
      let response = result.Oas_worker.response in
      try
        let parsed = Yojson.Safe.from_string (Oas_response.text_of_response response) in
        let generated_at = now_iso () in
        let expires_at = iso_of_unix (Unix.gettimeofday () +. cache_ttl_sec ()) in
        let items =
          match parsed |> member "items" with
          | `List rows -> rows
          | _ -> []
        in
        let judgments =
          items
          |> List.filter_map
               (parse_item_judgment ~generated_at ~expires_at
                  ~model_used:response.model)
        in
        Ok (response.model, generated_at, expires_at, judgments)
      with
      | Yojson.Json_error msg ->
          Error (Printf.sprintf "Governance judge returned invalid JSON: %s" msg)
      | exn ->
          Error (Printf.sprintf "Governance judge parse error: %s" (Printexc.to_string exn)))

(** Append judgments to date-split store.
    Thread-safe via Dated_jsonl internal mutex. *)
let append_judgments base_path judgments =
  let store = get_judgments_store base_path in
  List.iter (fun json -> Dated_jsonl.append store json) judgments

let refresh_once
    ~(masc_tools : Types.tool_schema list)
    ~(dispatch : name:string -> args:Yojson.Safe.t -> bool * string)
    ~base_path ~build_facts =
  let st = get_state base_path in
  with_lock st (fun () -> st.refreshing <- true);
  match compute_judgments ~masc_tools ~dispatch ~factual_json:(build_facts ()) with
  | Ok (model_used, generated_at, expires_at, judgments) ->
      append_judgments base_path judgments;
      with_lock st (fun () ->
          st.refreshing <- false;
          st.judge_online <- true;
          st.generated_at <- Some generated_at;
          st.generated_at_unix <- Some (Types.parse_iso8601 generated_at);
          st.expires_at <- Some expires_at;
          st.expires_at_unix <- Some (Types.parse_iso8601 expires_at);
          st.model_used <- Some model_used;
          st.last_error <- None;
          List.iter
            (fun json -> Hashtbl.replace st.judgments (judgment_key json) json)
            judgments)
  | Error message ->
      with_lock st (fun () ->
          st.refreshing <- false;
          st.judge_online <- false;
          st.last_error <- Some message)

let start ~sw ~clock ~base_path
    ~(masc_tools : Types.tool_schema list)
    ~(dispatch : name:string -> args:Yojson.Safe.t -> bool * string)
    ~build_facts () =
  let st = get_state base_path in
  let should_start =
    with_lock st (fun () ->
        if st.started || not (enabled ()) then false
        else (
          st.started <- true;
          true))
  in
  if should_start then
    Eio.Fiber.fork_daemon ~sw (fun () ->
        let rec loop () =
          refresh_once ~masc_tools ~dispatch ~base_path ~build_facts;
          Eio.Time.sleep clock (float_of_int (interval_sec ()));
          loop ()
        in
        loop ())
