(** Cascade_runner — Config, build, and run for OAS agent execution.

    Contains the [config] type, [build], [run], and [run_with_masc_tools]
    functions. All model-selection and cascade logic lives in
    {!Cascade_observation} and {!Keeper_turn_driver}.

    @since God file decomposition — extracted from oas_worker.ml *)

(* ================================================================ *)
(* Configuration                                                     *)
(* ================================================================ *)

type stop_reason =
  Cascade_agent_context.stop_reason =
  | Completed
  | TurnBudgetExhausted of { turns_used : int; limit : int }
  | MutationBoundaryReached of { turns_used : int; tool_name : string option }

type cli_transport_overrides =
  Cascade_transport.cli_transport_overrides = {
  cwd : string option;
  claude_mcp_config : string option;
  claude_allowed_tools : string list option;
  claude_permission_mode : string option;
  claude_max_turns : int option;
  gemini_yolo : bool option;
  cli_subprocess_idle_sec : float option;
}

type config =
  Cascade_agent_context.config = {
  name : string;
  provider_cfg : Llm_provider.Provider_config.t;
  provider : Agent_sdk.Provider.config;
  model_id : string;
  priority : Llm_provider.Request_priority.t option;
  system_prompt : string;
  tools : Agent_sdk.Tool.t list;
  runtime_mcp_policy :
    Llm_provider.Llm_transport.runtime_mcp_policy option;
  max_turns : int;
  max_idle_turns : int;
  stream_idle_timeout_s : float option;
  max_execution_time_s : float option;
  body_timeout_s : float option;
  max_tokens : int;
  max_input_tokens : int option;
  max_cost_usd : float option;
  temperature : float;
  hooks : Agent_sdk.Hooks.hooks option;
  context_reducer : Agent_sdk.Context_reducer.t option;
  guardrails : Agent_sdk.Guardrails.t option;
  event_bus : Agent_sdk.Event_bus.t option;
  checkpoint_dir : string option;
  session_id : string option;
  description : string option;
  memory : Agent_sdk.Memory.t option;
  initial_messages : Agent_sdk.Types.message list;
  raw_trace : Agent_sdk.Raw_trace.t option;
  tool_retry_policy : Agent_sdk.Tool_retry_policy.t option;
  required_tool_satisfaction :
    Agent_sdk.Completion_contract.required_tool_satisfaction;
  contract : Masc_mcp_cdal_runtime.Risk_contract.t option;
  enable_thinking : bool option;
  transport : Masc_grpc_transport.t;
  allowed_paths : string list;
  checkpoint_sidecar : Yojson.Safe.t option;
  cache_system_prompt : bool;
  yield_on_tool : bool;
  compact_ratio : float option;
  oas_auto_context_overflow_retry : bool;
  context_injector : Agent_sdk.Hooks.context_injector option;
  context : Agent_sdk.Context.t option;
  slot_id : int option;
  approval : Agent_sdk.Hooks.approval_callback option;
  exit_condition : (int -> bool) option;
  exit_condition_result : (int -> stop_reason * string option) option;
  summarizer : (Agent_sdk.Types.message list -> string) option;
  cli_transport_overrides : cli_transport_overrides option;
      (** Custom summarizer for OAS [Budget_strategy.reduce_for_budget]
          Emergency-phase compaction. Defaults to OAS's extractive
          default. Keeper workers inject [Keeper_summarizer.keeper_summarizer]
          to scrub [STATE] blocks before the 100-char truncation. *)
}

let default_config = Cascade_agent_context.default_config

type run_result = {
  response : Agent_sdk.Types.api_response;
  checkpoint : Agent_sdk.Checkpoint.t option;
  session_id : string;
  turns : int;
  trace_ref : Agent_sdk.Raw_trace.run_ref option;
  run_validation : Agent_sdk.Raw_trace.run_validation option;
  proof : Masc_mcp_cdal_runtime.Cdal_proof.t option;
  cascade_observation : Cascade_observation.cascade_observation option;
  stop_reason : stop_reason;
}

type worker_lifecycle_classification =
  { event : string
  ; status : string
  ; error : string option
  }

let worker_lifecycle_classification_of_result = function
  | Ok _ -> { event = "completed"; status = "completed"; error = None }
  | Error (Agent_sdk.Error.Agent (Agent_sdk.Error.MaxTurnsExceeded _)) ->
    { event = "completed"; status = "budget_exhausted"; error = None }
  | Error (Agent_sdk.Error.Agent (Agent_sdk.Error.AgentExecutionTimeout _)) ->
    { event = "failed"; status = "agent_execution_timeout"; error = None }
  | Error e ->
    { event = "failed"; status = "failed"; error = Some (Agent_sdk.Error.to_string e) }

(* Delegate to the canonical [Cdal_proof.result_status_to_string]
   (exposed in #14712 / T5).  Previously this re-implemented the same
   wire conversion via [show_result_status] + [String.rindex '.']
   substring split — a fragile [@@deriving show] strip that breaks
   silently when the type is moved or the module renamed.  Both this
   call site and [keeper_turn_telemetry.log_keeper_proof] (fixed in
   #14712) had grown independent copies of the same workaround. *)
let proof_result_status_to_string =
  Masc_mcp_cdal_runtime.Cdal_proof.result_status_to_string

(* ================================================================ *)
(* Internal: resolve provider                                        *)
(* ================================================================ *)

(** Resolve a model label string to an OAS Provider.config.
    Uses MASC [Cascade_config.parse_model_string] (with Provider_registry as SSOT).
    Explicit model-label execution must never silently substitute a
    discovery-only model. Callers are expected to validate labels
    before reaching this helper. *)
type label_resolution_error =
  Cascade_transport.label_resolution_error =
  | Invalid_model_label of string

let label_resolution_error_to_string =
  Cascade_transport.label_resolution_error_to_string

let label_resolution_error_to_sdk_error =
  Cascade_transport.label_resolution_error_to_sdk_error

let resolve_provider_config_of_label =
  Cascade_transport.resolve_provider_config_of_label

let invalid_runtime_config =
  Cascade_transport.invalid_runtime_config

let cli_model_override =
  Cascade_transport.cli_model_override

let provider_caps_of_config =
  Cascade_transport.provider_caps_of_config

let provider_supports_inline_tools =
  Cascade_transport.provider_supports_inline_tools

let provider_supports_runtime_mcp_lane =
  Cascade_transport.provider_supports_runtime_mcp_lane

let dedupe_preserve_order =
  Cascade_transport.dedupe_preserve_order

let public_mcp_tool_names_of_oas_tools =
  Cascade_transport.public_mcp_tool_names_of_oas_tools

let public_mcp_tool_requires_bound_actor =
  Cascade_transport.public_mcp_tool_requires_bound_actor

let runtime_mcp_tool_requires_bound_actor =
  Cascade_transport.runtime_mcp_tool_requires_bound_actor

let runtime_mcp_policy_with_masc_agent_name =
  Cascade_transport.runtime_mcp_policy_with_masc_agent_name

let cli_tool_a_can_auth_keeper_bound_runtime_mcp =
  Cascade_transport.cli_tool_a_can_auth_keeper_bound_runtime_mcp

let runtime_mcp_policy_for_provider =
  Cascade_transport.runtime_mcp_policy_for_provider

let public_mcp_tools_of_oas_tools =
  Cascade_transport.public_mcp_tools_of_oas_tools

let tool_names_are_public_mcp =
  Cascade_transport.tool_names_are_public_mcp

let public_mcp_runtime_policy_of_tool_names =
  Cascade_transport.public_mcp_runtime_policy_of_tool_names

let runtime_mcp_policy_of_tool_names =
  Cascade_transport.runtime_mcp_policy_of_tool_names

let provider_label =
  Cascade_transport.provider_label

let cli_tool_d_max_turns_hard_cap =
  Cascade_transport.cli_tool_d_max_turns_hard_cap

let provider_effective_max_turns =
  Cascade_transport.provider_effective_max_turns

let resolve_tool_lane_for_oas_tools =
  Cascade_transport.resolve_tool_lane_for_oas_tools

let make_per_call_switch_transport =
  Cascade_transport.make_per_call_switch_transport

let non_http_transport_of_provider =
  Cascade_transport.non_http_transport_of_provider

let request_runtime_fields_on_base_config
    ~(base : Llm_provider.Provider_config.t)
    (req_config : Llm_provider.Provider_config.t)
  =
  { base with
    max_tokens = req_config.max_tokens;
    temperature = req_config.temperature;
    top_p = req_config.top_p;
    top_k = req_config.top_k;
    min_p = req_config.min_p;
    system_prompt = req_config.system_prompt;
    enable_thinking = req_config.enable_thinking;
    thinking_budget = req_config.thinking_budget;
    clear_thinking = req_config.clear_thinking;
    tool_stream = req_config.tool_stream;
    tool_choice = req_config.tool_choice;
    disable_parallel_tool_use = req_config.disable_parallel_tool_use;
    response_format = req_config.response_format;
    output_schema = req_config.output_schema;
    cache_system_prompt = req_config.cache_system_prompt;
    supports_tool_choice_override =
      (match req_config.supports_tool_choice_override with
       | Some _ as override -> override
       | None -> base.supports_tool_choice_override);
    seed = req_config.seed;
  }

let provider_resource_slot_transport
    ~(kind : Fd_accountant.kind)
    (transport : Llm_provider.Llm_transport.t)
  : Llm_provider.Llm_transport.t =
  { complete_sync =
      (fun req ->
        Fd_accountant.with_slot ~kind (fun () ->
          transport.complete_sync req));
    complete_stream =
      (fun ?on_telemetry ~on_event req ->
        Fd_accountant.with_slot ~kind (fun () ->
          transport.complete_stream ?on_telemetry ~on_event req));
  }

let provider_http_slot_transport transport =
  provider_resource_slot_transport ~kind:Provider_http transport

let provider_cli_slot_transport transport =
  provider_resource_slot_transport ~kind:Provider_cli transport

let provider_config_preserving_http_transport
    ~sw
    ~net
    ~(provider_cfg : Llm_provider.Provider_config.t)
  : Llm_provider.Llm_transport.t =
  let http_transport = Llm_provider.Complete.make_http_transport ~sw ~net in
  let patch_request (req : Llm_provider.Llm_transport.completion_request) =
    { req with
      config =
        request_runtime_fields_on_base_config ~base:provider_cfg req.config;
    }
  in
  provider_http_slot_transport
    { complete_sync =
      (fun req ->
        (* RFC-0095 Phase 0 diagnostic trace — verify which transport path is invoked
           per turn for each provider. Removed at Phase 0 closeout. *)
        Log.Misc.debug
          "rfc0095-trace: cascade_runner http_transport.complete_sync invoked";
        http_transport.complete_sync (patch_request req));
      complete_stream =
      (fun ?on_telemetry ~on_event req ->
        (* RFC-0095 Phase 0 diagnostic trace — verify which transport path is invoked
           per turn for each provider. Removed at Phase 0 closeout. *)
        Log.Misc.debug
          "rfc0095-trace: cascade_runner http_transport.complete_stream invoked";
        http_transport.complete_stream ?on_telemetry ~on_event
          (patch_request req));
    }

let transport_for_provider ~sw ~net ~provider_cfg ?runtime_mcp_policy
    ?cli_transport_overrides () =
  if
    Llm_provider.Provider_config.is_subprocess_cli
      provider_cfg.Llm_provider.Provider_config.kind
  then
    match
      non_http_transport_of_provider ~sw ~provider_cfg ?runtime_mcp_policy
        ?cli_transport_overrides ()
    with
    | Ok (Some transport) -> Ok (Some (provider_cli_slot_transport transport))
    | Ok None -> Ok None
    | Error _ as err -> err
  else
    Ok
      (Some
         (provider_config_preserving_http_transport ~sw ~net ~provider_cfg))

module For_testing = struct
  let request_runtime_fields_on_base_config =
    request_runtime_fields_on_base_config

  let provider_http_slot_transport = provider_http_slot_transport
  let provider_cli_slot_transport = provider_cli_slot_transport
end

(* ================================================================ *)
(* Internal: event publishing                                        *)
(* ================================================================ *)

let publish_lifecycle =
  Keeper_oas_checkpoint.publish_lifecycle

let provider_lifecycle_attrs (config : config) =
  let provider_cfg = config.provider_cfg in
  let provider_kind =
    Llm_provider.Provider_config.string_of_provider_kind provider_cfg.kind
  in
  let nonempty_string key value =
    let value = String.trim value in
    if value = "" then [] else [ (key, `String value) ]
  in
  let endpoint =
    match String.trim provider_cfg.base_url, String.trim provider_cfg.request_path with
    | "", _ -> []
    | base_url, "" -> [ ("endpoint", `String base_url) ]
    | base_url, request_path -> [ ("endpoint", `String (base_url ^ request_path)) ]
  in
  [
    ("provider_kind", `String provider_kind);
    ("model_id", `String config.model_id);
    ("provider_model_id", `String provider_cfg.model_id);
    ("max_tokens", `Int config.max_tokens);
    ("max_turns", `Int config.max_turns);
  ]
  @ nonempty_string "base_url" provider_cfg.base_url
  @ nonempty_string "request_path" provider_cfg.request_path
  @ endpoint

(* ================================================================ *)
(* Internal: checkpoint persistence                                  *)
(* ================================================================ *)

let persist_checkpoint =
  Keeper_oas_checkpoint.persist_checkpoint

let build_checkpoint =
  Keeper_oas_checkpoint.build_checkpoint

let partial_response_of_stop =
  Keeper_oas_checkpoint.partial_response_of_stop

(* ================================================================ *)
(* Build                                                             *)
(* ================================================================ *)

let build
    ~(sw : Eio.Switch.t)
    ~(net : [ `Generic | `Unix ] Eio.Net.ty Eio.Resource.t)
    ~(config : config)
  : (Agent_sdk.Agent.t, Agent_sdk.Error.sdk_error) result =
  match
    transport_for_provider ~sw ~net ~provider_cfg:config.provider_cfg
      ?runtime_mcp_policy:config.runtime_mcp_policy
      ?cli_transport_overrides:config.cli_transport_overrides
      ()
  with
  | Error _ as e -> e
  | Ok transport ->
      let builder =
        Cascade_agent_context.builder_without_approval ~net ~config ?transport ()
      in
      let builder =
        match config.approval with
        | Some cb -> Agent_sdk.Builder.with_approval cb builder
        | None -> builder
      in
      Agent_sdk.Builder.build_safe builder

(* ================================================================ *)
(* Idle-detail enrichment                                           *)
(* ================================================================ *)

(** Enrich an [Agent_sdk.Error.to_string] detail with the name of the most
    recently called tool when the error is an "Idle detected" failure.
    For all other error strings the input is returned unchanged.

    Exposed at module level so it can be unit-tested independently of
    the network-bound [run] function. *)
let enrich_idle_detail =
  Keeper_oas_checkpoint.enrich_idle_detail

let run_duration_ms_since started_at =
  Float.max 0.0 ((Unix.gettimeofday () -. started_at) *. 1000.0)

let dashboard_status_of_stop_reason = function
  | Completed -> Dashboard_oas_bridge.Success
  | TurnBudgetExhausted _ -> Dashboard_oas_bridge.Timeout
  | MutationBoundaryReached _ ->
      Dashboard_oas_bridge.Cancelled { reason = "mutation_boundary_reached" }

let record_dashboard_oas_response ~config ~total_duration_ms ?serialization_ms
    ~status (response : Agent_sdk.Types.api_response) =
  try
    (* RFC-0132 PR-2: dashboard surface = external boundary; redact via SSOT. *)
    Dashboard_oas_bridge.record_response
      ~provider_id:
        (Boundary_redaction.to_string Boundary_redaction.runtime_provider_label)
      ~model_id:
        (Boundary_redaction.to_string Boundary_redaction.runtime_model_label)
      ~total_duration_ms ?serialization_ms ~status response
  with
  | Eio.Cancel.Cancelled _ as exn -> raise exn
  | exn ->
      Log.Misc.warn
        "oas_worker %s: dashboard_oas_bridge record failed: %s"
        config.name (Printexc.to_string exn)

let close_agent_for_cleanup ?(propagate_cancel = true) ~config agent =
  try Agent_sdk.Agent.close agent with
  | Eio.Cancel.Cancelled _ as e ->
      Log.Misc.warn
        "oas_worker %s: agent close cancelled during cleanup"
        config.name;
      if propagate_cancel then raise e
  | close_exn ->
      Log.Misc.warn "oas_worker %s: agent close failed during cleanup: %s"
        config.name (Printexc.to_string close_exn)

(* ================================================================ *)
(* Resume from checkpoint                                            *)
(* ================================================================ *)

(** Build an Agent.t from a checkpoint via [Agent.resume], overriding
    per-turn config values from the MASC config.

    The checkpoint provides: messages, turn_count, usage_stats.
    The MASC config provides: provider, model_id, system_prompt,
    max_turns, temperature, tools, hooks, guardrails, etc.

    [max_turns] and [max_cost_usd] are adjusted to account for
    cumulative values in the checkpoint — the keeper's per-call budget
    is added on top of the checkpoint's accumulated state.

    @boundary-contract
    - MASC owns: per-turn config selection (model, temperature, tools,
      system_prompt), per-turn budget allocation, checkpoint field patching
      to align MASC intent with OAS resume semantics.
    - OAS owns: cumulative token/cost accounting, turn_count tracking,
      Agent.resume state restoration, loop guard enforcement.
    - Neither may: MASC must not set [max_total_tokens] (OAS SSOT for
      cumulative budgets); OAS must not override MASC model/temperature
      selection after resume. *)
let resume_from_checkpoint
    ~(sw : Eio.Switch.t)
    ~(net : [ `Generic | `Unix ] Eio.Net.ty Eio.Resource.t)
    ~(config : config)
    ~(checkpoint : Agent_sdk.Checkpoint.t)
  : (Agent_sdk.Agent.t, Agent_sdk.Error.sdk_error) result =
  match
    transport_for_provider ~sw ~net ~provider_cfg:config.provider_cfg
      ?runtime_mcp_policy:config.runtime_mcp_policy
      ?cli_transport_overrides:config.cli_transport_overrides
      ()
  with
  | Error _ as e -> e
  | Ok transport ->
      let prepared_resume =
        Cascade_agent_context.prepare_resume ~config ~checkpoint
      in
      Log.Misc.info
        "oas_worker %s: resume checkpoint_turn_count=%d per_call_turn_budget=%d effective_max_turns=%d"
        config.name checkpoint.turn_count config.max_turns
        prepared_resume.agent_config.max_turns;
      let options = { prepared_resume.options with transport } in
      Ok
        (Agent_sdk.Agent.resume ~net ~checkpoint:prepared_resume.patched_checkpoint
           ~tools:config.tools ?context:config.context
           ~options ~config:prepared_resume.agent_config
           ~auto_context_overflow_retry:config.oas_auto_context_overflow_retry
           ())

(* ================================================================ *)
(* Run                                                               *)
(* ================================================================ *)

let run
    ~(sw : Eio.Switch.t)
    ~(net : [ `Generic | `Unix ] Eio.Net.ty Eio.Resource.t)
    ~(config : config)
    ?oas_checkpoint
    ?(on_event : (Agent_sdk.Types.sse_event -> unit) option)
    ?(on_yield : (unit -> unit) option)
    ?(on_resume : (unit -> unit) option)
    ?(agent_ref : Agent_sdk.Agent.t option ref option)
    ?(proof_ref : Masc_mcp_cdal_runtime.Cdal_proof.t option ref option)
    ?(contract : Masc_mcp_cdal_runtime.Risk_contract.t option)
    (goal : string)
  : (run_result, Agent_sdk.Error.sdk_error) result =
  let session_id = match config.session_id with
    | Some id -> id
    | None ->
      Printf.sprintf "%s-%d-%06x"
        config.name
        (int_of_float (Time_compat.now () *. 1000.0))
        (Hashtbl.hash (Unix.gettimeofday ()) land 0xFFFFFF)
  in
  (match config.transport with
  | Masc_grpc_transport.Local -> ()
  | t ->
    Log.Misc.info "oas_worker %s: transport=%s"
      config.name (Masc_grpc_transport.to_string t));
  Option.iter (fun bus ->
    publish_lifecycle bus ~name:config.name ~event:"build" ~detail:goal
      ~attrs:(provider_lifecycle_attrs config)
      ()
  ) config.event_bus;
  let agent_result = match oas_checkpoint with
    | Some checkpoint ->
      (try resume_from_checkpoint ~sw ~net ~config ~checkpoint
       with
       | Eio.Cancel.Cancelled _ as e -> raise e
       | exn ->
         Log.Misc.warn "oas_worker %s: resume_from_checkpoint failed (%s), falling back to build"
           config.name (Printexc.to_string exn);
         build ~sw ~net ~config)
    | None -> build ~sw ~net ~config
  in
  match agent_result with
  | Error e ->
    Option.iter (fun bus ->
      publish_lifecycle bus ~name:config.name ~event:"build_error"
        ~detail:(Agent_sdk.Error.to_string e)
        ~error:(Agent_sdk.Error.to_string e)
        ~status:"build_error"
        ~session_id
        ~attrs:(provider_lifecycle_attrs config)
        ()
    ) config.event_bus;
    Error e
  | Ok agent ->
  (match agent_ref with Some r -> r := Some agent | None -> ());
  let effective_contract = match contract with Some c -> Some c | None -> config.contract in
  let run_started_at = Unix.gettimeofday () in
  (try
    let result, proof = match effective_contract with
      | Some c ->
        let cr = Masc_mcp_cdal_runtime.Contract_runner.run ~sw ~contract:c agent goal in
        (cr.response, Some cr.proof)
      | None ->
        (* Pass the process-level Eio clock when available so agent_sdk's
           [with_optional_timeout] can fire on hang when the caller has
           also set [config.max_execution_time_s]. Both inputs must be
           [Some] for the timeout to engage; absent either, behaviour is
           historical (block until provider closes). *)
        let clock =
          match Process_eio.get_clock () with
          | Ok c -> Some c
          | Error _ -> None
        in
        let r = match on_event with
          | Some cb -> Agent_sdk.Agent.run_stream ~sw ?clock ?on_yield ?on_resume ~on_event:cb agent goal
          | None -> Agent_sdk.Agent.run ~sw ?clock ?on_yield ?on_resume agent goal
        in
        (r, None)
    in
    let run_total_duration_ms = run_duration_ms_since run_started_at in
    (match proof_ref, proof with
     | Some ref_, Some _ -> ref_ := proof
     | _ -> ());
    let checkpoint =
      let ckpt =
        build_checkpoint ~session_id
          ?checkpoint_sidecar:config.checkpoint_sidecar agent
      in
      (match config.checkpoint_dir with
       | Some dir ->
         (match persist_checkpoint ~dir ~session_id ckpt with
          | Ok () -> ()
          | Error err ->
            Log.Misc.error "oas_worker: %s" err)
       | None -> ());
      Some ckpt
    in
    Option.iter (fun bus ->
      let lifecycle = worker_lifecycle_classification_of_result result in
      publish_lifecycle bus ~name:config.name ~event:lifecycle.event
        ~detail:(Printf.sprintf "session=%s" session_id)
        ?error:lifecycle.error
        ~session_id
        ~status:lifecycle.status
        ~attrs:(provider_lifecycle_attrs config)
        ()
    ) config.event_bus;
    let turns = (Agent_sdk.Agent.state agent).turn_count in
    let trace_ref = Agent_sdk.Agent.last_raw_trace_run agent in
    let close_after_success () =
      close_agent_for_cleanup ~propagate_cancel:false ~config agent
    in
    let run_validation =
      match trace_ref with
      | Some ref_ ->
        (match Agent_sdk.Raw_trace_query.validate_run ref_ with
         | Ok v -> Some v
         | Error err ->
           Log.Misc.warn "oas_worker: run_validation failed: %s"
             (Agent_sdk.Error.to_string err);
           None)
      | None -> None
    in
    (match result with
    | Ok response ->
      close_after_success ();
      record_dashboard_oas_response ~config
        ~total_duration_ms:run_total_duration_ms
        ~status:Dashboard_oas_bridge.Success response;
      Ok
        {
          response;
          checkpoint;
          session_id;
          turns;
          trace_ref;
          run_validation;
          proof;
          cascade_observation = None;
          stop_reason = Completed;
        }
    | Error (Agent_sdk.Error.Agent (Agent_sdk.Error.MaxTurnsExceeded r)) ->
      close_after_success ();
      let partial_response =
        partial_response_of_stop
          ~session_id
          ~text:(Printf.sprintf
            "[turn budget exhausted: %d/%d turns used]" r.turns r.limit)
      in
      record_dashboard_oas_response ~config
        ~total_duration_ms:run_total_duration_ms
        ~status:Dashboard_oas_bridge.Timeout partial_response;
      Ok
        {
          response = partial_response;
          checkpoint;
          session_id;
          turns;
          trace_ref;
          run_validation;
          proof;
          cascade_observation = None;
          stop_reason = TurnBudgetExhausted { turns_used = r.turns; limit = r.limit };
        }
    | Error (Agent_sdk.Error.Agent (Agent_sdk.Error.ExitConditionMet r)) -> (
      match config.exit_condition_result with
      | Some render ->
        close_after_success ();
        let stop_reason, response_text_opt = render r.turn in
        let response_text =
          match response_text_opt with
          | Some text when String.trim text <> "" -> text
          | _ -> Printf.sprintf "[exit condition met at turn %d]" r.turn
        in
        let partial_response =
          partial_response_of_stop
            ~session_id
            ~text:response_text
        in
        record_dashboard_oas_response ~config
          ~total_duration_ms:run_total_duration_ms
          ~status:(dashboard_status_of_stop_reason stop_reason)
          partial_response;
        Ok
          {
            response = partial_response;
            checkpoint;
            session_id;
            turns;
            trace_ref;
            run_validation;
            proof;
            cascade_observation = None;
            stop_reason;
          }
      | None ->
        close_agent_for_cleanup ~propagate_cancel:false ~config agent;
        Error (Agent_sdk.Error.Agent (Agent_sdk.Error.ExitConditionMet r)))
    | Error
        (Agent_sdk.Error.Agent
           (Agent_sdk.Error.AgentExecutionTimeout r as agent_err)) ->
      let partial_response =
        partial_response_of_stop
          ~session_id
          ~text:
            (Printf.sprintf
               "[agent execution timeout: elapsed=%.1fs timeout=%.1fs turns=%d/%d]"
               r.elapsed_sec
               r.timeout_sec
               r.turn_count
               r.max_turns)
      in
      record_dashboard_oas_response
        ~config
        ~total_duration_ms:run_total_duration_ms
        ~status:Dashboard_oas_bridge.Timeout
        partial_response;
      close_agent_for_cleanup ~propagate_cancel:false ~config agent;
      Error (Agent_sdk.Error.Agent agent_err)
    | Error err ->
      let detail = Agent_sdk.Error.to_string err in
      let detail =
        enrich_idle_detail detail (Agent_sdk.Agent.state agent).messages
      in
      let error_response =
        partial_response_of_stop ~session_id ~text:detail
      in
      record_dashboard_oas_response ~config
        ~total_duration_ms:run_total_duration_ms
        ~status:(Dashboard_oas_bridge.Error { transient = false })
        error_response;
      (* Demoted from WARN to DEBUG (task-239): this fires once per tier,
         but a cascade caller (Keeper_turn_driver.run_named) retries on the
         next provider.  Emitting WARN/ERROR here creates noise on
         recovered cascades.  The cascade layer logs [cascade-fallback] at
         INFO when it retries and emits ERROR only on full exhaustion. *)
      (match proof with
       | Some p ->
         Log.Misc.debug "oas_worker: agent errored with CDAL proof: run_id=%s status=%s error=%s"
           p.run_id
           (proof_result_status_to_string p.result_status)
           detail
       | None ->
         Log.Misc.debug "oas_worker: agent errored (no proof): %s" detail);
      close_agent_for_cleanup ~propagate_cancel:false ~config agent;
      Error err)
  with
  | Eio.Cancel.Cancelled _ as exn ->
    close_agent_for_cleanup ~propagate_cancel:false ~config agent;
    raise exn
  | exn ->
    let bt = Printexc.get_backtrace () in
    close_agent_for_cleanup ~config agent;
    let detail =
      Printf.sprintf "execution exception: %s" (Printexc.to_string exn)
    in
    let error_response =
      partial_response_of_stop ~session_id ~text:detail
    in
    record_dashboard_oas_response ~config
      ~total_duration_ms:(run_duration_ms_since run_started_at)
      ~status:(Dashboard_oas_bridge.Error { transient = false })
      error_response;
    Log.Misc.error "oas_worker %s: execution exception: %s\nBacktrace: %s"
      config.name (Printexc.to_string exn) bt;
    (* Keep the typed internal-error envelope, but construct it locally so
       Cascade_runner does not depend on Cascade_error_classify. *)
    let typed_internal_error =
      "[masc_oas_error] "
      ^ Yojson.Safe.to_string
          (`Assoc
            [ "kind", `String "internal_unhandled_exception"
            ; "site", `String "cascade_runner.execute"
            ; "exn_repr", `String (Printexc.to_string exn)
            ])
    in
    Error (Agent_sdk.Error.Internal typed_internal_error))

(* ================================================================ *)
(* Convenience: run_with_masc_tools                                  *)
(* ================================================================ *)

let run_with_masc_tools
    ~(sw : Eio.Switch.t)
    ~(net : [ `Generic | `Unix ] Eio.Net.ty Eio.Resource.t)
    ~(config : config)
    ~(masc_tools : Masc_domain.tool_schema list)
    ~(dispatch : name:string -> args:Yojson.Safe.t -> Tool_result.result)
    ?contract
    ?on_event
    ?on_yield
    ?on_resume
    (goal : string)
  : (run_result, Agent_sdk.Error.sdk_error) result =
  match
    public_mcp_runtime_policy_of_tool_names
      (List.map (fun (td : Masc_domain.tool_schema) -> td.name) masc_tools)
  with
  | Some runtime_mcp_policy
    when Provider_tool_support.provider_supports_runtime_mcp_policy
           config.provider_cfg runtime_mcp_policy ->
      let config = { config with runtime_mcp_policy = Some runtime_mcp_policy } in
      run ~sw ~net ~config ?on_event ?on_yield ?on_resume ?contract goal
  | _ when masc_tools = [] ->
      run ~sw ~net ~config ?on_event ?on_yield ?on_resume ?contract goal
  | _ when provider_supports_inline_tools config.provider_cfg ->
      let oas_tools =
        List.map
          (fun (td : Masc_domain.tool_schema) ->
            Tool_bridge.oas_tool_of_masc
              ~name:td.name
              ~description:td.description
              ~input_schema:td.input_schema
              (fun input -> dispatch ~name:td.name ~args:input))
          masc_tools
      in
      let config = { config with tools = oas_tools @ config.tools } in
      run ~sw ~net ~config ?on_event ?on_yield ?on_resume ?contract goal
  | _ ->
      Error
        (invalid_runtime_config "tool_support"
           (Printf.sprintf
              "%s does not support inline tools or request-scoped runtime MCP tools"
              (provider_label config.provider_cfg)))
