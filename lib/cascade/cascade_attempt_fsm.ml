(** Cascade_attempt_fsm — SDK error to FSM outcome, session/resumption analysis.

    Extracted from oas_worker_named.ml (God file decomposition).
    Converts OAS SDK errors into Cascade_fsm provider outcomes,
    classifies CLI-wrapped hard-quota patterns,
    and enriches errors with provider-specific hints.

    @since God file decomposition *)

(* DEPRECATED (RFC-0057 Phase 2): These string classifiers will be
   replaced by typed Provider_error variants. The new dispatch path
   receives CliWrapped { kind = Hard_quota | ... }
   directly from the provider adapter, eliminating the need for
   substring reconstruction.

   During the transition window, these functions remain for
   backward compatibility with old provider adapters that still
   emit InvalidRequest { message }. They will be removed once
   the Llm_provider opam pin is bumped to the RFC-0057 Phase 1
   version. *)
let retry_message_looks_like_not_found (message : string) : bool =
  String_util.contains_substring_ci message "not found"
  || String_util.contains_substring_ci message "status code: 404"
  || String_util.contains_substring_ci message "404 page not found"

let label_provider = "provider"
let label_kind = "kind"
let label_cascade_name = "cascade_name"
let label_capacity_scope = "capacity_scope"
let label_cascade = "cascade"
let label_source = "source"
let fallback_class_hard_quota = "hard_quota"
let fallback_class_max_turns = "max_turns"
let fallback_class_required_tool_contract_violation =
  "required_tool_contract_violation"

let retry_message_looks_like_model_access_denied (message : string) : bool =
  String_util.contains_substring_ci message "permission to access"
  || String_util.contains_substring_ci message "not have access to"
  || String_util.contains_substring_ci message "does not have access to"
  || String_util.contains_substring_ci message "not authorized to access"

let provider_capacity_scope_to_http =
  Cascade_attempt_fsm_http_error.provider_capacity_scope_to_http
let provider_error_to_http_error =
  Cascade_attempt_fsm_http_error.provider_error_to_http_error

let capacity_backpressure_source_to_failure_scope = function
  | Cascade_error_classify.Provider_capacity ->
    Llm_provider.Http_client.Failure_scope_provider
  | Cascade_error_classify.Client_capacity ->
    Llm_provider.Http_client.Failure_scope_account
  | Cascade_error_classify.Tier_admission ->
    Llm_provider.Http_client.Failure_scope_model
  | Cascade_error_classify.Cascade_slot ->
    Llm_provider.Http_client.Failure_scope_unknown

(** Convert an OAS sdk_error into a Cascade_fsm provider_outcome.
    API-level errors and model-capability-dependent agent errors are
    cascadeable (a different provider may succeed).  Structural agent
    errors (budget, idle, exit) are not — they would recur on any model. *)
let sdk_error_to_cascade_outcome (err : Agent_sdk.Error.sdk_error)
    : Cascade_fsm.provider_outcome option =
  match Cascade_error_classify.classify_masc_internal_error err with
  | Some (Cascade_error_classify.Resumable_cli_session { detail; _ }) ->
    Some
      (Cascade_fsm.Call_err
         (Llm_provider.Http_client.NetworkError
            { message = detail; kind = Llm_provider.Http_client.Unknown }))
  (* All other MASC-internal classifications (and unclassified errors) fall
     through to the structured [match err with] below to derive the cascade
     outcome from the raw [sdk_error] payload. *)
  | Some (Cascade_error_classify.Capacity_backpressure { detail; retry_after_sec; source; _ }) ->
    Some
      (Cascade_fsm.Call_err
         (Llm_provider.Http_client.ProviderFailure
            { kind =
                Llm_provider.Http_client.Capacity_exhausted
                  { scope = capacity_backpressure_source_to_failure_scope source
                  ; retry_after = retry_after_sec
                  ; model = None
                  }
            ; message = detail
            }))
  | Some (Cascade_error_classify.Cascade_exhausted _)
  | Some (Cascade_error_classify.No_tool_capable_provider _)
  | Some (Cascade_error_classify.Accept_rejected _)
  | Some (Cascade_error_classify.Admission_queue_timeout _)
  | Some (Cascade_error_classify.Admission_queue_rejected _)
  | Some (Cascade_error_classify.Turn_timeout _)
  | Some (Cascade_error_classify.Provider_timeout _)
  | Some (Cascade_error_classify.Max_tokens_ceiling_violation _)
  | Some (Cascade_error_classify.Ambiguous_post_commit _)
  (* RFC-0158: admission denial falls through — no provider was attempted,
     so there is no cascade-outcome to derive from the provider response. *)
  | Some (Cascade_error_classify.Retry_admission_denied _)
  (* RFC-0159 Phase A: opaque internal failures fall through to the raw
     sdk_error match below; they have no typed cascade-outcome mapping. *)
  | Some (Cascade_error_classify.Internal_unhandled_exception _)
  | Some (Cascade_error_classify.Internal_bridge_exception _)
  | Some (Cascade_error_classify.Internal_contract_rejected _)
  | None -> (
  match err with
  | Agent_sdk.Error.Api api_err ->
    let http_err = match[@warning "-8"] api_err with
      | Llm_provider.Retry.InvalidRequest { message } ->
        if retry_message_looks_like_model_access_denied message then
          Llm_provider.Http_client.ProviderFailure
            {
              kind =
                Llm_provider.Http_client.Capability_mismatch
                  { capability = Some "model_access" };
              message;
            }
        else
          let code =
            if retry_message_looks_like_not_found message then 404 else 400
          in
          Llm_provider.Http_client.HttpError { code; body = message }
      | Llm_provider.Retry.ContextOverflow { message; _ } ->
        Llm_provider.Http_client.HttpError { code = 400; body = message }
      | Llm_provider.Retry.RateLimited { message; _ } ->
        Llm_provider.Http_client.HttpError { code = 429; body = message }
      | Llm_provider.Retry.NotFound { message } ->
        Llm_provider.Http_client.HttpError { code = 404; body = message }
      | Llm_provider.Retry.ServerError { status; message } ->
        Llm_provider.Http_client.HttpError { code = status; body = message }
      | Llm_provider.Retry.AuthError { message } ->
        Llm_provider.Http_client.HttpError { code = 401; body = message }
      | Llm_provider.Retry.Overloaded { message } ->
        Llm_provider.Http_client.HttpError { code = 529; body = message }
      | Llm_provider.Retry.NetworkError { message; kind } ->
        Llm_provider.Http_client.NetworkError { message; kind }
      | Llm_provider.Retry.Timeout { message } ->
        Llm_provider.Http_client.NetworkError
          { message; kind = Llm_provider.Http_client.Timeout }
    in
    Some (Cascade_fsm.Call_err http_err)
  | Agent_sdk.Error.Provider provider_err ->
    Some (Cascade_fsm.Call_err (provider_error_to_http_error provider_err))
  (* Model-capability errors: the next provider may handle these.
     CompletionContractViolation: model returned text when tool_use was
     required — a different model with better tool calling may succeed.
     UnrecognizedStopReason: model returned a non-standard stop reason
     that this provider does not map — another provider may not. *)
  | Agent_sdk.Error.Agent (Agent_sdk.Error.CompletionContractViolation { reason; _ }) ->
    Some (Cascade_fsm.Call_err
      (Llm_provider.Http_client.AcceptRejected { reason }))
  | Agent_sdk.Error.Agent (Agent_sdk.Error.UnrecognizedStopReason { reason }) ->
    Some (Cascade_fsm.Call_err
      (Llm_provider.Http_client.AcceptRejected { reason }))
  | Agent_sdk.Error.Config
      (Agent_sdk.Error.InvalidConfig { field = "runtime_mcp_auth"; detail })
  | Agent_sdk.Error.Config
      (Agent_sdk.Error.InvalidConfig { field = "tool_support"; detail }) ->
    Some
      (Cascade_fsm.Call_err
         (Llm_provider.Http_client.AcceptRejected { reason = detail }))
  (* Other Agent error variants are structural (budget, idle, exit, retries,
     guardrails, tripwires) and would recur on any model — not cascadeable. *)
  | Agent_sdk.Error.Agent (MaxTurnsExceeded _)
  | Agent_sdk.Error.Agent (AgentExecutionTimeout _)
  | Agent_sdk.Error.Agent (TokenBudgetExceeded _)
  | Agent_sdk.Error.Agent (CostBudgetExceeded _)
  | Agent_sdk.Error.Agent (CostBudgetUnenforceable _)
  | Agent_sdk.Error.Agent (IdleDetected _)
  | Agent_sdk.Error.Agent (ToolRetryExhausted _)
  | Agent_sdk.Error.Agent (GuardrailViolation _)
  | Agent_sdk.Error.Agent (TripwireViolation _)
  | Agent_sdk.Error.Agent (ExitConditionMet _)
  | Agent_sdk.Error.Agent (InputRequired _) -> None
  (* Other Config errors (different InvalidConfig field, MissingEnvVar,
     UnsupportedProvider) and non-Api / non-Agent / non-Config families are
     not cascade-recoverable. *)
  | Agent_sdk.Error.Config (InvalidConfig _)
  | Agent_sdk.Error.Config (MissingEnvVar _)
  | Agent_sdk.Error.Config (UnsupportedProvider _)
  | Agent_sdk.Error.Mcp _
  | Agent_sdk.Error.Serialization _
  | Agent_sdk.Error.Io _
  | Agent_sdk.Error.Orchestration _
  | Agent_sdk.Error.A2a _
  | Agent_sdk.Error.Internal _ -> None)

let sdk_error_is_model_access_denied (err : Agent_sdk.Error.sdk_error) =
  match sdk_error_to_cascade_outcome err with
  | Some
      (Cascade_fsm.Call_err
         (Llm_provider.Http_client.ProviderFailure
            {
              kind =
                Llm_provider.Http_client.Capability_mismatch
                  { capability = Some "model_access" };
              _;
            })) ->
    true
  | _ -> false

let provider_auth_hint_marker = "Provider auth returned 401"
let openai_compat_not_found_hint_marker = "OpenAI-compatible endpoint returned 404"

let cascade_name_to_string = Cascade_name.to_string

let resolve_provider_api_key_env_name ~cascade_name ~provider_cfg =
  let cascade_name = cascade_name_to_string cascade_name in
  let provider_name =
    Llm_provider.Provider_registry.provider_name_of_config provider_cfg
  in
  let fallback_env =
    Llm_provider.Provider_config.default_api_key_env provider_cfg.kind
    |> Option.value ~default:""
  in
  let resolve_from_overrides overrides =
    let find_non_empty key =
      match List.assoc_opt key overrides with
      | Some value when String.trim value <> "" -> Some value
      | _ -> None
    in
    match find_non_empty provider_name with
    | Some env_name -> env_name
    | None ->
      (match find_non_empty "*" with
       | Some env_name -> env_name
       | None -> fallback_env)
  in
  match Cascade_oas_runner.default_config_path () with
  | Some config_path ->
    let overrides =
      Cascade_config.resolve_api_key_env ~config_path ~name:cascade_name
    in
    resolve_from_overrides overrides
  | None -> fallback_env

let enrich_sdk_error ~cascade_name
    ~(provider_cfg : Llm_provider.Provider_config.t)
    (err : Agent_sdk.Error.sdk_error) =
  let append_hint message hint_marker detail =
    if String_util.contains_substring_ci message hint_marker then
      message
    else
      Printf.sprintf "%s (%s: %s)" message hint_marker detail
  in
  match err with
  | Agent_sdk.Error.Api (Llm_provider.Retry.AuthError { message })
    when not (Llm_provider.Provider_config.is_subprocess_cli provider_cfg.kind) ->
    let env_name =
      match resolve_provider_api_key_env_name ~cascade_name ~provider_cfg with
      | "" -> "configured provider API key env"
      | value -> value
    in
    let detail =
      if String.trim provider_cfg.api_key = "" then
        Printf.sprintf "%s is empty or unset in this process" env_name
      else
        Printf.sprintf
          "%s was loaded and the auth header was populated; verify that it is valid for the configured provider"
          env_name
    in
    Agent_sdk.Error.Api
      (Llm_provider.Retry.AuthError
         {
           message =
             append_hint message provider_auth_hint_marker detail;
         })
  | Agent_sdk.Error.Api (Llm_provider.Retry.InvalidRequest { message })
    when retry_message_looks_like_not_found message ->
    (* Endpoint URL hint is shape-agnostic data — every provider_cfg carries
       [base_url] / [request_path] (empty strings for CLI agents) — so the
       not_found hint applies to any provider whose retry message matches the
       not_found pattern.  CLI providers' not_found errors rarely surface
       through this code path (they emit text errors, not the OpenAI-compat
       InvalidRequest shape), so the practical effect is a no-op for CLI
       agents while still helping HTTP providers (provider_d-compat, provider_k, etc.)
       diagnose endpoint drift.  RFC-0058 §2.4: no closed-variant dispatch. *)
    let detail =
      Printf.sprintf "base_url=%s request_path=%s endpoint=%s"
        provider_cfg.base_url provider_cfg.request_path
        (provider_cfg.base_url ^ provider_cfg.request_path)
    in
    Agent_sdk.Error.Api
      (Llm_provider.Retry.InvalidRequest
         {
           message =
             append_hint message openai_compat_not_found_hint_marker detail;
         })
  | _ -> err

(** CLI-wrapped error variants where quota signals may appear serialized as
    text.  AuthError, NotFound, ContextOverflow, and Timeout never carry
    quota information — excluding them avoids unnecessary substring scans
    and makes the structural filter explicit. *)
let api_error_message_for_quota_scan (api_err : Llm_provider.Retry.api_error)
    : string option =
  match api_err with
  | Llm_provider.Retry.RateLimited { message; _ } ->
    (* Structured hard-quota check is handled separately by
       [Llm_provider.Retry.is_hard_quota]; this extractor is for the
       CLI-wrapped fallback path only.  RateLimited messages are included
       here so the compound CLI-exit-code heuristic can still fire on
       messages that [is_hard_quota] does not cover. *)
    Some message
  | Llm_provider.Retry.NetworkError { message; _ } -> Some message
  | Llm_provider.Retry.Overloaded { message } -> Some message
  | Llm_provider.Retry.ServerError { message; _ } -> Some message
  (* InvalidRequest covers Provider_a's HTTP 400 user-set monthly cap
     ("You have reached your specified API usage limits...").  Without
     this branch, direct (non-CLI) API calls treat the cap as a
     retryable client error and the cascade burns its full turn budget
     on a permanent failure.  Observed 2026-04-29. *)
  | Llm_provider.Retry.InvalidRequest { message } -> Some message
  | Llm_provider.Retry.AuthError _
  | Llm_provider.Retry.NotFound _
  | Llm_provider.Retry.ContextOverflow _
  | Llm_provider.Retry.Timeout _ ->
    None

(** Substring indicators for hard-quota signals in CLI-wrapped error text.

    These are necessary because CLI transports (Provider_f CLI, Claude Code CLI)
    serialize provider errors as plain text in [NetworkError.message] or
    [InvalidRequest.message].  The structured [Llm_provider.Retry.is_hard_quota]
    only inspects the [RateLimited] variant, so CLI-wrapped messages require
    text-level pattern matching.

    [Llm_provider.Retry.is_hard_quota_message] exists in the external library
    but is not exposed in its .mli, so these indicators cannot delegate to it.
    If it becomes public in a future agent_sdk release, this list can be
    replaced with a call to that function plus the CLI-specific extras. *)
let cli_wrapped_hard_quota_indicators = [
  "hard_quota";
  "terminalquotaerror";
  "quota_exhausted";
  "exhausted your capacity on this model";
  "quota will reset after";
  "\"api_error_status\":429";
  "you've hit your limit";
  "monthly usage limit";
  "org's monthly usage limit";
  "resets apr ";
  (* Provider_a console usage-limit error (HTTP 400 invalid_request_error,
     observed 2026-04-29 with 2-day reset window).  Body shape:
       {"type":"error","error":{"type":"invalid_request_error",
        "message":"You have reached your specified API usage limits.
        You will regain access on YYYY-MM-DD at HH:MM UTC."}}
     Earlier 429-based indicators don't match because Provider_a now
     returns 400 with the user-set monthly cap.  Without these markers
     [sdk_error_is_hard_quota] returns false → cascade keeps retrying
     cli_tool_d:auto for the full OAS turn budget (~60min). *)
  "reached your specified api usage limits";
  "you will regain access on";
]

let message_looks_like_cli_wrapped_hard_quota (message : string) : bool =
  let contains needle =
    String_util.contains_substring_ci message needle
  in
  List.exists contains cli_wrapped_hard_quota_indicators
  ||
  (contains "exited with code 1"
   && contains "\"api_error_status\":429"
   && contains "you've hit your limit")

let capacity_backpressure_indicators = [
  "client capacity";
  "capacity exhausted";
  "local_resource_exhaustion";
  "slot full";
]

let message_looks_like_capacity_backpressure (message : string) : bool =
  let contains needle =
    String_util.contains_substring_ci message needle
  in
  List.exists contains capacity_backpressure_indicators

let exit_code_of_message (message : string) : int option =
  let prefix = "exited with code " in
  match String.index_opt message ' ' with
  | None -> None
  | Some first_space ->
      let search_from = first_space + 1 in
      if search_from >= String.length message then None
      else
        let suffix =
          String.sub message search_from (String.length message - search_from)
        in
        if not (String.starts_with ~prefix suffix) then None
        else
          match String.index_from_opt suffix (String.length prefix) ':' with
          | None -> None
          | Some colon ->
              let raw =
                String.sub suffix (String.length prefix)
                  (colon - String.length prefix)
                |> String.trim
              in
              int_of_string_opt raw

let sdk_error_is_resumable_cli_session (err : Agent_sdk.Error.sdk_error) : bool =
  match Cascade_error_classify.classify_masc_internal_error err with
  | Some (Cascade_error_classify.Resumable_cli_session _) -> true
  | _ -> false

let message_looks_like_terminal_provider_runtime_failure message =
  let contains needle = String_util.contains_substring_ci message needle in
  (contains "provider cli rejected" && contains "exit 1")
  || (contains "provider cli startup crash" && contains "unicodedecodeerror")
  || contains "unicodedecodeerror"
  || (contains "jsonrpcmessage"
      && (contains "validationerror" || contains "invalid json"))
  || (contains "error parsing sse message"
      && (contains "jsonrpc" || contains "jsonrpcmessage"))

(* D6 fix: typed network-class terminals.

   [Llm_provider.Retry.NetworkError] carries a structured
   [Http_client.network_error_kind].  Pre-fix the classifier only inspected
   the embedded [message] string, so a single endpoint outage materialised
   as 3–5 retry events before cooldown.  Treating
   [Connection_refused]/[Dns_failure] as terminal directly off the typed
   variant collapses one outage to one event (~70% reduction of the
   residual network-class storm).

   The remaining [network_error_kind] arms ([Tls_error], [Timeout],
   [Local_resource_exhaustion], [End_of_file], [Unknown]) are *not*
   reclassified here: TLS/timeout/EOF can be transient and have separate
   policy paths upstream, and [Local_resource_exhaustion] is the OS-level
   class handled by [System_error_class]. *)
let network_error_kind_is_terminal
    (kind : Llm_provider.Http_client.network_error_kind) : bool =
  match kind with
  | Llm_provider.Http_client.Connection_refused -> true
  | Llm_provider.Http_client.Dns_failure -> true
  | Llm_provider.Http_client.Tls_error
  | Llm_provider.Http_client.Timeout
  | Llm_provider.Http_client.Local_resource_exhaustion
  | Llm_provider.Http_client.End_of_file
  | Llm_provider.Http_client.Unknown -> false

let sdk_error_is_terminal_provider_runtime_failure
    (err : Agent_sdk.Error.sdk_error) : bool =
  let direct_typed_network =
    match err with
    | Agent_sdk.Error.Api (Llm_provider.Retry.NetworkError { kind; _ }) ->
        network_error_kind_is_terminal kind
    | _ -> false
  in
  let direct_api_message =
    match err with
    | Agent_sdk.Error.Api
        (Llm_provider.Retry.NetworkError { message; _ }
        | Llm_provider.Retry.Overloaded { message }
        | Llm_provider.Retry.ServerError { message; _ }
        | Llm_provider.Retry.InvalidRequest { message }
        | Llm_provider.Retry.RateLimited { message; _ }
        | Llm_provider.Retry.AuthError { message }
        | Llm_provider.Retry.NotFound { message }
        | Llm_provider.Retry.ContextOverflow { message; _ }
        | Llm_provider.Retry.Timeout { message }) ->
        message_looks_like_terminal_provider_runtime_failure message
    | _ -> false
  in
  direct_typed_network
  || direct_api_message
  || message_looks_like_terminal_provider_runtime_failure
       (Agent_sdk.Error.to_string err)

let sdk_error_is_required_tool_contract_violation
    (err : Agent_sdk.Error.sdk_error) : bool =
  match err with
  | Agent_sdk.Error.Agent
      (Agent_sdk.Error.CompletionContractViolation { contract; _ }) ->
    contract = Agent_sdk.Completion_contract_id.Require_tool_use
  | _ -> false

let sdk_error_is_hard_quota (err : Agent_sdk.Error.sdk_error) : bool =
  match err with
  | Agent_sdk.Error.Provider (Llm_provider.Error.HardQuota _) -> true
  | Agent_sdk.Error.Api api_err ->
    (* Layer 1: structured variant check — [is_hard_quota] inspects the
       [RateLimited] variant for known hard-quota message patterns. *)
    Llm_provider.Retry.is_hard_quota api_err
    ||
    (* Layer 2: CLI-wrapped fallback — extract message from variants that
       may carry serialized CLI output, then scan for quota indicators.
       Variants excluded by [api_error_message_for_quota_scan] (AuthError,
       NotFound, ContextOverflow, Timeout) never carry quota signals. *)
    (match api_error_message_for_quota_scan api_err with
     | Some message ->
       message_looks_like_cli_wrapped_hard_quota message
     | None -> false)
  (* Non-Api error families never carry provider-level hard-quota signals. *)
  | Agent_sdk.Error.Provider _
  | Agent_sdk.Error.Agent _
  | Agent_sdk.Error.Mcp _
  | Agent_sdk.Error.Config _
  | Agent_sdk.Error.Serialization _
  | Agent_sdk.Error.Io _
  | Agent_sdk.Error.Orchestration _
  | Agent_sdk.Error.A2a _
  | Agent_sdk.Error.Internal _ -> false

(* [provider_label] sanitises an upstream-supplied provider string
   into a metric/log label value.  Empty / whitespace-only input
   is replaced with the literal "unknown" so Prometheus labels stay
   well-formed.  The replacement is fail-open by design (we never
   want metric emission to itself raise), but the empty case is
   almost always a bug at the call site — the caller forgot to
   thread a real provider through.  WARN + counter make the
   substitution visible so the root call site can be traced and
   fixed; the helper itself does not change behaviour. *)
let provider_label provider =
  match String.trim provider with
  | "" ->
      Prometheus.inc_counter
        Prometheus.metric_cascade_attempt_empty_provider_label
        ();
      let bt =
        Printexc.raw_backtrace_to_string (Printexc.get_callstack 6)
      in
      Log.Misc.warn
        "[cascade_attempt:empty_provider_label] provider_label received \
         empty/blank input; substituting \"unknown\".  Fix the call \
         site that passed an empty provider through \
         Cascade_attempt_fsm.provider_label. Callstack (top 6):\n%s"
        bt;
      "unknown"
  | value -> value

(* RFC-0132 PR-2: cascade-fsm public surface label = external boundary; redact via SSOT. *)
let public_runtime_provider_label =
  Boundary_redaction.to_string Boundary_redaction.runtime_provider_label

let provider_error_capacity_scope = function
  | Llm_provider.Error.CapacityModel -> `Model
  | Llm_provider.Error.CapacityAccount
  | Llm_provider.Error.CapacityRegion
  | Llm_provider.Error.CapacityProvider
  | Llm_provider.Error.CapacityUnknown ->
      `Provider

let provider_error_should_cascade = function
  | Llm_provider.Error.RateLimit _
  | Llm_provider.Error.HardQuota _
  | Llm_provider.Error.CapacityExhausted _
  | Llm_provider.Error.ProviderUnavailable _
  | Llm_provider.Error.ParseError _
  | Llm_provider.Error.Timeout _ ->
      true
  | Llm_provider.Error.ServerError { transient; _ } -> transient
  | Llm_provider.Error.NetworkError
      { kind = Llm_provider.Http_client.Tls_error
             | Llm_provider.Http_client.Local_resource_exhaustion; _ } ->
      false
  | Llm_provider.Error.NetworkError _ -> true
  | Llm_provider.Error.MissingApiKey _
  | Llm_provider.Error.InvalidConfig _
  | Llm_provider.Error.UnknownVariant _
  | Llm_provider.Error.AuthError _
  | Llm_provider.Error.InvalidRequest _
  | Llm_provider.Error.NotFound _
  | Llm_provider.Error.ProviderTerminal _ ->
      false

let transient_http_status code =
  code = 408 || code = 409 || code = 425 || code = 429 || code >= 500

let provider_capacity ?(scope = `Provider) _provider =
  Some (Provider_error.CapacityBackpressure { scope })

let retry_api_error_to_provider_error ~provider ~capacity_backpressure api_error =
  let provider = provider_label provider in
  match api_error with
  | Llm_provider.Retry.RateLimited { retry_after; _ } ->
      if capacity_backpressure then provider_capacity provider
      else Some (Provider_error.RateLimit { retry_after })
  | Llm_provider.Retry.Overloaded _ ->
      if capacity_backpressure then provider_capacity provider
      else Some (Provider_error.ServerError { code = 529; transient = true })
  | Llm_provider.Retry.ServerError { status; _ } ->
      Some
        (Provider_error.ServerError
           { code = status; transient = transient_http_status status })
  | Llm_provider.Retry.AuthError _ -> Some Provider_error.AuthError
  | Llm_provider.Retry.InvalidRequest { message } ->
      if capacity_backpressure then provider_capacity provider
      else Some (Provider_error.InvalidRequest { reason = message })
  | Llm_provider.Retry.NotFound { message } ->
      Some (Provider_error.InvalidRequest { reason = message })
  | Llm_provider.Retry.ContextOverflow _ ->
      provider_capacity ~scope:`Model provider
  | Llm_provider.Retry.NetworkError _
  | Llm_provider.Retry.Timeout _ ->
      if capacity_backpressure then provider_capacity provider else None

let sdk_provider_error_to_provider_error = function
  | Llm_provider.Error.RateLimit { retry_after; _ } ->
      Some (Provider_error.RateLimit { retry_after })
  | Llm_provider.Error.HardQuota { detail; _ } ->
      Some (Provider_error.CliWrappedHardQuota { detail })
  | Llm_provider.Error.CapacityExhausted { scope; _ } ->
      Some
        (Provider_error.CapacityBackpressure
           { scope = provider_error_capacity_scope scope })
  | Llm_provider.Error.AuthError _
  | Llm_provider.Error.MissingApiKey _ ->
      Some Provider_error.AuthError
  | Llm_provider.Error.ServerError { code; transient; _ } ->
      Some (Provider_error.ServerError { code; transient })
  | Llm_provider.Error.InvalidRequest { reason; _ } ->
      Some (Provider_error.InvalidRequest { reason })
  | Llm_provider.Error.NotFound _ -> Some Provider_error.ModelNotFound
  | Llm_provider.Error.InvalidConfig { detail; _ } ->
      Some (Provider_error.InvalidRequest { reason = detail })
  | Llm_provider.Error.ProviderTerminal { detail; _ } ->
      Some (Provider_error.InvalidRequest { reason = detail })
  | Llm_provider.Error.ProviderUnavailable _ ->
      Some (Provider_error.ServerError { code = 503; transient = false })
  | Llm_provider.Error.ParseError _
  | Llm_provider.Error.UnknownVariant _
  | Llm_provider.Error.NetworkError _
  | Llm_provider.Error.Timeout _ ->
      None
let sdk_error_to_provider_error ~provider err =
  match err with
  | Agent_sdk.Error.Api api_err ->
      retry_api_error_to_provider_error ~provider
        ~capacity_backpressure:(sdk_error_is_hard_quota err)
        api_err
  | Agent_sdk.Error.Provider provider_err ->
      sdk_provider_error_to_provider_error provider_err
  (* Non-Api families do not map to a provider-level error. *)
  | Agent_sdk.Error.Agent _
  | Agent_sdk.Error.Mcp _
  | Agent_sdk.Error.Config _
  | Agent_sdk.Error.Serialization _
  | Agent_sdk.Error.Io _
  | Agent_sdk.Error.Orchestration _
  | Agent_sdk.Error.A2a _
  | Agent_sdk.Error.Internal _ -> None

let provider_error_total_metric = "masc_provider_error_total"

let () =
  Prometheus.register_counter
    ~name:provider_error_total_metric
    ~help:
      "Total provider-level errors classified during cascade \
       attempts (rate limit, auth failure, capacity exhaustion, \
       server error, invalid request). Labels: kind \
       (Provider_error.to_error_kind), provider (neutral runtime \
       label), cascade_name (originating cascade), capacity_scope \
       (CapacityExhausted scope or \"none\")."
    ()

let provider_error_capacity_scope_label = function
  | Provider_error.CapacityBackpressure { scope } ->
      Provider_error.scope_to_string scope
  | Provider_error.RateLimit _
  | Provider_error.AuthError
  | Provider_error.ServerError _
  | Provider_error.InvalidRequest _
  | Provider_error.CliWrappedHardQuota _
  | Provider_error.CliWrappedMaxTurns _
  | Provider_error.CliWrappedResumableSession _
  | Provider_error.PermissionDenied _
  | Provider_error.ModelNotFound ->
      "none"

let emit_provider_error_metric ~cascade_name ~provider error =
  let cascade_name = provider_label (cascade_name_to_string cascade_name) in
  let provider = provider_label provider in
  Dashboard_oas_bridge.record_provider_error ~cascade_name ~provider_id:provider
    error;
  Prometheus.inc_counter provider_error_total_metric
    ~labels:
      [
        (label_kind, Provider_error.to_error_kind error);
        (label_provider, public_runtime_provider_label);
        (label_cascade_name, cascade_name);
        (label_capacity_scope, provider_error_capacity_scope_label error);
      ]
    ()

let timeout_phase_label_of_sdk_error (err : Agent_sdk.Error.sdk_error) : string =
  match err with
  | Agent_sdk.Error.Provider
      (Llm_provider.Error.Timeout { timeout_phase = Some phase; _ })
  | Agent_sdk.Error.Provider
      (Llm_provider.Error.NetworkError { timeout_phase = Some phase; _ }) ->
      Llm_provider.Http_client.timeout_phase_to_label phase
  | _ -> label_provider

let emit_oas_run_timeout_metric ~cascade_name ~provider:_ err =
  match err with
  | Agent_sdk.Error.Api (Llm_provider.Retry.Timeout _) ->
      let cascade_name = provider_label (cascade_name_to_string cascade_name) in
      Prometheus.inc_counter Keeper_metrics.(to_string OasRunTimeout)
        ~labels:
          [
            (label_cascade, cascade_name);
            (label_provider, public_runtime_provider_label);
            (label_source, timeout_phase_label_of_sdk_error err);
          ]
        ()
  | Agent_sdk.Error.Provider
      (Llm_provider.Error.Timeout _
      | Llm_provider.Error.NetworkError { timeout_phase = Some _; _ }) ->
      let cascade_name = provider_label (cascade_name_to_string cascade_name) in
      Prometheus.inc_counter Keeper_metrics.(to_string OasRunTimeout)
        ~labels:
          [
            (label_cascade, cascade_name);
            (label_provider, public_runtime_provider_label);
            (label_source, timeout_phase_label_of_sdk_error err);
          ]
        ()
  | _ -> ()

let classify_saturation_signal_kind
    (err : Agent_sdk.Error.sdk_error) :
    Cascade_saturation_signal.kind option =
  let typed_kind =
    match Cascade_error_classify.classify_masc_internal_error err with
    | Some (Cascade_error_classify.Provider_timeout _) ->
        Some Cascade_saturation_signal.K_time_cap_fired
    | _ -> None
  in
  match typed_kind with
  | Some _ as kind -> kind
  | None -> (
    match err with
    | Agent_sdk.Error.Api (Llm_provider.Retry.RateLimited _) ->
        Some Cascade_saturation_signal.K_provider_rate_limited
    | Agent_sdk.Error.Api (Llm_provider.Retry.Timeout _)
    | Agent_sdk.Error.Api (Llm_provider.Retry.Overloaded _)
    | Agent_sdk.Error.Api (Llm_provider.Retry.ServerError _)
    | Agent_sdk.Error.Api (Llm_provider.Retry.AuthError _)
    | Agent_sdk.Error.Api (Llm_provider.Retry.InvalidRequest _)
    | Agent_sdk.Error.Api (Llm_provider.Retry.NotFound _)
    | Agent_sdk.Error.Api (Llm_provider.Retry.ContextOverflow _)
    | Agent_sdk.Error.Api (Llm_provider.Retry.NetworkError _)
    | Agent_sdk.Error.Provider _
    | Agent_sdk.Error.Agent _
    | Agent_sdk.Error.Mcp _
    | Agent_sdk.Error.Config _
    | Agent_sdk.Error.Serialization _
    | Agent_sdk.Error.Io _
    | Agent_sdk.Error.Orchestration _
    | Agent_sdk.Error.A2a _
    | Agent_sdk.Error.Internal _ -> None)

let maybe_emit_cascade_saturation_signal ~cascade_name ~provider:_ err =
  if Env_config_keeper.CascadeSaturationSignal.enabled () then
    match classify_saturation_signal_kind err with
    | None -> ()
    | Some kind ->
        let cascade_name_str =
          provider_label (cascade_name_to_string cascade_name)
        in
        Prometheus.inc_counter
          Keeper_metrics.(to_string CascadeSaturationSignal)
          ~labels:
            [
              (label_kind, Cascade_saturation_signal.kind_to_string kind);
              (label_cascade, cascade_name_str);
            ]
          ()

let emit_sdk_provider_error_metric ~cascade_name ~provider err =
  emit_oas_run_timeout_metric ~cascade_name ~provider err;
  maybe_emit_cascade_saturation_signal ~cascade_name ~provider err;
  match sdk_error_to_provider_error ~provider err with
  | None -> None
  | Some provider_error ->
      emit_provider_error_metric ~cascade_name ~provider provider_error;
      Some provider_error


include Cascade_attempt_fsm_capacity_backpressure

let sdk_error_is_max_turns_exceeded (err : Agent_sdk.Error.sdk_error) : bool =
  match Cascade_error_classify.classify_masc_internal_error err with
  | Some
      (Cascade_error_classify.Cascade_exhausted
         { reason = Keeper_types.Max_turns_exceeded; _ }) ->
      true
  | Some (Cascade_error_classify.Cascade_exhausted _)
  | Some (Cascade_error_classify.Capacity_backpressure _)
  | Some (Cascade_error_classify.Resumable_cli_session _)
  | Some (Cascade_error_classify.No_tool_capable_provider _)
  | Some (Cascade_error_classify.Accept_rejected _)
  | Some (Cascade_error_classify.Admission_queue_timeout _)
  | Some (Cascade_error_classify.Admission_queue_rejected _)
  | Some (Cascade_error_classify.Turn_timeout _)
  | Some (Cascade_error_classify.Provider_timeout _)
  | Some (Cascade_error_classify.Max_tokens_ceiling_violation _)
  | Some (Cascade_error_classify.Ambiguous_post_commit _)
  (* RFC-0158: admission denial is not max-turns-exceeded. *)
  | Some (Cascade_error_classify.Retry_admission_denied _)
  (* RFC-0159 Phase A: opaque internal failures are not max-turns-exceeded. *)
  | Some (Cascade_error_classify.Internal_unhandled_exception _)
  | Some (Cascade_error_classify.Internal_bridge_exception _)
  | Some (Cascade_error_classify.Internal_contract_rejected _) ->
      false
  | None -> (
      match err with
      | Agent_sdk.Error.Agent (Agent_sdk.Error.MaxTurnsExceeded _) -> true
      | Agent_sdk.Error.Agent (Agent_sdk.Error.AgentExecutionTimeout _) -> false
      | Agent_sdk.Error.Api _ -> false
      | Agent_sdk.Error.Provider _ -> false
      | Agent_sdk.Error.Internal _ -> false
      | _ -> false)

let sdk_error_cascade_fallback_class (err : Agent_sdk.Error.sdk_error) :
    string option =
  if sdk_error_is_hard_quota err then Some fallback_class_hard_quota
  else if sdk_error_is_max_turns_exceeded err then Some fallback_class_max_turns
  else if sdk_error_is_required_tool_contract_violation err then
    Some fallback_class_required_tool_contract_violation
  else None
