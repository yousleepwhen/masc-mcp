(** Cascade_transport — Transport and tool-lane helpers for OAS worker exec.

    Keeps provider label resolution, runtime MCP lane selection, and per-call
    CLI transport construction separate from the build/run orchestration in
    {!Cascade_runner}. *)

(** Per-call overrides forwarded to CLI transports.  Each field is consulted
    only by the matching provider kind; missing fields fall back to the
    transport's [default_config]. *)
type cli_transport_overrides = {
  cwd : string option;
  claude_mcp_config : string option;
  claude_allowed_tools : string list option;
  claude_permission_mode : string option;
  claude_max_turns : int option;
  gemini_yolo : bool option;
  cli_subprocess_idle_sec : float option;
      (** When [Some s], the CLI subprocess is aborted via SIGINT if no
          stdout line arrives within [s] seconds.  Currently honoured
          only by [Json_stream_cli_transport_local], which calls
          [Cli_common_subprocess.run_stream_lines] directly.  Other CLI
          transports route through agent_sdk [Transport_*_cli.create]
          configs that do not yet expose [stdout_idle_timeout_s]; an
          OAS upstream change is needed to honour this field there. *)
}

(** Hard cap for Claude Code's internal agent loop.  MASC may run a keeper
    for more turns overall, but a single Claude Code subprocess attempt must
    not receive that keeper-level budget unchanged. *)
val cli_tool_d_max_turns_hard_cap : int

(** Clamp provider-internal max_turns to provider hard constraints. *)
val provider_effective_max_turns :
  Llm_provider.Provider_config.provider_kind -> int -> int

val sanitize_cli_completion_request_for_argv :
  Llm_provider.Llm_transport.completion_request ->
  Llm_provider.Llm_transport.completion_request
(** Scrub request text that CLI transports may flatten into argv.

    Codex CLI passes sub-threshold prompts as a positional argument, so any
    invalid UTF-8 in history, system prompt, or request-scoped MCP overrides
    can make the subprocess fail before the cascade reaches provider logic. *)

(* RFC-0167: the client-named omission-dedup helpers
   ([cli_tool_a_omission_fingerprint], [cli_tool_a_omission_fingerprint_seen],
   [record_cli_tool_a_omission], [record_cli_tool_a_omission_for_agent],
   [reset_cli_tool_a_omission_dedup_for_tests]) were removed in the
   big-bang sweep along with [Cascade_transport_codex_omission_dedup].
   Structural omission detection remains in the resolver below. *)

(** Failure modes for {!resolve_provider_config_of_label}. *)
type label_resolution_error =
  | Invalid_model_label of string

(** Render a label-resolution error for log/diagnostic surfaces. *)
val label_resolution_error_to_string : label_resolution_error -> string

(** Lift a label-resolution error into the OAS SDK error envelope. *)
val label_resolution_error_to_sdk_error :
  label_resolution_error -> Agent_sdk.Error.sdk_error

(** Resolve a model label string to a provider config via the MASC cascade
    parser.  Explicit labels never silently fall through to discovery-only
    models — unresolved labels return [Error (Invalid_model_label _)]. *)
val resolve_provider_config_of_label :
  string -> (Llm_provider.Provider_config.t, label_resolution_error) result

(** Construct an [Agent_sdk.Error.InvalidConfig] with the supplied [field] name and
    [detail] text. *)
val invalid_runtime_config : string -> string -> Agent_sdk.Error.sdk_error

(** Normalize a CLI [model_id] to an explicit override.  Returns [None] when
    the model id is empty or [auto] (case-insensitive after trim). *)
val cli_model_override : string -> string option

(** OAS capability snapshot for a provider config.  Alias for
    {!Provider_tool_support.oas_capabilities_of_config}. *)
val provider_caps_of_config :
  Llm_provider.Provider_config.t -> Llm_provider.Capabilities.capabilities

(** Whether a provider can accept inline tool definitions on a request.
    Alias for {!Provider_tool_support.provider_supports_inline_tools}. *)
val provider_supports_inline_tools :
  ?override:Provider_tool_support.runtime_capabilities_override ->
  Llm_provider.Provider_config.t -> bool

(** Whether a provider supports the runtime MCP tool lane.  Alias for
    {!Provider_tool_support.provider_supports_runtime_mcp_lane}. *)
val provider_supports_runtime_mcp_lane :
  ?override:Provider_tool_support.runtime_capabilities_override ->
  Llm_provider.Provider_config.t -> bool

(** Render the [mcpServers] config JSON consumed by JSON-stream CLI transports, filtering
    by [policy.allowed_server_names].  Returns [None] when no allowed
    server remains after filtering. *)
val cli_mcp_config_json_of_policy :
  Llm_provider.Llm_transport.runtime_mcp_policy -> string option

(** Resolve a CLI provider model name from explicit provider config first,
    then from the OAS runtime binding default/supported-model projection.
    Returns [None] when the CLI binding does not publish a default. *)
val cli_model_for_provider_config :
  Llm_provider.Provider_config.t -> string option

(** Render root config JSON (default_model + providers + models) for a
    JSON-stream CLI provider.  Returns [None] when either the model
    resolution or the auth value resolution fails. *)
val cli_runtime_config_json_for_provider :
  Llm_provider.Provider_config.t -> string option

(** Drop duplicates from a list while preserving the first-seen order. *)
val dedupe_preserve_order : string list -> string list

(** Extract the [name] field of every OAS tool. *)
val public_mcp_tool_names_of_oas_tools : Agent_sdk.Tool.t list -> string list

(** Filter [tools] to those whose name is a public MCP tool per
    {!Tool_catalog.is_public_mcp}. *)
val public_mcp_tools_of_oas_tools : Agent_sdk.Tool.t list -> Agent_sdk.Tool.t list

(** Whether every name in [tool_names] is a public MCP tool.  Empty input
    returns [false]. *)
val tool_names_are_public_mcp : string list -> bool

(** Whether a runtime MCP tool requires a request-scoped actor binding (alias
    for {!Tool_catalog.requires_actor_binding}). *)
val runtime_mcp_tool_requires_bound_actor : string -> bool

(** Whether a public MCP tool requires a request-scoped actor binding. *)
val public_mcp_tool_requires_bound_actor : string -> bool

(** Inject identity headers ([x-masc-agent-name], [x-masc-keeper-name]) into
    the [masc] HTTP server entry of [policy] when [agent_name] is non-empty.
    [x-masc-internal-token] is also injected by default when
    [MASC_INTERNAL_MCP_TOKEN] is available; pass
    [~include_internal_token:false] for providers such as [cli_tool_a] that
    cannot carry auth-bearing request headers.  Other servers are passed
    through. *)
val runtime_mcp_policy_with_masc_agent_name :
  ?include_internal_token:bool ->
  agent_name:string ->
  Llm_provider.Llm_transport.runtime_mcp_policy ->
  Llm_provider.Llm_transport.runtime_mcp_policy

val cli_tool_a_can_auth_keeper_bound_runtime_mcp :
  agent_name:string ->
  Llm_provider.Llm_transport.runtime_mcp_policy ->
  bool
(** [true] when [agent_name] maps to a keeper with a persisted raw bearer
    token and [policy] contains actor-bound runtime MCP tools.  Codex CLI
    can carry that token via OAS [bearer_token_env_var] without placing it in
    argv. *)

(** Provider-specific shaping of the runtime MCP policy.  For Cli_tool_a the
    policy is stripped to Codex-safe headers: [Authorization: Bearer ...]
    plus non-secret MASC identity headers.  Other providers receive the policy
    with [runtime_mcp_policy_with_masc_agent_name] applied when [agent_name] is
    non-empty. *)
val runtime_mcp_policy_for_provider :
  provider_cfg:Llm_provider.Provider_config.t ->
  agent_name:string ->
  Llm_provider.Llm_transport.runtime_mcp_policy option ->
  Llm_provider.Llm_transport.runtime_mcp_policy option

(** Compose JSON-stream CLI [--mcp-config] arguments from a [base] list and
    an optional runtime MCP policy.  Output is deduped, preserving order. *)
val cli_runtime_mcp_jsons :
  base:string list ->
  Llm_provider.Llm_transport.runtime_mcp_policy option ->
  string list

(** Build the runtime MCP policy that exposes [tool_names] back to the
    provider's CLI.  Returns [None] when the tool set is not eligible for
    the runtime MCP lane (e.g. mixed surface, missing keeper identity for
    keeper-internal tools, or empty input). *)
val runtime_mcp_policy_of_tool_names :
  ?agent_name:string ->
  ?allow_keeper_internal:bool ->
  string list ->
  Llm_provider.Llm_transport.runtime_mcp_policy option

(** Public-only variant of {!runtime_mcp_policy_of_tool_names}.  Forwards
    without [allow_keeper_internal]. *)
val public_mcp_runtime_policy_of_tool_names :
  ?agent_name:string ->
  string list ->
  Llm_provider.Llm_transport.runtime_mcp_policy option

(** Human-readable [provider_kind:model_id] label. *)
val provider_label : Llm_provider.Provider_config.t -> string

(** Decide whether [tools] are served via runtime MCP lane, inline, or
    rejected as unsupported.  Returns [(remaining_inline_tools, policy)]:
    - [(_, Some policy)] — runtime MCP lane carries the tools (inline list
      is empty in that case).
    - [(tools, None)] — fall back to inline tools (when supported by the
      provider).
    - [Error sdk_error] — provider supports neither lane.

    Cli_tool_a + keeper-bound actor tools use the per-keeper raw bearer
    token when it is available, routed through OAS [bearer_token_env_var].
    When no per-keeper token exists, they trigger the [#10097] omission
    counter/log path. Required turns reject because the omitted tools cannot
    satisfy the tool contract; optional turns keep the prior degraded
    discovery path and exclude those tools from the resulting policy. *)
val resolve_tool_lane_for_oas_tools :
  ?agent_name:string ->
  ?tool_requirement:[ `Required | `Optional ] ->
  provider_cfg:Llm_provider.Provider_config.t ->
  tools:Agent_sdk.Tool.t list ->
  unit ->
  ( Agent_sdk.Tool.t list
    * Llm_provider.Llm_transport.runtime_mcp_policy option,
    Agent_sdk.Error.sdk_error )
  result

(** Wrap a CLI transport factory in a per-call sub-switch so that any
    pipe/process resources allocated by the factory are deterministically
    released at the end of each completion call. *)
val make_per_call_switch_transport :
  (sw:Eio.Switch.t -> Llm_provider.Llm_transport.t) ->
  Llm_provider.Llm_transport.t

(** Construct a non-HTTP CLI transport for [provider_cfg].  Returns [Ok None]
    for HTTP-shaped providers.  Returns [Error] when the process manager is not
    initialized. *)
val non_http_transport_of_provider :
  sw:Eio.Switch.t ->
  provider_cfg:Llm_provider.Provider_config.t ->
  ?runtime_mcp_policy:Llm_provider.Llm_transport.runtime_mcp_policy ->
  ?cli_transport_overrides:cli_transport_overrides ->
  unit ->
  (Llm_provider.Llm_transport.t option, Agent_sdk.Error.sdk_error) result

(** JSON-stream print-mode CLI transport.  Owned by the transport layer;
    runner facades must not re-export this protocol-local surface. *)
module Json_stream_cli_transport_local : sig
  type config = {
    cli_path : string;
    process_name : string;
    model : string option;
    cwd : string option;
    config_json : string option;
    mcp_config_json : string list;
    extra_env : (string * string) list;
    cancel : unit Eio.Promise.t option;
    stdout_idle_timeout_s : float option;
        (** When [Some s], the CLI subprocess is aborted via SIGINT if no
            stdout line arrives within [s] seconds.  Forwarded to
            [Llm_provider.Cli_common_subprocess.run_stream_lines] together
            with the process clock obtained from [Process_eio.get_clock].
            Defaults to [None] (no idle bound; rely on the outer keeper
            turn timeout). *)
  }

  val default_config : config

  (** Build the CLI argv from a config + per-call request, deciding
      whether the prompt goes via [-p] or stdin. Non-ASCII or large prompts
      use stdin to avoid Python CLI setproctitle UTF-8 decode crashes. *)
  val build_args :
    config:config ->
    req_config:Llm_provider.Provider_config.t ->
    mcp_config_json:string list ->
    prompt:string ->
    string list

  (** Whether a CLI stderr line should be forwarded to the default
      stderr logger.  Drops the [resume hint] lines, which are noise. *)
  val should_log_stderr_line : string -> bool

  (** Constant detail string used for typed resumable-session reports. *)
  val resumable_session_detail : string

  (** Reclassify a [NetworkError] from the CLI into [AcceptRejected] when
      the message indicates a permanent per-provider error
      (auth/config/model), a local CLI startup crash, or an exit-75
      resumable-session process status.  Other variants pass through. *)
  val classify_cli_error :
    ('a, Llm_provider.Http_client.http_error) result ->
    ('a, Llm_provider.Http_client.http_error) result

  (** Create a JSON-stream CLI completion transport bound to [sw].  The transport
      runs [<cli> --print --output-format stream-json ...] via [mgr] and
      parses JSONL output into OAS response/event blocks. *)
  val create :
    sw:Eio.Switch.t ->
    mgr:_ Eio.Process.mgr ->
    config:config ->
    Llm_provider.Llm_transport.t
end
