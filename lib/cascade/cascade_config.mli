(** Cascade configuration: named provider profiles with JSON hot-reload
    and discovery-aware health filtering.

    Consumers define named cascade profiles
    mapping to ordered lists of providers. This module handles:
    - Parsing "provider:model" strings into {!Llm_provider.Provider_config.t}
    - Loading profiles from a JSON config file (mtime-based hot-reload)
    - Filtering providers by local endpoint health via {!Discovery}
    - Convenience cascade execution combining the above

    @since 0.59.0

    @stability Internal
    @since 0.93.1 *)

(** {1 Model Resolution} *)

(** Resolve ["auto"] for a provider through runtime binding defaults or local
    discovery. Explicit model IDs pass through unchanged.
    @since 0.89.1 *)
val resolve_auto_model_id : string -> string -> string

(** {1 Model String Parsing} *)

(** Normalize OpenAI-compatible request paths against versioned base URLs.

    When [base_url] already carries a path segment such as [/v1],
    [request_path] should not repeat that prefix. For example,
    [base_url = "http://127.0.0.1:18080/v1"] and
    [request_path = "/v1/chat/completions"] normalize to
    ["/chat/completions"].

    @since 0.193.9 *)
val normalize_openai_compat_request_path :
  base_url:string -> request_path:string -> string

(** Build HTTP headers for a resolved provider API key.

    Empty API keys return only the JSON content-type header; HTTP providers
    with bearer-token auth receive [Authorization: Bearer ...]. *)
val headers_with_auth :
  kind:Llm_provider.Provider_config.provider_kind ->
  api_key:string ->
  (string * string) list

(** Parse a "provider:model_id" string into a {!Llm_provider.Provider_config.t}.

    Supported providers are determined by {!Llm_provider.Provider_registry.default}.
    Built-in: llama, agent_llm_a, provider_f, provider_k, openrouter, custom.

    Returns [None] when the provider is unknown or the required API key
    env var is not set (provider is unavailable). *)
val parse_model_string :
  ?temperature:float ->
  ?max_tokens:int ->
  ?system_prompt:string ->
  ?api_key_env_overrides:(string * string) list ->
  ?supports_tool_choice_override:bool ->
  ?keep_alive:string ->
  ?num_ctx:int ->
  string -> Llm_provider.Provider_config.t option
(** [api_key_env_overrides] defaults to [[]]. When non-empty, it overrides
    the registry default API key env var for matching providers. Entries map
    provider names, or ["*"] for all providers, to env var names. Empty-string
    entries fall through to the next level of the resolution chain.

    [supports_tool_choice_override] is forwarded to
    {!Llm_provider.Provider_config.make}. [None] leaves the per-kind default
    from {!Llm_provider.Capabilities} in place; [Some b] forces [b].

    @since 0.122.0 api_key_env_overrides parameter added
    @since 0.150.0 supports_tool_choice_override parameter added *)

(* RFC-0058 iter 21: [val parse_weighted_entry] removed.  All
   callers migrated to [parse_weighted_entry_with_drop_metric]
   (iter 14).  Audit at iter 14 confirmed zero external callers.
   See {!parse_weighted_entry_with_drop_metric} and
   {!parse_weighted_entry_diag}. *)

type weighted_entry_drop =
  | Drop_unregistered_scheme of { model : string; scheme : string }
  | Drop_unavailable_scheme of { model : string; scheme : string }
  | Drop_invalid_syntax of string


(** Parse a {!Cascade_config_loader.weighted_entry} into a
    {!Llm_provider.Provider_config.t}, preserving the reason a
    candidate was rejected ({!weighted_entry_drop}) so callers can
    surface actionable validation errors.  Used by
    {!parse_weighted_entries} and by
    {!parse_weighted_entry_with_drop_metric}.

    @since 0.150.0 *)
val parse_weighted_entry_diag :
  ?temperature:float ->
  ?max_tokens:int ->
  ?system_prompt:string ->
  ?api_key_env_overrides:(string * string) list ->
  ?keep_alive:string ->
  ?num_ctx:int ->
  Cascade_config_loader.weighted_entry ->
  (Llm_provider.Provider_config.t, weighted_entry_drop) result

(** Resolve-path wrapper around {!parse_weighted_entry_diag} that
    returns an [option] AND ticks
    [Cascade_metrics.on_profile_candidate_drop ~cascade ~reason] on
    drop.  Replaces the iter-21-removed [parse_weighted_entry] which
    silently swallowed the drop reason; the resolve path surfaced
    drops only as [providers = []] downstream with no WHY. *)
val parse_weighted_entry_with_drop_metric :
  ?temperature:float ->
  ?max_tokens:int ->
  ?system_prompt:string ->
  ?api_key_env_overrides:(string * string) list ->
  ?keep_alive:string ->
  ?num_ctx:int ->
  cascade:string ->
  Cascade_config_loader.weighted_entry ->
  Llm_provider.Provider_config.t option

(** Parse a list of weighted entries, dropping ones that cannot produce a
    provider config. Preserves input order.

    Drops are categorised (unregistered provider scheme, unavailable
    provider, invalid syntax) and logged once per call through
    {!Log.Misc}: unregistered schemes and invalid syntax are promoted to
    ERROR because they usually indicate cascade.toml drift or a stale
    binary linked against an older provider registry. Unavailable
    schemes (missing API key, missing CLI binary) log at WARN. If every
    entry is filtered out the call escalates to an additional ERROR so
    zero-provider cascades surface at load time rather than silently
    producing no responses.

    [cascade_name] is included in diagnostics when supplied.

    @since 0.150.0 *)
val parse_weighted_entries :
  ?temperature:float ->
  ?max_tokens:int ->
  ?system_prompt:string ->
  ?api_key_env_overrides:(string * string) list ->
  ?cascade_name:string ->
  Cascade_config_loader.weighted_entry list ->
  Llm_provider.Provider_config.t list

val order_weighted_entries :
  ?rand_int:(int -> int) ->
  ?rotation_scope:string ->
  ?cascade:string ->
  Cascade_config_loader.weighted_entry list ->
  Cascade_config_loader.weighted_entry list
(** Order weighted entries using the same health-adjusted runtime logic as
    {!resolve_model_strings}. Exposed so runtime-authoritative catalog
    snapshots can preserve dynamic health ordering without rereading raw
    cascade source text.

    When [rotation_scope] is provided, equal-weight top-level provider entries
    are round-robined within that scope, and each [provider:auto] expansion is
    round-robined independently before the usual weight/health ordering is
    applied. *)

(** Like {!parse_model_string} but returns a [Result] with a typed
    failure mode explaining why parsing failed (unknown provider, missing
    API key, bad format).  Intended for MCP tool boundaries where callers
    need to report the reason back to the user — call
    {!parse_error_to_string} to render the human-facing message.

    @since 0.81.0 *)
type parse_error = Cascade_config_parser.parse_error =
  | Invalid_spec of string
  | Unknown_provider of { provider : string; spec : string }
  | Provider_unavailable of { provider : string; env_var : string }
  | Custom_empty_model of { spec : string }

val parse_error_to_string : parse_error -> string

val parse_model_string_result :
  ?temperature:float ->
  ?max_tokens:int ->
  ?system_prompt:string ->
  string -> (Llm_provider.Provider_config.t, parse_error) result

(** Expand provider:auto specs that map to multiple models.
    Direct API providers project their candidate list from OAS runtime
    bindings. CLI specs can expand through operator-provided auto-model
    lists. Other specs pass through unchanged. *)
val expand_auto_models : string list -> string list

val expand_weighted_auto_entries :
  ?rotation_scope:string ->
  Cascade_config_loader.weighted_entry list ->
  Cascade_config_loader.weighted_entry list
(** Like {!expand_auto_models} but for weighted entries. Each
    [provider:auto] entry expands into one entry per concrete model,
    preserving the original [weight], [supports_tool_choice], and
    [secondary]/[secondary_supports_tool_choice] overrides on every
    expanded entry. Non-auto entries pass through unchanged.

    @since 0.151.0 RFC-0027 PR-9b dual-track lookup needs the expanded
    entries to match a parsed primary [Provider_config] back to its
    weighted entry (and therefore to its [secondary] declaration). *)

(** {1 Cascade Config Loading} *)

(** How a cascade name was resolved. *)
type cascade_source =
  | Named              (** Found as a declarative profile in cascade.toml *)
  | Default_fallback   (** Name not found; used the [routes.keeper_turn] profile *)
  | Hardcoded_defaults (** Neither found; used hardcoded [defaults] *)
  | Load_failed of string
    (** Config file load failed (parse / IO / missing).  Returned in
        place of [Hardcoded_defaults] so that the dashboard can
        distinguish a fault from operator intent.  The string carries
        the underlying error for telemetry. *)

(** Resolve model strings for a named cascade.

    Resolution order:
    1. Named declarative profile from [config_path]
    2. [routes.keeper_turn] profile from [config_path] (fallback)
    3. Hardcoded [defaults]

    When [config_path] is [None], returns [defaults] directly. *)
val resolve_model_strings :
  ?config_path:string ->
  name:string ->
  defaults:string list ->
  unit ->
  string list

(** Expand execution-time convenience fallbacks while preserving stable order.

    Uses the same provider:auto expansion as {!expand_auto_models}, so
    provider-family entries execute in the same concrete order the
    dashboard shows by default.

    When [rotation_scope] is provided, each [provider:auto] entry is
    rotated independently within that scope so repeated execution calls
    do not always start from the same concrete model.

    Duplicate entries are removed after expansion, keeping the first
    appearance. This lets callers keep config concise while still
    getting automatic provider-internal failover at execution time.

    @since 0.116.2 *)
val expand_model_strings_for_execution :
  ?rotation_scope:string ->
  string list ->
  string list

(** Like {!resolve_model_strings} but also returns which resolution
    path was taken. Use this to detect typos: if [source <> Named]
    when you expected a named profile, the cascade name is likely wrong.

    @since 0.78.0 *)
val resolve_model_strings_traced :
  ?config_path:string ->
  name:string ->
  defaults:string list ->
  unit ->
  string list * cascade_source

(** Per-candidate info in a weighted selection decision.

    Captures the state that influenced a single candidate's ordering
    at decision time: its declared weight, health-adjusted effective
    weight, and current health signals.

    @since 0.139.0 *)
type candidate_info = {
  model_string : string;        (** "provider:model_id" as written in config *)
  display_model_string : string; (** User-facing label for the configured candidate *)
  provider_name : string option; (** Raw provider prefix when present *)
  display_provider_name : string option; (** User-facing provider family label *)
  runtime_kind : string option; (** "local" / "cli_agent" / "direct_api" when known *)
  expanded_models : string list; (** Concrete execution order for this configured candidate *)
  config_weight : int;          (** Weight from cascade config ([1] when absent) *)
  effective_weight : int;       (** Weight after health adjustment; [0] = cooled-down *)
  success_rate : float;         (** Rolling-window success rate, [0.0]–[1.0] *)
  in_cooldown : bool;           (** Provider currently skipped by cooldown *)
}

(** Full trace of a cascade selection decision.

    Consumers can use this to surface, in dashboards/telemetry,
    why a particular provider was attempted first and what signals
    were considered.

    [candidates] is in final attempt order — the first entry is the
    provider the cascade will try first.

    When the profile has no weights (every entry is [weight=1]), no
    probabilistic shuffle happens and [effective_weight = config_weight = 1]
    for each entry.

    @since 0.139.0 *)
type selection_trace = {
  candidates : candidate_info list;
  source : cascade_source;
}

(** Build a live selection trace from already-known weighted entries.

    Applies {!order_weighted_entries} and snapshots current health signals
    without rereading raw cascade source text. Useful when callers already hold
    validated runtime profile data and need the same dashboard trace shape.

    @since 0.150.4 *)
val selection_trace_of_weighted_entries :
  ?source:cascade_source ->
  Cascade_config_loader.weighted_entry list ->
  selection_trace

(** Like {!resolve_model_strings_traced} but also returns per-candidate
    health signals that influenced the ordering. Useful for rendering
    the cascade decision in dashboards without re-deriving state.

    Non-breaking: callers who only need the ordered model list can
    continue using {!resolve_model_strings} or {!resolve_model_strings_traced}.

    @since 0.139.0 *)
val resolve_model_strings_with_trace :
  ?config_path:string ->
  name:string ->
  defaults:string list ->
  unit ->
  string list * selection_trace

(** {1 Catalog Source Access} *)

(** Load and cache the cascade catalog source.

    Returns a [Yojson.Safe.t] in-memory view for internal consumers, but
    reads no on-disk JSON: [cascade.toml] is the SSOT and is parsed into
    the returned value in memory. Cached by source-path mtime.
    Exposed for consumers needing custom fields beyond model lists
    (e.g., per-cascade temperature/max_tokens overrides).

    @since 0.89.1
    @since RFC-0058 §9 Phase 9.3 renamed from [load_json]. *)
val load_catalog_source : string -> (Yojson.Safe.t, string) result

(** {1 Inference Parameters} *)

(** Per-cascade inference parameter overrides. *)
type inference_params = {
  temperature: float option;
  max_tokens: int option;
  keep_alive: string option;
  (** Ollama [keep_alive] override. Honored only when the resolved
      provider is Ollama. *)
  num_ctx: int option;
  (** Ollama [num_ctx] override. Honored only when the resolved
      provider is Ollama. *)
  thinking_enabled: bool option;
  thinking_budget: int option;
  (** [thinking_budget] is a per-turn thinking token budget seed.
      Keeper adaptive logic may adjust this per turn based on intent
      classification and error/retry signals.  Provider-specific
      mapping happens downstream in OAS. *)
}

(** Resolve inference parameters from cascade.toml.

    Resolution order:
    1. ["{name}_temperature"] / ["{name}_max_tokens"]
    2. ["default_temperature"] / ["default_max_tokens"]
    3. [None] (caller uses own defaults)

    @since 0.89.1 *)
val resolve_inference_params :
  config_path:string -> name:string -> inference_params

(** Resolve per-cascade API key env var overrides from cascade.toml.

    Supports two formats:
    - String: applies to all providers.
      [{"{name}_api_key_env": "MY_API_KEY_ENV"}]
    - Object: per-provider mapping.
      [{"{name}_api_key_env": {"<provider_a>": "API_KEY_ENV_A", "<provider_b>": "API_KEY_ENV_B"}}]

    Falls back to ["default_api_key_env"], then empty list (use registry defaults).

    @since 0.122.0 *)
val resolve_api_key_env :
  config_path:string -> name:string -> (string * string) list

(** {1 Discovery-Aware Health Filtering} *)

type health_filter_rejection =
  Cascade_health_filter.health_filter_rejection =
  | All_missing_api_key of int
  | All_local_unhealthy of { local_count : int; cloud_count : int }

val health_filter_rejection_to_string : health_filter_rejection -> string

(** Filter a provider list by local endpoint health.

    Probes local (llama-server) endpoints via {!Discovery}. When all
    local endpoints are unhealthy, removes local providers from the list
    so cloud providers serve as fallback.

    Returns [Error] when the cascade is configurationally broken
    (all providers missing API keys) or has drifted below the
    live-fallback threshold (all local unhealthy with no cloud
    fallback). Callers must handle the typed rejection — the prior
    fail-open variant is gone. *)
val filter_healthy_strict :
  sw:Eio.Switch.t ->
  net:[ `Generic | `Unix ] Eio.Net.ty Eio.Resource.t ->
  Llm_provider.Provider_config.t list ->
  (Llm_provider.Provider_config.t list, health_filter_rejection) result

(** {1 Context Window Resolution} *)

(** Resolve a provider/model context window from the OAS capability SSOT.

    This is shared by cascade profile generation and transport config paths so
    those paths do not drift through local context-window constants. *)
val resolve_provider_model_max_context : provider_name:string -> string -> int

(** Effective max context tokens for a provider entry.

    Returns [caps.max_context_tokens] when known (per-model), otherwise
    falls back to [entry.max_context] (per-provider default from the registry).

    @since 0.78.0 *)
val effective_max_context :
  Llm_provider.Provider_registry.entry -> Llm_provider.Capabilities.capabilities -> int

(** Resolve a model label to the per-slot context of the endpoint
    that would serve it.

    Uses the same resolution path as [make_registry_config]:
    - ["llama:*"] → peeks at current round-robin endpoint (no advance)
    - ["custom:model@url"] → looks up the parsed URL
    - Cloud providers → [None] (use static {!effective_max_context} instead)

    This is the SSOT for "how much context does this label have?"
    Consumers should call this instead of guessing from endpoint lists.

    @since 0.100.8 *)
val resolve_label_context : string -> int option

(** {1 Capability-Aware Filtering} *)

(** Filter providers by a capability predicate.

    Resolves capabilities from OAS runtime provider bindings for bound
    provider configs. Truly unbound configs keep the legacy per-model
    lookup with registry/default fallback. Removes providers that do not
    satisfy [pred]. If all providers would be removed, returns the original
    list unchanged (let the provider return an API error).

    Example: filter to providers supporting tools:
    {[ filter_by_capabilities ~pred:(fun c -> c.supports_tools) providers ]}

    @since 0.78.0 *)
val filter_by_capabilities :
  pred:(Llm_provider.Capabilities.capabilities -> bool) ->
  Llm_provider.Provider_config.t list ->
  Llm_provider.Provider_config.t list

(** {1 Helpers for Cascade Consumers} *)

(** Extract the concatenated text content from an API response.
    Joins all {!Llm_provider.Types.Text} blocks. Useful for accept validators. *)
val text_of_response : Llm_provider.Types.api_response -> string

type provider_filter_rejection =
  | Filter_matched_none of { filter : string list; available_kinds : string list }

val provider_filter_rejection_to_string : provider_filter_rejection -> string

val apply_provider_filter :
  provider_filter:string list option ->
  label:string ->
  Llm_provider.Provider_config.t list ->
  Llm_provider.Provider_config.t list

val apply_provider_filter_strict :
  provider_filter:string list option ->
  label:string ->
  Llm_provider.Provider_config.t list ->
  (Llm_provider.Provider_config.t list, provider_filter_rejection) result
(** Strict variant of {!apply_provider_filter}. Returns [Error] when
    the explicit provider_filter matches no available providers instead
    of silently broadening to the full set. Use for execution paths
    where provider drift must surface as a typed blocker. *)

(** {1 Pluggable strategy resolution}

    @since 0.9.6 *)

val resolve_strategy :
  ?config_path:string ->
  name:string ->
  unit ->
  Cascade_strategy.t
(** [resolve_strategy ~config_path ~name] reads
    [{name}_strategy], [{name}_max_cycles], [{name}_backoff_base_ms],
    [{name}_backoff_cap_ms] from [config_path] and returns the
    corresponding {!Cascade_strategy.t}.

    Behaviour when fields are absent or [config_path] is [None]:
    - returns {!Cascade_strategy.failover} (linear failover, single
      cycle, default backoff).  This guarantees bit-identical
      behaviour to cascade calls that have no strategy
      configuration.

    Behaviour on parse error:
    - unknown [strategy] value → emits a one-time stderr warning and
      falls back to [Failover].  Keeper startup is not blocked by
      config typos.
    - non-positive [max_cycles] → clamped to 1.
    - non-positive [backoff_base_ms] → clamped to 1.
    - [backoff_cap_ms < backoff_base_ms] → clamped up to
      [backoff_base_ms]. *)

val normalize_priority_tiers :
  config_path:string ->
  name:string ->
  string list list ->
  (string list list, string) result
(** Validate and normalize a [priority_tier] tier matrix against the
    configured candidate model ids for [name]. Returns [Error] when all
    tiers collapse or when the profile has no configured candidates. *)

val resolve_ollama_max_concurrent :
  ?config_path:string ->
  name:string ->
  unit ->
  int option
(** Per-cascade override for the HTTP-probe-capable provider's
    client-capacity registration default.  The caller in
    {!Keeper_turn_driver} consults provider-kind probe capability and registers
    matching cfgs through {!Cascade_client_capacity.register}.
    [None] means "use the literal default of 1". *)

val resolve_cli_max_concurrent :
  ?config_path:string ->
  name:string ->
  unit ->
  int option
(** Per-cascade override for the CLI client-capacity registration
    default ({!Cascade_client_capacity.auto_register_cli_for_candidates}).
    [None] means "use the env-var default
    ([MASC_CLI_MAX_CONCURRENT] or 1)".
    @since 0.9.8 *)

(** {2 Phonebook loading (RFC Cascade-Phonebook)} *)

val load_phonebook :
  string -> (Cascade_phonebook_types.cascade_phonebook, string) result
(** Load a TOML file as a typed phonebook with mtime-based caching.
    @since RFC Cascade-Phonebook *)

val invalidate_phonebook_cache : string -> unit
(** Drop the cached phonebook entry for a path.
    @since RFC Cascade-Phonebook *)

val load_phonebook_from_config :
  unit -> (Cascade_phonebook_types.cascade_phonebook, string) result option
(** Resolve cascade TOML path and load phonebook. Returns [None] when
    no config dir is configured.
    @since RFC Cascade-Phonebook *)
