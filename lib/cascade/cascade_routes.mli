(** Config-driven mapping from logical cascade usages to live profile names.

    Code should name the usage it needs (for example [Governance_judge]) and
    let [cascade.toml] decide which concrete profile handles it.
    This keeps profiles as configuration data instead of scattering profile
    literals through runtime call sites. *)

type logical_use = Cascade_ref.logical_use =
  | Keeper_turn
  | Phase_recovery
  | Phase_buffer
  | Tool_required
  | Governance_judge
  | Operator_judge
  | Cross_verifier
  | Verifier
  | Adversarial_reviewer
  | Auto_responder
  | Routing
  | Openai_compat
  | Persona_generation
  | Provider_benchmark
  | Simple_task
  | Moderate_task
  | Complex_task
  | Tool_rerank_use

val logical_use_key : logical_use -> string
val logical_use_of_string_opt : string -> logical_use option
val task_use_of_logical_use : logical_use -> Cascade_routing_policy.task_use

val all_logical_uses : logical_use list
(** Logical call-site uses known by the route registry. *)

val known_route_keys : string list
(** Stable keys accepted under [routes]. *)

val cascade_name_for_use : ?config_path:string -> logical_use -> string
(** Resolve a logical use through [routes.<logical_use_key>] in the live
    cascade catalog.  Falls back to the first catalog entry when no route
    binding is declared.  Falls back to the canonical [route.<key>] name when the
    catalog itself is empty — [Cascade_catalog_runtime.validate_path_result]
    remains the boot-time boundary that prevents this state from being
    reached at runtime.

    This returns a cascade profile name, never a provider/model string.
    Phonebook model/provider resolution is exposed separately through
    {!cascade_models_for_use_via_phonebook} and
    {!cascade_provider_configs_for_use_via_phonebook}. *)

val route_bindings_from_json : Yojson.Safe.t -> (string * string) list
(** Decode the [routes] object of the in-memory cascade view into a
    [(key, target)] association list. Only the RFC-0058 sub-table
    encoding [\[routes.X\] target = "Y"] is accepted. *)

val configured_route_targets : ?config_path:string -> unit -> string list
(** Unique non-empty profile names referenced by [routes]. *)

val configured_route_keys : ?config_path:string -> unit -> string list
(** Unique keys declared by [routes]. *)

val configured_unknown_route_keys : ?config_path:string -> unit -> string list
(** Declared route keys that are not part of {!known_route_keys}. *)

val fallback_name_for_catalog : logical_use -> catalog:string list -> string
(** Catalog-only fallback used by string normalizers that already have an
    explicit active profile list.  Returns the first catalog entry, or the
    canonical [route.<key>] name when [catalog] is empty. *)

val cascade_models_for_use_via_phonebook :
  ?config_path:string -> logical_use -> string list option
(** Resolve a logical use to model strings via the phonebook.
    Maps canonical route use → [task_use] → tier-group → model strings.
    Returns [None] when the phonebook is unavailable. *)

val cascade_provider_configs_for_use_via_phonebook :
  ?config_path:string ->
  ?temperature:float ->
  ?max_tokens:int ->
  logical_use ->
  Llm_provider.Provider_config.t list option
(** Resolve a logical use to [Provider_config.t] list via the phonebook.
    Full phonebook path: route use → task_use → tier-group → models →
    providers → endpoint/auth → Provider_config.t.
    Returns [None] when phonebook is unavailable or no models resolve. *)
