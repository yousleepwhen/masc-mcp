open Keeper_types
open Keeper_exec_shared

let keeper_task_result_json = function
  | Ok msg -> Yojson.Safe.to_string (`Assoc [ "ok", `Bool true; "result", `String msg ])
  | Error e ->
    Yojson.Safe.to_string
      (`Assoc [ "ok", `Bool false; "error", `String (Types.masc_error_to_string e) ])
;;

let keeper_tool_result_json ~(ok : bool) ~(message : string) =
  Yojson.Safe.to_string
    (`Assoc
       [
         "ok", `Bool ok;
         ((if ok then "result" else "error"), `String message);
       ])
;;

let handle_keeper_task_tool
      ~(config : Coord.config)
      ~(meta : keeper_meta)
      ~(name : string)
      ~(args : Yojson.Safe.t)
  =
  match name with
  | "keeper_tasks_list" ->
    let status_filter = Safe_ops.json_string_opt "status" args in
    let include_done = Safe_ops.json_bool ~default:false "include_done" args in
    let limit = Safe_ops.json_int ~default:50 "limit" args |> max 1 |> min 100 in
    let result = Coord.list_tasks ?status:status_filter ~include_done config in
    (match Yojson.Safe.from_string result with
     | `List items ->
       Yojson.Safe.to_string (`List (List.filteri (fun i _ -> i < limit) items))
     | _ -> result
     | exception Yojson.Json_error _ ->
       let lines = String.split_on_char '\n' result in
       String.concat "\n" (List.filteri (fun i _ -> i < limit + 2) lines))
  | "keeper_tasks_audit" ->
    let limit = Safe_ops.json_int ~default:20 "limit" args |> max 1 |> min 50 in
    let orphans = Coord.audit_orphan_tasks config in
    let orphans = List.filteri (fun i _ -> i < limit) orphans in
    let items =
      List.map
        (fun (task, assignee) ->
           let task : Types.task = task in
           `Assoc
             [ "task_id", `String task.id
             ; "title", `String task.title
             ; "assignee", `String assignee
             ; "status", `String (Types.string_of_task_status task.task_status)
             ])
        orphans
    in
    let action_hint =
      if orphans = [] then
        "ACTION: STOP calling keeper_tasks_audit — no orphans found. Move on to other work or end your turn."
      else
        Printf.sprintf "ACTION: %d orphan(s) found. Use keeper_task_force_release or keeper_task_force_done to resolve, then STOP re-auditing."
          (List.length orphans)
    in
    Yojson.Safe.to_string
      (`Assoc [ "orphan_count", `Int (List.length orphans); "orphans", `List items;
                "action", `String action_hint ])
  | "keeper_task_force_release" ->
    let task_id = Safe_ops.json_string ~default:"" "task_id" args |> String.trim in
    let reason = Safe_ops.json_string ~default:"" "reason" args in
    if task_id = ""
    then error_json "task_id is required. Use the task_id from keeper_tasks_list or keeper_tasks_audit."
    else (
      let agent = keeper_agent_sender ~meta in
      let _ =
        Coord.broadcast
          config
          ~from_agent:agent
          ~content:
            (Printf.sprintf
               "Force-releasing task %s (reason: %s)"
               task_id
               (if reason = "" then "no reason given" else reason))
      in
      keeper_task_result_json
        (Coord.force_release_task_r config ~agent_name:agent ~task_id ()))
  | "keeper_task_force_done" ->
    let task_id = Safe_ops.json_string ~default:"" "task_id" args |> String.trim in
    let notes = Safe_ops.json_string ~default:"" "notes" args in
    if task_id = ""
    then error_json "task_id is required. Use the task_id from keeper_tasks_list or keeper_tasks_audit."
    else
      keeper_task_result_json
        (Coord.force_done_task_r
           config
           ~agent_name:(keeper_agent_sender ~meta)
           ~task_id
           ~notes
           ())
  | "keeper_broadcast" ->
    let message = Safe_ops.json_string ~default:"" "message" args |> String.trim in
    if message = ""
    then error_json "message is required. Good: message='Build complete, all tests pass.'."
    else (
      let _ =
        Coord.broadcast config ~from_agent:(keeper_agent_sender ~meta) ~content:message
      in
      Yojson.Safe.to_string (`Assoc [ "ok", `Bool true; "broadcast", `String message ]))
  | "keeper_task_create" ->
    let title = Safe_ops.json_string ~default:"" "title" args |> String.trim in
    let description = Safe_ops.json_string ~default:"" "description" args |> String.trim in
    let priority = Safe_ops.json_int ~default:3 "priority" args |> max 1 |> min 5 in
    let goal_id =
      match Safe_ops.json_string_opt "goal_id" args with
      | Some s when String.trim s <> "" -> Some (String.trim s)
      | _ -> None
    in
    if title = ""
    then error_json "title is required. Provide a clear, actionable task title."
    else if description = ""
    then error_json "description is required. Explain what needs to be done and why."
    else (
      let result =
        Coord_task.add_task ?goal_id config ~title ~priority ~description
      in
      Yojson.Safe.to_string (`Assoc [ "ok", `Bool true; "result", `String result ]))
  | "keeper_task_claim" ->
    let task_filter =
      match meta.active_goal_ids with
      | [] -> fun (_task : Types.task) -> true
      | goal_ids ->
        fun task -> Keeper_runtime_contract.task_is_linked_to_keeper_goals goal_ids task
    in
    let result =
      Coord.claim_next_r config ~agent_name:meta.agent_name ~task_filter ()
    in
    let accountability_warning =
      if
        Keeper_accountability.accountability_risk_is_high config
          ~keeper_name:meta.name ~agent_name:meta.agent_name
      then
        Some
          "⚠ Accountability risk is high for this keeper. Prefer manual review or lower-risk routing when equivalent."
      else
        None
    in
    let message = match result with
      | Coord.Claim_next_claimed { message; _ } -> message
      | Coord.Claim_next_no_unclaimed -> "📋 No unclaimed tasks. ACTION: Stop task-checking — nothing to claim."
      | Coord.Claim_next_no_eligible { excluded_count; _ } ->
        let scope_suffix =
          match meta.active_goal_ids with
          | [] -> ""
          | goal_ids ->
              Printf.sprintf
                " within active_goal_ids=[%s]"
                (String.concat ", " goal_ids)
        in
        Printf.sprintf
          "📋 No eligible tasks%s. ACTION: Stop task-checking — blocked/excluded=%d."
          scope_suffix excluded_count
      | Coord.Claim_next_error e -> Printf.sprintf "❌ Error: %s" e
    in
    Yojson.Safe.to_string
      (`Assoc
         ([
            ("result", `String message);
          ]
         @
         match accountability_warning with
         | Some warning -> [ ("routing_warning", `String warning) ]
         | None -> []))
  | "keeper_task_done" ->
    let task_id = Safe_ops.json_string ~default:"" "task_id" args |> String.trim in
    let result_text = Safe_ops.json_string ~default:"" "result" args |> String.trim in
    if task_id = ""
    then error_json "task_id is required. Use the task_id you got from keeper_task_claim."
    else (
      let ok, message =
        Tool_task.handle_transition
          {
            Tool_task.config;
            agent_name = keeper_agent_sender ~meta;
            sw = Eio_context.get_switch_opt ();
          }
          (`Assoc
             [
               "task_id", `String task_id;
               "action", `String "done";
               "notes", `String result_text;
             ])
      in
      keeper_tool_result_json ~ok ~message)
  | "keeper_task_submit_for_verification" ->
    let task_id = Safe_ops.json_string ~default:"" "task_id" args |> String.trim in
    let notes = Safe_ops.json_string ~default:"" "notes" args |> String.trim in
    let pr_url = Safe_ops.json_string ~default:"" "pr_url" args |> String.trim in
    if task_id = ""
    then error_json "task_id is required. Use the task_id you got from keeper_task_claim."
    else if notes = ""
    then error_json "notes is required. Include verification evidence and test summary."
    else if pr_url = ""
    then error_json "pr_url is required. Include the PR opened for this task."
    else (
      let ok, message =
        Tool_task.handle_transition
          {
            Tool_task.config;
            agent_name = keeper_agent_sender ~meta;
            sw = Eio_context.get_switch_opt ();
          }
          (`Assoc
             [
               "task_id", `String task_id;
               "action", `String "submit_for_verification";
               "notes", `String (notes ^ "\nPR: " ^ pr_url);
             ])
      in
      keeper_tool_result_json ~ok ~message)
  | other -> error_json ~fields:[ "tool", `String other ] "unknown_task_tool"
;;
