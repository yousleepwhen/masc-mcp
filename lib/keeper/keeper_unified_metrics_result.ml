(** Success-path metric update for unified keeper cycle, extracted from
    keeper_unified_metrics.ml.

    Pure write-only side-effect: updates keeper_meta runtime/social
    fields based on a successful turn result. *)

open Keeper_types
open Keeper_context_runtime
module Social = Keeper_social_model

include Keeper_unified_metrics_support
include Keeper_unified_metrics_json_support

let update_metrics_from_result (meta : keeper_meta) ~(latency_ms : int)
    ~(observation : Keeper_world_observation.world_observation)
    ?(is_autonomous_turn = true)
    ?(update_proactive_rt = true)
    ?social_state
    ?social_transition_reason
    ?(context_max = 0)
    (result : Keeper_agent_run.run_result) : keeper_meta =
  let now_ts = Time_compat.now () in
  let usage_trust =
    classify_usage_trust
      ~usage_reported:result.usage_reported
      ~usage:result.usage
      ~context_max
  in
  (* #9959: surface classification into Prometheus exactly once per
     turn. Other [classify_usage_trust] call sites serialize the
     trust into JSONL but do not bump the counter. *)
  record_usage_trust ~keeper_name:meta.name ~trust:usage_trust;
  record_keeper_idle_seconds
    ~keeper_name:meta.name
    ~idle_seconds:observation.idle_seconds;
  let usage_trusted = usage_trust_is_trusted usage_trust in
  let trusted_input_tokens =
    if usage_trusted then result.usage.input_tokens else 0
  in
  let trusted_output_tokens =
    if usage_trusted then result.usage.output_tokens else 0
  in
  let trusted_total_tokens =
    if usage_trusted then Keeper_context_runtime.total_tokens result.usage else 0
  in
  let turn_cost =
    estimate_trusted_usage_cost_usd
      ~usage_trusted
      result.usage
  in
  let substantive_tool_call_count =
    result.tools_used
    |> List.filter (fun name -> not (is_observation_only_tool_name name))
    |> List.length
  in
  let has_substantive_tools = has_substantive_tool_calls result.tools_used in
  let has_text = String.trim result.response_text <> "" in
  let validated_evidence = visible_run_validation result in
  let has_validated_evidence = Option.is_some validated_evidence in
  let visible_tool_signal_present =
    has_substantive_tools || has_validated_evidence
  in
  let is_scheduled_autonomous_cycle =
    is_scheduled_autonomous_cycle_of_observation observation
  in
  let is_board_reactive = observation.pending_board_events <> [] in
  let is_mention_reactive = observation.pending_mentions <> [] in
  let has_meaningful_work =
    has_text || has_substantive_tools || has_validated_evidence
  in
  let rt = meta.runtime in
  let social_state : Social.social_state =
    Option.value social_state
      ~default:
        Social.
          {
            social_model = meta.social_model;
            belief_summary = "not_recorded";
            active_desire = None;
            current_intention = None;
            blocker = None;
            need = None;
            speech_act = Social.Inform;
            delivery_surface = Social.Visible_reply;
          }
  in
  (* #10474: proactive outcome counter for successful cycles. *)
  if update_proactive_rt && is_scheduled_autonomous_cycle then begin
    let outcome =
      if has_substantive_tools then "tool_called"
      else if is_noop_cycle ~has_text ~tools_used:result.tools_used
      then "noop"
      else "tool_called"
    in
    Prometheus.inc_counter Keeper_metrics.(to_string ProactiveOutcome)
      ~labels:[ ("keeper", meta.name); ("outcome", outcome) ]
      ()
  end;
  let updated_meta = {
    meta with
    updated_at = now_iso ();
    runtime = { rt with
      usage = {
        total_turns = rt.usage.total_turns + 1;
        total_input_tokens = rt.usage.total_input_tokens + trusted_input_tokens;
        total_output_tokens =
          rt.usage.total_output_tokens + trusted_output_tokens;
        total_tokens =
          rt.usage.total_tokens + trusted_total_tokens;
        total_cost_usd = rt.usage.total_cost_usd +. turn_cost;
        last_turn_ts = now_ts;
        last_model_used = "";
        last_input_tokens = trusted_input_tokens;
        last_output_tokens = trusted_output_tokens;
        last_total_tokens = trusted_total_tokens;
        last_latency_ms = latency_ms;
      };
      (* Deterministic scheduled autonomous cycle accounting is separated from
         nondeterministic model output visibility. *)
      proactive_rt = {
        count_total =
          rt.proactive_rt.count_total
          + (if update_proactive_rt && is_scheduled_autonomous_cycle then 1 else 0);
        last_ts =
          (if update_proactive_rt
              && (is_scheduled_autonomous_cycle
                  || ((is_board_reactive || is_mention_reactive)
                      && has_meaningful_work))
           then now_ts
           else rt.proactive_rt.last_ts);
        visible_count_total =
          rt.proactive_rt.visible_count_total
          + (if update_proactive_rt
               && is_scheduled_autonomous_cycle
               && (has_text || visible_tool_signal_present)
             then 1
             else 0);
        last_visible_ts =
          (if update_proactive_rt
              && is_scheduled_autonomous_cycle
              && (has_text || visible_tool_signal_present)
           then now_ts
           else rt.proactive_rt.last_visible_ts);
        last_outcome =
          (if update_proactive_rt && is_scheduled_autonomous_cycle then
             scheduled_autonomous_outcome_of_result ~has_text
               ~has_tool_calls:visible_tool_signal_present
           else rt.proactive_rt.last_outcome);
        last_reason =
          (if not update_proactive_rt || not is_scheduled_autonomous_cycle
           then rt.proactive_rt.last_reason
           else if has_substantive_tools then
             Printf.sprintf "unified:tools=[%s]"
               (String.concat "," result.tools_used)
           else if has_validated_evidence then
             (match validated_evidence with
              | Some v ->
                Printf.sprintf "unified:validated_evidence(ok=%b,file_write=%b,evidence=%d)"
                  v.ok v.has_file_write (List.length v.evidence)
              | None -> "unified:validated_evidence(unreachable)")
           else if not has_text then
             "unified:"
             ^ proactive_cycle_outcome_to_string Proactive_silent
            else if has_text then "unified:text_response"
            else rt.proactive_rt.last_reason);
        last_preview =
          (if not update_proactive_rt || not is_scheduled_autonomous_cycle
           then rt.proactive_rt.last_preview
           else if has_text then short_preview result.response_text
           else if has_substantive_tools then
             Printf.sprintf "(tools: %s)" (String.concat ", " result.tools_used)
           else
             (match validated_evidence with
              | Some v -> validated_evidence_preview v
              | None -> rt.proactive_rt.last_preview)
          );
        consecutive_noop_count =
          (if update_proactive_rt && is_scheduled_autonomous_cycle then
             if is_noop_cycle ~has_text ~tools_used:result.tools_used
             then rt.proactive_rt.consecutive_noop_count + 1
             else 0
           else rt.proactive_rt.consecutive_noop_count);
      };
      (* Autonomous action tracking from tool calls *)
      autonomous_action_count =
        rt.autonomous_action_count
        + (if is_autonomous_turn then substantive_tool_call_count else 0);
      autonomous_turn_count =
        rt.autonomous_turn_count + (if is_autonomous_turn then 1 else 0);
      autonomous_text_turn_count =
        rt.autonomous_text_turn_count
        + (if is_autonomous_turn && has_text && not has_substantive_tools then 1 else 0);
      autonomous_tool_turn_count =
        rt.autonomous_tool_turn_count
        + (if is_autonomous_turn && has_substantive_tools then 1 else 0);
      board_reactive_turn_count =
        rt.board_reactive_turn_count + (if is_board_reactive then 1 else 0);
      mention_reactive_turn_count =
        rt.mention_reactive_turn_count + (if is_mention_reactive then 1 else 0);
      noop_turn_count =
        rt.noop_turn_count
        + (if is_autonomous_turn && not has_text && not has_substantive_tools
              && not has_validated_evidence then 1 else 0);
      (* This timestamp stays scoped to substantive tool actions.
         Validated evidence affects proactive visibility, but it does not
         redefine the autonomous action counter semantics. *)
      last_autonomous_action_at =
        (if is_autonomous_turn && has_substantive_tools
         then now_iso ()
         else rt.last_autonomous_action_at);
      last_speech_act = Social.speech_act_to_string social_state.speech_act;
      last_social_transition_reason =
        (match social_transition_reason with
         | Some reason -> String.trim reason
         | None -> rt.last_social_transition_reason);
      last_active_desire =
        Option.value ~default:"" social_state.active_desire;
      last_current_intention =
        Option.value ~default:"" social_state.current_intention;
      (* A successful turn means the keeper is not blocked.
         Clear unconditionally so stale error strings from previous
         failures do not persist in the runtime JSON and mislead the
         dashboard into showing BLOCKED status.  The social model's
         blocker field is a protocol-level signal; runtime last_blocker
         tracks whether the keeper can make progress. *)
      last_blocker = None;
      last_need = Option.value ~default:"" social_state.need;
      last_turn_tool_calls = [];
    };
  } in
  record_keeper_total_cost_usd
    ~keeper_name:updated_meta.name
    ~total_cost_usd:updated_meta.runtime.usage.total_cost_usd;
  updated_meta
