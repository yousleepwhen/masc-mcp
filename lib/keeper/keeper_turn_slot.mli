exception Semaphore_wait_timeout of float

type slot_pool =
  | Turn_pool
  | Autonomous_pool
  | Reactive_pool

val slot_pool_to_string : slot_pool -> string

type semaphore_wait_phase =
  | Autonomous_queue_head
  | Autonomous_slot
  | Reactive_slot
  | Turn_slot

val semaphore_wait_phase_to_string : semaphore_wait_phase -> string

type semaphore_wait_timeout = {
  timeout_wait_sec : float;
  timeout_phase : semaphore_wait_phase;
  timeout_autonomous_available : int;
  timeout_reactive_available : int;
  timeout_turn_available : int;
  timeout_queue_depth : int;
  timeout_queue_ahead : int option;
  timeout_holders : (string * float) list;
}

(** Global turn slot cap. Safety ceiling for ALL keeper turns. *)
val keeper_turn_throttle_limit : int

(** Effective throttle limit after applying the 2x TOML cap (issue #17192).
    When the env override exceeds 2x the TOML baseline, this is capped to
    [toml_value * 2]. Otherwise equal to {!keeper_turn_throttle_limit}.
    The semaphore is initialized with this value, not the raw env limit. *)
val effective_turn_throttle_limit : int

(** Which configuration layer supplied the effective throttle limit. *)
type throttle_source =
  | Env_override
  | Toml
  | Default

val keeper_turn_throttle_source : throttle_source
(** Source of {!keeper_turn_throttle_limit}.
    - [Env_override] — [MASC_KEEPER_AUTOBOOT_MAX] was set in the process
      environment; it takes precedence over TOML.
    - [Toml] — the value came from [keeper_runtime.toml].
    - [Default] — neither env nor TOML supplied a value; the hardcoded
      default (32) is in effect.

    @since issue #17192 *)

val throttle_source_to_string : throttle_source -> string
(** Canonical string representation for logs and JSON surfaces:
    ["env_override" | "toml" | "default"]. *)

val turn_semaphore : Eio.Semaphore.t
val autonomous_turn_semaphore : Eio.Semaphore.t
val reactive_turn_semaphore : Eio.Semaphore.t

(** Test-only: resolve a keeper turn concurrency env var through the same
    test-executable isolation gate used at module initialization. *)
val turn_concurrency_int_of_env_default_for_test :
  string -> default:int -> min_v:int -> max_v:int -> int

(** Wall-clock cap on [Eio.Semaphore.acquire] when waiting for a keeper
    turn slot. Derived from [MASC_KEEPER_SEMAPHORE_WAIT_TIMEOUT_SEC]. *)
val semaphore_wait_timeout_sec : float

type autonomous_waiter = {
  ticket : int;
  keeper_name : string;
}

(** Test-only reset for the autonomous FIFO wait queue. *)
val reset_autonomous_turn_queue_for_test : unit -> unit

(** Test-only snapshot of keeper names currently queued for an autonomous turn. *)
val autonomous_waiter_snapshot_for_test : unit -> string list

(** Test-only snapshots of the current semaphore availability. *)
val turn_semaphore_value_for_test : unit -> int
val autonomous_turn_semaphore_value_for_test : unit -> int
val reactive_turn_semaphore_value_for_test : unit -> int

(** Diagnostic: keepers currently holding a slot in each pool, paired
    with how long (in seconds, relative to [now]) they have held it.
    Sorted by descending hold time so the longest-holding peer is first
    — that is typically the actual fleet blocker when [turn_available=0]
    starves the rest. Pure read; no mutation.

    [~now] MUST come from {!Time_compat.now}, the same clock source
    used to record [acquired_at] inside this module. Mixing
    [Unix.gettimeofday ()] or any other clock can produce nonsense
    hold-time values (negative, or off by the clock skew). *)
val turn_slot_holders : now:float -> (string * float) list
val autonomous_slot_holders : now:float -> (string * float) list
val reactive_slot_holders : now:float -> (string * float) list

(** Force-release semaphore permits held by [keeper_name] after the watchdog
    has classified the holder as stale. Returns the labels actually released.

    This is intentionally narrower than normal cleanup: it only releases
    holders still present in the diagnostic holder table, and the normal
    [with_keeper_turn_slot] finalizer consumes the same acquisition's
    force-release marker so a late-returning fiber cannot double-release the
    same permit, and a newer keeper generation cannot consume the stale
    predecessor's marker. *)
val force_release_stale_holder : keeper_name:string -> string list

(** Test-only: TTL used to bound orphaned force-release markers left behind
    when a cancelled stale fiber never reaches its finalizer. *)
val force_released_marker_ttl_sec_for_test : float

(** Test-only: count force-release markers still awaiting finalizer
    consumption or expiry pruning. *)
val force_released_marker_count_for_test : unit -> int

(** Test-only: inject a marker without touching semaphores, so marker-retention
    behavior can be exercised without creating a double-release path. *)
val add_force_released_marker_for_test :
  label:slot_pool ->
  keeper_name:string ->
  acquisition_id:int ->
  marked_at:float ->
  unit

(** Test-only: prune expired force-release markers using an injected clock. *)
val purge_force_released_markers_for_test : now:float -> unit

(** Test-only: clear force-release markers between tests. *)
val clear_force_released_markers_for_test : unit -> unit

(** Render a compact holder list such as [[keeper-a/181s, +2 more]].
    The input is expected to be sorted longest-first, as returned by the
    holder accessors above. *)
val format_slot_holders : ?limit:int -> (string * float) list -> string

(** Operator-facing one-line summary of all holder pools. *)
val slot_holders_summary : ?limit:int -> now:float -> unit -> string

(** Test-only FIFO queue primitives for autonomous fairness regression tests. *)
val enqueue_autonomous_waiter_for_test : string -> int
val drop_autonomous_waiter_for_test : int -> unit

(** Test-only: drive the queue-head wait loop directly with an injected
    [~started_at]. Exposed so a regression test can assert that a stale
    [started_at] (e.g. one captured before a fairness cooldown) immediately
    returns [Error `Semaphore_wait_timeout] — proving the parameter is the
    timing knob whose freshness must be controlled at every call site. *)
val wait_for_autonomous_queue_head_for_test :
  keeper_name:string ->
  ticket:int ->
  started_at:float ->
  (unit, [> `Semaphore_wait_timeout of semaphore_wait_timeout ]) result

(** Pure computation: seconds keeper should yield before re-entering queue
    at time [now].  0.0 = no yield needed. *)
val fairness_delay_sec_at : now:float -> keeper_name:string -> float

(** Force-release every slot recorded for [keeper_name] in the holder
    table. Returns the [(label, age_sec)] pairs that were released so the
    caller can stamp the diagnosis. Empty list means nothing was held.

    Intended caller: the supervisor's [force_unresolved_watchdog_crash]
    path, which fires when a keeper fiber is declared crashed but did
    not return through the natural [Fun.protect] release. Without this,
    the slot is leaked until process restart (fleet starvation behind
    [reactive_turn_semaphore]).

    Side effects: [Eio.Semaphore.release] on each held semaphore plus
    [Keeper_metrics.(to_string SlotForceReleased)]. A late-returning
    fiber may double-release; Eio counting semaphores tolerate this
    bounded over-release.

    See [keeper_turn_slot.ml] doc for full design rationale.

    {b WORKAROUND (RFC-0125)}: This function only releases the semaphore
    permit. The underlying stuck OS subprocess (LLM HTTPS read,
    [docker exec]) keeps running until process restart. The structural
    fix is RFC-0125 P4 [keeper-level max-turn watchdog]
    (PR #15964), which cancels the keepalive fiber at a typed wall-clock
    boundary BEFORE the slot is leaked, so this rescue path stops being
    reached. Removal target: 30-day soak on
    stale-watchdog timeout termination metric reaching
    zero after `MASC_KEEPER_MAX_TURN_WATCHDOG_TIMEOUT_SEC` is enabled
    fleet-wide. Do not invoke from new call sites. Existing legitimate
    callers (slated to be unwound under removal target above):
    - [Keeper_supervisor.force_unresolved_watchdog_crash] — primary
      watchdog rescue.
    - [Keeper_keepalive.stop_keepalive] — manual stop path
      (`lib/keeper/keeper_keepalive.ml`). *)
val force_release_holder_for : keeper_name:string -> (string * float) list

(** Test-only: stamp a completion time directly (bypasses [Time_compat.now]). *)
val record_autonomous_completion_at_for_test : keeper_name:string -> ts:float -> unit

(** Test-only: clear all per-keeper completion timestamps. *)
val reset_autonomous_completion_for_test : unit -> unit

(** Test-only: inject a callback immediately after an acquire flag is set
    and before the diagnostic holder row is recorded.  Used to pin that
    exception/cancel paths reclaim the semaphore even when no holder row
    exists yet. *)
val set_after_acquire_flag_hook_for_test :
  (label:string -> keeper_name:string -> unit) option -> unit

(** PR-M (Leak 9): consecutive [provider_timeout] cycle FAILED strikes
    per keeper. The heartbeat loop feeds this count into
    [Keeper_failure_policy] before choosing any lifecycle effect.

    Counts are stored in an in-process CAS map and can be seeded from the
    persisted [Provider_timeout_loop] failure reason on the first bump after
    restart or another process update. *)
val provider_timeout_strike_limit : int

type provider_timeout_strike_outcome =
  | Provider_timeout_warn
  | Provider_timeout_soft_backoff

val classify_provider_timeout_strike :
  strikes:int -> provider_timeout_strike_outcome

val bump_budget_exhaustion_seeded :
  keeper_name:string -> prior_strikes:int -> int
val bump_budget_exhaustion : keeper_name:string -> int
val reset_budget_exhaustion : keeper_name:string -> unit
val peek_budget_exhaustion_for_test : keeper_name:string -> int
val set_budget_exhaustion_for_test : keeper_name:string -> strikes:int -> unit

type keeper_turn_slot_state

type keeper_turn_slot_control = {
  release_for_retry : unit -> unit;
  reacquire_after_retry :
    unit ->
    (int, [ `Semaphore_wait_timeout of semaphore_wait_timeout ]) result;
}

val with_keeper_turn_slot_control :
  ?cascade_profile:string ->
  keeper_name:string ->
  channel:Keeper_world_observation.keeper_cycle_channel ->
  (semaphore_wait_ms:int -> slot_control:keeper_turn_slot_control -> 'a) ->
  ('a, [> `Semaphore_wait_timeout of semaphore_wait_timeout ]) result

val with_keeper_turn_slot :
  ?cascade_profile:string ->
  keeper_name:string ->
  channel:Keeper_world_observation.keeper_cycle_channel ->
  (semaphore_wait_ms:int -> 'a) ->
  ('a, [> `Semaphore_wait_timeout of semaphore_wait_timeout ]) result

(** Test-only wrapper around the keeper turn slot acquisition path with
    explicit in-turn release/reacquire controls. *)
val with_keeper_turn_slot_control_for_test :
  ?cascade_profile:string ->
  keeper_name:string ->
  channel:Keeper_world_observation.keeper_cycle_channel ->
  (semaphore_wait_ms:int -> slot_control:keeper_turn_slot_control -> 'a) ->
  ('a, [> `Semaphore_wait_timeout of semaphore_wait_timeout ]) result

(** Test-only wrapper around the keeper turn slot acquisition path. *)
val with_keeper_turn_slot_for_test :
  ?cascade_profile:string ->
  keeper_name:string ->
  channel:Keeper_world_observation.keeper_cycle_channel ->
  (semaphore_wait_ms:int -> 'a) ->
  ('a, [> `Semaphore_wait_timeout of semaphore_wait_timeout ]) result
