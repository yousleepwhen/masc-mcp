(** Config-driven cascade profile name resolution.

    Per RFC-0041 cascade routing SSOT, the live cascade catalog
    (cascade.toml) is the only source of truth for cascade profile
    names — there is no compile-time enum here.  Code that needs to
    know "what profiles are available" reads them through
    {!catalog_names} / {!catalog_names_result} /
    {!catalog_names_for_validation}; the boot-time gate at
    [Cascade_catalog_runtime.validate_path_result] rejects keeper boot
    when the catalog is empty so a missing catalog never reaches the
    helpers below.

    @since 0.9.5 *)

type logical_use = Cascade_ref.logical_use =
  | Keeper_turn
  | Phase_recovery
  | Phase_buffer
  | Tool_required
  | Governance_judge
  | Operator_judge
  | Cross_verifier
  | Verifier
  | Adversarial_reviewer
  | Auto_responder
  | Routing
  | Openai_compat
  | Persona_generation
  | Provider_benchmark
  | Simple_task
  | Moderate_task
  | Complex_task
  | Tool_rerank_use

val logical_use_key : logical_use -> string
(** Stable config key under [routes]. *)

val logical_use_of_string_opt : string -> logical_use option
(** Parse a canonical logical route key. Concrete cascade profile names are
    not logical route keys — they live in the catalog. *)

val cascade_name_for_use : ?config_path:string -> logical_use -> string
(** Runtime cascade profile for a logical call site.

    Resolution order:
    1. TOML [routes.<logical_use_key>] from the active cascade config,
       when it points at a live catalog profile.
    2. The first catalog entry from the live catalog.
    3. The canonical [route.<key>] name when the catalog itself is empty —
       boot-time validation is the upstream gate that prevents this state at
       runtime.

    This returns a cascade profile name, never a provider/model string.
    Phonebook model/provider resolution is exposed separately through
    {!model_strings_for_use} and {!provider_configs_for_use}.

    This is the boundary for code that used to hardcode profile names such as
    ["governance_judge"], ["operator_judge"], ["phase_recovery"], or
    ["cross_verifier"]. *)

val provider_configs_for_use :
  ?config_path:string ->
  ?temperature:float ->
  ?max_tokens:int ->
  logical_use ->
  Llm_provider.Provider_config.t list option
(** Resolve a logical use to [Provider_config.t] list via the phonebook.
    Direct phonebook path: logical_use → task_use → tier-group → models →
    providers → endpoint/auth → Provider_config.t.
    Returns [None] when phonebook is unavailable or no models resolve.
    @since RFC Cascade-Phonebook Phase 4 *)

val model_strings_for_use :
  ?config_path:string ->
  logical_use ->
  string list option
(** Resolve a logical use to model strings via the phonebook.
    Returns [None] when phonebook is unavailable.
    @since RFC Cascade-Phonebook Phase 4 *)

val configured_route_targets : ?config_path:string -> unit -> string list
(** Unique non-empty profile names referenced from [routes]. *)


val catalog_names : ?config_path:string -> unit -> string list
(** Live profile catalog discovered from the active [cascade.toml].
    When the file cannot be read, returns [[]]. *)

val catalog_names_result : ?config_path:string -> unit -> (string list, string) result
(** Like {!catalog_names}, but preserves the loader error so validation
    boundaries can fail loud instead of collapsing catalog drift into an empty
    dynamic profile set. *)

val catalog_lookup_names : ?config_path:string -> unit -> string list
(** Live catalog names usable for lookup. Includes canonical declarative
    names such as ["tier-group.primary"] / ["tier.primary"] plus public
    short aliases such as ["primary"]. *)

val catalog_names_for_validation :
  ?config_path:string -> unit -> (string list, string) result
(** Accept-list source for the keeper cascade-name validator.

    Requires the declarative cascade catalog; retired flat-profile TOML and
    flat-key catalog fallback are intentionally not accepted.  Includes both
    public names, such as [primary], and qualified declarative names, such as
    [tier.primary] / [tier-group.primary], so explicit keeper assignments do
    not collapse to the public route target. *)

val keeper_catalog_names : ?config_path:string -> unit -> string list
(** Assignable live profile names from {!catalog_names}, filtered by
    [keeper_assignable] metadata. *)

val system_catalog_names : ?config_path:string -> unit -> string list
(** Live system-only profile names present in [cascade.toml]. *)

val required_capability_profile_of_cascade_name : string -> string option
(** [required_capability_profile_of_cascade_name name] returns the
    [required_capability_profile] declared for cascade [name] in the
    live catalog snapshot, or [None] when the snapshot is missing or
    the cascade is not found.  Used by pre-dispatch capability-aware
    rotation filtering so [Tool_required] turns do not rotate into
    cascades whose providers cannot satisfy forced [tool_choice]. *)

val fallback_cascade_for : ?config_path:string -> string -> string option
(** Declarative escalation hint for [name].

    Returns [Some target] when:
    - the profile [name] is present in the live catalog, AND
    - it declares a non-empty [fallback_cascade], AND
    - the [fallback_cascade] target is itself a live catalog entry.

    Returns [None] otherwise (including when the target is missing
    or self-referential). Unknown targets are logged as a single
    WARN line per startup and treated as if absent — the runtime
    must never crash because of a stale fallback hint.

    @since 0.174.0 *)

val is_system_only_cascade : string -> bool
(** Exact-name membership check against the active config's
    {!system_catalog_names}. *)

val canonicalize_with_catalog : catalog:string list -> string -> string
(** Resolves dynamic profiles against an explicit live catalog. *)

val resolve_live_with_catalog_result :
  catalog:string list -> string -> (Cascade_name.t, [ `Unresolved of string ]) result
(** Resolves a keeper-declared cascade against an explicit live catalog.

    Returns [Ok normalized] when [raw] either matches the
    catalog directly or normalizes via a canonical logical route key that
    lands on a catalog member; otherwise [Error (`Unresolved raw)] carrying the
    original (un-trimmed) input so the operator-visible diagnostic can
    point to exactly what was provided.

    Names already present in the catalog pass through; canonical route
    keys resolve via [routes].  Callers that accept qualified
    declarative names must pass a lookup catalog containing those
    qualified names, not only display/public names.

    The legacy silent-fallback [resolve_live_with_catalog]/[resolve_live]
    entry points (+ their counter and WARN-once Hashtbl) were removed as
    part of the RFC-0149 §3.3 sunset closeout.

    @since RFC-0149 Phase 1 *)

val resolve_live_result :
  ?config_path:string -> string -> (Cascade_name.t, [ `Unresolved of string ]) result
(** Result-returning resolver that reads the active catalog from the
    resolved cascade config path.  Wraps {!resolve_live_with_catalog_result}
    with the catalog loaded from [config_path].

    @since RFC-0149 Phase 1 *)

val canonicalize : string -> string
(** Catalog-aware normalization: canonical route keys resolve through
    [routes], live catalog names pass through, otherwise [String.trim] is
    applied and the name is returned as-is.  If the catalog is empty, a
    canonical [route.<key>] name remains the fallback. *)

val normalize_declared_name : ?config_path:string -> string -> string
(** Normalizes keeper-side logical route aliases.
    Logical route aliases resolve through {!cascade_name_for_use}; public live
    catalog names resolve to their canonical [tier.*] / [tier-group.*] form;
    otherwise the trimmed input is returned. *)

val normalize_keeper_runtime_declared_name : ?config_path:string -> string -> string
(** Like {!normalize_declared_name}, but ignores stale keeper-local profile
    assignments that point at a route target reserved for non-keeper logical
    uses. In that case the active [routes.keeper_turn] target is returned. *)

(** {1 In-memory cascade key helpers} *)

(** First canonicalize, then build the key. *)
val models_key : string -> string
val temperature_key : string -> string
val max_tokens_key : string -> string

(** {1 RFC-0143 — typed catalog query}

    Distinguishes the three control-flow origins of an unavailable
    catalog so callers can decide what to do about each instead of
    collapsing them into [Error _ -> false] / [Error _ -> []].

    The legacy string-error [catalog_metadata_result] was deleted by
    the §4 PR-5 closeout once all in-file callers consumed the typed
    variant. *)

(** Catalog metadata record returned by the query.  Exposed here so
    the typed query is callable from outside the module. *)
type catalog_metadata = {
  qualified_names : string list;
  public_names : string list;
  keeper_assignable_names : string list;
  system_qualified_names : string list;
  system_names : string list;
  fallback_hints : (string * string) list;
}

type catalog_unavailable_reason =
  | Catalog_path_not_resolved
  | Catalog_load_failed of string
  | Catalog_metadata_invalid of string

type 'a catalog_query_result =
  | Catalog_ok of 'a
  | Catalog_unavailable of {
      reason : catalog_unavailable_reason;
      message : string;
    }

val catalog_unavailable_reason_to_string : catalog_unavailable_reason -> string
(** Short bounded-cardinality token suitable for log lines and
    [cascade_catalog_unavailable_count{reason=…}] metric labels. *)

val catalog_metadata_query
  :  ?config_path:string
  -> unit
  -> catalog_metadata catalog_query_result
(** Typed catalog metadata accessor.  The [Catalog_unavailable]
    payload carries both the typed [reason] (suitable for routing
    logic and metric labels) and the original [message] (suitable
    for operator-facing logs). *)
