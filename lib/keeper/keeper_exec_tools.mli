open Keeper_types

val keeper_allowed_tool_names :
  ?write_done:bool ->
  ?phase:Keeper_state_machine.phase ->
  keeper_meta -> string list
val keeper_allowed_model_tools :
  ?write_done:bool -> keeper_meta -> Types.tool_schema list

(** Universe tool names: candidates minus denied, no policy filter.
    Superset of [keeper_allowed_tool_names].  Used as the BM25 retrieval
    scope so progressive disclosure can surface tools beyond the preset. *)
val keeper_universe_tool_names : keeper_meta -> string list

(** Keeper-facing runtime candidate names before policy filtering. *)
val keeper_internal_candidate_tool_names : string list

(** Universe model tool schemas.  Returns schemas for all universe tools
    so [make_tools] can build {!Oas.Tool.t} for the full search scope. *)
val keeper_universe_model_tools : keeper_meta -> Types.tool_schema list

(** Preset-scoped universe: preset allowlist + core_always - denied.
    Strict subset of [keeper_universe_tool_names].  Used for BM25 indexing
    to reduce candidate pool size per keeper preset.  See #4637. *)
val keeper_preset_universe_tool_names : keeper_meta -> string list

(** Preset-scoped model tool schemas for BM25 indexing. *)
val keeper_preset_universe_model_tools : keeper_meta -> Types.tool_schema list

(** Core tools that bypass preset filtering and seed the disclosure floor.
    Runtime gating/pruning can still narrow their visibility on a given turn. *)
val core_always_tools : string list

(** Expanded core set for tool-discovery mode (MASC_KEEPER_TOOL_DISCOVERY). *)
val core_discovery_tools : string list

(** Returns [core_discovery_tools].  Discovery mode is the default. *)
val effective_core_tools : unit -> string list

(** Keeper tools dispatched by the server but withheld from the visible
    keeper tool set (admin/opt-in only — see [optional] group in
    [config/tool_policy.toml]).  Exported so startup validators can
    recognise them as runtime rather than orphan config. *)
val keeper_admin_dispatched_tools : string list

(** Keeper-local read-only tools that do not always flow through Tool_spec. *)
val keeper_read_only_tools : string list

(** [true] when a keeper-only tool is inherently read-only. *)
val is_keeper_read_only_tool : string -> bool

(** [true] when [name] is read-only or idempotent (safe to retry).
    Keeper-local fast-path (no mutex), then Tool_dispatch.is_read_only,
    then Tool_dispatch.is_idempotent. Prefer {!has_mutating_side_effect}
    at call sites for positive-sense readability. *)
val is_effectively_read_only_tool : string -> bool

(** [true] when calling [name] may produce non-idempotent side effects.
    Used by the side-effect observer to block retry after committed mutations. *)
val has_mutating_side_effect : string -> bool

(** Input-aware mutation check for mixed tools such as [keeper_shell] with
    op=gh where read-only and mutating subcommands share the same tool name. *)
val has_mutating_side_effect_with_input :
  tool_name:string -> input:Yojson.Safe.t -> bool

(** Schema for the keeper_tool_search tool. *)
val keeper_tool_search_schema : Types.tool_schema

(** Injected masc_* tool schemas (populated at startup by [inject_masc_schemas]). *)
val masc_schemas_ref : Types.tool_schema list ref

(** Injected masc_* tool names (populated at startup by [inject_masc_schemas]). *)
val injected_masc_tool_names : unit -> string list

(** [is_core_always_tool name] — true if [name] bypasses policy restrictions. *)
val is_core_always_tool : string -> bool

(** Deduplicate tool names, preserving order. *)
val dedupe_tool_names : string list -> string list

(** Inject all masc_* schemas for keeper allowlist/denylist filtering.
    Must be called once during server initialization.
    Keeper_denied tools are excluded at injection time. *)
val inject_masc_schemas : Types.tool_schema list -> unit

(** Load preset definitions from [config/tool_policy.toml].
    Must be called once during server initialization.
    Raises [Failure] if the config file is missing or malformed.
    For preset-scoped allowlist filtering to include injected [masc_*]
    schemas, call [inject_masc_schemas] before the first preset resolution. *)
val init_policy_config : base_path:string -> (unit, string) result

(** Check if a tool name is in the Keeper_denied surface (Tool_catalog).
    Denied tools are excluded from both the schema list sent to the LLM
    and blocked at execution time by the pre_tool_use hook. *)
val is_keeper_denied : string -> bool

(** Callback for recording keeper-internal tool calls.
    Set at server initialization to avoid Config dependency cycle. *)
val on_keeper_tool_call :
  (tool_name:string -> success:bool -> duration_ms:int -> unit) ref

(** Callback for keeper_tool_search BM25 search.
    Process-global fallback; prefer passing [~search_fn] to
    [execute_keeper_tool_call] for session-scoped, race-free search. *)
val tool_search_fn :
  (query:string -> max_results:int -> Yojson.Safe.t) ref

(** Classification of a keeper tool result payload for circuit-breaker
    bookkeeping.

    Plain text is treated as a valid success path because some keeper tools
    intentionally return markdown/text on success. JSON-looking payloads
    (leading [{] or [[] after whitespace) are parsed so malformed structured
    output does not silently reset the breaker. *)
type tool_result_payload =
  | Structured_success
  | Structured_error
  | Plain_text
  | Malformed_structured of string

(** Bridge-facing execution outcome.
    [tool_not_allowed] remains a non-failure outcome so preset/policy
    rejections do not trip repeated-failure guardrails. *)
type execution_outcome = [ `Success | `Failure ]

(** Typed keeper tool execution result.
    [raw_output] preserves the original payload, [outcome] is the
    authoritative success/failure decision for bridge consumers, and
    [payload_shape] captures the post-execution wire shape for telemetry
    and malformed-payload handling. *)
type executed_tool_result = {
  raw_output : string;
  outcome : execution_outcome;
  payload_shape : tool_result_payload;
}

(** Inspect a keeper tool result payload without applying side effects. *)
val classify_tool_result_payload : string -> tool_result_payload

(** Tag-based dispatch callback for masc_* tools without handler registry entries.
    Set at server init to [Keeper_tag_dispatch.dispatch]. Default: returns None.
    See #4579. *)

(** masc_* tool names available for a keeper (filtered by allowlist/denylist). *)
val keeper_masc_tool_names : keeper_meta -> string list

(** masc_* tool schemas available for a keeper (filtered by allowlist/denylist). *)
val keeper_masc_tool_schemas : keeper_meta -> Types.tool_schema list

(** Compute the keeper's sender identity for portals and broadcasts.
    Guards against double "keeper-" prefix. See #5104. *)

val execute_keeper_tool_call_with_outcome :
  config:Coord.config ->
  meta:keeper_meta ->
  ctx_work:working_context ->
  ?turn_sandbox_runtime:Keeper_turn_sandbox_runtime.t ->
  ?turn_sandbox_runtime_git:Keeper_turn_sandbox_runtime.t ->
  ?search_fn:(query:string -> max_results:int -> Yojson.Safe.t) ->
  name:string ->
  input:Yojson.Safe.t ->
  unit ->
  executed_tool_result

val execute_keeper_tool_call :
  config:Coord.config ->
  meta:keeper_meta ->
  ctx_work:working_context ->
  ?turn_sandbox_runtime:Keeper_turn_sandbox_runtime.t ->
  ?search_fn:(query:string -> max_results:int -> Yojson.Safe.t) ->
  name:string ->
  input:Yojson.Safe.t ->
  unit ->
  string
