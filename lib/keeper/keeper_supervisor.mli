(** Keeper_supervisor — keeper keepalive fiber supervision.

    Wraps the MASC-owned keeper heartbeat fibers with Promise-based
    liveness tracking via [Keeper_registry]. Detects zombie fibers
    (resolved Promise) and performs automatic restart with exponential
    backoff.

    This does not supervise the OAS [Agent.run] lifecycle.

    @since 2.102.0 *)

open Keeper_types

(** {1 Supervised Execution} *)

val supervise_keepalive :
  proactive_warmup_sec:int -> 'a context -> keeper_meta -> unit
(** Start a keeper heartbeat loop inside a supervised fiber.
    Registers in [Keeper_registry] (SSOT) and launches the fiber.
    On fiber termination, resolves the Promise and publishes
    keeper-lifecycle events via Event_bus. *)

(** {1 Watchdog} *)

val fork_stale_watchdog :
     'a context
  -> keeper_meta
  -> ?startup_warmup_sec:int
  -> Keeper_registry.registry_entry
  -> unit
(** Fork a stale-turn watchdog fiber for the given keeper.  This is a
    re-export of {!Keeper_stale_watchdog.fork_stale_watchdog}; see
    that module's docstring for the authoritative description of the
    four detection modes ([Idle_turn] / [In_turn_hung] /
    [Mid_turn_no_progress] / [Noop_failure_loop]) and per-class
    Prometheus counter. *)

(** {1 Sweep and Recovery} *)

val sweep_and_recover : 'a context -> unit
(** Scan all supervised keepers in [Keeper_registry]. Detect zombies
    (resolved Promise), restart with exponential backoff if within
    budget, mark dead otherwise. Called periodically by the keeper
    supervisor loop. *)

(** {1 Pure Helpers (exposed for testing)} *)

val backoff_delay : int -> float
(** Compute exponential backoff delay for the given attempt number.
    Uses MASC_KEEPER_SUPERVISOR_BACKOFF_BASE_S and _MAX_S. *)

val keep_last_n : int -> 'a -> 'a list -> 'a list
(** [keep_last_n n item lst] prepends [item] and keeps at most [n] entries. *)

val persona_name_for_drift_check : keeper_meta -> string
(** Resolve the persona handle used by supervisor persona-drift checks.
    Honors keeper TOML [persona_name] overlays before falling back to
    the keeper name. *)

val persona_profile_path_for_drift_check :
  base_path:string -> string -> string
(** Return the concrete persona [profile.json] path reported by supervisor
    drift diagnostics. *)

(** supervision_cohort type + cohort/persona helpers live in
    Keeper_supervisor_types (intra-library file split, 2026-05-16).
    Re-exported here so existing callers keep using
    [Keeper_supervisor.supervision_cohort] etc. unchanged. *)
include module type of Keeper_supervisor_types


val cohort_key_of_reason : Keeper_registry.failure_reason option -> string
(** Map a structured failure_reason to a cohort key for self-preservation grouping. *)

val failure_reason_policy_decision_for_test :
  Keeper_registry.failure_reason option -> Keeper_failure_policy.decision option
(** Pure supervisor-side bridge from registry failure reasons into the
    keeper failure policy matrix. Exposed for regression tests so pause-vs-restart
    lifecycle ownership remains pinned to [Keeper_failure_policy]. *)

val apply_self_preservation :
  keepers_dir:string ->
  total_keepers:int ->
  (Keeper_registry.registry_entry * string) list ->
  (Keeper_registry.registry_entry * string) list
(** Self-preservation gate. Suppresses restarts when a dominant failure
    cohort exceeds ratio threshold AND minimum candidate count.
    Bounded minority [stale_turn_timeout] cohorts are allowed through so
    alive-but-stuck recovery can drain partial slot starvation; larger stale
    cohorts still use the circuit breaker/probe path.
    Returns the filtered list of entries that should proceed with restart. *)

val reset_self_preservation_escape_state_for_test : unit -> unit
(** Reset the self-preservation probe state. Test-only. *)

val active_supervision_keeper_count :
  Keeper_registry.registry_entry list -> int
(** Count currently active keepers for self-preservation denominators. *)

val set_restart_launch_noop_for_test : bool -> unit
(** Test-only: when enabled, restart bookkeeping still runs but the
    replacement heartbeat/watchdog fibers are not forked. *)

val restart_launch_noop_enabled_for_test : unit -> bool
(** Test-only: inspect the restart-launch noop flag. *)

val with_restart_launch_noop_for_test : (unit -> 'a) -> 'a
(** Test-only: scoped restart-launch noop override. Nested and overlapping
    scopes restore the prior flag only after the outer scope exits. *)

(** {1 Liveness Recovery} *)

val liveness_recovery_scan : 'a context -> unit
(** Scan all Dead keepers in [Keeper_registry].  For each Dead keeper whose
    root cause is recoverable and that has been dead for at least
    [MASC_KEEPER_LIVENESS_RECOVERY_MIN_DEAD_SEC], attempt to re-register and
    relaunch the keepalive fiber.  Uses exponential backoff per keeper and a
    per-keeper attempt budget.  Gated behind
    [MASC_KEEPER_LIVENESS_RECOVERY_ENABLED] (default: true).

    [credential_archived] is recoverable through the per-keeper credential
    self-heal path before relaunch. [zombie_timeout_reached] remains
    structural and is skipped.

    Emits [metric_keeper_liveness_recovery_attempts] and
    [metric_keeper_liveness_recovery_outcomes] Prometheus counters. *)

type credential_recovery_outcome =
  | Credential_recovery_not_needed
  | Credential_recovery_reissued of string
  | Credential_recovery_failed of string

val credential_recovery_before_restart_for_test :
  base_path:string ->
  Keeper_registry.registry_entry ->
  credential_recovery_outcome
(** Test hook for the credential self-heal step used before liveness recovery
    relaunches a [credential_archived] keeper. *)


(** {1 Alive-but-stuck detector (#12838)} *)

(** detect_alive_but_stuck moved to Keeper_supervisor_types (intra-library
    file split, 2026-05-16). Re-exported via include above. *)

val alive_but_stuck_scan : 'a context -> unit
(** Scan all keepers in [Keeper_registry].  For each keeper detected as
    alive-but-stuck, emit one [metric_keeper_alive_but_stuck] counter
    increment and write a single warn log line.  Per-keeper dedup keeps
    counter / wakeup emission at most once per
    [alive_but_stuck_dedup_ttl_sec] window.

    The recovery side effect is queued only when BOTH gates are on:
    - [MASC_KEEPER_ALIVE_BUT_STUCK_ENABLED] (default: true) — the
      detector itself; turning this off skips the entire scan, no
      counters or warns are emitted.
    - [MASC_KEEPER_ALIVE_BUT_STUCK_RECOVERY_ENABLED] (default: true)
      — the recovery action.  When this is off, the detector still
      runs (counter + warn), but no Event Layer wakeup or supervised
      restart request is queued; this lets operators observe the signal
      in production before turning on the side-effect.  When enabled,
      [alive_but_stuck_scan] enqueues an Event Layer stimulus and sets
      [failure_reason] plus [fiber_stop]/[fiber_wakeup] so the next
      sweep can route the keeper through the existing crash/restart
      path.  PR #13123. *)

val request_alive_but_stuck_recovery_for_test :
  base_path:string ->
  elapsed:float ->
  Keeper_registry.registry_entry ->
  unit
(** Test-only hook for the recovery request side effect used by
    [alive_but_stuck_scan]. *)

val alive_but_stuck_reset_for_test : unit -> unit
(** Test-only: clear the alive-but-stuck dedup table. *)
