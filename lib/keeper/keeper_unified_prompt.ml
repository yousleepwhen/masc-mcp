(** Keeper_unified_prompt — Build a single unified prompt from keeper identity
    and world observation.

    Sections removed in #6814: Available Tools (OAS tool schema handles),
    Recent Tool Activity, Last Cycle Outcome, Tool Diversity Signal,
    Peer Keepers, Your Recent Board Posts,
    Behavioral Self-Assessment, Actionable Routes, Signal Interpretation.
    Telemetry for removed sections is preserved via decision_audit and
    independent storage paths.

    @since Unified Keeper Loop *)

(** Format a list of (from_agent, content) mentions into a prompt section. *)
let format_mentions (mentions : (string * string) list) : string =
  String.concat "\n"
    (List.map
       (fun (from_agent, content) ->
         Printf.sprintf "- @%s: %s" from_agent
           (Keeper_types.short_preview ~max_len:200 content))
       mentions)

let scope_message_prompt_limit = 12
let scope_message_preview_len = 120

let take_last_with_omitted limit items =
  let len = List.length items in
  if len <= limit then (items, 0)
  else
    let rec drop n xs =
      if n <= 0 then xs
      else
        match xs with
        | [] -> []
        | _ :: rest -> drop (n - 1) rest
    in
    (drop (len - limit) items, len - limit)

(** Format active goals into a prompt section. *)
let format_goals (goal_ids : string list) : string =
  String.concat "\n"
    (List.map (fun gid -> Printf.sprintf "- %s" gid) goal_ids)

let format_scope_messages
    (messages : (string * string) list) : string =
  let shown_messages, omitted =
    take_last_with_omitted scope_message_prompt_limit messages
  in
  let omitted_line =
    if omitted <= 0 then []
    else
      [
        Printf.sprintf
          "- [omitted %d older scope messages; cursor still advances past the full batch]"
          omitted;
      ]
  in
  String.concat "\n"
    (omitted_line
     @ List.map
         (fun (from_agent, content) ->
           Printf.sprintf "- %s: %s"
             from_agent
             (Keeper_types.short_preview ~max_len:scope_message_preview_len content))
         shown_messages)

let format_board_events
    (events : Keeper_world_observation.pending_board_event list) : string =
  String.concat "\n"
    (List.map
       (fun (event : Keeper_world_observation.pending_board_event) ->
         let kind =
           match event.post_kind with
           | Board.Human_post -> "direct"
           | Board.Automation_post -> "automation"
           | Board.System_post -> "system"
         in
         let mention_note =
           if event.explicit_mention then
             let targets =
               match event.matched_targets with
               | [] -> "explicit mention"
               | xs -> "mentions " ^ String.concat ", " xs
             in
             " [" ^ targets ^ "]"
           else ""
         in
         let hearth_note =
           match event.hearth with
           | Some hearth when String.trim hearth <> "" -> " {" ^ hearth ^ "}"
           | _ -> ""
         in
         let self_note =
           if event.self_commented && event.new_external_since > 0 then
             Printf.sprintf " [%d new reply since yours%s]"
               event.new_external_since
               (match event.latest_external_author, event.latest_external_preview with
                | Some a, Some p -> Printf.sprintf ", latest by %s: %s" a p
                | _ -> "")
           else ""
         in
         Printf.sprintf "- [%s] post_id=%s title=%S author=%s%s%s%s preview: %s"
           kind
           event.post_id
           (Keeper_types.short_preview ~max_len:80 event.title)
           event.author
           hearth_note
           mention_note
           self_note
           event.preview)
       events)

let line_block label value =
  if value = "" then ""
  else Printf.sprintf "%s: %s\n" label value

let replace_all ~needle ~replacement input =
  let needle_len = String.length needle in
  if needle_len = 0 || input = "" then input
  else
    let input_len = String.length input in
    let buf = Buffer.create input_len in
    let rec loop pos =
      if pos >= input_len then ()
      else if
        pos + needle_len <= input_len
        && String.sub input pos needle_len = needle
      then (
        Buffer.add_string buf replacement;
        loop (pos + needle_len))
      else (
        Buffer.add_char buf input.[pos];
        loop (pos + 1))
    in
    loop 0;
    Buffer.contents buf

let is_tool_token_char = function
  | 'A' .. 'Z'
  | 'a' .. 'z'
  | '0' .. '9'
  | '_'
  | '-'
  | '*' ->
      true
  | _ -> false

let remove_tool_tokens_with_prefix ~prefix input =
  let prefix_len = String.length prefix in
  if prefix_len = 0 || input = "" then input
  else
    let input_len = String.length input in
    let buf = Buffer.create input_len in
    let rec skip_token pos =
      if pos < input_len && is_tool_token_char input.[pos]
      then skip_token (pos + 1)
      else pos
    in
    let rec loop pos =
      if pos >= input_len then ()
      else if
        pos + prefix_len <= input_len
        && String.sub input pos prefix_len = prefix
        && (pos = 0 || not (is_tool_token_char input.[pos - 1]))
      then loop (skip_token (pos + prefix_len))
      else (
        Buffer.add_char buf input.[pos];
        loop (pos + 1))
    in
    loop 0;
    Buffer.contents buf

let remove_standalone_tool_token ~token input =
  let token_len = String.length token in
  if token_len = 0 || input = "" then input
  else
    let input_len = String.length input in
    let buf = Buffer.create input_len in
    let rec loop pos =
      if pos >= input_len then ()
      else if
        pos + token_len <= input_len
        && String.sub input pos token_len = token
        && (pos = 0 || not (is_tool_token_char input.[pos - 1]))
        &&
        (let after = pos + token_len in
         after >= input_len || not (is_tool_token_char input.[after]))
      then loop (pos + token_len)
      else (
        Buffer.add_char buf input.[pos];
        loop (pos + 1))
    in
    loop 0;
    Buffer.contents buf

let sanitize_retired_tool_names text =
  let retired_prefix left right = left ^ "_" ^ right in
  let old_command_shape =
    retired_prefix "keeper" "bash_command_shape_blocked"
  in
  text
  |> replace_all ~needle:old_command_shape ~replacement:"execute_command_shape_blocked"
  |> remove_tool_tokens_with_prefix ~prefix:(retired_prefix "keeper" "bash")
  |> remove_tool_tokens_with_prefix ~prefix:(retired_prefix "keeper" "shell")
  |> remove_tool_tokens_with_prefix ~prefix:(retired_prefix "keeper" "fs")
  |> remove_standalone_tool_token ~token:("B" ^ "ash")
  |> remove_standalone_tool_token ~token:("G" ^ "rep")
  |> remove_tool_tokens_with_prefix ~prefix:(retired_prefix "masc" "code")
  |> remove_tool_tokens_with_prefix ~prefix:(retired_prefix "Masc" "code")
  |> remove_tool_tokens_with_prefix ~prefix:(retired_prefix "keeper" "pr")
  |> remove_tool_tokens_with_prefix ~prefix:(retired_prefix "keeper" "preflight_check")
  |> remove_tool_tokens_with_prefix
       ~prefix:(retired_prefix "keeper" "task_submit_for_verification")
  |> remove_tool_tokens_with_prefix ~prefix:(retired_prefix "keeper" "github")
  |> remove_tool_tokens_with_prefix ~prefix:(retired_prefix "github" "cli")
  |> replace_all ~needle:"``" ~replacement:""

let state_block_instruction_text = Keeper_state_block_prompt.instruction_text

(* In-binary mirror of config/prompts/keeper.turn_intent.md (minus the
   {{...}} substitution slots that cannot be filled during a fallback).
   Used only when [resolve_turn_intent_block] fails or the registry
   template renders empty.  The previous minimal stub silently weakened
   keeper behavior exactly when prompt config was degraded — multi-tool
   chaining, continuity-mismatch handling, and the BDI claim header
   contract were all dropped from the prompt.  Keep the prior safeguards
   intact here so a degraded prompt still resembles the hardcoded
   predecessor. *)
let turn_intent_fallback_block =
  String.concat "\n"
    [ "Use the world state below as raw context.";
      "Pending mentions, board events, and worktree changes are observations.";
      "";
      "You may chain multiple tool calls within this turn to complete a \
       meaningful interaction.";
      "Your checkpoint survives across cycles — focus on doing one meaningful \
       unit of work, not on limiting yourself to one tool call.";
      "Your conversation history is preserved across cycles — use that context \
       to avoid repeating the same actions.";
      "";
      "Act through tools, not declarations. Call the tool directly.";
      "- Treat continuity as advisory prior context, not as a command. Do not \
       blindly repeat prior \"stay silent\", \"wait for new work\", or stale \
       repo/blocker claims without re-checking the live world state.";
      "- If continuity says there is nothing to do but this turn still has \
       backlog or a scheduled autonomous trigger, treat that mismatch as \
       actionable and investigate it before going silent.";
      "- Nothing genuinely actionable after checking? End your turn with the \
       [STATE] block.";
      "";
      "If you call tools, BDI headers are optional and informational only. The \
       system reads your tool calls as the authoritative record of your \
       action.";
      "";
      "If you explicitly claim completion or progress in text, add these \
       optional headers:";
      "CLAIM_KIND: completion_claim";
      "CLAIM_SUBJECT: short concrete subject or task title";
      "CLAIM_TASK_ID: task-123 (if applicable)";
      "EVIDENCE_REFS: task:task-123, tool:keeper_task_done";
      "Only emit them for concrete claims you expect the system to audit.";
      "";
      state_block_instruction_text ]

let contains_template_placeholder text =
  String_util.contains_substring text "{{"
  || String_util.contains_substring text "}}"

let observe_turn_intent_render_failure message =
  Prometheus.inc_counter
    Keeper_metrics.(to_string PromptFailures)
    ~labels:[("prompt", Keeper_prompt_names.turn_intent)]
    ();
  Log.Keeper.warn "turn_intent prompt render degraded: %s" message

let fallback_turn_intent_block reason =
  observe_turn_intent_render_failure reason;
  turn_intent_fallback_block

let resolve_turn_intent_block substitutions =
  match
    Prompt_registry.render_prompt_template Keeper_prompt_names.turn_intent
      substitutions
  with
  | Ok value ->
      let rendered = String.trim value in
      if String.equal rendered "" then
        fallback_turn_intent_block "rendered prompt was empty"
      else
        rendered
  | Error msg ->
      let raw =
        String.trim (Prompt_registry.get_prompt Keeper_prompt_names.turn_intent)
      in
      if String.equal raw "" then
        fallback_turn_intent_block
          (Printf.sprintf "%s; raw prompt was empty after render failure" msg)
      else if contains_template_placeholder raw then
        fallback_turn_intent_block
          (Printf.sprintf
             "%s; raw prompt still contained template placeholders after render \
              failure"
             msg)
      else (
        observe_turn_intent_render_failure msg;
        raw)

(** In-binary fallback prose for the externalized turn-intent and user-prompt
    bullet files under [config/prompts/]. Used only when the registry returns
    an empty body (missing file, frontmatter-only file, or markdown_dir not
    set in tests). The in-binary copy is kept byte-for-byte equal to the
    canonical markdown body (post-trim, single trailing newline injected by
    [load_externalized_bullet]) so that a degraded prompt config still emits
    the same guidance text. Edits should land in both places to keep them
    aligned with the keeper.* markdown files. *)
let fallback_externalized_bullet key =
  if String.equal key Keeper_prompt_names.turn_intent_claim_guidance_a then
    Some
      "- Claimable backlog is visible and you do not already hold a task. \
       `keeper_task_claim {}` is available, not mandatory: claim only when \
       the work fits your current goal, persona, and capacity. Use \
       `keeper_tasks_list` when you need to inspect backlog state before \
       deciding."
  else if String.equal key Keeper_prompt_names.turn_intent_claim_guidance_b then
    Some
      "- Repo and forge inspection is observation, not progress by itself. \
       If you decide to do code-changing task work, claim first, then use \
       only the visible file, edit, and Execute tools from the repo \
       worktree. Do not invent hidden shell or forge tools when they are \
       not listed."
  else if String.equal key Keeper_prompt_names.turn_intent_board_activity_guidance then
    Some
      "- See board activity? Use the listed post_id. If the preview is \
       enough, comment directly with keeper_board_comment. If you need the \
       full post, call keeper_board_get and keeper_board_comment in the same \
       response; keeper_board_get alone is passive and fails actionable turns."
  else if String.equal key Keeper_prompt_names.turn_intent_board_post_guidance then
    Some
      "- Have a substantive finding or update? Call keeper_board_post with \
       concrete content. If you have nothing specific to share this turn, \
       skip the post entirely — do NOT emit placeholder strings like \
       \"empty\", \"ok\", \"nothing\", or \"n/a\". An absent post is \
       preferable to a content-less one (other keepers waste turns \
       responding to noise)."
  else if String.equal key Keeper_prompt_names.turn_intent_board_curation_guidance then
    Some
      "- See enough board activity to summarize or route? Call \
       keeper_board_curation_submit with a concise snapshot."
  else if String.equal key Keeper_prompt_names.turn_intent_broadcast_guidance then
    Some "- Need to share broadly? Call keeper_broadcast."
  else if String.equal key Keeper_prompt_names.immediate_task_move then
    Some
      "- Claimable backlog exists. `keeper_task_claim {}` may claim the next \
       eligible unclaimed task, but this is an intake option rather than a \
       required move.\n\
       - Use keeper_tasks_list to inspect backlog state, diagnose missing \
       work, or verify task lifecycle before deciding. Never substitute \
       Execute probes (ls/cat/find against .masc/, backlog.json, or \
       repo-local task files) for keeper_tasks_list; the runtime blocks \
       those with `task_state_file_probe_blocked`.\n\
       - Prefer the strongest live signal: pending mention, board activity, \
       active goal, or submitted verification evidence may be better than \
       claiming unrelated work.\n\
       - If you choose to take code-changing task work, claim first and then \
       work through the visible file, edit, and Execute tools from the repo \
       worktree. Create or update a forge PR only after the branch is \
       prepared and the task requires it."
  else None

(** Load a turn-intent or user-prompt bullet from [config/prompts/].
    Returns the body with a single trailing newline so multiple bullets
    concatenate cleanly. Returns [""] when the key is toggled off; the
    toggle is supplied by the caller. *)
let load_externalized_bullet ~enabled key =
  if not enabled then ""
  else
    let trimmed =
      String.trim (Prompt_registry.get_prompt key)
    in
    if String.equal trimmed "" then
      match fallback_externalized_bullet key with
      | Some prose ->
          Prometheus.inc_counter
            Keeper_metrics.(to_string PromptFailures)
            ~labels:[("prompt", key)]
            ();
          Log.Keeper.warn
            "externalized prompt '%s' resolved empty; using in-binary fallback"
            key;
          prose ^ "\n"
      | None ->
          Log.Keeper.warn
            "externalized prompt '%s' resolved empty and no fallback registered"
            key;
          ""
    else trimmed ^ "\n"

let autonomous_trigger_lines
    ~(decision : Keeper_world_observation.keeper_cycle_decision)
    ~(observation : Keeper_world_observation.world_observation) : string list =
  match decision.channel, decision.should_run with
  | Keeper_world_observation.Scheduled_autonomous, true ->
      let lines =
        [
          Some "- Scheduler: scheduled autonomous keepalive turn.";
          (match Keeper_world_observation.verdict_reasons_to_strings decision.verdict with
           | [] -> None
           | reasons ->
               Some
                 (Printf.sprintf "- Reasons: %s"
                    (String.concat ", " reasons)));
          (match decision.idle_gate_sec with
           | Some idle_gate ->
               Some
                 (Printf.sprintf "- Idle gate: %ds (current idle: %ds)"
                    idle_gate observation.idle_seconds)
           | None -> None);
          (match decision.since_last_scheduled_autonomous, decision.effective_cooldown with
           | Some since_last, Some cooldown ->
               Some
                 (Printf.sprintf
                    "- Since last autonomous turn: %ds, effective cooldown: %ds"
                    since_last cooldown)
           | _ -> None);
          (match decision.task_reactive_cooldown with
           | Some cooldown
             when observation.claimable_task_count > 0
                  || observation.failed_task_count > 0 ->
               Some
                 (Printf.sprintf
                    "- Backlog acceleration cooldown: %ds for claimable/failed tasks"
                    cooldown)
           | _ -> None);
        ]
      in
      List.filter_map Fun.id lines
  | _ -> []

let build_prompt ~(meta : Keeper_types.keeper_meta) ~(base_path : string)
    ?(profile_defaults : Keeper_types_profile.keeper_profile_defaults option)
    ~(observation : Keeper_world_observation.world_observation)
    () : string * string
    =
  ignore base_path;
  let will, needs, desires, instructions =
    match profile_defaults with
    | Some d ->
        ( Option.value d.will ~default:meta.will
        , Option.value d.needs ~default:meta.needs
        , Option.value d.desires ~default:meta.desires
        , Option.value d.instructions ~default:meta.instructions )
    | None -> (meta.will, meta.needs, meta.desires, meta.instructions)
  in
  let trait_lines =
    String.concat ""
      [

        line_block "Will" will;
        line_block "Needs" needs;
        line_block "Desires" desires;
      ]
  in
  let instructions_block =
    if instructions = "" then ""
    else Printf.sprintf "\nInstructions:\n%s\n" instructions
  in
  let goal_lines =
    String.concat ""
      [
        line_block "Primary goal" meta.goal;
        (if meta.short_goal <> "" && meta.short_goal <> meta.goal then
           line_block "Short-term goal" meta.short_goal
         else "");
        (if meta.mid_goal <> "" && meta.mid_goal <> meta.goal then
           line_block "Mid-term goal" meta.mid_goal
         else "");
        (if meta.long_goal <> "" && meta.long_goal <> meta.goal then
           line_block "Long-term goal" meta.long_goal
         else "");
      ]
  in
  let base_system_prompt =
    match
      Prompt_registry.render_prompt_template Keeper_prompt_names.unified_system
        [
          ("identity_header", Printf.sprintf "You are %s, a keeper agent." meta.name);
          ("trait_lines", trait_lines);
          ("instructions_block", instructions_block);
          ("goal_lines", goal_lines);
        ]
    with
    | Ok value -> value
    | Error _ -> Prompt_registry.get_prompt Keeper_prompt_names.unified_system
  in
  let allowed_tool_names = Keeper_tool_policy.keeper_allowed_tool_names meta in
  let tool_allowed name = List.mem name allowed_tool_names in
  let claim_tool_available = tool_allowed "keeper_task_claim" in
  let show_claim_guidance =
    observation.claimable_task_count > 0
    && observation.provider_capacity_blocked_task_count = 0
    && claim_tool_available
    && not meta.paused
    && Option.is_none meta.current_task_id
  in
  (* Turn intent body and each conditional guidance bullet live as markdown
     under config/prompts/. The OCaml side only computes the boolean toggle
     for each bullet and loads the prose via Prompt_registry; the prose
     itself (and any future edits) stay in the markdown files alongside the
     other keeper prompts. See lib/keeper/keeper_prompt_names.ml for the
     key set and fallback_externalized_bullet above for in-binary fallbacks. *)
  let board_activity_guidance =
    load_externalized_bullet
      ~enabled:(tool_allowed "keeper_board_get"
                && tool_allowed "keeper_board_comment")
      Keeper_prompt_names.turn_intent_board_activity_guidance
  in
  let board_post_guidance =
    load_externalized_bullet
      ~enabled:(tool_allowed "keeper_board_post")
      Keeper_prompt_names.turn_intent_board_post_guidance
  in
  let board_curation_guidance =
    load_externalized_bullet
      ~enabled:(tool_allowed "keeper_board_curation_submit")
      Keeper_prompt_names.turn_intent_board_curation_guidance
  in
  let broadcast_guidance =
    load_externalized_bullet
      ~enabled:(tool_allowed "keeper_broadcast")
      Keeper_prompt_names.turn_intent_broadcast_guidance
  in
  let claim_guidance_a =
    load_externalized_bullet
      ~enabled:show_claim_guidance
      Keeper_prompt_names.turn_intent_claim_guidance_a
  in
  let claim_guidance_b =
    load_externalized_bullet
      ~enabled:show_claim_guidance
      Keeper_prompt_names.turn_intent_claim_guidance_b
  in
  let turn_intent_substitutions =
    [
      ("board_activity_guidance", board_activity_guidance);
      ("claim_guidance_a", claim_guidance_a);
      ("claim_guidance_b", claim_guidance_b);
      ("board_post_guidance", board_post_guidance);
      ("board_curation_guidance", board_curation_guidance);
      ("broadcast_guidance", broadcast_guidance);
      ("state_block_instruction", state_block_instruction_text);
    ]
  in
  let turn_intent_block =
    resolve_turn_intent_block turn_intent_substitutions
  in
  let system_prompt =
    Printf.sprintf "%s\n\n## Turn Intent\n%s" base_system_prompt turn_intent_block
  in
  (* User message: structured world observation — reactive triggers + resource state only.
     Metacognition sections (tool activity, cycle outcome, diversity, behavioral stats)
     removed in #6814; telemetry preserved in decision_audit and independent paths. *)
  let ubuf = Buffer.create 1024 in
  Buffer.add_string ubuf "## Current World State\n\n";
  (* Prefix-cache ordering: emit larger, more stable sections first so
     providers can reuse a longer shared prefix across cycles. Highly
     volatile reactive signals stay later in the same user message. *)
  (* 1. Active goals — stable turn context *)
  if observation.active_goals <> [] then (
    Buffer.add_string ubuf
      (Printf.sprintf "### Active Goals (%d)\n"
         (List.length observation.active_goals));
    Buffer.add_string ubuf (format_goals observation.active_goals);
    Buffer.add_string ubuf "\n\n");
  (* 2. Namespace state — usually lower churn than inbox/board detail *)
  if
    observation.unclaimed_task_count > 0
    || observation.claimable_task_count > 0
    || observation.provider_capacity_blocked_task_count > 0
    || observation.failed_task_count > 0
    || observation.active_agent_count > 0
  then (
    Buffer.add_string ubuf "### Namespace State\n";
    if observation.unclaimed_task_count > 0 then
      Buffer.add_string ubuf
        (Printf.sprintf "- Unclaimed tasks: %d\n"
           observation.unclaimed_task_count);
    if observation.claimable_task_count > 0 then
      Buffer.add_string ubuf
        (Printf.sprintf "- Claimable tasks for this keeper: %d\n"
           observation.claimable_task_count);
    if observation.unclaimed_task_count > 0
       && observation.claimable_task_count = 0
    then
      Buffer.add_string ubuf
        "- Claimable tasks for this keeper: 0\n";
    let keeper_or_scope_blocked =
      max 0
        (observation.unclaimed_task_count
         - observation.claimable_task_count)
    in
    if keeper_or_scope_blocked > 0 then
      Buffer.add_string ubuf
        (Printf.sprintf
           "- Blocked by keeper/tool/goal scope: %d\n"
           keeper_or_scope_blocked);
    if observation.provider_capacity_blocked_task_count > 0 then
      Buffer.add_string ubuf
        (Printf.sprintf
           "- Provider-capacity blocked claimable tasks: %d\n"
           observation.provider_capacity_blocked_task_count);
    if observation.failed_task_count > 0 then
      Buffer.add_string ubuf
        (Printf.sprintf "- Failed tasks: %d\n" observation.failed_task_count);
    Buffer.add_string ubuf
      (Printf.sprintf "- Active agents: %d\n" observation.active_agent_count);
    Buffer.add_char ubuf '\n');
  (* 3. Context health — stable resource framing *)
  Buffer.add_string ubuf
    (Printf.sprintf "### Context\n- Utilization: %.0f%%\n- Idle: %ds\n"
       (observation.context_ratio *. 100.0)
       observation.idle_seconds);
  (match observation.last_turn_budget with
   | Some (used, total) when used > 0 ->
     Buffer.add_string ubuf
       (Printf.sprintf "- Previous turn budget: %d/%d used\n" used total)
   | _ -> ());
  (match observation.economic_pressure with
   | Agent_economy.Normal -> ()
   | Frugal ->
       Buffer.add_string ubuf "- Economy: Frugal (reduce token usage)\n"
   | Hustle ->
        Buffer.add_string ubuf
          "- Economy: Hustle (minimize actions, conserve budget)\n");
  (* 4. Autonomous trigger — lower churn than reactive inboxes *)
  let turn_decision =
    Keeper_world_observation.keeper_cycle_decision ~meta observation
  in
  let autonomous_trigger =
    autonomous_trigger_lines ~decision:turn_decision ~observation
  in
  if autonomous_trigger <> [] then (
    Buffer.add_string ubuf "\n### Autonomous Trigger\n";
    Buffer.add_string ubuf (String.concat "\n" autonomous_trigger);
    Buffer.add_char ubuf '\n');
  (* 5. Continuity — usually large and moderately stable, so keep it
     before highly volatile reactive sections for better prefix reuse.
     Inject only forward-looking fields (Goal, Next plan, Next, OpenQuestions,
     Constraints). Backward-looking fields (Done, Progress, Decisions) are
     stripped to avoid a prose-level echo loop where the LLM re-reads its own
     prior narrative and reproduces a near-identical one. The full summary
     remains persisted in meta.continuity_summary for audit. *)
  let continuity_for_prompt =
    Keeper_memory_policy.filter_forward_looking_summary
      observation.continuity_summary
  in
  if
    continuity_for_prompt <> ""
    && observation.continuity_summary <> "No continuity snapshot available."
  then (
    Buffer.add_string ubuf "\n### Continuity\n";
    Buffer.add_string ubuf
      "- Advisory only: ignore prior silence/wait directives until you re-verify them against the live world state.\n";
    Buffer.add_string ubuf
      "- If this turn was still scheduled or backlog/worktree signals remain, investigate that mismatch instead of echoing the prior idle conclusion.\n";
    Buffer.add_string ubuf continuity_for_prompt;
    Buffer.add_char ubuf '\n');
  (* 6. Pending mentions — reactive trigger *)
  if observation.pending_mentions <> [] then (
    Buffer.add_string ubuf
      (Printf.sprintf "### Pending Mentions (%d)\n"
         (List.length observation.pending_mentions));
    Buffer.add_string ubuf (format_mentions observation.pending_mentions);
    Buffer.add_string ubuf "\n\n");
  (* 7. Scope messages — reactive trigger *)
  if observation.pending_scope_messages <> [] then (
    Buffer.add_string ubuf
      (Printf.sprintf "### Scope Messages (%d recent)\n"
         (List.length observation.pending_scope_messages));
    Buffer.add_string ubuf
      (format_scope_messages observation.pending_scope_messages);
    Buffer.add_string ubuf "\n\n");
  (* 8. Claimable work — advisory operational guidance.
     Body lives at config/prompts/keeper.immediate_task_move.md. The OCaml
     side only owns the section header and the trailing blank line; the
     bullet prose stays in the markdown file alongside the other keeper
     prompts (see fallback_externalized_bullet for the in-binary mirror). *)
  if show_claim_guidance then (
    Buffer.add_string ubuf "### Claimable Work\n";
    Buffer.add_string ubuf
      (load_externalized_bullet
         ~enabled:true
         Keeper_prompt_names.immediate_task_move);
    Buffer.add_char ubuf '\n');
  (* 9. Board activity — reactive trigger *)
  if observation.pending_board_events <> [] then (
    Buffer.add_string ubuf
      (Printf.sprintf "### Board Activity (%d new)\n"
         (List.length observation.pending_board_events));
    Buffer.add_string ubuf (format_board_events observation.pending_board_events);
    if
      tool_allowed "keeper_board_curation_submit"
      && List.length observation.pending_board_events >= 2
    then
      Buffer.add_string ubuf
        "\n- Curation due: after reading enough context, call keeper_board_curation_submit with a concise snapshot for this board window.";
    Buffer.add_string ubuf "\n\n");
  let user_message =
    Buffer.contents ubuf
  in
  ( sanitize_retired_tool_names system_prompt,
    sanitize_retired_tool_names user_message )
