(** Shell_ir — subset AST produced by the Menhir bash grammar (A1).

    The arm set is closed.  Anything outside the subset (heredoc, [$()]
    expansion, subshell, control flow, logic operators, function def,
    glob/brace expansion, backgrounding) is rejected at parse time as
    [Parsed.Too_complex _]. *)

type arg_meta = {
  quoted : bool;
  glob : bool;
  escaped : bool;
}

val default_meta : arg_meta

type arg =
  | Lit of string * arg_meta      (** single- or double-quoted literal *)
  | Concat of arg list            (** adjacent arg pieces: [foo"bar"$X] *)
  | Var of string * arg_meta      (** [$HOME], [${VAR}], [${VAR:-default}] *)

type simple = {
  bin : Exec_program.t;
  args : arg list;
  env : (string * arg) list;      (** [FOO=bar] env prefix on the command *)
  cwd : Path_scope.t option;
  redirects : Redirect_scope.t list;
  sandbox : Sandbox_target.t;
  (** Dispatch target — defaults to [Sandbox_target.host ()].  Keeper
      callers override with a Docker runner closure built from
      [Keeper_turn_sandbox_runtime]. *)
}

type t =
  | Simple of simple
  | Pipeline of t list            (** length >= 2 — head | middle* | tail *)

val pp : Format.formatter -> t -> unit
