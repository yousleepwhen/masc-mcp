
open Types
open Server_utils
open Server_auth

type dashboard_compute_mode =
  | Inline_shared
  | Offloaded_readonly

type cached_surface = {
  mutable json : Yojson.Safe.t;
  mutable last_success_at : string option;
  mutable last_success_unix : float option;
  mutable last_attempt_at : string option;
  mutable last_attempt_unix : float option;
  mutable last_error : string option;
  mutable last_error_at : string option;
  mutable last_error_unix : float option;
}

let create_cached_surface json =
  {
    json;
    last_success_at = None;
    last_success_unix = None;
    last_attempt_at = None;
    last_attempt_unix = None;
    last_error = None;
    last_error_at = None;
    last_error_unix = None;
  }

let now_cache_stamp () =
  let ts = Unix.gettimeofday () in
  (ts, Types.now_iso ())

let json_of_string_option = function
  | Some value -> `String value
  | None -> `Null

let mark_cached_surface_attempt surface =
  let ts, iso = now_cache_stamp () in
  surface.last_attempt_unix <- Some ts;
  surface.last_attempt_at <- Some iso

let mark_cached_surface_success surface json =
  let ts, iso = now_cache_stamp () in
  surface.json <- json;
  surface.last_success_unix <- Some ts;
  surface.last_success_at <- Some iso;
  surface.last_error <- None;
  surface.last_error_at <- None;
  surface.last_error_unix <- None

let mark_cached_surface_error surface exn =
  let ts, iso = now_cache_stamp () in
  surface.last_error <- Some (Printexc.to_string exn);
  surface.last_error_at <- Some iso;
  surface.last_error_unix <- Some ts

let upsert_assoc_field key value fields =
  (key, value) :: List.remove_assoc key fields

let extend_projection_diagnostics json extra_fields =
  match json with
  | `Assoc fields ->
      let existing =
        match List.assoc_opt "projection_diagnostics" fields with
        | Some (`Assoc diagnostics) -> diagnostics
        | _ -> []
      in
      let merged =
        List.fold_left
          (fun acc (key, value) -> upsert_assoc_field key value acc)
          existing extra_fields
      in
      `Assoc
        (upsert_assoc_field "projection_diagnostics" (`Assoc merged)
           (List.remove_assoc "projection_diagnostics" fields))
  | other -> other

let cached_surface_json surface =
  let now_ts = Unix.gettimeofday () in
  let cache_state, stale_reason, stale_age_ms =
    match surface.last_success_unix, surface.last_error_unix with
    | None, _ -> ("initializing", surface.last_error, None)
    | Some success_ts, Some error_ts when error_ts > success_ts ->
        ( "stale",
          surface.last_error,
          Some (int_of_float ((now_ts -. success_ts) *. 1000.0)) )
    | Some _, _ -> ("fresh", None, None)
  in
  extend_projection_diagnostics surface.json
    [
      ("cache_state", `String cache_state);
      ("last_success_at", json_of_string_option surface.last_success_at);
      ("last_attempt_at", json_of_string_option surface.last_attempt_at);
      ("last_error_at", json_of_string_option surface.last_error_at);
      ("stale_reason", json_of_string_option stale_reason);
      ( "stale_age_ms",
        match stale_age_ms with
        | Some value -> `Int value
        | None -> `Null );
    ]

(** Executor pool for CPU-heavy dashboard compute.  Parameterized dashboard
    requests can otherwise monopolize the main Eio domain long enough to
    starve unrelated MCP tool calls. *)
let _executor_pool : Eio.Executor_pool.t option ref = ref None

let set_executor_pool pool = _executor_pool := Some pool

let run_dashboard_compute ?(mode = Offloaded_readonly) ~sw ~clock
    ~(config : Room.config) compute =
  let fallback () = compute ~config ~sw in
  let run_in_pool pool_sw =
    match config.backend_config.Backend.backend_type with
    | Backend.PostgresNative ->
        let net = Eio_context.get_net () in
        let mono_clock = Eio_context.get_mono_clock () in
        (match
           Room_utils_backend_setup.with_domain_local_pg_backend
             ~sw:pool_sw ~net ~clock ~mono_clock config
         with
         | Some domain_config -> `Done (compute ~config:domain_config ~sw:pool_sw)
         | None -> `Fallback)
    | Backend.Memory | Backend.FileSystem ->
        `Done (compute ~config ~sw:pool_sw)
  in
  let offloaded () =
    match !_executor_pool with
    | Some pool ->
        (try
           match
             Eio.Executor_pool.submit_exn pool ~weight:1.0 (fun () ->
               Eio.Switch.run run_in_pool)
           with
           | `Done value -> value
           | `Fallback ->
               Log.Dashboard.warn
                 "dashboard offload fallback: domain-local backend unavailable";
               fallback ()
         with
         | Eio.Cancel.Cancelled _ as e -> raise e
         | exn ->
             Log.Dashboard.warn "dashboard offload failed, using inline compute: %s"
               (Printexc.to_string exn);
             fallback ())
    | None -> fallback ()
  in
  match mode with
  | Inline_shared -> fallback ()
  | Offloaded_readonly -> offloaded ()

(* ================================================================ *)
(* Dashboard Data (Batch API)                                       *)
(* ================================================================ *)

include Dashboard_http_helpers
include Dashboard_http_monitoring
include Dashboard_http_keeper
include Dashboard_http_mdal

(** Wrap a dashboard computation with a 30-second timeout.
    Returns a partial-response JSON on timeout instead of hanging. *)
let with_dashboard_timeout ~clock compute =
  match Eio.Time.with_timeout clock 30.0 (fun () -> Ok (compute ())) with
  | Ok v -> v
  | Error `Timeout ->
      `Assoc [
        ("error", `String "timeout");
        ("partial", `Bool true);
        ("message", `String "Dashboard computation timed out after 30s. First request may be slow due to filesystem scan.");
        ("generated_at", `String (Types.now_iso ()));
      ]

let dashboard_active_or_recent_sessions config =
  let cutoff_unix = Time_compat.now () -. 86400.0 in
  let cutoff_iso = Dashboard_utils.iso_of_unix cutoff_unix in
  Team_session_store.list_sessions ~since_unix:cutoff_unix config
  |> List.filter (fun (session : Team_session_types.session) ->
         match session.status with
         | Running | Paused -> true
         | _ -> session.updated_at_iso >= cutoff_iso)

let attach_projection_diagnostics json diagnostics =
  match json with
  | `Assoc fields -> `Assoc (("projection_diagnostics", diagnostics) :: fields)
  | other -> other

let projection_diagnostics_json ~surface ~started_at ~extra json =
  let build_ms = int_of_float ((Unix.gettimeofday () -. started_at) *. 1000.0) in
  let payload_bytes = String.length (Yojson.Safe.to_string json) in
  `Assoc
    ([
       ("surface", `String surface);
       ("build_ms", `Int build_ms);
       ("payload_bytes", `Int payload_bytes);
       ("generated_at", `String (Types.now_iso ()));
     ]
    @ extra)

let with_projection_diagnostics ~surface ~started_at ~extra json =
  attach_projection_diagnostics json
    (projection_diagnostics_json ~surface ~started_at ~extra json)

let initialized_json_opt = function
  | `Assoc fields as json -> (
      match List.assoc_opt "status" fields with
      | Some (`String "initializing") -> None
      | _ -> Some json)
  | _ -> None

let command_plane_summary_cache_parts ~state =
  match
    Server_command_plane_http_support.command_plane_summary_http_json ~state
    |> initialized_json_opt
  with
  | Some (`Assoc fields) ->
      let swarm_status =
        match List.assoc_opt "swarm_status" fields with
        | Some (`Assoc _ as json) -> Some json
        | _ -> None
      in
      (Some (`Assoc (List.remove_assoc "swarm_status" fields)), swarm_status)
  | _ -> (None, None)
let dashboard_semantics_http_json () =
  Dashboard_semantics.json ()

let dashboard_batch_json ?(compact = false) (config : Room.config) : Yojson.Safe.t =
  let room_state = Room.read_state config in
  let tempo = Tempo.get_tempo config in
  (* M-17 fix: use room-scoped queries consistent with compact/shell dashboard *)
  let room_id = Room.current_room_id config in
  let tasks = Room.get_tasks_raw_in_room config room_id in
  let agents = Room.get_agents_raw_in_room config room_id in
  let msgs = Room.get_messages_raw_in_room config ~room_id ~since_seq:0 ~limit:20 in
  let now_ts = Time_compat.now () in
  let (board_monitor_json, board_contract_ok) = board_monitoring_json ~now_ts in
  let (governance_monitor_json, governance_feed_ok) =
    governance_monitoring_json ~now_ts ~base_path:config.base_path
  in

  let proactive_fallback_warn =
    float_of_env_default
      "MASC_DASHBOARD_PROACTIVE_FALLBACK_WARN"
      ~default:0.20
      ~min_v:0.0
      ~max_v:1.0
  in
  let proactive_fallback_bad =
    float_of_env_default
      "MASC_DASHBOARD_PROACTIVE_FALLBACK_BAD"
      ~default:0.40
      ~min_v:0.0
      ~max_v:1.0
  in
  let proactive_similarity_warn =
    float_of_env_default
      "MASC_DASHBOARD_PROACTIVE_SIMILARITY_WARN"
      ~default:0.90
      ~min_v:0.0
      ~max_v:1.0
  in
  let proactive_similarity_bad =
    float_of_env_default
      "MASC_DASHBOARD_PROACTIVE_SIMILARITY_BAD"
      ~default:0.97
      ~min_v:0.0
      ~max_v:1.0
  in
  let alert_toast_cooldown_sec =
    int_of_env_default
      "MASC_DASHBOARD_ALERT_TOAST_COOLDOWN_SEC"
      ~default:300
      ~min_v:10
      ~max_v:86400
  in
  let status_json =
    `Assoc [
      ( "room",
        `String
          (if Room.is_initialized config then Room.current_room_id config
           else Filename.basename config.base_path) );
      ("room_base_path", `String config.base_path);
      ("cluster", `String (Option.value ~default:"unknown" (Sys.getenv_opt "MASC_CLUSTER_NAME")));
      ("project", `String room_state.project);
      ("tempo_interval_s", `Float tempo.current_interval_s);
      ("paused", `Bool room_state.paused);
      ("tool_call_health", tool_call_health_json config);
      ("alert_thresholds", `Assoc [
        ("proactive_fallback_warn", `Float proactive_fallback_warn);
        ("proactive_fallback_bad", `Float (max proactive_fallback_warn proactive_fallback_bad));
        ("proactive_similarity_warn", `Float proactive_similarity_warn);
        ("proactive_similarity_bad", `Float (max proactive_similarity_warn proactive_similarity_bad));
        ("toast_cooldown_sec", `Int alert_toast_cooldown_sec);
      ]);
      ("monitoring", `Assoc [
        ("board", board_monitor_json);
        ("governance", governance_monitor_json);
      ]);
      ("data_quality", `Assoc [
        ("board_contract_ok", `Bool board_contract_ok);
        ("governance_feed_ok", `Bool governance_feed_ok);
        ("last_sync_at", `String (Types.now_iso ()));
      ]);
    ]
  in
  let tasks_json =
    List.map (fun (t : Types.task) ->
      `Assoc [
        ("id", `String t.id);
        ("title", `String t.title);
        ("status", `String (Types.string_of_task_status t.task_status));
        ("priority", `Int t.priority);
        ("assignee",
         match t.task_status with
         | Claimed { assignee; _ } | InProgress { assignee; _ } | Done { assignee; _ } ->
             `String assignee
         | _ -> `Null);
      ]
    )
      (List.filter
         (fun (t : Types.task) ->
           match t.task_status with
           | Types.Cancelled _ -> false
           | Types.Done _ -> not compact
           | _ -> true)
         tasks)
  in
  let agents_json =
    List.map (fun (a : Types.agent) ->
      let profile = Dashboard_execution_helpers.get_agent_profile a.name in
      `Assoc [
        ("name", `String a.name);
        ("status", `String (Types.string_of_agent_status a.status));
        ("current_task", match a.current_task with Some t -> `String t | None -> `Null);
        ("last_seen", `String a.last_seen);
        ("emoji", `String profile.emoji);
        ("koreanName", `String profile.korean_name);
        ("model", match profile.model with Some m -> `String m | None -> `Null);
        ("traits", `List (List.map (fun t -> `String t) profile.traits));
        ("interests", `List (List.map (fun i -> `String i) profile.interests));
        ("activityLevel", match profile.activity_level with Some v -> `Float v | None -> `Null);
        ("primaryValue", match profile.primary_value with Some v -> `String v | None -> `Null);
        ("generation", `Null);
        ("context_ratio", `Null);
        ("turn_count", `Null);
      ]
    ) agents
  in
  let msgs_json =
    List.map
      (fun (m : Types.message) ->
        `Assoc [
          ("from", `String m.from_agent);
          ("content", `String m.content);
          ("timestamp", `String m.timestamp);
          ("seq", `Int m.seq);
        ])
      (List.filteri (fun idx _ -> idx < 20) msgs)
  in
  `Assoc [
    ("status", status_json);
    ("tasks", `Assoc [ ("tasks", `List tasks_json); ("total", `Int (List.length tasks_json)) ]);
    ("agents", `Assoc [ ("agents", `List agents_json); ("total", `Int (List.length agents_json)) ]);
    ("messages", `Assoc [ ("messages", `List msgs_json); ("total", `Int (List.length msgs_json)) ]);
    ("keepers", keepers_dashboard_json ~compact config);
    ("perpetual", perpetual_dashboard_json ());
  ]

(** Strip non-ASCII characters from actor string.
    Prevents IME artifacts (e.g. Korean ㅊ) from polluting cache keys. *)
let sanitize_actor s =
  let buf = Buffer.create (String.length s) in
  String.iter (fun c ->
    match c with
    | 'a' .. 'z' | 'A' .. 'Z' | '0' .. '9' | '_' | '-' -> Buffer.add_char buf c
    | _ -> ()
  ) s;
  Buffer.contents buf

let operator_actor_hint request =
  match agent_from_request request with
  | Some raw ->
      let sanitized = sanitize_actor (String.trim raw) in
      if sanitized = "" then None else Some sanitized
  | None -> None

(* --- Operator proactive refresh ---
   Default (no-param) requests are served from a background-refreshed ref.
   Parameterized requests fall back to on-demand compute with SWR cache.

   The snapshot compute can take 1-28s (command_plane_json is the bottleneck).
   Using Proactive_refresh gives circuit breaker + exponential backoff on
   repeated failures, matching the pattern used by execution and mission loops.

   Interval: 10s (was 120s). Even if compute takes ~8s, the ref is updated
   every ~18s worst-case, which is acceptable for dashboard SSE polling. *)

let _operator_snapshot_cache =
  create_cached_surface
    (`Assoc [ ("status", `String "initializing"); ("generated_at", `String (Types.now_iso ())) ])

let _operator_digest_cache =
  create_cached_surface
    (`Assoc [ ("health", `String "initializing"); ("generated_at", `String (Types.now_iso ())) ])

let _operator_refresh_interval_s =
  float_of_env_default
    "MASC_OPERATOR_REFRESH_INTERVAL_S"
    ~default:10.0
    ~min_v:2.0
    ~max_v:600.0

let operator_snapshot_extra sessions =
  [
    ("session_count", `Int (List.length sessions));
    ("session_list", Team_session_store.session_list_diagnostics_json ());
    ("readonly_pool", Room_utils.domain_local_pg_backend_diagnostics_json ());
  ]

let start_operator_snapshot_refresh_loop ~state ~sw ~clock =
  let config = state.Mcp_server.room_config in
  let proc_mgr = state.Mcp_server.proc_mgr in
  let compute () =
    mark_cached_surface_attempt _operator_snapshot_cache;
    let started_at = Unix.gettimeofday () in
    try
      let sessions =
        if Room.is_initialized config then
          dashboard_active_or_recent_sessions config
        else
          []
      in
      let ctx : _ Operator_control.context =
        { config; agent_name = "dashboard"; sw; clock; proc_mgr; mcp_session_id = None }
      in
      Operator_control.snapshot_json ~actor:"dashboard" ~view:"summary"
        ~include_messages:true ~include_sessions:true ~include_keepers:true
        ~include_command_plane:false ~sessions ctx
      |> with_projection_diagnostics ~surface:"operator_snapshot" ~started_at
           ~extra:(operator_snapshot_extra sessions)
    with exn ->
      mark_cached_surface_error _operator_snapshot_cache exn;
      raise exn
  in
  Proactive_refresh.start ~sw ~clock
    ~config:{ (Proactive_refresh.default_config
                 ~label:"operator_snapshot"
                 ~interval_s:_operator_refresh_interval_s)
              with timeout_s = 30.0 }
    ~compute
    ~on_result:(mark_cached_surface_success _operator_snapshot_cache)

let start_operator_digest_refresh_loop ~state ~sw ~clock =
  let config = state.Mcp_server.room_config in
  let proc_mgr = state.Mcp_server.proc_mgr in
  let compute () =
    mark_cached_surface_attempt _operator_digest_cache;
    let started_at = Unix.gettimeofday () in
    try
      let sessions =
        if Room.is_initialized config then
          dashboard_active_or_recent_sessions config
        else
          []
      in
      let command_plane_summary, swarm_status =
        command_plane_summary_cache_parts ~state
      in
      let ctx : _ Operator_control.context =
        { config; agent_name = "dashboard"; sw; clock; proc_mgr; mcp_session_id = None }
      in
      match
        Operator_control.digest_json ~actor:"dashboard" ~target_type:"room"
          ~sessions ?command_plane_summary ?swarm_status ctx
      with
      | Ok json ->
          with_projection_diagnostics ~surface:"operator_digest" ~started_at
            ~extra:(operator_snapshot_extra sessions) json
      | Error err -> failwith err
    with exn ->
      mark_cached_surface_error _operator_digest_cache exn;
      raise exn
  in
  Proactive_refresh.start ~sw ~clock
    ~config:{ (Proactive_refresh.default_config
                 ~label:"operator_digest"
                 ~interval_s:_operator_refresh_interval_s)
              with timeout_s = 30.0 }
    ~compute
    ~on_result:(mark_cached_surface_success _operator_digest_cache)

let operator_snapshot_http_json ~state ~sw ~clock request =
  let actor = operator_actor_hint request in
  let view = query_param request "view" in
  let default_summary_request =
    actor = None
    && query_param request "include_messages" = None
    && query_param request "include_sessions" = None
    && query_param request "include_keepers" = None
    &&
    match view with
    | None -> true
    | Some raw -> String.equal (String.lowercase_ascii (String.trim raw)) "summary"
  in
  if default_summary_request then
    cached_surface_json _operator_snapshot_cache
  else begin
    let started_at = Unix.gettimeofday () in
    let include_messages =
      match query_param request "include_messages" with
      | Some ("0" | "false" | "no") -> false
      | _ -> true
    in
    let include_sessions =
      match query_param request "include_sessions" with
      | Some ("0" | "false" | "no") -> false
      | _ -> true
    in
    let include_keepers =
      match query_param request "include_keepers" with
      | Some ("0" | "false" | "no") -> false
      | _ -> true
    in
    let include_command_plane =
      match view with
      | Some raw -> not (String.equal (String.lowercase_ascii (String.trim raw)) "summary")
      | None -> true
    in
    let mode =
      if include_command_plane then Offloaded_readonly else Inline_shared
    in
    match Eio.Time.with_timeout clock 30.0 (fun () ->
      Ok
        (run_dashboard_compute ~mode ~sw ~clock
           ~config:state.Mcp_server.room_config
           (fun ~config ~sw ->
             let ctx : _ Operator_control.context =
               {
                 config;
                 agent_name = Option.value ~default:"dashboard" actor;
                 sw;
                 clock;
                 proc_mgr = state.Mcp_server.proc_mgr;
                 mcp_session_id = None;
               }
             in
             Operator_control.snapshot_json ?actor ?view
               ~include_messages ~include_sessions ~include_keepers
               ~include_command_plane ctx))
    ) with
    | Ok json ->
        let extra =
          [
            ("session_list", Team_session_store.session_list_diagnostics_json ());
            ("readonly_pool", Room_utils.domain_local_pg_backend_diagnostics_json ());
          ]
        in
        with_projection_diagnostics ~surface:"operator_snapshot" ~started_at ~extra
          json
    | Error `Timeout ->
        `Assoc [
          ("error", `String "timeout");
          ("message", `String "Operator snapshot timed out after 30s");
          ("generated_at", `String (Types.now_iso ()));
        ]
  end

let operator_digest_http_json ~state ~sw ~clock request =
  let actor = operator_actor_hint request in
  let target_type = query_param request "target_type" in
  let target_id = query_param request "target_id" in
  let include_workers =
    match query_param request "include_workers" with
    | Some ("0" | "false" | "no") -> Some false
    | Some ("1" | "true" | "yes") -> Some true
    | _ -> None
  in
  let default_room_request =
    actor = None
    && target_id = None
    && include_workers = None
    &&
    match target_type with
    | None -> true
    | Some raw -> String.equal (String.lowercase_ascii (String.trim raw)) "room"
  in
  if default_room_request then
    Ok (cached_surface_json _operator_digest_cache)
  else
    let started_at = Unix.gettimeofday () in
    let effective_target_type =
      Option.value ~default:"room" target_type
    in
    let mode =
      if String.equal effective_target_type "room" then Inline_shared
      else Offloaded_readonly
    in
    match Eio.Time.with_timeout clock 30.0 (fun () ->
      Ok
        (run_dashboard_compute ~mode ~sw ~clock
           ~config:state.Mcp_server.room_config
           (fun ~config ~sw ->
             let ctx : _ Operator_control.context =
               {
                 config;
                 agent_name = Option.value ~default:"dashboard" actor;
                 sw;
                 clock;
                 proc_mgr = state.Mcp_server.proc_mgr;
                 mcp_session_id = None;
               }
             in
             let command_plane_summary, swarm_status =
               if String.equal effective_target_type "room" then
                 command_plane_summary_cache_parts ~state
               else
                 (None, None)
             in
             match
               Operator_control.digest_json ?actor ~target_type:effective_target_type
                 ?target_id ?include_workers ?command_plane_summary ?swarm_status
                 ctx
             with
             | Ok json -> json
             | Error err ->
                 `Assoc
                   [
                     ("error", `String "validation_error");
                     ("message", `String err);
                     ("generated_at", `String (Types.now_iso ()));
                   ]))
    ) with
    | Ok json ->
        let extra =
          [
            ("session_list", Team_session_store.session_list_diagnostics_json ());
            ("readonly_pool", Room_utils.domain_local_pg_backend_diagnostics_json ());
          ]
        in
        Ok
          (with_projection_diagnostics ~surface:"operator_digest" ~started_at
             ~extra json)
    | Error `Timeout ->
        Ok
          (`Assoc
            [
              ("error", `String "timeout");
              ("message", `String "Operator digest timed out after 30s");
              ("generated_at", `String (Types.now_iso ()));
            ])

(* --- Mission proactive refresh ----------------------------------------
   A background fiber recomputes the mission snapshot periodically.
   The HTTP handler returns the cached ref immediately (0ms).
   Actor-parameterized requests fall back to on-demand compute with
   SWR cache. *)

let _mission_cache =
  create_cached_surface
    (`Assoc
      [
        ("generated_at", `String (Types.now_iso ()));
        ("summary", `Assoc [("room_health", `String "initializing")]);
        ("incidents", `List []);
        ("recommended_actions", `List []);
        ("command_focus", `Assoc []);
        ("operator_targets", `Assoc []);
        ("attention_queue", `List []);
        ("sessions", `List []);
        ("session_briefs", `List []);
        ("agent_briefs", `List []);
        ("keeper_briefs", `List []);
        ("internal_signals", `List []);
      ])

let start_mission_refresh_loop ~state ~sw ~clock =
  let room_config = state.Mcp_server.room_config in
  let proc_mgr = state.Mcp_server.proc_mgr in
  let compute () =
    mark_cached_surface_attempt _mission_cache;
    try
      run_dashboard_compute ~mode:Inline_shared ~sw ~clock ~config:room_config
        (fun ~config ~sw -> Dashboard_mission.json ~config ~sw ~clock ~proc_mgr ())
    with exn ->
      mark_cached_surface_error _mission_cache exn;
      raise exn
  in
  Proactive_refresh.start ~sw ~clock
    ~config:{ (Proactive_refresh.default_config ~label:"mission" ~interval_s:120.0)
              with timeout_s = 120.0 }
    ~compute
    ~on_result:(mark_cached_surface_success _mission_cache)

(* Trim a full mission JSON to a lightweight snapshot:
   summary, session_briefs (goal/elapsed/blocker only), attention_queue (top 5), counts.
   Target: <20KB vs ~483KB full. *)
let mission_snapshot_of_full (full : Yojson.Safe.t) : Yojson.Safe.t =
  let field key = Yojson.Safe.Util.member key full in
  let briefs =
    match field "session_briefs" with
    | `List items ->
        `List
          (List.map
             (fun item ->
               let f k = Yojson.Safe.Util.member k item in
               `Assoc
                 [
                   ("session_id", f "session_id");
                   ("goal", f "goal");
                   ("status", f "status");
                   ("health", f "health");
                   ("elapsed_sec", f "elapsed_sec");
                   ("blocker_summary", f "blocker_summary");
                 ])
             items)
    | other -> other
  in
  let attention_top5 =
    match field "attention_queue" with
    | `List items -> `List (List.filteri (fun i _ -> i < 5) items)
    | other -> other
  in
  `Assoc
    [
      ("generated_at", field "generated_at");
      ("summary", field "summary");
      ("session_briefs", briefs);
      ("attention_queue", attention_top5);
      ("session_count",
       `Int
         (match field "sessions" with `List l -> List.length l | _ -> 0));
      ("agent_count",
       `Int
         (match field "agent_briefs" with
         | `List l -> List.length l
         | _ -> 0));
      ("keeper_count",
       `Int
         (match field "keeper_briefs" with
         | `List l -> List.length l
         | _ -> 0));
    ]

let dashboard_mission_http_json ~state ~sw ~clock request =
  let actor = operator_actor_hint request in
  let mode = query_param request "mode" in
  let full_json =
    match actor with
    | None ->
      (* Default: return proactively cached value immediately (0ms). *)
      cached_surface_json _mission_cache
    | Some _ ->
      (* Actor-parameterized: on-demand with SWR cache. *)
      let cache_key =
        Printf.sprintf "mission:%s" (Option.value ~default:"" actor)
      in
      Dashboard_cache.get_or_compute_with_timeout cache_key ~ttl:120.0
        ~clock ~timeout_sec:120.0 (fun () ->
        run_dashboard_compute ~mode:Offloaded_readonly ~sw ~clock
          ~config:state.Mcp_server.room_config
          (fun ~config ~sw ->
            Dashboard_mission.json ?actor
              ~config ~sw ~clock
              ~proc_mgr:state.Mcp_server.proc_mgr ()))
  in
  match mode with
  | Some "snapshot" -> mission_snapshot_of_full full_json
  | _ -> full_json

let dashboard_session_http_json ~state ~sw ~clock request =
  match query_param request "session_id" with
  | Some session_id when String.trim session_id <> "" ->
      Dashboard_mission.session_json ?actor:(operator_actor_hint request)
        ~session_id:(String.trim session_id)
        ~config:state.Mcp_server.room_config ~sw ~clock
        ~proc_mgr:state.Mcp_server.proc_mgr ()
  | _ ->
      `Assoc
        [
          ("generated_at", `String (Types.now_iso ()));
          ("session_id", `Null);
          ("session", `Null);
          ("timeline", `List []);
          ("participants", `List []);
          ("operations", `List []);
          ("keepers", `List []);
          ("error", `String "session_id is required");
        ]

let dashboard_mission_briefing_http_json ~state ~sw ~clock request =
  let actor = operator_actor_hint request in
  let force = bool_query_param request "force" ~default:false in
  let compute () =
    Dashboard_mission_briefing.json ?actor ~force
      ~config:state.Mcp_server.room_config ~sw ~clock
      ~proc_mgr:state.Mcp_server.proc_mgr ()
  in
  if force then with_dashboard_timeout ~clock compute
  else
    let cache_key =
      Printf.sprintf "mission_briefing:%s" (Option.value ~default:"" actor)
    in
    Dashboard_cache.get_or_compute_with_timeout cache_key ~ttl:5.0
      ~clock ~timeout_sec:60.0 compute

let dashboard_proof_http_json ~state request =
  let session_id = query_param request "session_id" in
  let operation_id = query_param request "operation_id" in
  Dashboard_proof.json ?actor:(operator_actor_hint request) ?session_id
    ?operation_id ~config:state.Mcp_server.room_config ()

let dashboard_shell_status_json (config : Room.config) : Yojson.Safe.t =
  let room_state = Room.read_state config in
  let current_room =
    Room.read_current_room config |> Option.value ~default:"default"
  in
  let tempo = Tempo.get_tempo config in
  let build = Build_identity.current () in
  `Assoc
    [
      ("room", `String current_room);
      ("current_room", `String current_room);
      ("room_base_path", `String config.base_path);
      ( "cluster",
        `String (Option.value ~default:"unknown" (Sys.getenv_opt "MASC_CLUSTER_NAME"))
      );
      ("project", `String room_state.project);
      ("tempo_interval_s", `Float tempo.current_interval_s);
      ("paused", `Bool room_state.paused);
      ("version", `String build.release_version);
      ("build", Build_identity.to_yojson build);
    ]

let dashboard_task_assignee (task : Types.task) =
  match task.task_status with
  | Claimed { assignee; _ } | InProgress { assignee; _ } | Done { assignee; _ } ->
      Some assignee
  | Todo | Cancelled _ -> None

let dashboard_task_json (task : Types.task) =
  `Assoc
    [
      ("id", `String task.id);
      ("title", `String task.title);
      ("description", `String task.description);
      ("status", `String (Types.string_of_task_status task.task_status));
      ("priority", `Int task.priority);
      ("assignee", match dashboard_task_assignee task with Some v -> `String v | None -> `Null);
      ("created_at", `String task.created_at);
    ]

let dashboard_agent_json (agent : Types.agent) =
  let profile = Dashboard_execution_helpers.get_agent_profile agent.name in
  `Assoc
    [
      ("name", `String agent.name);
      ("agent_type", `String agent.agent_type);
      ("status", `String (Types.string_of_agent_status agent.status));
      ("current_task", match agent.current_task with Some task -> `String task | None -> `Null);
      ("joined_at", `String agent.joined_at);
      ("last_seen", `String agent.last_seen);
      ("capabilities", `List (List.map (fun item -> `String item) agent.capabilities));
      ("emoji", `String profile.emoji);
      ("koreanName", `String profile.korean_name);
      ("model", match profile.model with Some m -> `String m | None -> `Null);
      ("traits", `List (List.map (fun t -> `String t) profile.traits));
      ("interests", `List (List.map (fun i -> `String i) profile.interests));
      ("activityLevel", match profile.activity_level with Some v -> `Float v | None -> `Null);
      ("primaryValue", match profile.primary_value with Some v -> `String v | None -> `Null);
    ]

let dashboard_message_json (message : Types.message) =
  `Assoc
    [
      ("from", `String message.from_agent);
      ("content", `String message.content);
      ("timestamp", `String message.timestamp);
      ("seq", `Int message.seq);
    ]

let dashboard_current_room_id config =
  Room.current_room_id config

let dashboard_tasks_safe config =
  Room.get_tasks_raw_in_room config (dashboard_current_room_id config)

let dashboard_agents_safe config =
  Room.get_agents_raw_in_room config (dashboard_current_room_id config)

let dashboard_messages_safe config ~since_seq ~limit =
  Room.get_messages_raw_in_room config ~room_id:(dashboard_current_room_id config) ~since_seq ~limit

let provider_capacity_json () : Yojson.Safe.t =
  `Assoc []

let dashboard_shell_http_json (config : Room.config) : Yojson.Safe.t =
  Dashboard_cache.get_or_compute "shell" ~ttl:15.0 (fun () ->
    let agents = dashboard_agents_safe config in
  let tasks = dashboard_tasks_safe config in
  let keepers_json = keepers_dashboard_json ~compact:true config in
  let keepers_total = json_int_field "total" keepers_json ~default:0 in
  `Assoc
    [
      ("generated_at", `String (Types.now_iso ()));
      ("status", dashboard_shell_status_json config);
      ( "counts",
        `Assoc
          [
            ("agents", `Int (List.length agents));
            ("tasks", `Int (List.length tasks));
            ("keepers", `Int keepers_total);
          ] );
      ("providers", provider_capacity_json ());
      ])
