module Format = Stdlib.Format
module Map = Stdlib.Map
module Set = Stdlib.Set
module Queue = Stdlib.Queue
module Hashtbl = Stdlib.Hashtbl
module Mutex = Stdlib.Mutex
module Option = Stdlib.Option
module Result = Stdlib.Result
module Sys = Stdlib.Sys
module Filename = Stdlib.Filename
module List = Stdlib.List
module Array = Stdlib.Array
module String = Stdlib.String
module Char = Stdlib.Char
module Int = Stdlib.Int
module Float = Stdlib.Float

(** Tool_input_validation — Pre-dispatch validation via OAS Tool_middleware.

    Delegates to [Agent_sdk.Tool_middleware.make_validation_hook] for type
    coercion and structured error feedback.

    @since 2.220.0 — OAS delegation
    @since 2.221.0 — use Tool_middleware.make_validation_hook *)

(** Register input validation as a Tool_dispatch pre-hook.
    Must be called after all tool schemas are registered (server init).

    Tools without a registered schema are rejected fail-closed.  Empty
    schemas are accepted only for empty/no-arg calls. *)
let is_internal_marker_key key = String.length key > 0 && Char.equal key.[0] '_'

let strip_internal_marker_args (args : Yojson.Safe.t) : Yojson.Safe.t =
  match args with
  | `Assoc fields ->
    `Assoc (List.filter (fun (key, _) -> not (is_internal_marker_key key)) fields)
  | _ -> args
;;

let required_names schema =
  match Yojson.Safe.Util.member "required" schema with
  | `List items ->
    List.filter_map
      (function
        | `String name -> Some name
        | _ -> None)
      items
  | _ -> []
;;

let has_enum schema =
  match Yojson.Safe.Util.member "enum" schema with
  | `List (_ :: _) -> true
  | _ -> false
;;

let optional_enum_fields schema =
  let required = required_names schema in
  match Yojson.Safe.Util.member "properties" schema with
  | `Assoc props ->
    List.filter_map
      (fun (name, prop_schema) ->
         if (not (List.mem name required)) && has_enum prop_schema
         then Some name
         else None)
      props
  | _ -> []
;;

let normalize_blank_optional_enum_args ?schema args =
  match schema, args with
  | Some schema, `Assoc fields ->
    let optional_enums = optional_enum_fields schema in
    if optional_enums = []
    then args
    else
      `Assoc
        (List.filter
           (fun (key, value) ->
              match value with
              | `String raw when List.mem key optional_enums && String.trim raw = "" ->
                false
              | _ -> true)
           fields)
  | _ -> args
;;

let prepare_args ?schema ~name args =
  let args = strip_internal_marker_args args in
  normalize_blank_optional_enum_args ?schema args
;;

let schema_has_properties = function
  | `Assoc fields ->
    (match List.assoc_opt "properties" fields with
     | Some (`Assoc (_ :: _)) -> true
     | _ ->
       (match List.assoc_opt "oneOf" fields with
        | Some (`List (_ :: _)) -> true
        | _ -> false))
  | _ -> false
;;

let property_names schema =
  match Yojson.Safe.Util.member "properties" schema with
  | `Assoc props -> List.map fst props
  | _ -> []
;;

let forbids_additional_properties schema =
  match Yojson.Safe.Util.member "additionalProperties" schema with
  | `Bool false -> true
  | _ -> false
;;

let unsupported_arg_names schema = function
  | `Assoc fields when forbids_additional_properties schema ->
    let properties = property_names schema in
    fields
    |> List.filter_map (fun (name, _) ->
      if List.mem name properties then None else Some name)
    |> List.sort_uniq String.compare
  | _ -> []
;;

let schema_has_property schema name = List.mem name (property_names schema)

let typed_shell_unsupported_field_hint schema names =
  let has_shell_fields =
    schema_has_property schema "executable" && schema_has_property schema "argv"
  in
  let has_legacy_shell_string =
    List.exists (fun name -> String.equal name "cmd" || String.equal name "command") names
  in
  if has_shell_fields && has_legacy_shell_string
  then
    Some
      "typed shell execution has no cmd/command field; use executable/argv, \
       e.g. executable=\"git\" argv=[\"status\",\"--short\"]. Do not include the \
       executable again in argv"
  else None
;;

type one_of_branch = {
  required : string list;
  consts : (string * Yojson.Safe.t) list;
  forbidden_required : string list;
}

let one_of_branch_constraints schema =
  match Yojson.Safe.Util.member "oneOf" schema with
  | `List branches ->
    let constraints =
      List.filter_map
        (fun branch ->
           let required = required_names branch in
           if required = []
           then None
           else
             let consts =
               match Yojson.Safe.Util.member "properties" branch with
               | `Assoc props ->
                 List.filter_map
                   (fun (name, prop_schema) ->
                      match prop_schema with
                      | `Assoc prop_fields ->
                        (match List.assoc_opt "const" prop_fields with
                         | Some const_value -> Some (name, const_value)
                         | None -> None)
                      | _ -> None)
                   props
               | _ -> []
             in
             let forbidden_required =
               match Yojson.Safe.Util.member "not" branch with
               | `Assoc _ as not_schema -> required_names not_schema
               | _ -> []
             in
             Some { required; consts; forbidden_required })
        branches
    in
    if List.length constraints = List.length branches then constraints else []
  | _ -> []
;;

let branch_label b =
  let const_parts =
    List.map
      (fun (name, value) ->
         Printf.sprintf "%s=%s" name (Yojson.Safe.to_string value))
      b.consts
  in
  let req_without_consts =
    List.filter (fun name -> not (List.mem_assoc name b.consts)) b.required
  in
  String.concat "+" (const_parts @ req_without_consts)
;;

let one_of_required_shape_error schema = function
  | `Assoc fields ->
    let branches = one_of_branch_constraints schema in
    if branches = []
    then None
    else (
      let has_present name =
        match List.assoc_opt name fields with
        | None -> false
        | Some `Null -> false
        | Some (`List []) -> false
        | Some _ -> true
      in
      let key_is_present name = Option.is_some (List.assoc_opt name fields) in
      let const_field_matches name expected =
        match List.assoc_opt name fields with
        | Some actual -> Yojson.Safe.equal actual expected
        | None -> true (* const is optional; absence does not disqualify *)
      in
      let branch_matches branch =
        List.for_all has_present branch.required
        && not (List.exists key_is_present branch.forbidden_required)
        && List.for_all
             (fun (name, expected) -> const_field_matches name expected)
             branch.consts
      in
      let matching = List.filter branch_matches branches in
      match matching with
      | [ _ ] -> None
      | [] ->
        let options =
          branches |> List.map branch_label |> String.concat " | "
        in
        Some (Printf.sprintf "arguments must include exactly one of: %s" options)
      | _ :: _ :: _ ->
        let options =
          matching |> List.map branch_label |> String.concat " | "
        in
        Some
          (Printf.sprintf
             "arguments match multiple mutually exclusive schemas: %s"
             options))
  | _ -> None
;;

let schema_shape_error schema args =
  match unsupported_arg_names schema args with
  | name :: names ->
    let names = name :: names in
    let names_text = String.concat ", " names in
    let hint =
      match typed_shell_unsupported_field_hint schema names with
      | None -> ""
      | Some hint -> "; " ^ hint
    in
    Some (Printf.sprintf "received unsupported field(s): %s%s" names_text hint)
  | [] -> one_of_required_shape_error schema args
;;

let retired_transition_alias_names ~name = function
  | `Assoc fields when String.equal name "masc_transition" ->
    fields
    |> List.filter_map (fun (field, _) ->
      if String.equal field "to" || String.equal field "note" then Some field else None)
    |> List.sort_uniq String.compare
  | _ -> []
;;

let empty_tool_args = function
  | `Null | `Assoc [] -> true
  | _ -> false
;;

let emit_validation_telemetry ~tool ~result ~reason =
  Prometheus.inc_counter
    Prometheus.metric_tool_input_validation
    ~labels:[ "tool", tool; "result", result; "reason", reason ]
    ();
  Otel_spans.add_event
    ~name:"tool.param.validation"
    ~attrs:
      [ "tool.name", `String tool
      ; "tool.param.validation.result", `String result
      ; "tool.param.validation.reason", `String reason
      ]
    ()
;;

let pass_reason ~schema ~args ~prepared_args =
  match schema with
  | Some schema when not (schema_has_properties schema) -> "empty_schema"
  | Some _ when not (Yojson.Safe.equal prepared_args args) -> "normalized"
  | Some _ -> "valid"
  | None -> "missing_schema"
;;

let validation_schema_of_json ~name json_schema : Agent_sdk.Types.tool_schema =
  { name; description = ""; parameters = Tool_bridge.params_of_json_schema json_schema }
;;

let reject_validation ~name ~reason ~message =
  emit_validation_telemetry ~tool:name ~result:"fail" ~reason;
  Log.info "tool_input_validation rejected %s: %s" name message;
  Tool_dispatch.Reject
    (Error
       { Tool_result.class_ = Tool_result.Policy_rejection
       ; message
       ; data =
           `Assoc
             [ "error", `String message
             ; "validation", `String "oas_tool_middleware"
             ; "reason", `String reason
             ]
       ; tool_name = name
       ; duration_ms = 0.0
       })
;;

let validation_exception_action ~name exn : Tool_dispatch.pre_hook_action =
  let error_text = Printexc.to_string exn in
  let message =
    Printf.sprintf
      "Tool '%s' parameter validation failed before dispatch: %s"
      name
      error_text
  in
  emit_validation_telemetry ~tool:name ~result:"fail" ~reason:"validation_exception";
  Log.error "%s" message;
  Tool_dispatch.Reject
    (Error
       { Tool_result.class_ = Tool_result.Runtime_failure
       ; message
       ; data =
           `Assoc
             [ "error", `String message
             ; "validation", `String "oas_tool_middleware"
             ; "exception", `String error_text
             ]
       ; tool_name = name
       ; duration_ms = 0.0
       })
;;

let validation_action ?schema ~name ~args () : Tool_dispatch.pre_hook_action =
  try
    let schema =
      match schema with
      | Some _ as schema -> schema
      | None -> Tool_dispatch.lookup_schema name
    in
    let prepared_args = prepare_args ?schema ~name args in
    match schema with
    | None ->
      reject_validation
        ~name
        ~reason:"missing_schema"
        ~message:
          (Printf.sprintf
             "Tool '%s' has no registered input schema; refusing schema-less dispatch"
             name)
    | Some schema when not (schema_has_properties schema) ->
      let required = required_names schema in
      if required <> []
      then
        reject_validation
          ~name
          ~reason:"malformed_schema"
          ~message:
            (Printf.sprintf
               "Tool '%s' schema declares required fields without input properties"
               name)
      else if empty_tool_args prepared_args
      then (
        emit_validation_telemetry ~tool:name ~result:"pass" ~reason:"empty_schema";
        if Yojson.Safe.equal prepared_args args
        then Tool_dispatch.Pass
        else Tool_dispatch.Proceed prepared_args)
      else
        reject_validation
          ~name
          ~reason:"empty_schema_args"
          ~message:
            (Printf.sprintf
               "Tool '%s' declares no input fields but received arguments"
               name)
    | Some schema ->
      (match retired_transition_alias_names ~name prepared_args with
       | alias :: aliases ->
         let aliases = String.concat ", " (alias :: aliases) in
         reject_validation
           ~name
           ~reason:"invalid_args"
           ~message:
             (Printf.sprintf
                "Tool '%s' received retired transition alias field(s): %s; use \
                 action and notes"
                name
                aliases)
       | [] ->
      (match schema_shape_error schema prepared_args with
       | Some message ->
         reject_validation
           ~name
           ~reason:"invalid_args"
           ~message:(Printf.sprintf "Tool '%s' %s" name message)
       | None ->
         let lookup lookup_name =
           let schema_opt =
             if String.equal lookup_name name
             then Some schema
             else Tool_dispatch.lookup_schema lookup_name
           in
           Option.map (validation_schema_of_json ~name:lookup_name) schema_opt
         in
         let hook = Agent_sdk.Tool_middleware.make_validation_hook ~lookup in
         (match hook ~name ~args:prepared_args with
    | Agent_sdk.Tool_middleware.Pass when not (Yojson.Safe.equal prepared_args args) ->
      let reason = pass_reason ~schema:(Some schema) ~args ~prepared_args in
      emit_validation_telemetry ~tool:name ~result:"pass" ~reason;
      Log.debug "tool_input_validation normalized args for %s" name;
      Tool_dispatch.Proceed prepared_args
    | Agent_sdk.Tool_middleware.Pass ->
      let reason = pass_reason ~schema:(Some schema) ~args ~prepared_args in
      emit_validation_telemetry ~tool:name ~result:"pass" ~reason;
      Tool_dispatch.Pass
    | Agent_sdk.Tool_middleware.Proceed coerced ->
      emit_validation_telemetry ~tool:name ~result:"pass" ~reason:"coerced";
      Log.debug "tool_input_validation coerced args for %s" name;
      Tool_dispatch.Proceed coerced
    | Agent_sdk.Tool_middleware.Reject { message; _ } ->
      emit_validation_telemetry ~tool:name ~result:"fail" ~reason:"invalid_args";
      Log.info "tool_input_validation rejected %s: %s" name message;
      (* Input-schema / policy rejection — classify so the
         dispatch-level metric label (failure_class) reflects the
         actual category instead of bucketing as "unclassified". *)
      Tool_dispatch.Reject
        (Error
           { Tool_result.class_ = Tool_result.Policy_rejection
           ; message
           ; data =
               `Assoc
                 [ "error", `String message
                 ; "validation", `String "oas_tool_middleware"
                 ]
           ; tool_name = name
           ; duration_ms = 0.0
           })
      )))
  with
  | Eio.Cancel.Cancelled _ as e -> raise e
  | exn -> validation_exception_action ~name exn
;;

let validate_args ?schema ~name ~args () =
  match validation_action ?schema ~name ~args () with
  | Tool_dispatch.Pass -> Ok args
  | Tool_dispatch.Proceed coerced -> Ok coerced
  | Tool_dispatch.Reject result -> Error result
;;

let register_pre_hook () =
  Tool_dispatch.register_pre_hook (fun ~name ~args -> validation_action ~name ~args ())
;;
